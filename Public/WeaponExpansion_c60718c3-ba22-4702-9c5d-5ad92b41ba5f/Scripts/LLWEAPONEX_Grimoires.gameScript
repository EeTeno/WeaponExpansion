INIT
	
EVENTS

EVENT LLWEAPONEX_Grimoire_OpenBook
VARS
	CHARACTER:_Caster
ON
	OnCharacterEvent(_Caster, "LLWEAPONEX_Events_GrimoirePreparingToFire")
ACTIONS
IF "!c1&!c2"
	CharacterHasStatus(_Caster, UNSHEATHED)
	HasFlag(_Caster, "LLWEAPONEX_GrimoireOpened")
THEN
	SetFlag(_Caster, "LLWEAPONEX_GrimoireOpened")
ENDIF

EVENT LLWEAPONEX_Grimoire_CloseBook
VARS
	CHARACTER:_Caster
ON
	OnCharacterEvent(_Caster, "LLWEAPONEX_Events_GrimoireFired")
ACTIONS
IF "!c1&c2"
	CharacterHasStatus(_Caster, UNSHEATHED)
	HasFlag(_Caster, "LLWEAPONEX_GrimoireOpened")
THEN
	ClearFlag(_Caster, "LLWEAPONEX_GrimoireOpened")
ENDIF
	
EVENT LLWEAPONEX_Grimoire_WindAttack_Target
VARS
	CHARACTER:_Caster
	CHARACTER:_CharacterTarget
	ITEM:_ItemTarget
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterCharacterEvent(_Caster, _CharacterTarget, "LLWEAPONEX_Grimoire_ShootWindMagic")
	OnCharacterItemEvent(_Caster, _ItemTarget, "LLWEAPONEX_Grimoire_ShootWindMagic")
ACTIONS
IF "c1"
	CharacterGetStat(_LevelF, _Caster, Level)
THEN
	Cast(_Level, _LevelF)
ENDIF
IF "!c1"
	IsEqual(_CharacterTarget, null)
THEN
	ShootLocalProjectileAt(Projectile_LLWEAPONEX_Grimoire_ShootWind, _Caster, FLOAT3:{0.0;1.0;0.0}, _CharacterTarget, _Level, _Caster)
	//ShootLocalConeAt(Cone_LLWEAPONEX_Grimoire_ProjectileGust, _Caster, FLOAT3:{0.0;0.0;0.0}, _CharacterTarget, _Level, _Caster)
ENDIF
IF "!c1"
	IsEqual(_ItemTarget, null)
THEN
	ShootLocalProjectileAt(Projectile_LLWEAPONEX_Grimoire_ShootWind, _Caster, FLOAT3:{0.0;1.0;0.0}, _ItemTarget, _Level, _Caster)
ENDIF

EVENT LLWEAPONEX_Grimoire_WindAttack_Position
VARS
	CHARACTER:_Caster
	FLOAT3:_Target
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterEvent(_Caster, "LLWEAPONEX_Grimoire_ShootWindMagic")
ACTIONS
IF "c1"
	CharacterGetStat(_LevelF, _Caster, Level)
THEN
	Cast(_Level, _LevelF)
ENDIF
IF "c1&!c2"
	GetVar(_Target, _Caster, "LLWEAPOENX_GrimoireTargetPosition")
	IsEqual(_Target, null)
THEN
	ShootLocalProjectileAt(Projectile_LLWEAPONEX_Grimoire_ShootWind, _Caster, FLOAT3:{0.0;1.0;0.0}, _Target, _Level, _Caster)
ENDIF

EVENT LLWEAPONEX_Grimoire_Swap_OnTeleport
VARS
	CHARACTER:_Caster
	CHARACTER:_Target
ON
	OnCharacterTeleported(_Target, _Caster, _, _, _)
ACTIONS
IF "c1"
	CharacterHasStatus(_Caster, LLWEAPONEX_GRIMOIRE_SWAP_ACTIVE)
THEN
	IF "c1"
		CharacterIsEnemy(_Caster, _Target)
	THEN
		CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_SWAP_DAMAGE, 0, 0, _Caster)
	ELSE
		CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_SWAP_SURGE, 1, 0, _Caster)
	ENDIF
ENDIF

EVENT LLWEAPONEX_Grimoire_Swap_OnTeleported
VARS
	CHARACTER:_Caster
	CHARACTER:_Target
ON
	OnCharacterTeleported(_Caster, _Target, _, _, _)
ACTIONS
IF "c1"
	CharacterHasStatus(_Caster, LLWEAPONEX_GRIMOIRE_SWAP_ACTIVE)
THEN
	CharacterApplyStatus(_Caster, LLWEAPONEX_GRIMOIRE_SWAP_SURGE, 1, 0, _Caster)
ENDIF

EVENT LLWEAPONEX_Grimoire_Swap_StatusCheck
VARS
	CHARACTER:_Caster
	CHARACTER:_Target
ON
	OnCharacterCharacterEvent(_Caster, _Target, "LLWEAPONEX_Grimoire_ApplySwapMagic")
ACTIONS
IF "c1"
	CharacterIsEnemy(_Caster, _Target)
THEN
	CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_SWAP_DAMAGE, 0, 0, _Caster)
ELSE
	CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_SWAP_SURGE, 1, 0, _Caster)
ENDIF

EVENT LLWEAPONEX_Grimoire_Swap_SetIsMovable
VARS
	ITEM:_Object
	CHARACTER:_Cause
ON
	//OnItemStatusAttempt(_Object, LLWEAPONEX_GRIMOIRE_SCRAMBLE_CHECK)
	OnCharacterItemEvent(_Cause, _Object, "LLWEAPONEX_Grimoire_Scramble_CheckMovable")
ACTIONS
IF "c1&!c2"
	ItemIsMovable(_Object)
	IsStoryItem(_Object)
THEN
	CharacterItemEvent(_Cause, _Object, "LLWEAPONEX_Grimoire_Scramble_ItemIsMovable")
ENDIF

EVENT LLWEAPONEX_Grimoire_TransitDamageCheck
VARS
	CHARACTER:_Source
	CHARACTER:_Target
ON
	OnCharacterStatusApplied(_Target, LLWEAPONEX_GRIMOIRE_TRANSIT_DAMAGE_CHECK)
ACTIONS
IF "c1&c2"
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_GRIMOIRE_TRANSIT_DAMAGE_CHECK, _Source)
	CharacterHasStatus(_Source, LLWEAPONEX_GRIMOIRE_SWAP_ACTIVE)
THEN
	CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_TRANSIT_DAMAGE, 0, 0, _Source)
ENDIF

EVENT LLWEAPONEX_Grimoire_Mimic_Ready
VARS
	CHARACTER:_Character
ON
	OnCharacterStatusRemoved(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN)
ACTIONS
IF "c1"
	CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMICRY_ACTIVE)
THEN
	IF "c1"
		IsInCombat(_Character)
	THEN
		CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_READY, 1, 0, _Character)
	ELSE
		SetFlag(_Character, "LLWEAPONEX_Grimoire_MimicReady")
	ENDIF
ENDIF

EVENT LLWEAPONEX_Grimoire_Mimic_Ready_Apply
VARS
	CHARACTER:_Character
ON
	OnEnteredCombat(_Character, _)
ACTIONS
IF "c1"
	HasFlag(_Character, "LLWEAPONEX_Grimoire_MimicReady")
THEN
	ClearFlag(_Character, "LLWEAPONEX_Grimoire_MimicReady")
	IF "c1"
		CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMICRY_ACTIVE)
	THEN
		CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_READY, 1, 0, _Character)
	ENDIF
ELSE
	IF "c1"
		CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMICRY_ACTIVE)
	THEN
		CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN, 3, 0, _Character)
	ENDIF
ENDIF

EVENT LLWEAPONEX_Grimoire_Mimic_StartCountdown
VARS
	CHARACTER:_Character
ON
	OnCharacterStatusRemoved(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_READY)
	OnLeftCombat(_Character, _)
ACTIONS
IF "c1&!c2"
	CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMICRY_ACTIVE)
	HasFlag(_Character, "LLWEAPONEX_Grimoire_MimicReady")
THEN
	CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN, 3, 0, _Character)
ENDIF


EVENT LLWEAPONEX_Grimoire_PlaySheathedFX
VARS
	CHARACTER:_Character
	STRING:_FX
	FIXEDSTRING:_Bone
	INT64:_Handle
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Grimoire_PlaySheathedFX")
ACTIONS
IF "c1&c2"
	GetVar(_FX, _Character, "LLWEAPONEX_Grimoire_NextLoopFX")
	GetVar(_Bone, _Character, "LLWEAPONEX_Grimoire_NextBone")
THEN
	CharacterPlayLoopEffect(_Handle, _Character, _FX, _Bone)
	SetVar(_Character, "LLWEAPONEX_Grimoire_SheathedFXHandle", _Handle)
	IF "c1"
		GetVar(_Handle, _Character, "LLWEAPONEX_Grimoire_UnsheathedFXHandle")
	THEN
		StopLoopEffect(_Handle, 1)
		SetVar(_Character, "LLWEAPONEX_Grimoire_UnsheathedFXHandle", INT64:null)
	ENDIF
ENDIF

EVENT LLWEAPONEX_Grimoire_PlayUnsheathedFX
VARS
	CHARACTER:_Character
	STRING:_FX
	FIXEDSTRING:_Bone
	INT64:_Handle
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Grimoire_PlayUnsheathedFX")
ACTIONS
IF "c1&c2"
	GetVar(_FX, _Character, "LLWEAPONEX_Grimoire_NextLoopFX")
	GetVar(_Bone, _Character, "LLWEAPONEX_Grimoire_NextBone")
THEN
	CharacterPlayLoopEffect(_Handle, _Character, _FX, _Bone)
	SetVar(_Character, "LLWEAPONEX_Grimoire_UnsheathedFXHandle", _Handle)
	/*
	IF "c1"
		GetVar(_Handle, _Character, "LLWEAPONEX_Grimoire_SheathedFXHandle")
	THEN
		StopLoopEffect(_Handle, 1)
		SetVar(_Character, "LLWEAPONEX_Grimoire_SheathedFXHandle", INT64:null)
	ENDIF
	*/
ENDIF

EVENT LLWEAPONEX_Grimoire_StopUnsheathedFX
VARS
	CHARACTER:_Character
	STRING:_FX
	INT64:_Handle
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Grimoire_StopUnsheathedFX")
ACTIONS
IF "c1"
	GetVar(_Handle, _Character, "LLWEAPONEX_Grimoire_UnsheathedFXHandle")
THEN
	StopLoopEffect(_Handle, 1)
	SetVar(_Character, "LLWEAPONEX_Grimoire_UnsheathedFXHandle", INT64:null)
ENDIF

EVENT LLWEAPONEX_Grimoire_StopSheathedFX
VARS
	CHARACTER:_Character
	STRING:_FX
	INT64:_Handle
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Grimoire_StopSheathedFX")
ACTIONS
IF "c1"
	GetVar(_Handle, _Character, "LLWEAPONEX_Grimoire_SheathedFXHandle")
THEN
	StopLoopEffect(_Handle, 1)
	SetVar(_Character, "LLWEAPONEX_Grimoire_SheathedFXHandle", INT64:null)
ENDIF

EVENT LLWEAPONEX_Grimoire_StopEquippedFX
VARS
	CHARACTER:_Character
	INT64:_Handle
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Grimoire_StopEquippedFX")
ACTIONS
IF "c1"
	GetVar(_Handle, _Character, "LLWEAPONEX_Grimoire_UnsheathedFXHandle")
THEN
	StopLoopEffect(_Handle, 1)
	SetVar(_Character, "LLWEAPONEX_Grimoire_UnsheathedFXHandle", INT64:null)
ENDIF
IF "c1"
	GetVar(_Handle, _Character, "LLWEAPONEX_Grimoire_SheathedFXHandle")
THEN
	StopLoopEffect(_Handle, 1)
	SetVar(_Character, "LLWEAPONEX_Grimoire_SheathedFXHandle", INT64:null)
ENDIF

EVENT LLWEAPONEX_Transit_CastEffect
VARS
	CHARACTER:_Character
	INT64:_Handle
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_UsedTransitSkill")
ACTIONS
	CharacterPlayEffect(_Character, "LLWEAPONEX_FX_Skills_Cast_Transit_Portal_01", "Dummy_FrontFX")