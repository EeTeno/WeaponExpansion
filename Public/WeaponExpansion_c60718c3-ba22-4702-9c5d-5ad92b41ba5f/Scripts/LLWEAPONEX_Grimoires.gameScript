INIT
	LIST<FIXEDSTRING>:%LLWEAPONEX_Grimoire_AllEffects
	INT:%LLWEAPONEX_Grimoire_ListInitialized = 0
EVENTS

EVENT LLWEAPONEX_Grimoire_InitFX_Start
ON
	OnItemEvent(_, "LeaderLib_Initialized")
ACTIONS
IF "c1"
	IsEqual(%LLWEAPONEX_Grimoire_ListInitialized, 0)
THEN
	CallFunction("LLWEAPONEX_Grimoire_InitEffects")
ENDIF

EVENT LLWEAPONEX_Grimoire_InitFXList
ON
	OnFunction("LLWEAPONEX_Grimoire_InitEffects")
ACTIONS
	ListClear(%LLWEAPONEX_Grimoire_AllEffects)
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Sheathed_Death_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Unsheathed_Death_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Transition_Death_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Sheathed_Mimicry_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Unsheathed_Mimicry_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Transition_Mimicry_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Sheathed_Swap_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Unsheathed_Swap_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Transition_Swap_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Sheathed_Wind_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Unsheathed_Wind_A")
	ListAdd(%LLWEAPONEX_Grimoire_AllEffects, "LLWEAPONEX_FX_Grimoire_Equipped_Transition_Wind_A")
	Set(%LLWEAPONEX_Grimoire_ListInitialized, 1)
	
EVENT LLWEAPONEX_Grimoire_WindAttack_Target
VARS
	CHARACTER:_Caster
	CHARACTER:_CharacterTarget
	ITEM:_ItemTarget
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterCharacterEvent(_Caster, _CharacterTarget, "LLWEAPONEX_Grimoire_ShootWindMagic")
	OnCharacterItemEvent(_Caster, _ItemTarget, "LLWEAPONEX_Grimoire_ShootWindMagic")
ACTIONS
IF "c1"
	CharacterGetStat(_LevelF, _Caster, Level)
THEN
	Cast(_Level, _LevelF)
ENDIF
IF "!c1"
	IsEqual(_CharacterTarget, null)
THEN
	ShootLocalProjectileAt(Projectile_LLWEAPONEX_Grimoire_ShootWind, _Caster, {0.0;2.0;0.0}, _CharacterTarget, _Level, _Caster)
	//ShootLocalConeAt(Cone_LLWEAPONEX_Grimoire_ProjectileGust, _Caster, FLOAT3:{0.0;0.0;0.0}, _CharacterTarget, _Level, _Caster)
ENDIF
IF "!c1"
	IsEqual(_ItemTarget, null)
THEN
	ShootLocalProjectileAt(Projectile_LLWEAPONEX_Grimoire_ShootWind, _Caster, {0.0;2.0;0.0}, _ItemTarget, _Level, _Caster)
ENDIF

EVENT LLWEAPONEX_Grimoire_WindAttack_Position
VARS
	CHARACTER:_Caster
	FLOAT3:_Target
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterEvent(_Caster, "LLWEAPONEX_Grimoire_ShootWindMagic")
ACTIONS
	Set(_Level, -1)
IF "c1"
	CharacterGetStat(_LevelF, _Caster, Level)
THEN
	Cast(_Level, _LevelF)
ENDIF
IF "c1&!c2"
	GetVar(_Target, _Caster, "LLWEAPOENX_GrimoireTargetPosition")
	IsEqual(_Target, null)
THEN
	ShootLocalProjectileAt(Projectile_LLWEAPONEX_Grimoire_ShootWind, _Caster, {0.0;2.0;0.0}, _Target, _Level, _Caster)
ENDIF

EVENT LLWEAPONEX_Grimoire_Death_Position_ExplodeTorment
VARS
	CHARACTER:_Caster
	FLOAT3:_Target
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterEvent(_Caster, "LLWEAPONEX_Grimoire_TormentExplode")
ACTIONS
IF "c1"
	CharacterGetStat(_LevelF, _Caster, Level)
THEN
	Cast(_Level, _LevelF)
ENDIF
IF "c1&!c2"
	GetVar(_Target, _Caster, "LLWEAPOENX_GrimoireTargetPosition")
	IsEqual(_Target, null)
THEN
	ExplodeAt(_Target, Projectile_LLWEAPONEX_Status_ApplyTorment, _Level, _Caster)
	//ShootLocalProjectileAt(Projectile_LLWEAPONEX_Status_ApplyTorment, _Caster, FLOAT3:{0.0;1.0;0.0}, _Target, _Level, _Caster)
ENDIF

EVENT LLWEAPONEX_Grimoire_Swap_OnTeleport
VARS
	CHARACTER:_Caster
	CHARACTER:_Target
ON
	OnCharacterTeleported(_Target, _Caster, _, _, _)
ACTIONS
IF "c1"
	CharacterHasStatus(_Caster, LLWEAPONEX_GRIMOIRE_SWAP_ACTIVE)
THEN
	IF "c1"
		CharacterIsEnemy(_Caster, _Target)
	THEN
		CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_SWAP_DAMAGE, 0, 0, _Caster)
	ELSE
		CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_SWAP_SURGE, 1, 0, _Caster)
	ENDIF
ENDIF

EVENT LLWEAPONEX_Grimoire_Swap_OnTeleported
VARS
	CHARACTER:_Caster
	CHARACTER:_Target
ON
	OnCharacterTeleported(_Caster, _Target, _, _, _)
ACTIONS
IF "c1"
	CharacterHasStatus(_Caster, LLWEAPONEX_GRIMOIRE_SWAP_ACTIVE)
THEN
	CharacterApplyStatus(_Caster, LLWEAPONEX_GRIMOIRE_SWAP_SURGE, 1, 0, _Caster)
ENDIF

EVENT LLWEAPONEX_Grimoire_Swap_StatusCheck
VARS
	CHARACTER:_Caster
	CHARACTER:_Target
ON
	OnCharacterCharacterEvent(_Caster, _Target, "LLWEAPONEX_Grimoire_ApplySwapMagic")
ACTIONS
IF "c1"
	CharacterIsEnemy(_Caster, _Target)
THEN
	CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_SWAP_DAMAGE, 0, 0, _Caster)
ELSE
	CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_SWAP_SURGE, 1, 0, _Caster)
ENDIF

EVENT LLWEAPONEX_Grimoire_Swap_SetIsMovable
VARS
	ITEM:_Object
	CHARACTER:_Cause
ON
	//OnItemStatusAttempt(_Object, LLWEAPONEX_GRIMOIRE_SCRAMBLE_CHECK)
	OnCharacterItemEvent(_Cause, _Object, "LLWEAPONEX_Grimoire_Scramble_CheckMovable")
ACTIONS
IF "c1&!c2"
	ItemIsMovable(_Object)
	IsStoryItem(_Object)
THEN
	CharacterItemEvent(_Cause, _Object, "LLWEAPONEX_Grimoire_Scramble_ItemIsMovable")
ENDIF

EVENT LLWEAPONEX_Grimoire_TransitDamageCheck
VARS
	CHARACTER:_Source
	CHARACTER:_Target
ON
	OnCharacterStatusApplied(_Target, LLWEAPONEX_GRIMOIRE_TRANSIT_DAMAGE_CHECK)
ACTIONS
IF "c1&c2"
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_GRIMOIRE_TRANSIT_DAMAGE_CHECK, _Source)
	CharacterHasStatus(_Source, LLWEAPONEX_GRIMOIRE_SWAP_ACTIVE)
THEN
	CharacterApplyStatus(_Target, LLWEAPONEX_GRIMOIRE_TRANSIT_DAMAGE, 0, 0, _Source)
ENDIF

EVENT LLWEAPONEX_Grimoire_Mimic_Ready
VARS
	CHARACTER:_Character
ON
	OnCharacterStatusRemoved(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN)
ACTIONS
IF "c1"
	CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMICRY_ACTIVE)
THEN
	IF "c1"
		IsInCombat(_Character)
	THEN
		CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_READY, -1, 0, _Character)
	ELSE
		SetFlag(_Character, "LLWEAPONEX_Grimoire_MimicReady")
	ENDIF
ENDIF

EVENT LLWEAPONEX_Grimoire_Mimic_Ready_Apply
VARS
	CHARACTER:_Character
ON
	OnEnteredCombat(_Character, _)
	OnTurn(_Character, _)
ACTIONS
IF "c1"
	HasFlag(_Character, "LLWEAPONEX_Grimoire_MimicReady")
THEN
	ClearFlag(_Character, "LLWEAPONEX_Grimoire_MimicReady")
	IF "c1&!c2"
		CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMICRY_ACTIVE)
		CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_READY)
	THEN
		CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_READY, 1, 0, _Character)
	ENDIF
ELSE
	IF "c1&!c2&!c3"
		CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMICRY_ACTIVE)
		CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_READY)
		CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN)
	THEN
		CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN, 2, 0, _Character)
	ENDIF
ENDIF

EVENT LLWEAPONEX_Grimoire_Mimic_StartCountdown_Initial
VARS
	CHARACTER:_Character
ON
	OnCharacterStatusApplied(_Character, LLWEAPONEX_GRIMOIRE_MIMICRY_ACTIVE)
ACTIONS
IF "!c1&!c2&!c3"
	HasFlag(_Character, "LLWEAPONEX_Grimoire_MimicReady")
	CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_READY)
	CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN)
THEN
	IF "c1"
		IsInCombat(_Character)
	THEN
		CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN, 2, 0, _Character)
	ELSE
		CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN, 1, 0, _Character)
	ENDIF
ENDIF

EVENT LLWEAPONEX_Grimoire_Mimic_StartCountdown
VARS
	CHARACTER:_Character
ON
	OnCharacterStatusRemoved(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_READY)
	OnLeftCombat(_Character, _)
ACTIONS
IF "c1&!c2&!c3"
	CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMICRY_ACTIVE)
	HasFlag(_Character, "LLWEAPONEX_Grimoire_MimicReady")
	CharacterHasStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN)
THEN
	CharacterApplyStatus(_Character, LLWEAPONEX_GRIMOIRE_MIMIC_COUNTDOWN, 2, 0, _Character)
ENDIF

EVENT LLWEAPONEX_Grimoire_PlayFX
VARS
	CHARACTER:_Character
	STRING:_FX
	FIXEDSTRING:_Bone
	FIXEDSTRING:_HandleName
	INT64:_OldHandle
	INT64:_Handle
	LIST<INT64>:_ActiveFX
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Grimoire_PlayLoopFX")
ACTIONS
IF "c1"
	GetVar(_HandleName, _Character, "LLWEAPONEX_Grimoire_PlayHandleID")
THEN
	/*
	IF "c1"
		GetVar(_OldHandle, _Character, _HandleName)
	THEN
		SetVar(_Character, _HandleName, INT64:null)
	ENDIF
	*/
	IF "c1&c2"
		GetVar(_FX, _Character, "LLWEAPONEX_Grimoire_PlayFX")
		GetVar(_Bone, _Character, "LLWEAPONEX_Grimoire_PlayBone")
	THEN
		CharacterPlayLoopEffect(_Handle, _Character, _FX, _Bone)
		SetVar(_Character, _HandleName, _Handle)
		//ListAdd(%LLWEAPONEX_Grimoire_ActiveFX, _Handle)
	ENDIF
	
	/*
	IF "!c1"
		IsEqual(_OldHandle, null)
	THEN
		StopLoopEffect(_OldHandle, 1)
	ENDIF
	*/
	
	CharacterEvent(_Character, "LLWEAPONEX_Grimoire_FX_PlayEventComplete")
ENDIF

EVENT LLWEAPONEX_Grimoire_StopFX
VARS
	CHARACTER:_Character
	STRING:_FX
	STRING:_CompletionEvent
	FIXEDSTRING:_Bone
	FIXEDSTRING:_HandleName
	INT64:_Handle
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Grimoire_StopLoopFX")
ACTIONS
IF "c1&c2&!c3"
	GetVar(_HandleName, _Character, "LLWEAPONEX_Grimoire_StopHandleID")
	GetVar(_Handle, _Character, _HandleName)
	IsEqual(_Handle, null)
THEN
	StopLoopEffect(_Handle, 1)
	SetVar(_Character, _HandleName, INT64:null)
ENDIF
IF "c1&(!c2&!c3)"
	GetVar(_CompletionEvent, _Character, "LLWEAPONEX_Grimoire_StopCompletionEvent")
	IsEqual(_CompletionEvent, null)
	IsEqual(_CompletionEvent, "")
THEN
	CharacterEvent(_Character, _CompletionEvent)
	SetVar(_Character, "LLWEAPONEX_Grimoire_StopCompletionEvent", STRING:null)
ELSE
	CharacterEvent(_Character, "LLWEAPONEX_Grimoire_FX_StopEventComplete")
ENDIF

EVENT LLWEAPONEX_Grimoire_StopEquippedFX
VARS
	CHARACTER:_Character
	INT64:_Handle
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Grimoire_StopEquippedFX")
ACTIONS
IF "c1"
	GetVar(_Handle, _Character, "LLWEAPONEX_Grimoire_UnsheathedFXHandle")
THEN
	StopLoopEffect(_Handle, 1)
	SetVar(_Character, "LLWEAPONEX_Grimoire_UnsheathedFXHandle", INT64:null)
ENDIF
IF "c1"
	GetVar(_Handle, _Character, "LLWEAPONEX_Grimoire_SheathedFXHandle")
THEN
	StopLoopEffect(_Handle, 1)
	SetVar(_Character, "LLWEAPONEX_Grimoire_SheathedFXHandle", INT64:null)
ENDIF

EVENT LLWEAPONEX_Grimoire_DestroyAllFX
VARS
	CHARACTER:_Character
	INT64:_Handle
	STRING:_UUID
	STRING:_FXStr
	FIXEDSTRING:_FX
	FIXEDSTRING:_FXName
	INT:_Index
	INT:_Size
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Grimoire_DestroyAllFX")
ACTIONS
IF "c1"
	GetVar(_UUID, _Character, "LLWEAPONEX_UUID")
THEN
	IF "c1"
		ListGetSize(%LLWEAPONEX_Grimoire_AllEffects, _Size)
	THEN
		Set(_Index, 1)
		WHILE "!c1"
			IsGreaterThen(_Index, _Size)
		DO
			IF "c1"
				ListGet(%LLWEAPONEX_Grimoire_AllEffects, _Index, _FXName)
			THEN
				Print(_FXStr, "[1][2]", _UUID, _FXName)
				Cast(_FX, _FXStr)
				IF "c1&!c2"
					GetVar(_Handle, _Character, _FX)
					IsEqual(_Handle, null)
				THEN
					StopLoopEffect(_Handle, 1)
					SetVar(_Character, _FX, INT64:null)
				ENDIF
			ENDIF
		Add(_Index, 1)
		ENDWHILE
	ENDIF
ENDIF

EVENT LLWEAPONEX_Grimoire_Transit_CastEffect
VARS
	CHARACTER:_Character
	INT64:_Handle
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_UsedTransitSkill")
ACTIONS
	CharacterPlayEffect(_Character, "LLWEAPONEX_FX_Skills_Cast_Transit_Portal_01", "Dummy_FrontFX")