INIT
	ITEM:__Me
	CHARACTER:%LLWEAPONEX_Mine_Owner = null
	ITEM:%LLWEAPONEX_Mine_Inventory = null
	EXTERN SKILL:%LLWEAPONEX_Mine_Skill = Projectile_LLWEAPONEX_RemoteMine_Explosive
	EXTERN SKILL:%LLWEAPONEX_Mine_Skill_NoWeapon = Projectile_LLWEAPONEX_RemoteMine_Explosive_NoWeapon
	EXTERN INT:%LLWEAPONEX_Mine_UseSelfAsSource = 0
	EXTERN STRING:%LLWEAPONEX_Mine_SkillID = null
	EXTERN STATUS:%LLWEAPONEX_Mine_ExplosionStatus = null
	EXTERN STATUS:%LLWEAPONEX_Mine_ExplosionStatusID = null
	EXTERN INT:%LLWEAPONEX_Mine_UseStatus = 0
	INT:%LLWEAPONEX_Mine_Amount = 1
EVENTS

EVENT LLWEAPONEX_RemoteMine_Detonate_FromSkill
VARS
	CHARACTER:_Character
ON
	//OnCharacterUsedSkillOnMe(_Character, Target_LLWEAPONEX_RemoteMine_Detonate)
	//OnItemStatus(__Me, LLWEAPONEX_REMOTEMINE_DETONATE)
	OnCharacterItemEvent(_Character, __Me, "LLWEAPONEX_RemoteMine_DetonationStatus")
ACTIONS
	IF "c1"
		IsEqual(%LLWEAPONEX_Mine_Owner, null)
	THEN
		Set(%LLWEAPONEX_Mine_Owner, _Character)
	ENDIF
	CallFunction("LLWEAPONEX_RemoteMine_Detonate")

EVENT LLWEAPONEX_RemoteMine_DetonateInWorld
VARS
	INT:_Level
	FLOAT:_LevelF
	SKILL:_Skill
	FLOAT3:_Pos
	ITEM:_OtherMine
ON
	OnFunction("LLWEAPONEX_RemoteMine_Detonate")
	OnItemEvent(__Me, "LLWEAPONEX_RemoteMine_Detonate")
ACTIONS
IF "c1"
	IsEqual(%LLWEAPONEX_Mine_UseStatus, 0)
THEN
	IF "c1|c2"
		IsEqual(%LLWEAPONEX_Mine_SkillID, null)
		IsEqual(%LLWEAPONEX_Mine_SkillID, "")
	THEN
		Set(_Level, -1)
		IF "!c1&c2"
			IsEqual(%LLWEAPONEX_Mine_Owner, null)
			CharacterGetStat(_LevelF, %LLWEAPONEX_Mine_Owner, Level)
		THEN
			Cast(_Level, _LevelF)
		ELSE
			Set(_Level, 1)
		ENDIF
		IF "!c1"
			IsTagged(%LLWEAPONEX_Mine_Owner, "LLWEAPONEX_AnyWeaponEquipped")
		THEN
			Set(_Skill, %LLWEAPONEX_Mine_Skill_NoWeapon)
		ELSE
			Set(_Skill, %LLWEAPONEX_Mine_Skill)
		ENDIF
		
		IF "c1&c2"
			ItemIsInCharacterInventory(__Me, %LLWEAPONEX_Mine_Owner)
			GetPosition(%LLWEAPONEX_Mine_Owner, _Pos)
		THEN
		ELIF "c1"
			GetPosition(__Me, _Pos)
		THEN
		ENDIF
		
		IF "c1"
			IsEqual(_Skill, Projectile_LLWEAPONEX_RemoteMine_Displacement)
		THEN
			IF "c1&!c2"
				ItemGet(_OtherMine, __Me, 12.0, Random, Distance, null, "LLWEAPONEX_RemoteMine_Displacer")
				IsEqual(_OtherMine, __Me)
			THEN
				CharacterItemEvent(%LLWEAPONEX_Mine_Owner, _OtherMine, "LLWEAPONEX_Demolition_DisplacementTargetFound")
			ELIF "c1&!c2"
				ItemGet(_OtherMine, __Me, 12.0, Highest, Distance, null, "LLWEAPONEX_RemoteMine_Displacer")
				IsEqual(_OtherMine, __Me)
			THEN
				CharacterItemEvent(%LLWEAPONEX_Mine_Owner, _OtherMine, "LLWEAPONEX_Demolition_DisplacementTargetFound")
			ELSE
				CharacterItemEvent(%LLWEAPONEX_Mine_Owner, __Me, "LLWEAPONEX_Demolition_DisplacementTargetFound")
			ENDIF
		ENDIF
		
		IF "!c1"
			IsEqual(%LLWEAPONEX_Mine_UseSelfAsSource, 1)
		THEN
			ExplodeAt(_Pos, _Skill, _Level, %LLWEAPONEX_Mine_Owner)		
		ELSE
			ExplodeAt(__Me, _Skill, _Level, __Me)
		ENDIF
		IF "c1"
			IsEqual(_Skill, Projectile_LLWEAPONEX_RemoteMine_Breach)
		THEN
			SetVar(__Me, "LLWEAPONEX_Demolition_BreachPosition", _Pos)
			CharacterItemEvent(%LLWEAPONEX_Mine_Owner, __Me, "LLWEAPONEX_Demolition_StartBreach")
		ENDIF
		ItemEvent(__Me, "LLWEAPONEX_RemoteMine_DetonationDone")
	ELSE
		CharacterItemEvent(%LLWEAPONEX_Mine_Owner, __Me, "LLWEAPONEX_RemoteMine_DetonateInWorld_BySkillID")
	ENDIF
ELSE
	IF "!c1"
		IsEqual(%LLWEAPONEX_Mine_ExplosionStatus, null)
	THEN
		IF "!c1"
			IsEqual(%LLWEAPONEX_Mine_UseSelfAsSource, 1)
		THEN
			ItemApplyStatus(__Me, %LLWEAPONEX_Mine_ExplosionStatus, 0, 1, %LLWEAPONEX_Mine_Owner)	
		ELSE
			ItemApplyStatus(__Me, %LLWEAPONEX_Mine_ExplosionStatus, 0, 1, __Me)	
		ENDIF
	ELSE
		CharacterItemEvent(%LLWEAPONEX_Mine_Owner, __Me, "LLWEAPONEX_RemoteMine_DetonateInWorld_ByStatusID")
	ENDIF
ENDIF

EVENT LLWEAPONEX_RemoteMine_DetonateInInventory_Character
VARS
	CHARACTER:_Target
	INT:_Level
	FLOAT:_LevelF
	SKILL:_Skill
	FLOAT3:_Pos
ON
	OnCharacterItemEvent(_Target, __Me, "LLWEAPONEX_RemoteMine_StartDetonation_Character")
ACTIONS
IF "c1"
	IsEqual(%LLWEAPONEX_Mine_UseStatus, 0)
THEN
	IF "c1|c2"
		IsEqual(%LLWEAPONEX_Mine_SkillID, null)
		IsEqual(%LLWEAPONEX_Mine_SkillID, "")
	THEN
		Set(_Level, -1)
		IF "!c1&c2"
			IsEqual(%LLWEAPONEX_Mine_Owner, null)
			CharacterGetStat(_LevelF, %LLWEAPONEX_Mine_Owner, Level)
		THEN
			Cast(_Level, _LevelF)
		ELSE
			Set(_Level, 1)
		ENDIF
		IF "!c1"
			IsTagged(%LLWEAPONEX_Mine_Owner, "LLWEAPONEX_AnyWeaponEquipped")
		THEN
			Set(_Skill, %LLWEAPONEX_Mine_Skill_NoWeapon)
		ELSE
			Set(_Skill, %LLWEAPONEX_Mine_Skill)
		ENDIF
		
		IF "c1"
			GetPosition(_Target, _Pos)
		THEN
		ELIF "c1"
			GetPosition(__Me, _Pos)
		THEN
		ENDIF
		
		IF "!c1"
			IsEqual(%LLWEAPONEX_Mine_UseSelfAsSource, 1)
		THEN
			ExplodeAt(_Pos, _Skill, _Level, %LLWEAPONEX_Mine_Owner)
		ELSE
			ExplodeAt(_Pos, _Skill, _Level, __Me)
		ENDIF
		ItemEvent(__Me, "LLWEAPONEX_RemoteMine_DetonationDone")
	ELSE
		CharacterItemEvent(_Target, __Me, "LLWEAPONEX_RemoteMine_DetonateInInventory_BySkillID")
	ENDIF
ELSE
	IF "!c1"
		IsEqual(%LLWEAPONEX_Mine_ExplosionStatus, null)
	THEN
		IF "!c1"
			IsEqual(%LLWEAPONEX_Mine_UseSelfAsSource, 1)
		THEN
			ItemApplyStatus(__Me, %LLWEAPONEX_Mine_ExplosionStatus, 0, 1, %LLWEAPONEX_Mine_Owner)
		ELSE
			ItemApplyStatus(__Me, %LLWEAPONEX_Mine_ExplosionStatus, 0, 1, __Me)
		ENDIF
		ItemEvent(__Me, "LLWEAPONEX_RemoteMine_DetonationDone")
	ELSE
		CharacterItemEvent(_Target, __Me, "LLWEAPONEX_RemoteMine_DetonateInInventory_ByStatusID")
	ENDIF
ENDIF

EVENT LLWEAPONEX_RemoteMine_DetonateInInventory_Container
VARS
	CHARACTER:_Character
	INT:_Level
	FLOAT:_LevelF
	SKILL:_Skill
	FLOAT3:_Pos
ON
	OnCharacterItemEvent(_Character, __Me, "LLWEAPONEX_RemoteMine_StartDetonation_Container")
ACTIONS
IF "c1"
	IsEqual(%LLWEAPONEX_Mine_UseStatus, 0)
THEN
	IF "c1|c2"
		IsEqual(%LLWEAPONEX_Mine_SkillID, null)
		IsEqual(%LLWEAPONEX_Mine_SkillID, "")
	THEN
		Set(_Level, -1)
		IF "!c1&c2"
			IsEqual(%LLWEAPONEX_Mine_Owner, null)
			CharacterGetStat(_LevelF, %LLWEAPONEX_Mine_Owner, Level)
		THEN
			Cast(_Level, _LevelF)
		ELSE
			Set(_Level, 1)
		ENDIF
		IF "!c1"
			IsTagged(_Character, "LLWEAPONEX_AnyWeaponEquipped")
		THEN
			Set(_Skill, %LLWEAPONEX_Mine_Skill_NoWeapon)
		ELSE
			Set(_Skill, %LLWEAPONEX_Mine_Skill)
		ENDIF
		
		IF "c1"
			GetPosition(%LLWEAPONEX_Mine_Inventory, _Pos)
		THEN
		ELIF "c1"
			GetPosition(__Me, _Pos)
		THEN
		ENDIF
		
		IF "!c1"
			IsEqual(%LLWEAPONEX_Mine_UseSelfAsSource, 1)
		THEN
			ExplodeAt(_Pos, _Skill, _Level, _Character)
		ELSE
			ExplodeAt(_Pos, _Skill, _Level, __Me)
		ENDIF
		ItemEvent(__Me, "LLWEAPONEX_RemoteMine_DetonationDone")
	ELSE
		CharacterItemEvent(_Character, __Me, "LLWEAPONEX_RemoteMine_DetonateInInventory_BySkillID_Container")
	ENDIF
ELSE
	IF "!c1"
		IsEqual(%LLWEAPONEX_Mine_ExplosionStatus, null)
	THEN
		IF "!c1"
			IsEqual(%LLWEAPONEX_Mine_UseSelfAsSource, 1)
		THEN
			ItemApplyStatus(%LLWEAPONEX_Mine_Inventory, %LLWEAPONEX_Mine_ExplosionStatus, 0, 1, _Character)
		ELSE
			ItemApplyStatus(%LLWEAPONEX_Mine_Inventory, %LLWEAPONEX_Mine_ExplosionStatus, 0, 1, __Me)
		ENDIF
		ItemEvent(__Me, "LLWEAPONEX_RemoteMine_DetonationDone")
	ELSE
		CharacterItemEvent(_Character, __Me, "LLWEAPONEX_RemoteMine_DetonateInInventory_ByStatusID_Container")
	ENDIF
ENDIF

EVENT LLWEAPONEX_RemoteMine_OnDetonated
ON
	OnItemEvent(__Me, "LLWEAPONEX_RemoteMine_DetonationDone")
ACTIONS
IF "!c1"
	ItemIsDestroyed(__Me)
THEN
	IF "c1"
		IsGreaterThen(%LLWEAPONEX_Mine_Amount, 1)
	THEN
		Subtract(%LLWEAPONEX_Mine_Amount, 1)
		ItemSetAmount(__Me, %LLWEAPONEX_Mine_Amount)
	ELSE
		SetFlag(__Me, "LLWEAPONEX_RemoteMine_Detonated")
		ItemDestroy(__Me)
	ENDIF
ENDIF

EVENT LLWEAPONEX_RemoteMine_Destroying
VARS
	FLOAT3:_Pos
ON
	OnItemDestroying(__Me)
ACTIONS
IF "!c1"
	ItemIsInInventory(__Me)
THEN
	IF "!c1"
		HasFlag(__Me, "LLWEAPONEX_RemoteMine_Detonated")
	THEN
		CallFunction("LLWEAPONEX_RemoteMine_Detonate")
	ELIF "c1"
		GetPosition(__Me, _Pos)
	THEN
		ExplodeAt(_Pos, Projectile_LLWEAPONEX_Status_RemoteMine_DetonateNear, 1, %LLWEAPONEX_Mine_Owner)
	ENDIF
ENDIF