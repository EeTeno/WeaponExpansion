INIT
	ITEM:__Me
	//External settings
	EXTERN SKILL:%LLWEAPONEX_Mine_Skill = Projectile_LLWEAPONEX_RemoteMine_Explosive
	EXTERN SKILL:%LLWEAPONEX_Mine_Skill_NoWeapon = Projectile_LLWEAPONEX_RemoteMine_Explosive_NoWeapon
	EXTERN STRING:%LLWEAPONEX_Mine_ActiveLoopEffect = null
	EXTERN INT:%LLWEAPONEX_Mine_UseSelfAsSource = 0
	
	//Dynamic/Important Vars
	CHARACTER:%LLWEAPONEX_Mine_Owner = null
	ITEM:%LLWEAPONEX_Mine_Inventory = null
	INT:%LLWEAPONEX_Mine_Amount = 1
	
	//State Vars
	INT:%LLWEAPONEX_Mine_Exploding = 0
	
	//Temp Vars
	INT64:%LLWEAPONEX_Mine_LoopFXHandle = null
	FLOAT3:%LLWEAPONEX_Mine_ExplodePos = null
EVENTS

EVENT LLWEAPONEX_RemoteMine_Detonate_FromSkill
VARS
	CHARACTER:_Character
ON
	//OnCharacterUsedSkillOnMe(_Character, Target_LLWEAPONEX_RemoteMine_Detonate)
	//OnItemStatus(__Me, LLWEAPONEX_REMOTEMINE_DETONATE)
	OnCharacterItemEvent(_Character, __Me, "LLWEAPONEX_RemoteMine_DetonationStatus")
ACTIONS
	IF "c1"
		IsEqual(%LLWEAPONEX_Mine_Owner, null)
	THEN
		Set(%LLWEAPONEX_Mine_Owner, _Character)
	ENDIF
	CallFunction("LLWEAPONEX_RemoteMine_Detonate")
/*
IF "c1"
	ItemGet(_Target, __Me, 12.0, Random, Distance, "WPN_LLWEAPONEX_RemoteMine_A_Displacement_0dc36b1c-278d-4c88-9b24-8d4d5dd685fc", "LLWEAPONEX_RemoteMine_Ready")
THEN
	CharacterItemEvent(%LLWEAPONEX_Mine_Owner, _Target, "LLWEAPONEX_Demolition_DisplacementTargetFound")
ELSE
	CharacterItemEvent(%LLWEAPONEX_Mine_Owner, __Me, "LLWEAPONEX_Demolition_DisplacementTargetFound")
ENDIF
*/

EVENT LLWEAPONEX_RemoteMine_Detonate_Self
VARS
	FLOAT3:_Pos
ON
	OnItemEvent(__Me, "LLWEAPONEX_RemoteMine_Detonate")
ACTIONS
IF "c1"
	GetPosition(__Me, _Pos)
THEN
	Set(%LLWEAPONEX_Mine_ExplodePos, _Pos)
ENDIF
	CallFunction("LLWEAPONEX_RemoteMine_Detonate")

/*	
EVENT LLWEAPONEX_RemoteMine_Detonate_Container
VARS
	FLOAT3:_Pos
	CHARACTER:_Source
ON
	OnCharacterItemEvent(_Source, __Me, "LLWEAPONEX_RemoteMine_StartDetonation_Container")
ACTIONS
IF "!c1&c2"
	IsEqual(%LLWEAPONEX_Mine_Inventory, null)
	GetPosition(%LLWEAPONEX_Mine_Inventory, _Pos)
THEN
	Set(%LLWEAPONEX_Mine_ExplodePos, _Pos)
ELIF "c1"
	GetPosition(__Me, _Pos)
THEN
	Set(%LLWEAPONEX_Mine_ExplodePos, _Pos)
ENDIF
	StatusText(%LLWEAPONEX_Mine_Inventory, "Detonating, baby2!")
	//CallFunction("LLWEAPONEX_RemoteMine_Detonate")
	CharacterItemEvent(_Target, __Me, "LLWEAPONEX_RemoteMine_Detonate")
*/

EVENT LLWEAPONEX_RemoteMine_Detonate
VARS
	INT:_Level
	FLOAT:_LevelF
	SKILL:_Skill
	CHARACTER:_Target
ON
	OnFunction("LLWEAPONEX_RemoteMine_Detonate")
	OnCharacterItemEvent(_Target, __Me, "LLWEAPONEX_RemoteMine_Detonate")
ACTIONS
	Set(%LLWEAPONEX_Mine_Exploding, 1)
	Set(_Level, 1)
	IF "!c1&c2"
		IsEqual(%LLWEAPONEX_Mine_Owner, null)
		CharacterGetStat(_LevelF, %LLWEAPONEX_Mine_Owner, Level)
	THEN
		Cast(_Level, _LevelF)
		//StatusText(%LLWEAPONEX_Mine_Owner, "We detonatin'!")
	ENDIF
	IF "!c1"
		IsTagged(%LLWEAPONEX_Mine_Owner, "LLWEAPONEX_AnyWeaponEquipped")
	THEN
		Set(_Skill, %LLWEAPONEX_Mine_Skill_NoWeapon)
	ELSE
		Set(_Skill, %LLWEAPONEX_Mine_Skill)
	ENDIF
	
	IF "c1&c2"
		IsEqual(%LLWEAPONEX_Mine_ExplodePos, null)
		GetPosition(__Me, %LLWEAPONEX_Mine_ExplodePos)
	THEN
		
	ENDIF
	
	IF "c1"
		IsEqual(_Skill, Projectile_LLWEAPONEX_RemoteMine_Displacement)
	THEN
		CallFunction("LLWEAPONEX_RemoteMine_PrepareDisplacement")
	ENDIF
	
	IF "!c1"
		IsEqual(%LLWEAPONEX_Mine_UseSelfAsSource, 1)
	THEN
		ExplodeAt(%LLWEAPONEX_Mine_ExplodePos, _Skill, _Level, %LLWEAPONEX_Mine_Owner)		
	ELSE
		ExplodeAt(%LLWEAPONEX_Mine_ExplodePos, _Skill, _Level, __Me)
	ENDIF
	IF "c1"
		IsEqual(_Skill, Projectile_LLWEAPONEX_RemoteMine_Breach)
	THEN
		SetVar(__Me, "LLWEAPONEX_Demolition_BreachPosition", %LLWEAPONEX_Mine_ExplodePos)
		CharacterItemEvent(%LLWEAPONEX_Mine_Owner, __Me, "LLWEAPONEX_Demolition_StartBreach")
	ENDIF
	ItemEvent(__Me, "LLWEAPONEX_RemoteMine_DetonationDone")

EVENT LLWEAPONEX_RemoteMine_Destroying
VARS
	FLOAT3:_Pos
ON
	OnItemDestroying(__Me)
ACTIONS
IF "!c1"
	ItemIsInInventory(__Me)
THEN
	IF "!c1"
		HasFlag(__Me, "LLWEAPONEX_RemoteMine_Detonated")
	THEN
		CallFunction("LLWEAPONEX_RemoteMine_Detonate")
	ELIF "c1"
		GetPosition(__Me, _Pos)
	THEN
		//ExplodeAt(_Pos, Projectile_LLWEAPONEX_Status_RemoteMine_DetonateNear, 1, %LLWEAPONEX_Mine_Owner)
	ENDIF
ENDIF

EVENT LLWEAPONEX_RemoteMine_OnPickup
ON
	//OnPickupItem(_, __Me)
	OnCharacterItemEvent(_, __Me, "LLWEAPONEX_RemoteMine_OnPrePickedUp")
ACTIONS
	//SetPriority("LLWEAPONEX_RemoteMine_InWorld", 0)
	DelayReaction("LLWEAPONEX_RemoteMine_InWorld", 5.0)
	ClearTag(__Me, "LLWEAPONEX_RemoteMine_Ready")
IF "!c1"
	IsEqual(%LLWEAPONEX_Mine_LoopFXHandle, null)
THEN
	StopLoopEffect(%LLWEAPONEX_Mine_LoopFXHandle)
	Set(%LLWEAPONEX_Mine_LoopFXHandle, INT64:null)
ENDIF

/*
EVENT LLWEAPONEX_RemoteMine_OnDropped
ON
	OnItemEvent(__Me, "LLWEAPONEX_RemoteMine_Thrown")
ACTIONS
	SetPriority("LLWEAPONEX_RemoteMine_InWorld", 999)
*/

BEHAVIOUR

REACTION LLWEAPONEX_RemoteMine_InWorld,999
USAGE ALL
CHECK "c1&!c2&!c3&!c4"
	IsActive(__Me)
	ItemIsInInventory(__Me)
	IsTagged(__Me, "LLWEAPONEX_RemoteMine_Ready")
	IsEqual(%LLWEAPONEX_Mine_Exploding, 1)
ACTIONS
	SetTag(__Me, "LLWEAPONEX_RemoteMine_Ready")
IF "!c1&!c2&c3"
	IsEqual(%LLWEAPONEX_Mine_ActiveLoopEffect, null)
	IsEqual(%LLWEAPONEX_Mine_ActiveLoopEffect, "")
	IsEqual(%LLWEAPONEX_Mine_LoopFXHandle, null)
THEN
	ItemPlayLoopEffect(%LLWEAPONEX_Mine_LoopFXHandle, __Me, %LLWEAPONEX_Mine_ActiveLoopEffect)
ENDIF