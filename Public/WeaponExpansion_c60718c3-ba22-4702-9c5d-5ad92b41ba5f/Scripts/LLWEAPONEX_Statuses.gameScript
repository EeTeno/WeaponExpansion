INIT

EVENTS

EVENT LLWEAPONEX_Rapier_SuckerPunch_Knockdown
VARS
	CHARACTER:_Character
	INT:_Turns
	CHARACTER:_Source
ON
	OnCharacterStatusApplied(_Character, LLWEAPONEX_SUCKER_PUNCH)
ACTIONS
IF "c1&c2"
	CharacterGetStatusSourceCharacter(_Character, LLWEAPONEX_SUCKER_PUNCH, _Source)
	GetStatusTurns(_Character, LLWEAPONEX_SUCKER_PUNCH, _Turns)
THEN
	IF "c1"
		IsTagged(_Source, "LLWEAPONEX_Rapier_SuckerCombo1Enabled")
	THEN
		Add(_Turns, 1)
	ENDIF
	CharacterApplyStatus(_Character, KNOCKED_DOWN, _Turns, 0, _Source)
	CharacterRemoveStatus(_Character, LLWEAPONEX_SUCKER_PUNCH, null, 0)
ENDIF

EVENT LLWEAPONEX_Rapier_SuckerCombo1
VARS
	CHARACTER:_Character
	STATUS:_Result
	LIST<STATUS>:_RemoveList
	INT:_Turns
	CHARACTER:_Source
ON
	FetchCharacterApplyStatusData(_Character, LLWEAPONEX_RAPIER_MASTERY_SUCKERCOMBO1)
ACTIONS
	Set(_Result, null)
IF "c1&c2"
	GetStatusTurns(_Character, KNOCKED_DOWN, _Turns)
	CharacterGetStatusSourceCharacter(_Character, KNOCKED_DOWN, _Source)
THEN
	Clamp(_Turns, 1, _Turns)
	Add(_Turns, 1)
	Set(_Result, LLWEAPONEX_RAPIER_MASTERY_DELAYED_DAZED)
ENDIF
	RETURN(_RemoveList, _Result, _Turns)


EVENT LLWEAPONEX_Rapier_DelayedDaze_ApplyDazed
VARS
	CHARACTER:_Character
	CHARACTER:_Source
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Events_DelayDazedReady")
ACTIONS
IF "c1"
	CharacterGetStatusSourceCharacter(_Character, LLWEAPONEX_RAPIER_MASTERY_DELAYED_DAZED, _Source)
THEN
ELSE
	Set(_Source, _Character)
ENDIF
	CharacterRemoveStatus(_Character, LLWEAPONEX_RAPIER_MASTERY_DELAYED_DAZED, null, 0)
	CharacterApplyStatus(_Character, LLWEAPONEX_DIZZY, 2, 1, _Source)

EVENT LLWEAPONEX_FumbleRoll
VARS
	CHARACTER:_Attacker
	CHARACTER:_Source
	INT:_Ran
ON
	OnCharacterStartAttackObject(_, _, _, _Attacker)
ACTIONS
IF "c1&c2"
	CharacterHasStatus(_Attacker, LLWEAPONEX_CONCUSSION)
	CharacterGetStatusSourceCharacter(_Attacker, LLWEAPONEX_CONCUSSION, _Source)
THEN
	GetRandomBetween(_Ran, 0, 100)
	IF "c1"
		IsLessThen(_Ran, 10)
	THEN
		CharacterApplyStatus(_Attacker, LLWEAPONEX_FUMBLE, 0, 1, _Source)
	ENDIF
ENDIF

EVENT LLWEAPONEX_TrippingKnockdown
VARS
	CHARACTER:_Target
	CHARACTER:_Source
ON
	OnCharacterStatusAttempt(_Target, LLWEAPONEX_TRIPPING)
ACTIONS
IF "c1&c2"
	CharacterHasStatus(_Target, LLWEAPONEX_TRIPPING)
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_TRIPPING, _Source)
THEN
	CharacterApplyStatus(_Target, KNOCKED_DOWN, 1, 0, _Source)
	CharacterRemoveStatus(_Target, LLWEAPONEX_TRIPPING)
ENDIF

EVENT LLWEAPONEX_Tripping_Cleanse
VARS
	CHARACTER:_Target
	CHARACTER:_Source
ON
	OnCharacterStatusApplied(_Target, KNOCKED_DOWN)
	OnCharacterStatusApplied(_Target, FIRST_AID)
	OnCharacterStatusApplied(_Target, HEAL_SHARING)
	OnCharacterStatusApplied(_Target, FORTIFIED)
ACTIONS
IF "c1"
	CharacterHasStatus(_Target, LLWEAPONEX_TRIPPING)
THEN
	CharacterRemoveStatus(_Target, LLWEAPONEX_TRIPPING)
ENDIF

EVENT LLWEAPONEX_DragonsBane_GuaranteedKnockdown
VARS
	CHARACTER:_Target
	CHARACTER:_Source
ON
	OnCharacterStatusApplied(_Target, LLWEAPONEX_DRAGONS_BANE)
ACTIONS
IF "!c1&c2"
	CharacterHasStatus(_Target, KNOCKED_DOWN)
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_DRAGONS_BANE, _Source)
THEN
	CharacterApplyStatus(_Target, KNOCKED_DOWN, 1, 1, _Source)
ENDIF

//If LLWEAPONEX_MAGIC_KNOCKDOWN_CHECK is applied, that means they have 0 magic armor and aren't immune to knockdown
EVENT LLWEAPONEX_MagicKnockdownCheck
VARS
	CHARACTER:_Target
	CHARACTER:_Source
	INT:_Turns
ON
	OnCharacterStatusApplied(_Target, LLWEAPONEX_MAGIC_KNOCKDOWN_CHECK)
ACTIONS
IF "(c1&c2)|!c1"
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_MAGIC_KNOCKDOWN_CHECK, _Source)
	IsEqual(_Source, null)
THEN
	Set(_Source, _Target)
ENDIF
IF "!c1|c2"
	GetStatusTurns(_Target, LLWEAPONEX_MAGIC_KNOCKDOWN_CHECK, _Turns)
	IsEqual(_Turns, null)
THEN
	Set(_Turns, 1)
ELSE
	Clamp(_Turns, 1, _Turns)
ENDIF
IF "!c1&!c2"
	CharacterIsDead(_Target)
	CharacterHasStatus(_Target, KNOCKED_DOWN) // Don't force if they're already knocked down
THEN
	CharacterApplyStatus(_Target, KNOCKED_DOWN, _Turns, 1, _Source)
ENDIF
	CharacterRemoveStatus(_Target, LLWEAPONEX_MAGIC_KNOCKDOWN_CHECK)
	
EVENT LLWEAPONEX_MagicBleedingCheck
VARS
	CHARACTER:_Target
	CHARACTER:_Source
	INT:_Turns
ON
	OnCharacterStatusApplied(_Target, LLWEAPONEX_MAGIC_BLEEDING_CHECK)
ACTIONS
IF "(c1&c2)|!c1"
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_MAGIC_BLEEDING_CHECK, _Source)
	IsEqual(_Source, null)
THEN
	Set(_Source, _Target)
ENDIF
IF "!c1|c2"
	GetStatusTurns(_Target, LLWEAPONEX_MAGIC_BLEEDING_CHECK, _Turns)
	IsEqual(_Turns, null)
THEN
	Set(_Turns, 1)
ELSE
	Clamp(_Turns, 1, _Turns)
ENDIF
IF "!c1&!c2"
	CharacterIsDead(_Target)
	CharacterHasStatus(_Target, BLEEDING) // Don't force if they're already bleeding
THEN
	CharacterApplyStatus(_Target, BLEEDING, _Turns, 1, _Source)
ENDIF
	CharacterRemoveStatus(_Target, LLWEAPONEX_MAGIC_BLEEDING_CHECK)
	
EVENT LLWEAPONEX_MagicCrippledCheck
VARS
	CHARACTER:_Target
	CHARACTER:_Source
	INT:_Turns
ON
	OnCharacterStatusApplied(_Target, LLWEAPONEX_MAGIC_CRIPPLED_CHECK)
ACTIONS
IF "(c1&c2)|!c1"
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_MAGIC_CRIPPLED_CHECK, _Source)
	IsEqual(_Source, null)
THEN
	Set(_Source, _Target)
ENDIF
IF "!c1|c2"
	GetStatusTurns(_Target, LLWEAPONEX_MAGIC_CRIPPLED_CHECK, _Turns)
	IsEqual(_Turns, null)
THEN
	Set(_Turns, 1)
ELSE
	Clamp(_Turns, 1, _Turns)
ENDIF
IF "!c1&!c2"
	CharacterIsDead(_Target)
	CharacterHasStatus(_Target, CRIPPLED)
THEN
	CharacterApplyStatus(_Target, CRIPPLED, _Turns, 1, _Source)
ENDIF
	CharacterRemoveStatus(_Target, LLWEAPONEX_MAGIC_CRIPPLED_CHECK)

/*
EVENT LLWEAPONEX_Tar_BurningBypass
VARS
	CHARACTER:_Character
	CHARACTER:_Source
	INT:_BurningTurns
ON
	OnCharacterCharacterEvent(_Character, "LLWEAPONEX_Burning_ArmorBypass")
ACTIONS
IF "c1&c2"
	GetStatusTurns(_Character, LLWEAPONEX_TARRED, _BurningTurns)
THEN
	IF "c1"
		CharacterGetStatusSourceCharacter(_Character, BURNING, _Source)
	THEN
	
	ENDIF
	IF "c1"
		IsLessThen(_BurningTurns, 1)
	THEN
		Set(_BurningTurns, 1)
	ENDIF
	CharacterApplyStatus(_Character, BURNING, _BurningTurns, 1, _Source)
ENDIF
*/

EVENT LLWEAPONEX_DeathSentence_Block
VARS
	CHARACTER:_Character
ON
	OnCharacterStatusRemoved(_Character, LLWEAPONEX_DEATH_SENTENCE)
ACTIONS
IF "!c1"
	CharacterIsDead(_Character, 1)
THEN
	CharacterApplyStatus(_Character, LLWEAPONEX_DEATH_SENTENCE_BLOCKED, 4, 0)
ENDIF

EVENT LLWEAPONEX_Shocked_Block
VARS
	CHARACTER:_Character
	CHARACTER:_Source
	INT:_Turns
ON
	OnCharacterStatusApplied(_Character, LLWEAPONEX_RUNEBLADE_SHOCKED)
ACTIONS
IF "!c1"
	CharacterHasStatus(_Character, LLWEAPONEX_SHOCKED_RESISTANCE_RUNEBLADE)
THEN
	IF "(c1&c2)|!c1"
		CharacterGetStatusSourceCharacter(_Character, LLWEAPONEX_RUNEBLADE_SHOCKED, _Source)
		IsEqual(_Source, null)
	THEN
		Set(_Source, _Character)
	ENDIF
	IF "!c1"
		GetStatusTurns(_Character, LLWEAPONEX_RUNEBLADE_SHOCKED, _Turns)
	THEN
		Set(_Turns, 1)
	ELSE
		Clamp(_Turns, 1, _Turns)
	ENDIF
	CharacterApplyStatus(_Character, SHOCKED, _Turns, 0, _Source)
ENDIF
	CharacterRemoveStatus(_Character, LLWEAPONEX_RUNEBLADE_SHOCKED, null, 0)
/*
EVENT LLWEAPONEX_Kevin_MiniExplosion
VARS
	CHARACTER:_Target
	CHARACTER:_Player
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterStatusApplied(_Target, LLWEAPONEX_KEVIN_MINIEXPLOSION)
ACTIONS
IF "c1&c2"
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_KEVIN_MINIEXPLOSION, _Player)
	CharacterGetStat(_LevelF, _Player, Level)
THEN
	Cast(_Level, _LevelF)
	ExplodeAt(_Target, Projectile_LLWEAPONEX_Kevin_MiniExplosion, _Level, _Player)
ENDIF
*/

EVENT LLWEAPONEX_ShieldToss_Bonus
VARS
	CHARACTER:_Character
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_ShieldToss_ApplyAuraBonus")
ACTIONS
	ExplodeAt(_Character, Projectile_LLWEAPONEX_ApplyShieldTossBonus, -1, _Character)

EVENT LLWEAPONEX_DeflectProjectiles_CountdownStart
VARS
	CHARACTER:_Character
ON
	OnEnteredCombat(_Character, _)
ACTIONS
IF "c1&!c2"
	CharacterHasStatus(_Character, LLWEAPONEX_DEFLECT_PROJECTILES_INNATE)
	CharacterHasStatus(_Character, LLWEAPONEX_DEFLECT_PROJECTILES_COUNTDOWN)
THEN
	CharacterApplyStatus(_Character, LLWEAPONEX_DEFLECT_PROJECTILES_COUNTDOWN, 4, 0, _Character)
ENDIF

EVENT LLWEAPONEX_DeflectProjectiles_CountdownCancel_LeftCombat
VARS
	CHARACTER:_Character
ON
	OnLeftCombat(_Character, _)
ACTIONS
IF "c1"
	CharacterHasStatus(_Character, LLWEAPONEX_DEFLECT_PROJECTILES_COUNTDOWN)
THEN
	CharacterRemoveStatus(_Character, LLWEAPONEX_DEFLECT_PROJECTILES_COUNTDOWN)
ENDIF

EVENT LLWEAPONEX_DeflectProjectiles_CountdownCancel_NotInCombat
VARS
	CHARACTER:_Character
	STATUS:_Result
	LIST<STATUS>:_RemoveList
ON
	FetchCharacterApplyStatusData(_Character, LLWEAPONEX_DEFLECT_PROJECTILES_COUNTDOWN)
ACTIONS
	Set(_Result, LLWEAPONEX_DEFLECT_PROJECTILES_COUNTDOWN)
IF "!c1|!c2"
	IsInCombat(_Character)
	CharacterHasStatus(_Character, LLWEAPONEX_DEFLECT_PROJECTILES_INNATE)
THEN
	Set(_Result, null)
	ListAdd(_RemoveList, LLWEAPONEX_DEFLECT_PROJECTILES)
	CombatLogText(_Character, "LLWEAPONEX_CombatLog_DeflectProjectilesCountdownFailed", 1, 1)
ENDIF
	RETURN(_RemoveList, _Result, null)
	
EVENT LLWEAPONEX_PoisonBurst_ApplyExplosion
VARS
	CHARACTER:_Target
	CHARACTER:_Source
	INT:_Turns
ON
	OnCharacterStatusApplied(_Target, LLWEAPONEX_POISONBURST_CHECK)
ACTIONS
IF "c1&c2"
	GetStatusTurns(_Target, POISONED, _Turns)
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_POISONBURST_CHECK, _Source)
THEN
	IF "c1|c2"
		IsGreaterThen(_Turns, 6)
		IsEqual(_Turns, 6)
	THEN
		CharacterApplyStatus(_Target, LLWEAPONEX_POISONBURST_EXPLODE_6, 0, 1, _Source)
	ELIF "c1"
		IsEqual(_Turns, 5)
	THEN
		CharacterApplyStatus(_Target, LLWEAPONEX_POISONBURST_EXPLODE_5, 0, 1, _Source)
	ELIF "c1"
		IsEqual(_Turns, 4)
	THEN
		CharacterApplyStatus(_Target, LLWEAPONEX_POISONBURST_EXPLODE_4, 0, 1, _Source)
	ELIF "c1"
		IsEqual(_Turns, 3)
	THEN
		CharacterApplyStatus(_Target, LLWEAPONEX_POISONBURST_EXPLODE_3, 0, 1, _Source)
	ELIF "c1"
		IsEqual(_Turns, 2)
	THEN
		CharacterApplyStatus(_Target, LLWEAPONEX_POISONBURST_EXPLODE_2, 0, 1, _Source)
	ELSE
		CharacterApplyStatus(_Target, LLWEAPONEX_POISONBURST_EXPLODE_1, 0, 1, _Source)
	ENDIF
ENDIF


EVENT LLWEAPONEX_SoulBurn_Proc
VARS
	CHARACTER:_Target
	LIST<STATUS>:_RemoveList
	STATUS:_Result
	INT:_Turns
	INT:_TurnsLeft
ON
	FetchCharacterApplyStatusData(_Target, LLWEAPONEX_SOUL_BURN_PROC)
ACTIONS
	Set(_Result, null)
	Set(_Turns, 1)
	CharacterEvent(_Target, "LLWEAPONEX_Commands_StopSoulBurnTick")
IF "c1"
	GetStatusTurns(_Target, LLWEAPONEX_SOUL_BURN1, _TurnsLeft)
THEN
	Add(_Turns, _TurnsLeft)
	Set(_Result, LLWEAPONEX_SOUL_BURN2)
	ListAdd(_RemoveList, LLWEAPONEX_SOUL_BURN1)
ELIF "c1"
	GetStatusTurns(_Target, LLWEAPONEX_SOUL_BURN2, _TurnsLeft)
THEN
	Add(_Turns, _TurnsLeft)
	Set(_Result, LLWEAPONEX_SOUL_BURN3)
	ListAdd(_RemoveList, LLWEAPONEX_SOUL_BURN2)
ELIF "c1"
	GetStatusTurns(_Target, LLWEAPONEX_SOUL_BURN3, _TurnsLeft)
THEN
	Add(_Turns, _TurnsLeft)
	Set(_Result, LLWEAPONEX_SOUL_BURN4)
	ListAdd(_RemoveList, LLWEAPONEX_SOUL_BURN3)
ELIF "c1"
	GetStatusTurns(_Target, LLWEAPONEX_SOUL_BURN4, _TurnsLeft)
THEN
	IF "c1"
		IsGreaterThen(_TurnsLeft, 0)
	THEN
		Add(_Turns, _TurnsLeft)
		Clamp(_Turns, 1, 5)
	ELSE
		Set(_Turns, 1)
	ENDIF
	
	Set(_Result, LLWEAPONEX_SOUL_BURN5)
	ListAdd(_RemoveList, LLWEAPONEX_SOUL_BURN4)
ELIF "!c1"
	CharacterHasStatus(_Target, LLWEAPONEX_SOUL_BURN5)
THEN
	Set(_Result, LLWEAPONEX_SOUL_BURN1)
	Set(_Turns, 1)
ENDIF
IF "!c1"
	IsEqual(_Result, null)
THEN
	CharacterEvent(_Target, "LLWEAPONEX_Commands_StartSoulBurnTick")
ENDIF

RETURN(_RemoveList, _Result, _Turns)
	
//Regress the status
EVENT LLWEAPONEX_SoulBurn_Tick
VARS
	CHARACTER:_Target
	CHARACTER:_Source
	STATUS:_Result
	INT:_Turns
	INT:_TurnsLeft
ON
	OnCharacterStatusRemoved(_Target, LLWEAPONEX_SOUL_BURN_TICK)
ACTIONS
IF "!c1"
	HasFlag(_Target, "LLWEAPONEX_SkipSoulBurnTick")
THEN
	Set(_Result, null)
	Set(_Turns, null)
	IF "c1&c2&c3"
		CharacterHasStatus(_Target, LLWEAPONEX_SOUL_BURN2)
		GetStatusTurns(_Target, LLWEAPONEX_SOUL_BURN2, _TurnsLeft)
		CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_SOUL_BURN2, _Source)
	THEN
		Set(_Turns, _TurnsLeft)
		Set(_Result, LLWEAPONEX_SOUL_BURN1)
	ELIF "c1&c2&c3"
		CharacterHasStatus(_Target, LLWEAPONEX_SOUL_BURN3)
		GetStatusTurns(_Target, LLWEAPONEX_SOUL_BURN3, _TurnsLeft)
		CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_SOUL_BURN3, _Source)
	THEN
		Set(_Turns, _TurnsLeft)
		Set(_Result, LLWEAPONEX_SOUL_BURN2)
	ELIF "c1&c2&c3"
		CharacterHasStatus(_Target, LLWEAPONEX_SOUL_BURN4)
		GetStatusTurns(_Target, LLWEAPONEX_SOUL_BURN4, _TurnsLeft)
		CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_SOUL_BURN4, _Source)
	THEN
		Set(_Turns, _TurnsLeft)
		Set(_Result, LLWEAPONEX_SOUL_BURN3)
	ELIF "c1&c2&c3"
		CharacterHasStatus(_Target, LLWEAPONEX_SOUL_BURN5)
		GetStatusTurns(_Target, LLWEAPONEX_SOUL_BURN5, _TurnsLeft)
		CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_SOUL_BURN5, _Source)
	THEN
		Set(_Turns, _TurnsLeft)
		Set(_Result, LLWEAPONEX_SOUL_BURN4)
	ENDIF
	IF "!c1&!c2&!c3"
		IsEqual(_Result, null)
		IsEqual(_Turns, null)
		IsEqual(_Source, null)
	THEN
		CharacterApplyStatus(_Target, _Result, _Turns, 1, _Source)
		CharacterEvent(_Target, "LLWEAPONEX_Commands_StartSoulBurnTick")
	ENDIF
ELSE
	//StatusText(_Target, "LLWEAPONEX_SkipSoulBurnTick = true")
ENDIF

EVENT LLWEAPONEX_SoulBurn_Reset
VARS
	CHARACTER:_Character
ON
	OnCharacterStatusRemoved(_Character, LLWEAPONEX_SOUL_BURN1)
	OnDie(_Character, _, _, _)
ACTIONS
IF "c1"
	HasFlag(_Character, "LLWEAPONEX_SkipSoulBurnTick")
THEN
	ClearFlag(_Character, "LLWEAPONEX_SkipSoulBurnTick")
ENDIF