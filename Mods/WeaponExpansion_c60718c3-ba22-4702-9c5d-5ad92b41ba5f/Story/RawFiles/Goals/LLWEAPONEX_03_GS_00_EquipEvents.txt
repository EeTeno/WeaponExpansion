Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION MAIN_EVENTS
IF
ItemUnEquipped(_Item, _Char)
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
THEN
LLWEAPONEX_Ext_OnItemUnEquipped(_Char, _Item);

IF
ItemEquipped(_Item, _Char)
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
THEN
LLWEAPONEX_Ext_OnItemEquipped(_Char, _Item);
//END_REGION

//REGION WEAPON_EVENTS
PROC
LLWEAPONEX_OnWeaponEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_OnWeaponUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
NOT LeaderLib_Helper_QRY_WeaponIsEquipped(_Char)
THEN
LeaderLib_Tags_ClearPreservedTag(_Char, "LLWEAPONEX_AnyWeaponEquipped");

PROC
LLWEAPONEX_OnWeaponTypeEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_WeaponType, (INTEGER)_IsPlayer)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_OnWeaponTypeUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_WeaponType, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_TagHelper_EquippedTags(_WeaponType, _Tag)
THEN
LeaderLib_Tags_ClearPreservedTag(_Char, _Tag);
//END_REGION

//REGION PRESET_APPLIED
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Char, _Slot,  (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LLWEAPONEX_OnItemEquipped(_Char, _Item, _Template);
//END_REGION

//REGION ANIMATION_OVERRIDE
PROC
LLWEAPONEX_OnWeaponTypeUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_WeaponType, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_Temp_AnimationSetOverride(_Char, "LLWEAPONEX_Override1", _WeaponType)
THEN
LLWEAPONEX_AnimationSetOverride_Clear(_Char);
//END_REGION

EXITSECTION
ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_03_GameScripts"