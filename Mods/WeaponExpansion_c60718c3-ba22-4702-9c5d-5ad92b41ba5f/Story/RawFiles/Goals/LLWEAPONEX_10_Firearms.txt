Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Firearms_InitSettings();

KBSECTION
//REGION SETTINGS
PROC
LLWEAPONEX_Firearms_InitSettings()
THEN
SysClear("DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage", 3);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_01", 0.0, 2.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_02", 2.0, 3.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_03", 3.0, 4.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_04", 4.0, 5.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_05", 5.0, 6.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_06", 6.0, 7.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_07", 7.0, 8.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_08", 8.0, 9.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_09", 9.0, 10.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_10", 10.0, 11.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_11", 11.0, 12.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_12", 12.0, 13.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_13", 13.0, 14.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_14", 14.0, 15.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_15", 15.0, 16.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_16", 16.0, 17.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_17", 17.0, 18.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_18", 18.0, 19.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_19", 19.0, 20.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_20", 20.0, 999.0);
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 0,9,18,1)
THEN
LLWEAPONEX_Firearms_InitSettings();
//END_REGION

//REGION EQUIP_EVENTS
PROC
LLWEAPONEX_OnItemUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_Firearm_Flamethrower_2H_A_6b7e1caa-a610-4e40-91dc-7f2df1b86d42")
THEN
LeaderLib_ToggleScripts_DisableScriptForObjectAfterDelay(_Char, "LLWEAPONEX_FlamethrowerEquipped", "WeaponExpansion", 500);
NOT DB_LLWEAPONEX_Flamethrower_Equipped(_Char, _Item);

PROC
LLWEAPONEX_OnItemEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_Firearm_Flamethrower_2H_A_6b7e1caa-a610-4e40-91dc-7f2df1b86d42")
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLWEAPONEX_FlamethrowerEquipped", "WeaponExpansion");
DB_LLWEAPONEX_Flamethrower_Equipped(_Char, _Item);
//END_REGION

//REGION AIMED_SHOT_DISTANCE_DAMAGE
IF
CharacterStatusApplied(_Target, "LLWEAPONEX_FIREARM_AIMEDSHOT_HIT", (CHARACTERGUID)_Char)
AND
GetPosition(_Target, _x, _y, _z)
THEN
LLWEAPONEX_Firearms_AimedShot_ApplyDistanceDamage(_Char, (GUIDSTRING)_Target);

IF
ItemStatusChange(_Target, "LLWEAPONEX_FIREARM_AIMEDSHOT_HIT", (CHARACTERGUID)_Char)
THEN
LLWEAPONEX_Firearms_AimedShot_ApplyDistanceDamage(_Char, (GUIDSTRING)_Target);

PROC
LLWEAPONEX_Firearms_AimedShot_ApplyDistanceDamage((CHARACTERGUID)_Char, (GUIDSTRING)_Target)
AND
GetDistanceTo(_Char, _Target, _Dist)
AND
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage(_Status, _Min, _Max)
AND
_Dist >= _Min
AND
_Dist < _Max
THEN
ApplyStatus(_Target, _Status, 0.0, 1, _Char);
//END_REGION

//REGION AIMED_SHOT_BONUSES
IF
CharacterUsedSkillOnTarget(_Char, _Target, "Projectile_LLWEAPONEX_Rifle_AimedShot", _, _)
AND
HasActiveStatus(_Target, "MARKED", 1)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_CRITICAL", 12.0, 1, _Char);

IF
CharacterUsedSkill(_Char, "Projectile_LLWEAPONEX_Rifle_AimedShot", _, _)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_ACCURACY", 12.0, 1, _Char);

IF
CharacterUsedSkill(_Char, "Projectile_LLWEAPONEX_Rifle_AimedShot_Enemy", _, _)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_ACCURACY", 12.0, 1, _Char);

IF
SkillCast(_Char, "Projectile_LLWEAPONEX_Rifle_AimedShot", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 1500, "Timers_LLWEAPONEX_Rifle_AimedShot_ClearBonuses", "LLWEAPONEX_Rifle_AimedShot_ClearBonuses");

IF
SkillCast(_Char, "Projectile_LLWEAPONEX_Rifle_AimedShot_Enemy", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 1500, "Timers_LLWEAPONEX_Rifle_AimedShot_ClearBonuses", "LLWEAPONEX_Rifle_AimedShot_ClearBonuses");

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_HIT_END", _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 50, "Timers_LLWEAPONEX_Rifle_AimedShot_ClearBonuses", "LLWEAPONEX_Rifle_AimedShot_ClearBonuses");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Rifle_AimedShot_ClearBonuses")
THEN
RemoveStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_CRITICAL");
RemoveStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_ACCURACY");
//END_REGION

//REGION BLUNDERBUSS_SCATTERSHOT
IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_BLUNDERBUSS_SCATTERSHOT_HIT", _Source)
THEN
LeaderLib_Variables_DB_ModifyVariableInt(_Target, "LLWEAPONEX_Blunderbuss_PointBlankHits", 1, 3, 0, _Source);
LeaderLib_Timers_StartObjectObjectTimer((GUIDSTRING)_Target, _Source, 250, "Timers_LLWEAPONEX_Blunderbuss_ClearPointBlankHits", "LLWEAPONEX_Blunderbuss_ClearPointBlankHits");

IF
ItemStatusAttempt(_Target, "LLWEAPONEX_BLUNDERBUSS_SCATTERSHOT_HIT", _Source)
THEN
LeaderLib_Variables_DB_ModifyVariableInt(_Target, "LLWEAPONEX_Blunderbuss_PointBlankHits", 1, 3, 0, _Source);
LeaderLib_Timers_StartObjectObjectTimer((GUIDSTRING)_Target, _Source, 250, "Timers_LLWEAPONEX_Blunderbuss_ClearPointBlankHits", "LLWEAPONEX_Blunderbuss_ClearPointBlankHits");

PROC
LeaderLib_Timers_ObjectObjectTimerFinished(_Target, _Source, "LLWEAPONEX_Blunderbuss_ClearPointBlankHits")
THEN
LeaderLib_Variables_DB_ClearVariableInt(_Target, "LLWEAPONEX_Blunderbuss_PointBlankHits", _Source);

PROC
LeaderLib_Variables_OnVariableIntChanged((GUIDSTRING)_Target, "LLWEAPONEX_Blunderbuss_PointBlankHits", (INTEGER)_LastValue, 3, (GUIDSTRING)_Source)
THEN
LeaderLib_Variables_DB_ClearVariableInt(_Target, "LLWEAPONEX_Blunderbuss_PointBlankHits", _Source);
ApplyStatus(_Target, "LLWEAPONEX_BLUNDERBUSS_SCATTERSHOT_BONUS", 0.0, 1, _Source);
LeaderLib_Force_Apply(_Source, _Target, 4);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"