Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Firearms_InitSettings();

KBSECTION
//REGION SETTINGS
PROC
LLWEAPONEX_Firearms_InitSettings()
THEN
SysClear("DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage", 3);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_01", 0.0, 2.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_02", 2.0, 3.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_03", 3.0, 4.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_04", 4.0, 5.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_05", 5.0, 6.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_06", 6.0, 7.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_07", 7.0, 8.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_08", 8.0, 9.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_09", 9.0, 10.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_10", 10.0, 11.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_11", 11.0, 12.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_12", 12.0, 13.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_13", 13.0, 14.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_14", 14.0, 15.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_15", 15.0, 16.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_16", 16.0, 17.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_17", 17.0, 18.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_18", 18.0, 19.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_19", 19.0, 20.0);
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage("LLWEAPONEX_FIREARM_AIMEDSHOT_DAMAGE_20", 20.0, 999.0);

DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_Multishot", "Projectile_LLWEAPONEX_Firearm_Multishot", 1);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_EnemyMultishot", "Projectile_LLWEAPONEX_Firearm_Multishot_Enemy", 0);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_SkyShot", "Projectile_LLWEAPONEX_Firearm_SkyShot", 1);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_EnemySkyShot", "Projectile_LLWEAPONEX_Firearm_SkyShot_Enemy", 0);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_ArrowSpray", "Projectile_LLWEAPONEX_Firearm_ArrowSpray", 1);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_EnemyArrowSpray", "Projectile_LLWEAPONEX_Firearm_ArrowSpray_Enemy", 0);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_PiercingShot", "Projectile_LLWEAPONEX_Firearm_PiercingShot", 1);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_EnemyPiercingShot", "Projectile_LLWEAPONEX_Firearm_PiercingShot_Enemy", 0);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_Snipe", "Projectile_LLWEAPONEX_Firearm_Snipe", 1);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_EnemySnipe", "Projectile_LLWEAPONEX_Firearm_Snipe_Enemy", 0);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_Ricochet", "Projectile_LLWEAPONEX_Firearm_Ricochet", 1);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_EnemyRicochet", "Projectile_LLWEAPONEX_Firearm_Ricochet_Enemy", 0);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_BallisticShot", "Projectile_LLWEAPONEX_Firearm_BallisticShot", 1);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_EnemyBallisticShot", "Projectile_LLWEAPONEX_Firearm_BallisticShot_Enemy", 0);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_PinDown", "Projectile_LLWEAPONEX_Firearm_PinDown", 1);
DB_LLWEAPONEX_Firearms_RangerReplacementSkills("Projectile_EnemyPinDown", "Projectile_LLWEAPONEX_Firearm_PinDown_Enemy", 0);
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 0,9,18,1)
THEN
LLWEAPONEX_Firearms_InitSettings();
//END_REGION

//REGION EQUIP_EVENTS
IF
ItemTemplateUnEquipped("WPN_LLWEAPONEX_Firearm_2H_Rifle_A_0e98584a-7c57-4a5c-b7d9-57ec79aee85e", _Char)
THEN
ObjectClearFlag(_Char, "LLWEAPONEX_Firearms_RifleEquipped", 0);

IF
ItemTemplateUnEquipped("WPN_LLWEAPONEX_Firearm_2H_Rifle_B_068246d7-b4cc-4b6d-9ac8-716fdfd1822d", _Char)
THEN
ObjectClearFlag(_Char, "LLWEAPONEX_Firearms_RifleEquipped", 0);

IF
ItemTemplateEquipped("WPN_LLWEAPONEX_Firearm_2H_Rifle_A_0e98584a-7c57-4a5c-b7d9-57ec79aee85e", _Char)
THEN
ObjectSetFlag(_Char, "LLWEAPONEX_Firearms_RifleEquipped", 0);

IF
ItemTemplateEquipped("WPN_LLWEAPONEX_Firearm_2H_Rifle_B_068246d7-b4cc-4b6d-9ac8-716fdfd1822d", _Char)
THEN
ObjectSetFlag(_Char, "LLWEAPONEX_Firearms_RifleEquipped", 0);
//END_REGION

//REGION AIMED_SHOT_DISTANCE_DAMAGE
IF
CharacterStatusApplied(_Target, "LLWEAPONEX_FIREARM_AIMEDSHOT_HIT", (CHARACTERGUID)_Char)
AND
GetPosition(_Target, _x, _y, _z)
THEN
LLWEAPONEX_Firearms_AimedShot_ApplyDistanceDamage(_Char, (GUIDSTRING)_Target);

IF
ItemStatusChange(_Target, "LLWEAPONEX_FIREARM_AIMEDSHOT_HIT", (CHARACTERGUID)_Char)
THEN
LLWEAPONEX_Firearms_AimedShot_ApplyDistanceDamage(_Char, (GUIDSTRING)_Target);

PROC
LLWEAPONEX_Firearms_AimedShot_ApplyDistanceDamage((CHARACTERGUID)_Char, (GUIDSTRING)_Target)
AND
GetDistanceTo(_Char, _Target, _Dist)
AND
DB_LLWEAPONEX_Firearms_AimedShot_DistanceDamage(_Status, _Min, _Max)
AND
_Dist >= _Min
AND
_Dist < _Max
THEN
ApplyStatus(_Target, _Status, 0.0, 1, _Char);
//END_REGION

//REGION AIMED_SHOT_BONUSES
IF
CharacterUsedSkillOnTarget(_Char, _Target, "Projectile_LLWEAPONEX_Rifle_AimedShot", _, _)
AND
HasActiveStatus(_Target, "MARKED", 1)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_CRITICAL", 12.0, 1, _Char);

IF
CharacterUsedSkill(_Char, "Projectile_LLWEAPONEX_Rifle_AimedShot", _, _)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_ACCURACY", 12.0, 1, _Char);

IF
CharacterUsedSkill(_Char, "Projectile_LLWEAPONEX_Rifle_AimedShot_Enemy", _, _)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_ACCURACY", 12.0, 1, _Char);

IF
SkillCast(_Char, "Projectile_LLWEAPONEX_Rifle_AimedShot", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 1500, "Timers_LLWEAPONEX_Rifle_AimedShot_ClearBonuses", "LLWEAPONEX_Rifle_AimedShot_ClearBonuses");

IF
SkillCast(_Char, "Projectile_LLWEAPONEX_Rifle_AimedShot_Enemy", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 1500, "Timers_LLWEAPONEX_Rifle_AimedShot_ClearBonuses", "LLWEAPONEX_Rifle_AimedShot_ClearBonuses");

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_HIT_END", _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 50, "Timers_LLWEAPONEX_Rifle_AimedShot_ClearBonuses", "LLWEAPONEX_Rifle_AimedShot_ClearBonuses");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Rifle_AimedShot_ClearBonuses")
THEN
RemoveStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_CRITICAL");
RemoveStatus(_Char, "LLWEAPONEX_FIREARM_AIMEDSHOT_ACCURACY");
//END_REGION

//REGION BLUNDERBUSS_SCATTERSHOT
IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_BLUNDERBUSS_SCATTERSHOT_HIT", _Source)
THEN
LeaderLib_Variables_DB_ModifyVariableInt(_Target, "LLWEAPONEX_Blunderbuss_ScattershotHits", 1, 3, 0, _Source);
LeaderLib_Timers_StartObjectObjectTimer((GUIDSTRING)_Target, _Source, 250, "Timers_LLWEAPONEX_Blunderbuss_ClearScattershotHits", "LLWEAPONEX_Blunderbuss_ClearScattershotHits");

IF
ItemStatusAttempt(_Target, "LLWEAPONEX_BLUNDERBUSS_SCATTERSHOT_HIT", _Source)
THEN
LeaderLib_Variables_DB_ModifyVariableInt(_Target, "LLWEAPONEX_Blunderbuss_ScattershotHits", 1, 3, 0, _Source);
LeaderLib_Timers_StartObjectObjectTimer((GUIDSTRING)_Target, _Source, 250, "Timers_LLWEAPONEX_Blunderbuss_ClearScattershotHits", "LLWEAPONEX_Blunderbuss_ClearScattershotHits");

PROC
LeaderLib_Timers_ObjectObjectTimerFinished(_Target, _Source, "LLWEAPONEX_Blunderbuss_ClearScattershotHits")
THEN
LeaderLib_Variables_DB_ClearVariableInt(_Target, "LLWEAPONEX_Blunderbuss_ScattershotHits", _Source);

PROC
LeaderLib_Variables_OnVariableIntChanged((GUIDSTRING)_Target, "LLWEAPONEX_Blunderbuss_ScattershotHits", (INTEGER)_LastValue, 3, (GUIDSTRING)_Source)
THEN
LeaderLib_Variables_DB_ClearVariableInt(_Target, "LLWEAPONEX_Blunderbuss_ScattershotHits", _Source);
LeaderLib_Force_Apply(_Source, _Target, 4, "LLWEAPONEX_Scattershot_Landed");

PROC
LeaderLib_Force_OnLanded((GUIDSTRING)_Target, (GUIDSTRING)_Source, "LLWEAPONEX_Scattershot_Landed")
AND
GetPosition(_Target, _x, _y, _z)
THEN
PlayEffectAtPosition("LLWEAPONEX_FX_Skills_Scattershot_Impact_BonusDamage_01", _x, _y, _z);
ApplyStatus(_Target, "LLWEAPONEX_BLUNDERBUSS_SCATTERSHOT_BONUS", 0.0, 1, _Source);
//END_REGION

//REGION BULLET_IMPACT_SOUNDS
IF
CharacterStatusApplied(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX", _)
AND
ObjectGetFlag(_Char, "LLWEAPONEX_BlunderbussEquipped", 1)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX_BLUNDERBUSS", 1.0, 1, _Char);

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX", _)
AND
ObjectGetFlag(_Char, "LLWEAPONEX_Firearms_RifleEquipped", 1)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX_RIFLE", 1.0, 1, _Char);

IF
SkillCast(_Char, "Projectile_LLWEAPONEX_Firearm_Multishot", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 150, "Timers_LLWEAPONEX_Firearms_PlayBarrageExplosions", "LLWEAPONEX_Firearms_PlayBarrageExplosions");
LeaderLib_Timers_StartObjectTimer(_Char, 225, "Timers_LLWEAPONEX_Firearms_PlayBarrageExplosions2", "LLWEAPONEX_Firearms_PlayBarrageExplosions");

IF
SkillCast(_Char, "Projectile_LLWEAPONEX_Firearm_Multishot_Enemy", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 150, "Timers_LLWEAPONEX_Firearms_PlayBarrageExplosions", "LLWEAPONEX_Firearms_PlayBarrageExplosions");
LeaderLib_Timers_StartObjectTimer(_Char, 225, "Timers_LLWEAPONEX_Firearms_PlayBarrageExplosions2", "LLWEAPONEX_Firearms_PlayBarrageExplosions");

IF
StoryEvent(_Char, "LLWEAPONEX_Firearms_PlayBarrageExplosions")
AND
ObjectGetFlag(_Char, "LLWEAPONEX_BlunderbussEquipped", 1)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX_BLUNDERBUSS", 12.0, 1, _Char);

IF
StoryEvent(_Char, "LLWEAPONEX_Firearms_PlayBarrageExplosions")
AND
ObjectGetFlag(_Char, "LLWEAPONEX_Firearms_RifleEquipped", 1)
THEN
ApplyStatus(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX_RIFLE", 12.0, 1, _Char);

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX", _)
THEN
RemoveStatus(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX");

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX_BLUNDERBUSS", _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 250, "Timers_LLWEAPONEX_Firearms_RemoveFXStatus", "LLWEAPONEX_Firearms_RemoveFXStatus");

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX_RIFLE", _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 250, "Timers_LLWEAPONEX_Firearms_RemoveFXStatus", "LLWEAPONEX_Firearms_RemoveFXStatus");

IF
StoryEvent(_Char, "LLWEAPONEX_Firearms_RemoveFXStatus")
THEN
RemoveStatus(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX_BLUNDERBUSS");
RemoveStatus(_Char, "LLWEAPONEX_FIREARM_SHOOT_EXPLOSION_FX_RIFLE");

/* [OSITOOLS_ONLY]
IF
CharacterStatusApplied(_Target, "LLWEAPONEX_BULLET_HIT", _)
AND
String(_Target, _TargetStr)
THEN
NRD_LuaCall("LLWEAPONEX_Ext_PlayBulletImpact", _TargetStr);

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_PISTOL_SHOOT_HIT", _)
AND
String(_Target, _TargetStr)
THEN
NRD_LuaCall("LLWEAPONEX_Ext_PlayBulletImpact", _TargetStr);

IF
ItemStatusChange(_Target, "LLWEAPONEX_BULLET_HIT", _)
AND
String(_Target, _TargetStr)
THEN
NRD_LuaCall("LLWEAPONEX_Ext_PlayBulletImpact", _TargetStr);

IF
ItemStatusChange(_Target, "LLWEAPONEX_PISTOL_SHOOT_HIT", _)
AND
String(_Target, _TargetStr)
THEN
NRD_LuaCall("LLWEAPONEX_Ext_PlayBulletImpact", _TargetStr);
*/
//END_REGION

//REGION RANGER_SKILL_SWAP
/* [OSITOOLS_ONLY]
IF
TextEventSet("llweap_rangerskills")
AND
CharacterGetHostCharacter(_Char)
THEN
LLWEAPONEX_DEBUG_ClearSkillSlots(_Char, 20);

PROC
LLWEAPONEX_DEBUG_ClearSkillSlots((CHARACTERGUID)_Char, (INTEGER)_Slot)
THEN
NRD_SkillBarClear(_Char, _Slot);

PROC
LLWEAPONEX_DEBUG_ClearSkillSlots((CHARACTERGUID)_Char, (INTEGER)_Slot)
AND
IntegerSubtract(_Slot, 1, _Next)
AND
_Next >= 0
THEN
LLWEAPONEX_DEBUG_ClearSkillSlots(_Char, _Next);
*/

IF
TextEventSet("llweap_rangerskills")
AND
CharacterGetHostCharacter(_Char)
AND
DB_LLWEAPONEX_Firearms_RangerReplacementSkills(_FindSkill, _ReplaceSkill, _IsPlayer)
THEN
CharacterRemoveSkill(_Char, _FindSkill);

IF
TextEventSet("llweap_rangerskills")
AND
CharacterGetHostCharacter(_Char)
AND
DB_LLWEAPONEX_Firearms_RangerReplacementSkills(_FindSkill, _ReplaceSkill, 1)
THEN
CharacterAddSkill(_Char, _FindSkill, 0);
/* [OSITOOLS_ONLY]
PROC
LLWEAPONEX_Firearms_SwapRangerSkill((CHARACTERGUID)_Char, (STRING)_FindSkill, (STRING)_ReplaceSkill, 1)
AND
NRD_SkillBarFindSkill(_Char, _FindSkill, _Slot)
THEN
NRD_SkillBarSetSkill(_Char, _Slot, _ReplaceSkill);
CharacterRemoveSkill(_Char, _FindSkill);
CharacterAddSkill(_Char, _ReplaceSkill, 0);

PROC
LLWEAPONEX_Firearms_RestoreRangerSkill((CHARACTERGUID)_Char, (STRING)_FindSkill, (STRING)_ReplaceSkill, 1)
AND
NRD_SkillBarFindSkill(_Char, _ReplaceSkill, _Slot)
THEN
NRD_SkillBarSetSkill(_Char, _Slot, _FindSkill);
CharacterRemoveSkill(_Char, _ReplaceSkill);
CharacterAddSkill(_Char, _FindSkill, 0);

PROC
LLWEAPONEX_Firearms_RestoreAllRangerSkills((CHARACTERGUID)_Char)
AND
DB_LLWEAPONEX_Firearms_RangerReplacementSkills(_FindSkill, _ReplaceSkill, 1)
AND
NRD_SkillBarFindSkill(_Char, _ReplaceSkill, _Slot)
THEN
NRD_SkillBarSetSkill(_Char, _Slot, _FindSkill);
CharacterRemoveSkill(_Char, _ReplaceSkill);
CharacterAddSkill(_Char, _FindSkill, 0);
*/

// [BEGIN_NO_OSITOOLS]
PROC
LLWEAPONEX_Firearms_SwapRangerSkill((CHARACTERGUID)_Char, (STRING)_FindSkill, (STRING)_ReplaceSkill, (INTEGER)_IsPlayer)
AND
CharacterHasSkill(_Char, _FindSkill, 1)
THEN
CharacterRemoveSkill(_Char, _FindSkill);
CharacterAddSkill(_Char, _ReplaceSkill, 0);

PROC
LLWEAPONEX_Firearms_RestoreRangerSkill((CHARACTERGUID)_Char, (STRING)_FindSkill, (STRING)_ReplaceSkill, (INTEGER)_IsPlayer)
AND
CharacterHasSkill(_Char, _ReplaceSkill, 1)
THEN
CharacterRemoveSkill(_Char, _ReplaceSkill);
CharacterAddSkill(_Char, _FindSkill, 0);

PROC
LLWEAPONEX_Firearms_RestoreAllRangerSkills((CHARACTERGUID)_Char)
AND
DB_LLWEAPONEX_Firearms_RangerReplacementSkills(_FindSkill, _ReplaceSkill, 1)
AND
CharacterHasSkill(_Char, _FindSkill, 0)
AND
CharacterHasSkill(_Char, _ReplaceSkill, 1)
THEN
CharacterRemoveSkill(_Char, _ReplaceSkill);
CharacterAddSkill(_Char, _FindSkill, 0);
// [END_NO_OSITOOLS]

PROC
LLWEAPONEX_OnWeaponTypeEquipped(_Char, _Item, "LLWEAPONEX_Firearm", _IsPlayer)
AND
DB_LLWEAPONEX_Firearms_RangerReplacementSkills(_FindSkill, _ReplaceSkill, _IsPlayer)
AND
CharacterHasSkill(_Char, _FindSkill, 1)
THEN
DB_LLWEAPONEX_Firearms_SwappedRangerSkill(_Char, _FindSkill, _ReplaceSkill);
LLWEAPONEX_Firearms_SwapRangerSkill(_Char, _FindSkill, _ReplaceSkill, _IsPlayer);

PROC
LLWEAPONEX_OnWeaponTypeUnEquipped(_Char, _Item, "LLWEAPONEX_Firearm", _IsPlayer)
AND
DB_LLWEAPONEX_Firearms_SwappedRangerSkill(_Char, _FindSkill, _ReplaceSkill)
THEN
NOT DB_LLWEAPONEX_Firearms_SwappedRangerSkill(_Char, _FindSkill, _ReplaceSkill);
LLWEAPONEX_Firearms_RestoreRangerSkill(_Char, _FindSkill, _ReplaceSkill, _IsPlayer);

PROC
LLWEAPONEX_OnWeaponTypeUnEquipped(_Char, _Item, "LLWEAPONEX_Firearm", _IsPlayer)
THEN
LLWEAPONEX_Firearms_RestoreAllRangerSkills(_Char);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"