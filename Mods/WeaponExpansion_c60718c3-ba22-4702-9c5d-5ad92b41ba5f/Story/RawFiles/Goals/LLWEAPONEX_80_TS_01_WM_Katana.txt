Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION

IF
CharacterStatusAttempt(_Target, _ComboFinisherStatus, (CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_Katanas_ComboFinisher(_ComboFinisherStatus, _ComboFinisher)
AND
DB_LLWEAPONEX_Katanas_ComboStatus(_Hits, _Status)
AND
ObjectGetFlag(_Target, "LLWEAPONEX_Katanas_DisableCombo", 0)
AND
HasActiveStatus(_Target, _Status, 1)
THEN
ObjectSetFlag(_Target, "LLWEAPONEX_Katanas_DisableCombo", 0);
LLWEAPONEX_Katanas_SetFinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher);
LLWEAPONEX_Katanas_StartFinisher(_Target, _Source);

IF
ObjectTurnEnded((CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, _Status)
THEN
NOT DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, _Status);
LeaderLib_Statuses_RemoveIfActive(_Target, _Status);
LLWEAPONEX_Katanas_ComboRemoved(_Source, _Target);

IF
CharacterStatusRemoved(_Target, _Status, _)
AND
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, _Status)
THEN
NOT DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, _Status);
LLWEAPONEX_Katanas_ComboRemoved(_Source, _Target);

IF
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _, _)
AND
IsTagged(_Source, "LLWEAPONEX_Blademaster_Target_Available", 0)
THEN
SetTag(_Source, "LLWEAPONEX_Blademaster_Target_Available");

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"