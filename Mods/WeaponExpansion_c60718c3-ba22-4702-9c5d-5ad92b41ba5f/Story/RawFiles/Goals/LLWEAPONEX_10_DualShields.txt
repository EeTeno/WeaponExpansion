Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Common", "S_LLWEAPONEX_CombatShield_Blackring_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Blackring_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Rare", "S_LLWEAPONEX_CombatShield_Blackring_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Epic", "S_LLWEAPONEX_CombatShield_Blackring_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Legendary", "S_LLWEAPONEX_CombatShield_Blackring_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Divine", "S_LLWEAPONEX_CombatShield_Blackring_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Common", "S_LLWEAPONEX_CombatShield_Common_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Common_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Rare", "S_LLWEAPONEX_CombatShield_Common_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Epic", "S_LLWEAPONEX_CombatShield_Common_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Legendary", "S_LLWEAPONEX_CombatShield_Common_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Divine", "S_LLWEAPONEX_CombatShield_Common_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Common", "S_LLWEAPONEX_CombatShield_Dwarves_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Dwarves_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Rare", "S_LLWEAPONEX_CombatShield_Dwarves_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Epic", "S_LLWEAPONEX_CombatShield_Dwarves_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Legendary", "S_LLWEAPONEX_CombatShield_Dwarves_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Divine", "S_LLWEAPONEX_CombatShield_Dwarves_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Common", "S_LLWEAPONEX_CombatShield_Elves_B_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Uncommon", "S_LLWEAPONEX_CombatShield_Elves_B_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Rare", "S_LLWEAPONEX_CombatShield_Elves_B_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Epic", "S_LLWEAPONEX_CombatShield_Elves_B_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Legendary", "S_LLWEAPONEX_CombatShield_Elves_B_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Divine", "S_LLWEAPONEX_CombatShield_Elves_B_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Common", "S_LLWEAPONEX_CombatShield_Humans_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Humans_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Rare", "S_LLWEAPONEX_CombatShield_Humans_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Epic", "S_LLWEAPONEX_CombatShield_Humans_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Legendary", "S_LLWEAPONEX_CombatShield_Humans_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Divine", "S_LLWEAPONEX_CombatShield_Humans_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Common", "S_LLWEAPONEX_CombatShield_Lizards_C_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Uncommon", "S_LLWEAPONEX_CombatShield_Lizards_C_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Rare", "S_LLWEAPONEX_CombatShield_Lizards_C_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Epic", "S_LLWEAPONEX_CombatShield_Lizards_C_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Legendary", "S_LLWEAPONEX_CombatShield_Lizards_C_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Divine", "S_LLWEAPONEX_CombatShield_Lizards_C_Divine");

KBSECTION
QRY
LLWEAPONEX_DualShields_QRY_ItemLevelSet((ITEMGUID)_Item)
AND
GetVarInteger(_Item, "LeaderLib_Level", _Level)
AND
_Level > 0
THEN
LeaderLog_LogInt("DEBUG", "[LLWEAPONEX:DualShields:QRY:ItemLevelSet] Dual Shields level variable is [", _Level, "].");

QRY
LLWEAPONEX_DualShields_QRY_HasCombatShield((ITEMGUID)_Item)
AND
GetVarObject(_Item, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
AND
ObjectExists(_CombatShield, 1)
THEN
DB_NOOP(1);

//REGION UPDATING
//Combat shield identify fix
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_OldVersion, "0.9.1.2")
AND
DB_IsPlayer(_Player)
THEN
InventoryLaunchTagIterator(_Player, "LLWEAPONEX_DualShields", "", "LLWEAPONEX_Iterators_FoundDualShield_Identify", "");

IF
StoryEvent((ITEMGUID)_Shield, "LLWEAPONEX_DualShields")
THEN
ContainerIdentifyAll(_Shield);
//END_REGION

//REGION UNIQUE_SHIELDS
PROC
LLWEAPONEX_OnItemEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_Shield_DualShields_GiantDoor_A_5e8ca310-616d-4f18-b455-e510dade5b62")
AND
ObjectGetFlag(_Item, "LLWEAPONEX_DualShields_GeneratedCombatShield", 0)
AND
GetPosition(_Player, _x, _y, _z)
AND
CreateItemTemplateAtPosition("WPN_UNIQUE_LLWEAPONEX_CombatShield_GiantDoor_A_b4748a58-427c-4652-bf6c-d596ddcc134d", _x, _y, _z, _CombatShield)
AND
CharacterGetLevel(_Player, _Level)
THEN
ObjectSetFlag(_Item, "LLWEAPONEX_DualShields_GeneratedCombatShield", 0);
SetTag(_CombatShield, "LLWEAPONEX_Debug_StorableCombatShield");
SetVarObject(_Item, "LLWEAPONEX_CombatShield", _CombatShield);

SetVarObject(_CombatShield, "LLWEAPONEX_ParentDualShield", _Item);
SetVarFixedString(_CombatShield, "LeaderLib_Rarity", "Unique");
ItemSetOwner(_CombatShield, _Player);

ItemLevelUpTo(_Item, _Level);
ItemLevelUpTo(_CombatShield, _Level);
SetVarInteger(_Item, "LeaderLib_Level", _Level);
SetVarInteger(_CombatShield, "LeaderLib_Level", _Level);

ItemToInventory(_CombatShield, _Item);
//END_REGION

//REGION EQUIPPED
PROC
LLWEAPONEX_DualShieldS_Internal_GenerateCombatShield((ITEMGUID)_Shield)
AND
NOT LLWEAPONEX_DualShields_QRY_HasCombatShield(_Shield)
AND
GetVarInteger(_Shield, "LeaderLib_Level", _Level)
AND
GetVarFixedString(_Shield, "LeaderLib_Rarity", _Rarity)
AND
GetStatString(_Shield, _Stat)
AND
DB_LLWEAPONEX_DualShields_CombatShieldGenerator(_Stat, _Rarity, _TreasureTable)
THEN
DB_LLWEAPONEX_DualShields_Temp_ListenForCombatShield(_Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_RaritySet] [",_Stat,"] Generating combat shield with rarity [",_Rarity,"] from treasure table [",_TreasureTable,"].");
GenerateTreasure(_Shield, _TreasureTable, _Level, NULL_00000000-0000-0000-0000-000000000000);

IF
ItemAddedToContainer(_CombatShield, _Shield)
AND
DB_LLWEAPONEX_DualShields_Temp_ListenForCombatShield(_Shield)
AND
GetVarFixedString(_Shield, "LeaderLib_Rarity", _Rarity)
AND
GetVarInteger(_Shield, "LeaderLib_Level", _Level)
AND
ItemGetOwner(_Shield, _Owner)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ListenForCombatShield(_Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:ItemAddedToContainer] Combat shield generated.");
ObjectSetFlag(_Shield, "LLWEAPONEX_DualShields_GeneratedCombatShield", 0);
SetTag(_CombatShield, "LLWEAPONEX_Debug_StorableCombatShield");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);
SetVarObject(_CombatShield, "LLWEAPONEX_ParentDualShield", _Shield);
SetVarFixedString(_CombatShield, "LeaderLib_Rarity", _Rarity);
SetVarInteger(_CombatShield, "LeaderLib_Level", _Level);
ItemSetOwner(_CombatShield, _Owner);
LeaderLib_Deltamods_ApplyDeltamodsByGroup(_CombatShield, "WeaponExpansion.CombatShields");
//ItemSetOnlyOwnerCanUse(_CombatShield, 1);
LLWEAPONEX_DualShields_CombatShieldGenerated(_Owner, _Shield, _CombatShield);

PROC
LLWEAPONEX_OnItemEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Template)
AND
IsTagged(_Item, "LLWEAPONEX_DualShields", 1)
AND
ObjectGetFlag(_Item, "LLWEAPONEX_DualShields_GeneratedCombatShield", _GeneratedShield)
THEN
LLWEAPONEX_DualShields_OnDualShieldEquipped(_Player, _Item, _GeneratedShield);

PROC
LLWEAPONEX_DualShields_OnDualShieldEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Shield, 1)
AND
GetVarObject(_Shield, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
AND
ObjectExists(_CombatShield, _Exists)
AND
ItemIsEquipable(_CombatShield, _Equipable)
AND
IntegerMin(_Exists, _Equipable, _GenerateNew)
THEN
LLWEAPONEX_DualShields_EquipCombatShield(_Player, _Shield, _CombatShield, _GenerateNew);

PROC
LLWEAPONEX_DualShields_OnDualShieldEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Shield, 0)
AND
LLWEAPONEX_DualShields_QRY_ItemLevelSet(_Shield)
THEN
LLWEAPONEX_DualShieldS_Internal_GenerateCombatShield(_Shield);

PROC
LLWEAPONEX_DualShields_OnDualShieldEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Shield, 0)
AND
NOT LLWEAPONEX_DualShields_QRY_ItemLevelSet(_Shield)
THEN
ItemSetOwner(_Shield, _Player);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_OnDualShieldEquipped] Level/Rarity variables not set on Dual Shield. Running [LeaderLib_Commands_SetItemVariables].");
SetVarString(_Shield, "LeaderLib_ItemVariablesSetEvent", "LLWEAPONEX_DualShields_VariablesSet");
SetStoryEvent(_Shield, "LeaderLib_Commands_SetItemVariables");

IF
StoryEvent((ITEMGUID)_Shield, "LLWEAPONEX_DualShields_VariablesSet")
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_VariablesSet] Generating combat shield.");
ObjectSetFlag(_Shield, "LLWEAPONEX_DualShields_EquipAfterGenerating", 0);
LLWEAPONEX_DualShieldS_Internal_GenerateCombatShield(_Shield);

PROC
LLWEAPONEX_DualShields_CombatShieldGenerated((CHARACTERGUID)_Owner, (ITEMGUID)_Shield, (ITEMGUID)_CombatShield)
AND
ObjectGetFlag(_Shield, "LLWEAPONEX_DualShields_EquipAfterGenerating", 1)
THEN
ObjectClearFlag(_Shield, "LLWEAPONEX_DualShields_EquipAfterGenerating");
ContainerIdentifyAll(_Shield);
LLWEAPONEX_DualShields_EquipCombatShield(_Owner, _Shield, _CombatShield, 1);

PROC
LLWEAPONEX_DualShields_EquipCombatShield((CHARACTERGUID)_Player, (ITEMGUID)_Shield, (ITEMGUID)_CombatShield, 1)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:OnDualShieldsEquipped] Equipping combat shield stored in a dual shield.");
DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Player, _Shield, _CombatShield);
//CharacterEquipItem(_Player, _CombatShield);
CharacterUseItem(_Player, _CombatShield, "LLWEAPONEX_DualShields_CombatShieldEquipped");

IF
//ItemEquipped(_CombatShield, _Player)
StoryEvent((CHARACTERGUID)_Player, "LLWEAPONEX_DualShields_CombatShieldEquipped")
AND
DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Player, _Shield, _CombatShield)
AND
CharacterGetEquippedItem(_Player, "Weapon", _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Player, _Shield, _CombatShield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_CombatShieldEquipped] Combat shield was equipped.");

IF
StoryEvent((CHARACTERGUID)_Player, "LLWEAPONEX_DualShields_CombatShieldEquipped")
AND
DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Player, _Shield, _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Player, _Shield, _CombatShield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_CombatShieldEquipped] Combat shield was not equipped.");
//ItemToInventory(_CombatShield, _Player);
ContainerIdentifyAll(_Shield);
CharacterEquipItem(_Player, _CombatShield);

IF
CharacterUsedItemFailed(_Player, _CombatShield)
AND
DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Player, _Shield, _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Player, _Shield, _CombatShield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:CharacterUsedItemFailed] Failed to equip combat shield. Generating a new shield.");
ItemDestroy(_CombatShield);
LLWEAPONEX_DualShields_OnDualShieldEquipped(_Player, _Shield, 0);

PROC
LLWEAPONEX_DualShields_EquipCombatShield((CHARACTERGUID)_Player, (ITEMGUID)_Shield, (ITEMGUID)_CombatShield, 0)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:OnDualShieldsEquipped] [ERROR] Item for variable [LLWEAPONEX_CombatShield] does not exist. Checking inventory.");
LLWEAPONEX_DualShields_FindCombatShield(_Player, _Shield, 1);
//END_REGION

//REGION FIND_COMBAT_SHIELD
PROC
LLWEAPONEX_DualShields_FindCombatShield((CHARACTERGUID)_Player, (ITEMGUID)_Shield, 1)
AND
GetUUID(_Shield, _ID)
AND
StringConcatenate("LLWEAPONEX_FindCombatShield_IteratorDone_", _ID, _GlobalFlag)
THEN
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
InventoryLaunchTagIterator(_Shield, "LLWEAPONEX_CombatShield", "", "LLWEAPONEX_Iterators_FoundDualShield_Equip", _GlobalFlag);

IF
StoryEvent((ITEMGUID)_CombatShield, "LLWEAPONEX_Iterators_FoundDualShield_Equip")
AND
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_Iterators_FoundDualShield] Found combat shield in dual shield inventory. Setting variable & equipping.");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);
LLWEAPONEX_DualShields_EquipCombatShield(_Player, _Shield, _CombatShield, 1);

PROC
LLWEAPONEX_DualShields_FindCombatShield((CHARACTERGUID)_Player, (ITEMGUID)_Shield, 0)
AND
GetUUID(_Shield, _ID)
AND
StringConcatenate("LLWEAPONEX_FindCombatShield_IteratorDone_", _ID, _GlobalFlag)
THEN
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
InventoryLaunchTagIterator(_Shield, "LLWEAPONEX_CombatShield", "", "LLWEAPONEX_Iterators_FoundCombatShield", _GlobalFlag);

IF
StoryEvent((ITEMGUID)_CombatShield, "LLWEAPONEX_Iterators_FoundDualShield_Equip")
AND
ItemGetOwner(_CombatShield, _Player)
AND
GetInventoryOwner(_CombatShield, (ITEMGUID)_Shield)
AND
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_Iterators_FoundDualShield] Found combat shield in dual shield inventory. Setting variable.");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);

IF
GlobalFlagSet(_GlobalFlag)
AND
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:",_GlobalFlag,"] Combat shield not found. Regenerating.");
LLWEAPONEX_DualShieldS_Internal_GenerateCombatShield(_Shield);
//END_REGION

//REGION UNEQUIPPED
PROC
LLWEAPONEX_OnItemUnEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Template)
AND
IsTagged(_Item, "LLWEAPONEX_DualShields", 1)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Item, _)
AND
CharacterGetEquippedItem(_Player, "Weapon", _CombatShield)
AND
GetVarObject(_Item, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
THEN
DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Item, _CombatShield);
ItemToInventory(_CombatShield, _Item);

PROC
LLWEAPONEX_OnItemUnEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Template)
AND
IsTagged(_Item, "LLWEAPONEX_CombatShield", 1)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _, _Item)
AND
CharacterGetEquippedShield(_Player, (ITEMGUID)_Shield)
AND
_Shield != NULL_00000000-0000-0000-0000-000000000000
THEN 
DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _Item);
CharacterUnequipItem(_Player, _Shield);
ItemToInventory(_Item, _Shield);

IF
DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _CombatShield)
THEN
LeaderLib_Timers_StartCharacterItemTimer(_Player, (ITEMGUID)_Shield, 150, "LLWEAPONEX_Timers_ResetDualShieldHandled", "LLWEAPONEX_DualShields_ResetHandled");

IF
CharacterItemEvent(_Player, _Shield, "LLWEAPONEX_DualShields_ResetHandled")
AND
DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _CombatShield);
//END_REGION

//REGION LEADERLIB_PRESET_APPLIED
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
CharacterGetEquippedItem(_Player, "Shield", (ITEMGUID)_Shield)
AND
IsTagged(_Shield, "LLWEAPONEX_DualShields", 1)
AND
ObjectGetFlag(_Shield, "LLWEAPONEX_DualShields_GeneratedCombatShield", _GeneratedShield)
THEN
LLWEAPONEX_DualShields_OnDualShieldEquipped(_Player, _Shield, _GeneratedShield);
//END_REGION

//REGION CC_PRESET_FIX
/*
//Post-CC Preset Fix
PROC
LLWEAPONEX_OnCharacterCreationFinished((CHARACTERGUID)_Player)
AND
CharacterGetEquippedItem(_Player, "Shield", (ITEMGUID)_Shield)
AND
_Shield != NULL_00000000-0000-0000-0000-000000000000
AND
IsTagged(_Shield, "LLWEAPONEX_DualShields", 1)
AND
ObjectGetFlag(_Shield, "LLWEAPONEX_DualShields_GeneratedCombatShield", _GeneratedShield)
THEN
LLWEAPONEX_DualShields_OnDualShieldEquipped(_Player, _Shield, _GeneratedShield);
*/
//END_REGION

//REGION DYNAMIC_COVER
IF
CharacterStatusApplied(_Char, "LLWEAPONEX_COVERED", (CHARACTERGUID)_Blocker)
THEN
DB_LLWEAPONEX_DualShields_Temp_Covering(_Blocker, _Char);
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_ShieldCoverActive", "WeaponExpansion");

IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_COVERED", _)
AND
DB_LLWEAPONEX_DualShields_Temp_Covering(_Blocker, _Char)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_Covering(_Blocker, _Char);
LLWEAPONEX_DualShields_OnCoveredRemoved(_Blocker, _Char);

PROC
LLWEAPONEX_DualShields_OnCoveredRemoved((CHARACTERGUID)_Blocker, (CHARACTERGUID)_Char)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_Covering(_Blocker, _)
THEN
RemoveStatus(_Blocker, "LLWEAPONEX_COVERING");

IF
CharacterStatusRemoved(_Blocker, "LLWEAPONEX_COVERING", _)
AND
DB_LLWEAPONEX_DualShields_Temp_Covering(_Blocker, _Char)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_Covering(_Blocker, _Char);
LLWEAPONEX_DualShields_OnCoveredRemoved(_Blocker, _Char);

PROC
LLWEAPONEX_DualShields_OnCoveredRemoved((CHARACTERGUID)_Blocker, (CHARACTERGUID)_Char)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_Covering(_, _Char)
THEN
RemoveStatus(_Char, "LLWEAPONEX_COVERING");

PROC
LLWEAPONEX_DualShields_OnCoveredRemoved((CHARACTERGUID)_Blocker, (CHARACTERGUID)_Char)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_Covering(_,_)
THEN
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_ShieldCoverActive", "WeaponExpansion", 500);

PROC
LLWEAPONEX_DualShields_DynamicCover((CHARACTERGUID)_Attacker, (CHARACTERGUID)_Blocker, (CHARACTERGUID)_Defender)
AND
HasActiveStatus(_Blocker, "LLWEAPONEX_COVERING", 1)
THEN
SetVarObject(_Blocker, "LLWEAPONEX_ShieldCover_Covering", _Defender);
CharacterCharacterSetEvent(_Blocker, _Attacker, "LLWEAPONEX_ShieldCover_CounterAttack_Teleport");
//TeleportToPosition(_Blocker, _tx, _ty, _tz, "LLWEAPONEX_AttachToGrid", 0, 1);
//TeleportToPosition(_Char, _x, _y, _z, "LLWEAPONEX_AttachToGrid", 0, 1);

PROC
LLWEAPONEX_DualShields_DynamicCover((CHARACTERGUID)_Attacker, (CHARACTERGUID)_Blocker, (CHARACTERGUID)_Defender)
THEN
ApplyStatus(_Defender, "LLWEAPONEX_COVERED_SHELL", 6.0, 0, _Blocker);
RemoveStatus(_Defender, "LLWEAPONEX_COVERED");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_AttachToGrid")
THEN
CharacterSetDetached(_Char, 0);

IF
CharacterCharacterEvent(_Blocker, _Attacker, "LLWEAPONEX_ShieldCover_CounterAttack_UseSkill")
AND
GetPosition(_Blocker, _tx, _ty, _tz)
AND
GetVarFloat3(_Blocker, "LLWEAPONEX_ShieldCover_TeleportPos", _x, _y, _z)
THEN
PlayEffectAtPosition("LLWEAPONEX_FX_Skills_ShieldCover_Counter_Disappear_Root_01", _tx, _ty, _tz);
PlayEffectAtPosition("LLWEAPONEX_FX_Skills_ShieldCover_Counter_Disappear_Root_01", _x, _y, _z);
PlayEffect(_Blocker, "LLWEAPONEX_FX_Skills_ShieldCover_Counter_Disappear_Overlay_01");
TeleportToPosition(_Blocker, _x, _y, _z, "", 0, 1);
LeaderLib_Timers_StartCharacterCharacterTimer(_Blocker, _Attacker, 50, "LLWEAPONEX_Timers_ShieldCover_UseCounterSkill", "UseCounterSkill");

//When a valid position isn't found, teleport to the attacker
IF
CharacterCharacterEvent(_Blocker, _Attacker, "LLWEAPONEX_ShieldCover_CounterAttack_UseSkill_NoPositionFound")
AND
GetPosition(_Blocker, _tx, _ty, _tz)
AND
GetPosition(_Attacker, _x, _y, _z)
THEN
PlayEffectAtPosition("LLWEAPONEX_FX_Skills_ShieldCover_Counter_Disappear_Root_01", _tx, _ty, _tz);
PlayEffectAtPosition("LLWEAPONEX_FX_Skills_ShieldCover_Counter_Disappear_Root_01", _x, _y, _z);
PlayEffect(_Blocker, "LLWEAPONEX_FX_Skills_ShieldCover_Counter_Disappear_Overlay_01");
TeleportTo(_Blocker, _Attacker, "", 0, 1, 0);
LeaderLib_Timers_StartCharacterCharacterTimer(_Blocker, _Attacker, 50, "LLWEAPONEX_Timers_ShieldCover_UseCounterSkill", "UseCounterSkill");

IF
CharacterCharacterEvent(_Blocker, _Attacker, "UseCounterSkill")
THEN
DisplayText(_Attacker, "LLWEAPONEX_StatusText_Countered");
CharacterUseSkill(_Blocker, "Target_LLWEAPONEX_ShieldCover_CounterAttack", _Attacker, 1, 1, 1);
//END_REGION

//REGION IRON_MAIDEN
IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_SHIELD_PRISON", _)
AND
HasActiveStatus(_Char, "LLWEAPONEX_SHIELD_PRISON_FX", 1)
THEN
ProcObjectTimerCancel(_Char, "LLWEAPONEX_Timers_RemoveShieldPrisonFX");
ProcObjectTimer(_Char, "LLWEAPONEX_Timers_RemoveShieldPrisonFX", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LLWEAPONEX_Timers_RemoveShieldPrisonFX")
AND
HasActiveStatus(_Char, "LLWEAPONEX_SHIELD_PRISON_FX", 1)
THEN
RemoveStatus(_Char, "LLWEAPONEX_SHIELD_PRISON_FX");
LeaderLib_Timers_StartObjectTimer(_Char, 250, "LLWEAPONEX_Timers_PlayShieldPrisonEndFX", "LLWEAPONEX_PlayShieldPrisonEndFX");

IF
StoryEvent(_Char, "LLWEAPONEX_PlayShieldPrisonEndFX")
AND
HasActiveStatus(_Char, "LLWEAPONEX_IRONMAIDEN_SHIELDPRISON_FX", 0)
THEN
PlayEffect(_Char, "LLWEAPONEX_FX_Status_ShieldPrison_End_01", "Dummy_Root");

IF
CharacterUsedSkillOnTarget(_Char, (CHARACTERGUID)_Target, "Target_LLWEAPONEX_IronMaiden", _, _)
AND
HasActiveStatus(_Target, "LLWEAPONEX_SHIELD_PRISON", 1)
THEN
RemoveStatus(_Target, "LLWEAPONEX_SHIELD_PRISON");
ApplyStatus(_Target, "LLWEAPONEX_IRONMAIDEN_SHIELDPRISON_FX", 3.0, 1, _Char);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"