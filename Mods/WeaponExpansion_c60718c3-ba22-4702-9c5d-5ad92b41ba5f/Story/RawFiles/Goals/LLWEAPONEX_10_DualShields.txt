Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Common", "S_LLWEAPONEX_CombatShield_Blackring_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Blackring_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Rare", "S_LLWEAPONEX_CombatShield_Blackring_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Epic", "S_LLWEAPONEX_CombatShield_Blackring_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Legendary", "S_LLWEAPONEX_CombatShield_Blackring_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Divine", "S_LLWEAPONEX_CombatShield_Blackring_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Common", "S_LLWEAPONEX_CombatShield_Common_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Common_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Rare", "S_LLWEAPONEX_CombatShield_Common_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Epic", "S_LLWEAPONEX_CombatShield_Common_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Legendary", "S_LLWEAPONEX_CombatShield_Common_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Divine", "S_LLWEAPONEX_CombatShield_Common_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Common", "S_LLWEAPONEX_CombatShield_Dwarves_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Dwarves_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Rare", "S_LLWEAPONEX_CombatShield_Dwarves_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Epic", "S_LLWEAPONEX_CombatShield_Dwarves_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Legendary", "S_LLWEAPONEX_CombatShield_Dwarves_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Divine", "S_LLWEAPONEX_CombatShield_Dwarves_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Common", "S_LLWEAPONEX_CombatShield_Elves_B_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Uncommon", "S_LLWEAPONEX_CombatShield_Elves_B_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Rare", "S_LLWEAPONEX_CombatShield_Elves_B_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Epic", "S_LLWEAPONEX_CombatShield_Elves_B_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Legendary", "S_LLWEAPONEX_CombatShield_Elves_B_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Divine", "S_LLWEAPONEX_CombatShield_Elves_B_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Common", "S_LLWEAPONEX_CombatShield_Humans_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Humans_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Rare", "S_LLWEAPONEX_CombatShield_Humans_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Epic", "S_LLWEAPONEX_CombatShield_Humans_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Legendary", "S_LLWEAPONEX_CombatShield_Humans_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Divine", "S_LLWEAPONEX_CombatShield_Humans_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Common", "S_LLWEAPONEX_CombatShield_Lizards_C_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Uncommon", "S_LLWEAPONEX_CombatShield_Lizards_C_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Rare", "S_LLWEAPONEX_CombatShield_Lizards_C_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Epic", "S_LLWEAPONEX_CombatShield_Lizards_C_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Legendary", "S_LLWEAPONEX_CombatShield_Lizards_C_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Divine", "S_LLWEAPONEX_CombatShield_Lizards_C_Divine");

KBSECTION
QRY
LLWEAPONEX_DualShields_QRY_ItemLevelSet((ITEMGUID)_Item)
AND
GetVarInteger(_Item, "LeaderLib_Level", _Level)
AND
_Level > 0
THEN
LeaderLog_LogInt("DEBUG", "[LLWEAPONEX:DualShields:QRY:ItemLevelSet] Dual Shields level variable is [", _Level, "].");

QRY
LLWEAPONEX_DualShields_QRY_HasCombatShield((ITEMGUID)_Item)
AND
GetVarObject(_Item, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
AND
ObjectExists(_CombatShield, 1)
THEN
DB_NOOP(1);

IF
CanUseItem(_Player, _Shield, _)
AND
IsTagged(_Shield, "LLWEAPONEX_DualShields", 1)
AND
NOT LLWEAPONEX_DualShields_QRY_ItemLevelSet(_Shield)
THEN
SetVarString(_Shield, "LeaderLib_ItemVariablesSetEvent", "LLWEAPONEX_DualShields_VariablesSet");
SetStoryEvent(_Shield, "LeaderLib_Commands_SetItemVariables");

IF
StoryEvent((ITEMGUID)_Shield, "LLWEAPONEX_DualShields_VariablesSet")
THEN
LLWEAPONEX_DUalShieldS_Internal_GenerateCombatShield(_Shield);

PROC
LLWEAPONEX_DUalShieldS_Internal_GenerateCombatShield((ITEMGUID)_Shield)
AND
NOT LLWEAPONEX_DualShields_QRY_HasCombatShield(_Shield)
AND
GetVarInteger(_Shield, "LeaderLib_Level", _Level)
AND
GetVarFixedString(_Shield, "LeaderLib_Rarity", _Rarity)
AND
GetStatString(_Shield, _Stat)
AND
DB_LLWEAPONEX_DualShields_CombatShieldGenerator(_Stat, _Rarity, _TreasureTable)
THEN
DB_LLWEAPONEX_DualShields_Temp_ListenForCombatShield(_Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_RaritySet] [",_Stat,"] Generating combat shield with rarity [",_Rarity,"] from treasure table [",_TreasureTable,"].");
GenerateTreasure(_Shield, _TreasureTable, _Level, NULL_00000000-0000-0000-0000-000000000000);

IF
ItemAddedToContainer(_CombatShield, _Shield)
AND
DB_LLWEAPONEX_DualShields_Temp_ListenForCombatShield(_Shield)
AND
ItemGetOwner(_Shield, _Owner)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ListenForCombatShield(_Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:ItemAddedToContainer] Combat shield generated.");
ObjectSetFlag(_Shield, "LLWEAPONEX_DualShields_GeneratedCombatShield");
SetTag(_CombatShield, "LLWEAPONEX_Debug_StorableCombatShield");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);
SetVarObject(_CombatShield, "LLWEAPONEX_ParentDualShield", _Shield);
ItemSetOwner(_CombatShield, _Owner);
LeaderLib_Deltamods_ApplyDeltamodsByGroup(_CombatShield, "WeaponExpansion.CombatShields");
//ItemSetOnlyOwnerCanUse(_CombatShield, 1);

IF
ItemEquipped(_Shield, _Player)
AND
IsTagged(_Shield, "LLWEAPONEX_DualShields", 1)
AND
ObjectGetFlag(_Shield, "LLWEAPONEX_DualShields_GeneratedCombatShield", 1)
AND
GetVarObject(_Shield, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
AND
ObjectExists(_CombatShield, _Exists)
THEN
LLWEAPONEX_DualShields_EquipCombatShield(_Player, _Shield, _CombatShield, _Exists);

PROC
LLWEAPONEX_DualShields_EquipCombatShield((CHARACTERGUID)_Player, (ITEMGUID)_Shield, (ITEMGUID)_CombatShield, 1)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:OnDualShieldsEquipped] Equipping combat shield stored in a dual shield.");
CharacterEquipItem(_Player, _CombatShield);

PROC
LLWEAPONEX_DualShields_EquipCombatShield((CHARACTERGUID)_Player, (ITEMGUID)_Shield, (ITEMGUID)_CombatShield, 0)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:OnDualShieldsEquipped] [ERROR] Item for variable [LLWEAPONEX_CombatShield] does not exist. Checking inventory.");
LLWEAPONEX_DualShields_FindCombatShield(_Player, _Shield, 1);

PROC
LLWEAPONEX_DualShields_FindCombatShield((CHARACTERGUID)_Player, (ITEMGUID)_Shield, 1)
AND
GetUUID(_Shield, _ID)
AND
StringConcatenate("LLWEAPONEX_FindCombatShield_IteratorDone_", _ID, _GlobalFlag)
THEN
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
InventoryLaunchTagIterator(_Shield, "LLWEAPONEX_CombatShield", "", "LLWEAPONEX_Iterators_FoundDualShield_Equip", _GlobalFlag);

IF
StoryEvent((ITEMGUID)_CombatShield, "LLWEAPONEX_Iterators_FoundDualShield_Equip")
AND
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_Iterators_FoundDualShield] Found combat shield in dual shield inventory. Setting variable & equipping.");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);
LLWEAPONEX_DualShields_EquipCombatShield(_Player, _Shield, _CombatShield, 1);

PROC
LLWEAPONEX_DualShields_FindCombatShield((CHARACTERGUID)_Player, (ITEMGUID)_Shield, 0)
AND
GetUUID(_Shield, _ID)
AND
StringConcatenate("LLWEAPONEX_FindCombatShield_IteratorDone_", _ID, _GlobalFlag)
THEN
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
InventoryLaunchTagIterator(_Shield, "LLWEAPONEX_CombatShield", "", "LLWEAPONEX_Iterators_FoundCombatShield", _GlobalFlag);

IF
StoryEvent((ITEMGUID)_CombatShield, "LLWEAPONEX_Iterators_FoundDualShield_Equip")
AND
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_Iterators_FoundDualShield] Found combat shield in dual shield inventory. Setting variable.");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);

IF
GlobalFlagSet(_GlobalFlag)
AND
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Player, _Shield, _GlobalFlag);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:",_GlobalFlag,"] Combat shield not found. Regenerating.");
LLWEAPONEX_DUalShieldS_Internal_GenerateCombatShield(_Shield);

/*
IF
ItemEquipped(_CombatShield, _Player)
AND
IsTagged(_CombatShield, "LLWEAPONEX_CombatShield", 1)
THEN
LLWEAPONEX_DualShields_OnCombatShieldEquipped(_Player, _CombatShield);

PROC
LLWEAPONEX_DualShields_OnCombatShieldEquipped((CHARACTERGUID)_Player, (ITEMGUID)_CombatShield)
AND
CharacterGetEquippedShield(_Player, (ITEMGUID)_Shield)
AND
LLWEAPONEX_DualShields_QRY_ItemLevelSet(_Shield)
AND
GetVarInteger(_Shield, "LeaderLib_Level", _Level)
THEN
LeaderLog_LogInt("DEBUG", "[LLWEAPONEX:DualShields:OnCombatShieldEquipped] Shield is level [", _Level, "]. Leveling up Combat Shield.");
ItemLevelUpTo(_CombatShield, _Level);

IF
StoryEvent((ITEMGUID)_Shield, "LLWEAPONEX_Commands_LevelUpCombatShield")
AND
GetVarObject(_Shield, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
AND
GetVarInteger(_Shield, "LeaderLib_Level", _Level)
THEN
ItemLevelUpTo(_CombatShield, _Level);
*/

IF
ItemUnEquipped(_Shield, _Player)
AND
IsTagged(_Shield, "LLWEAPONEX_DualShields", 1)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _)
AND
GetVarObject(_Shield, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
AND
CharacterGetEquippedWeapon(_Player, _CombatShield)
THEN
DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _CombatShield);
ItemToInventory(_CombatShield, _Shield);

IF
ItemUnEquipped(_CombatShield, _Player)
AND
IsTagged(_CombatShield, "LLWEAPONEX_CombatShield", 1)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _, _CombatShield)
AND
CharacterGetEquippedShield(_Player, (ITEMGUID)_Shield)
AND
_Shield != NULL_00000000-0000-0000-0000-000000000000
THEN 
DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _CombatShield);
CharacterUnequipItem(_Player, _Shield);
ItemToInventory(_CombatShield, _Shield);

IF
DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _CombatShield)
THEN
LeaderLib_StartCharacterItemTimer(_Player, _Shield, 150, "LLWEAPONEX_Timers_ResetDualShieldHandled", "LLWEAPONEX_DualShields_ResetHandled");

IF
CharacterItemEvent(_Player, _Shield, "LLWEAPONEX_DualShields_ResetHandled")
AND
DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_Handled(_Player, _Shield, _CombatShield);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"