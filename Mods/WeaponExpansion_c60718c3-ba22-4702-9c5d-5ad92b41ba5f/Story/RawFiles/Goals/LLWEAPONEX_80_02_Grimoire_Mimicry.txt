Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION LAST_SKILL
IF
CharacterUsedSkill(_Character, _Skill, _SkillType, _SkillElement)
AND
IsTagged(_Character, "LeaderLib_Dummy", 0)
AND
LLWEAPONEX_Grimoire_QRY_CanMimicSkill(_Skill, _SkillType, _SkillElement)
THEN
SetVarString(_Character, "LLWEAPONEX_LastUsedSkill", _Skill);
SetVarString(_Character, "LLWEAPONEX_LastUsedSkill_Type", _SkillType);
SetVarString(_Character, "LLWEAPONEX_LastUsedSkill_Element", _SkillElement);

//In case a copied skill is learned
IF
SkillAdded(_Character, _Skill, 1)
AND
NOT DB_LLWEAPONEX_Grimoire_SkillJustCopied(_Character, _Skill)
AND
DB_LLWEAPONEX_Grimoire_CopiedSkill(_Character, _Skill)
THEN
NOT DB_LLWEAPONEX_Grimoire_CopiedSkill(_Character, _Skill);

IF
SkillAdded(_Character, _Skill, _)
AND
DB_LLWEAPONEX_Grimoire_SkillJustCopied(_Character, _Skill)
THEN
DB_LLWEAPONEX_Grimoire_CopiedSkill(_Character, _Skill);
NOT DB_LLWEAPONEX_Grimoire_SkillJustCopied(_Character, _Skill);

//Invalid skill safety check
IF
DB_LLWEAPONEX_Grimoire_SkillJustCopied(_Character, _Skill)
AND
CharacterHasSkill(_Character, _Skill, 0)
THEN
NOT DB_LLWEAPONEX_Grimoire_SkillJustCopied(_Character, _Skill);

IF
CharacterUsedSkill(_Character, _Skill, _, _)
AND
DB_LLWEAPONEX_Grimoire_CopiedSkill(_Character, _Skill)
THEN
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_RemoveCopiedSkill", 2000);

IF
SkillCast(_Character, _Skill, _, _)
AND
DB_LLWEAPONEX_Grimoire_CopiedSkill(_Character, _Skill)
AND
DB_ObjectTimer(_Character, _ObjectTimerName, "LLWEAPONEX_Timers_RemoveCopiedSkill")
THEN
ProcObjectTimerCancel(_Character, "LLWEAPONEX_Timers_RemoveCopiedSkill");
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_RemoveCopiedSkill", 2000);
//END_REGION

//REGION MIMICKING
IF
CharacterUsedSkillOnZoneWithTarget(_Character, _Target, _Skill, _SkillType, _SkillElement)
AND
HasActiveStatus(_Character, "LLWEAPONEX_GRIMOIRE_MIMIC_READY", 1)
AND
NOT LLWEAPONEX_Grimoire_QRY_MimicSkillQueued(_Character)
AND
LLWEAPONEX_Grimoire_QRY_CanMimicSkill(_Skill, _SkillType, _SkillElement)
THEN
LLWEAPONEX_Grimoire_Internal_AddMimicSkill(_Character, _Skill, _Target);

IF
CharacterUsedSkillOnTarget(_Character, _Target, _Skill, _SkillType, _SkillElement)
AND
HasActiveStatus(_Character, "LLWEAPONEX_GRIMOIRE_MIMIC_READY", 1)
AND
NOT LLWEAPONEX_Grimoire_QRY_MimicSkillQueued(_Character)
AND
LLWEAPONEX_Grimoire_QRY_CanMimicSkill(_Skill, _SkillType, _SkillElement)
THEN
LLWEAPONEX_Grimoire_Internal_AddMimicSkill(_Character, _Skill, _Target);

IF
CharacterUsedSkill(_Character, _Skill, "shout", _SkillElement)
AND
HasActiveStatus(_Character, "LLWEAPONEX_GRIMOIRE_MIMIC_READY", 1)
AND
NOT LLWEAPONEX_Grimoire_QRY_MimicSkillQueued(_Character)
AND
LLWEAPONEX_Grimoire_QRY_CanMimicSkill(_Skill, "shout", _SkillElement)
THEN
LLWEAPONEX_Grimoire_Internal_AddMimicSkill(_Character, _Skill, _Character);

IF
CharacterUsedSkillAtPosition(_Character, _x, _y, _z, _Skill, _SkillType, _SkillElement)
AND
HasActiveStatus(_Character, "LLWEAPONEX_GRIMOIRE_MIMIC_READY", 1)
AND
NOT LLWEAPONEX_Grimoire_QRY_MimicSkillQueued(_Character)
AND
NOT DB_LLWEAPONEX_Grimoire_MimicSkillPosition(_Character, _Skill, _x, _y, _z)
AND
LLWEAPONEX_Grimoire_QRY_CanMimicSkill(_Skill, _SkillType, _SkillElement)
THEN
DB_LLWEAPONEX_Grimoire_MimicSkillPosition(_Character, _Skill, _x, _y, _z);
ProcObjectTimerCancel(_Character, "LLWEAPONEX_Timers_Grimoire_MimicSkillPosition");
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_Grimoire_MimicSkillPosition", 50);

IF
SkillCast(_Grimoire, _, _, _)
AND
DB_LLWEAPONEX_Grimoires_Temp_CastingSkill(_Character, _Grimoire)
THEN
ProcObjectTimerCancel(_Grimoire, "LLWEAPONEX_Timers_RemoveSkillGrimoire");
ProcObjectTimer(_Grimoire, "LLWEAPONEX_Timers_RemoveSkillGrimoire", 1500);
LLWEAPONEX_Grimoire_Internal_RemoveSkillPlatform(_Grimoire);

IF
CharacterUsedSkill(_Grimoire, _, _, _)
AND
DB_LLWEAPONEX_Grimoires_Temp_CastingSkill(_Caster, _Grimoire)
THEN
PROC_StopLoopEffect(_Caster, "LLWEAPONEX.Grimoires.FX.MimicSkill.Loop");
LLWEAPONEX_Grimoire_Internal_RemoveSkillPlatform(_Grimoire);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"