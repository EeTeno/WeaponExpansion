Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//REGION MIMICRY_GRIMOIRE_LAST_SKILL
PROC
LLWEAPONEX_Grimoire_OnGrimoireUnEquipped((CHARACTERGUID)_Character, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_Grimoire_Mimicry_c9fcc947-4423-40b9-9629-ae2d1e156dfc")
AND
DB_LLWEAPONEX_Grimoire_CopiedSkill(_Character, _Skill)
THEN
NOT DB_LLWEAPONEX_Grimoire_CopiedSkill(_Character, _Skill);
CharacterRemoveSkill(_Character, _Skill);

PROC
LLWEAPONEX_Grimoire_RemoveCopiedSkill((CHARACTERGUID)_Character)
AND
DB_LLWEAPONEX_Grimoire_CopiedSkill(_Character, _Skill)
THEN
NOT DB_LLWEAPONEX_Grimoire_CopiedSkill(_Character, _Skill);
CharacterRemoveSkill(_Character, _Skill);

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_COPYLASTSKILL", (CHARACTERGUID)_Character)
AND
GetVarString(_Target, "LLWEAPONEX_LastUsedSkill", _Skill)
AND
GetVarString(_Target, "LLWEAPONEX_LastUsedSkill_Type", _SkillType)
AND
GetVarString(_Target, "LLWEAPONEX_LastUsedSkill_Element", _SkillElement)
AND
LLWEAPONEX_Grimoire_QRY_CanMimicSkill(_Skill, _SkillType, _SkillElement)
AND
CharacterHasSkill(_Character, _Skill, 0)
THEN
LLWEAPONEX_Grimoire_RemoveCopiedSkill(_Character);
CharacterAddSkill(_Character, _Skill);
DB_LLWEAPONEX_Grimoire_SkillJustCopied(_Character, _Skill);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_RemoveCopiedSkill")
THEN
LLWEAPONEX_Grimoire_RemoveCopiedSkill(_Character);
//END_REGION

//REGION GRIMOIRE_MIMIC
/*
IF
ObjectTurnEnded(_Character)
AND
HasActiveStatus(_Character, "LLWEAPONEX_GRIMOIRE_MIMIC_READY", 1)
THEN
RemoveStatus(_Character, "LLWEAPONEX_GRIMOIRE_MIMIC_READY");
*/
PROC
LLWEAPONEX_Grimoire_Internal_AddMimicSkill((CHARACTERGUID)_Character, (STRING)_Skill, (GUIDSTRING)_Target)
AND
HasActiveStatus(_Character, "UNSHEATHED", _Unsheathed)
THEN
DB_LLWEAPONEX_Grimoire_MimicSkill(_Character, _Skill, _Target);
LLWEAPONEX_Grimoire_StartMimicking(_Character, _Skill, _Target);
LLWEAPONEX_Grimoire_Internal_StartMimicEffects(_CHaracter, _Unsheathed);

PROC
LLWEAPONEX_Grimoire_Internal_StartMimicEffects((CHARACTERGUID)_Character, 0)
THEN
PROC_StopLoopEffect(_Character, "LLWEAPONEX.Grimoires.FX.MimicSkill.Loop");
PROC_LoopEffect("LLWEAPONEX_FX_Skills_Grimoire_MimicSkill_Prepare_Root_01", _Character, "LLWEAPONEX.Grimoires.FX.MimicSkill.Loop", "__ANY__", "Dummy_BackFX_L");

PROC
LLWEAPONEX_Grimoire_Internal_StartMimicEffects((CHARACTERGUID)_Character, 1)
THEN
PROC_StopLoopEffect(_Character, "LLWEAPONEX.Grimoires.FX.MimicSkill.Loop");
PROC_LoopEffect("LLWEAPONEX_FX_Skills_Grimoire_MimicSkill_Prepare_Root_01", _Character, "LLWEAPONEX.Grimoires.FX.MimicSkill.Loop", "__ANY__", "Dummy_FrontFX_L");

QRY
LLWEAPONEX_Grimoire_QRY_CanMimicSkill((STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
_Skill != "Target_LLWEAPONEX_CopyLastSkill"
AND
IsSourceSkill(_Skill, 0)
AND
LeaderUpdater_QRY_ModIsActive("Mimicry", "LaughingLeader")
AND
LLMIME_Mimicking_QRY_SkillIsAllowed(_Skill, _SkillType, _SkillElement)
THEN
DB_NOOP(1);

//Never true, but needed so we can use Mimicry's query.
QRY
LLMIME_Mimicking_QRY_SkillIsAllowed((STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
DB_GlobalFlag("LLWEAPONEX_Grimoire_MimicBlacklistDisabled")
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Grimoire_QRY_CanMimicSkill((STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
_Skill != "Target_LLWEAPONEX_CopyLastSkill"
AND
IsSourceSkill(_Skill, 0)
AND
NOT LeaderUpdater_QRY_ModIsActive("Mimicry", "LaughingLeader")
AND
StringContains(_Skill, "_Quest_", 0)
AND
LLWEAPONEX_Grimoire_QRY_MimicryDisabled_SkillIsAllowed(_Skill, _SkillType, _SkillElement)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Grimoire_QRY_MimicryDisabled_SkillIsAllowed((STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
DB_LLMIME_Mimicking_Whitelist_Skills(_Skill)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Grimoire_QRY_MimicryDisabled_SkillIsAllowed((STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
NOT DB_LLMIME_Mimicking_Blacklist_Skills(_, _Skill)
AND
NOT DB_LLMIME_Mimicking_Blacklist_SkillElements(_SkillType, _SkillElement)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Grimoire_QRY_MimicSkillQueued((CHARACTERGUID)_Character)
AND
DB_LLWEAPONEX_Grimoire_MimicSkill(_Character, _Skill, _Target)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Grimoire_QRY_MimicSkillQueued((CHARACTERGUID)_Character)
AND
DB_LLWEAPONEX_Grimoire_MimicSkill(_Character, _Grimoire, _Skill, _Target)
THEN
DB_NOOP(1);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Grimoire, "LLWEAPONEX_Timers_Grimoire_MimicSkillPosition")
AND
DB_LLWEAPONEX_Grimoire_MimicSkillPosition(_Character, _Skill, _x, _y, _z)
AND
LLWEAPONEX_Grimoire_QRY_MimicSkillQueued(_Character)
THEN
NOT DB_LLWEAPONEX_Grimoire_MimicSkillPosition(_Character, _Skill, _x, _y, _z);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Grimoire, "LLWEAPONEX_Timers_Grimoire_MimicSkillPosition")
AND
DB_LLWEAPONEX_Grimoire_MimicSkillPosition(_Character, _Skill, _x, _y, _z)
AND
NOT LLWEAPONEX_Grimoire_QRY_MimicSkillQueued(_Character)
AND
GetUUID(_Character, _ID)
AND
LeaderLib_Skills_QRY_CreateTargetDummy(_ID, _x, _y, _z, "", 5000, 1, 0)
AND
DB_LeaderLib_Skills_TargetDummies(_ID, _Target)
THEN
NOT DB_LLWEAPONEX_Grimoire_MimicSkillPosition(_Character, _Skill, _x, _y, _z);
LLWEAPONEX_Grimoire_Internal_AddMimicSkill(_Character, _Skill, _Target);

PROC
LLWEAPONEX_Grimoire_StartMimicking((CHARACTERGUID)_Character, (STRING)_Skill, (GUIDSTRING)_Target)
AND
DB_LLWEAPONEX_Grimoire_MimicSkill(_Character, _Skill, _Target)
AND
LLWEAPONEX_Grimoire_GetGrimoirePosition(_Character)
AND
DB_LLWEAPONEX_Grimoire_Temp_GrimoirePosition(_Character, _x, _y, _z)
AND
TemporaryCharacterCreateAtPosition(_x, _y, _z, "LLWEAPONEX_Grimoire_Dummy_9e0450df-20bf-419d-86b6-18e5ba8a8944", 0, _Grimoire)
AND
GetUUID(_Grimoire, _UUID)
AND
LeaderLIb_Platforms_QRY_SpawnPlatform(_UUID, "Small", 5000, _x, _y, _z)
AND
RealProduct(_y, 2.0, _ty)
THEN
NOT DB_LLWEAPONEX_Grimoire_Temp_GrimoirePosition(_Character, _x, _y, _z);
NOT DB_LLWEAPONEX_Grimoire_MimicSkill(_Character, _Skill, _Target);
DB_LLWEAPONEX_Grimoire_MimicSkill(_Character, _Grimoire, _Skill, _Target);
LeaderLib_Helper_SetupDummy(_Grimoire);
//CharacterTransformFromCharacter(_Grimoire, _Character, 0, 0, 1, 1, 0, 0, 0);
LeaderLib_Helper_CopyEquipment(_Grimoire, _Character, 1);
LeaderLib_Helper_CopyStats(_Grimoire, _Character, 1, 1, 1, 1);
LeaderLib_Helper_CopyFaction(_Grimoire, _Character);
SetVarObject(_Grimoire, "LLWEAPONEX_Grimoire_Owner", _Character);
TeleportToPosition(_Grimoire, _x, _ty, _z, "", 0, 0);
CharacterMoveToPosition(_Grimoire, _x, _ty, _z, 1, "");

PROC
LeaderLib_Helper_OnCopyEquipmentFinished((CHARACTERGUID)_Grimoire, (CHARACTERGUID)_Character)
AND
DB_LLWEAPONEX_Grimoire_MimicSkill(_Character, _Grimoire, _Skill, _Target)
THEN
ProcObjectTimerCancel(_Grimoire, "LLWEAPONEX_Timers_CastGrimoireMimicSkill");
ProcObjectTimer(_Grimoire, "LLWEAPONEX_Timers_CastGrimoireMimicSkill", 50);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Grimoire, "LLWEAPONEX_Timers_CastGrimoireMimicSkill")
AND
DB_LLWEAPONEX_Grimoire_MimicSkill(_Character, _Grimoire, _Skill, _Target)
THEN
NOT DB_LLWEAPONEX_Grimoire_MimicSkill(_Character, _Grimoire, _Skill, _Target);
//LeaderLib_Skills_UseSkillOnTarget(_Character, _Target, _Skill, 1, 0);
//LeaderLib_Skills_UseSkillOnTargetFromPosition(_Caster, _Target, _Skill, _x, _y, _z, 1, 0);
DB_LLWEAPONEX_Grimoires_Temp_CastingSkill(_Character, _Grimoire);
ProcObjectTimer(_Grimoire, "LLWEAPONEX_Timers_RemoveSkillGrimoire", 4000);
CharacterUseSkill(_Grimoire, _Skill, _Target, 1, 1, 1);
RemoveStatus(_Character, "LLWEAPONEX_GRIMOIRE_MIMIC_READY");

PROC
LLWEAPONEX_Grimoire_Internal_RemoveSkillPlatform((CHARACTERGUID)_Grimoire)
AND
GetUUID(_Grimoire, _UUID)
AND
DB_LeaderLib_Platforms(_UUID, _Platform)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Grimoire:RemoveSkillPlatform] Removed mimicked skill platform.");
LeaderLib_Platforms_RemovePlatform(_Platform);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Grimoire, "LLWEAPONEX_Timers_RemoveSkillGrimoire")
AND
DB_LLWEAPONEX_Grimoires_Temp_CastingSkill(_Caster, _Grimoire)
THEN
NOT DB_LLWEAPONEX_Grimoires_Temp_CastingSkill(_Caster, _Grimoire);
RemoveTemporaryCharacter(_Grimoire);
PROC_StopLoopEffect(_Caster, "LLWEAPONEX.Grimoires.FX.MimicSkill.Loop");
LLWEAPONEX_Grimoire_Internal_RemoveSkillPlatform(_Grimoire);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Grimoire:LLWEAPONEX_Timers_RemoveSkillGrimoire] Cleaning up mimic dummy and stopping effects.");

QRY
LLWEAPONEX_Grimoire_GetGrimoirePosition((CHARACTERGUID)_Caster)
AND
HasActiveStatus(_Caster, "UNSHEATHED", _Unsheathed)
AND
DB_LLWEAPONEX_Grimoire_PositionOffset(_Unsheathed, _BoneX, _BoneZ)
AND
GetPosition(_Caster, _cx, _y, _cz)
AND
GetRotation(_Caster, _, _ry, _)
AND
Integer(_ry, _AngleInt)
AND
LeaderLib_Math_QRY_RotatePoint(_BoneX, _BoneZ, _ry, 0)
AND
DB_LeaderLib_Math_RotatedPoint(_BoneX, _BoneZ, _ry, _x2, _z2)
AND
RealSum(_cx, _x2, _x)
AND
RealSubtract(_cz, _z2, _z)
THEN
NOT DB_LeaderLib_Math_RotatedPoint(_BoneX, _BoneZ, _ry, _x2, _z2);
DB_LLWEAPONEX_Grimoire_Temp_GrimoirePosition(_Caster, _x, _y, _z);
//END_REGION

//REGION DEBUG

//oe llweap_grimoiredebug_castskillself Shout_Whirlwind
IF
TextEventSet("llweap_grimoiredebug_castskillself")
AND
GetTextEventParamString(1, _Skill)
AND
CharacterGetHostCharacter(_Character)
THEN
LLWEAPONEX_Grimoire_Internal_AddMimicSkill(_Character, _Skill, _Character);

//oe llweap_grimoiredebug_castskilltarget Projectile_Fireball -10 0 0
//oe llweap_grimoiredebug_castskilltarget Projectile_PyroclasticRock -10 0 0
IF
TextEventSet("llweap_grimoiredebug_castskilltarget")
AND
GetTextEventParamString(1, _Skill)
AND
GetTextEventParamReal(2, _xSum)
AND
GetTextEventParamReal(3, _ySum)
AND
GetTextEventParamReal(4, _zSum)
AND
CharacterGetHostCharacter(_Character)
AND
GetPosition(_Character, _x, _y, _z)
AND
RealSum(_x, _xSum, _tx)
AND
RealSum(_y, _ySum, _ty)
AND
RealSum(_z, _zSum, _tz)
AND
GetUUID(_Character, _ID)
AND
LeaderLib_Skills_QRY_CreateTargetDummy(_ID, _tx, _ty, _tz, "", 5000, 1, 0)
AND
DB_LeaderLib_Skills_TargetDummies(_ID, _Target)
THEN
LLWEAPONEX_Grimoire_Internal_AddMimicSkill(_Character, _Skill, _Target);

IF
TextEventSet("llweap_grimoiredebug_setoffset")
AND
DB_LLWEAPONEX_Grimoire_PositionOffset(_Unsheathed, _BoneX, _BoneZ)
THEN
NOT DB_LLWEAPONEX_Grimoire_PositionOffset(_Unsheathed, _BoneX, _BoneZ);

IF
TextEventSet("llweap_grimoiredebug_setoffset")
AND
GetTextEventParamReal(1, _BoneBackX)
AND
GetTextEventParamReal(2, _BoneBackZ)
AND
GetTextEventParamReal(3, _BoneFrontX)
AND
GetTextEventParamReal(4, _BoneFrontZ)
THEN
DB_LLWEAPONEX_Grimoire_PositionOffset(0, _BoneBackX, _BoneBackZ);
DB_LLWEAPONEX_Grimoire_PositionOffset(1, _BoneFrontX, _BoneFrontZ);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"