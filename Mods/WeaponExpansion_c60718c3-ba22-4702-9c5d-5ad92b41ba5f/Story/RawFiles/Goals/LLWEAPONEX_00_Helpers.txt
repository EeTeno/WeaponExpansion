Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION QUERIES
/*
QRY
LLWEAPONEX_QRY_IgnoreWeapon((ITEMGUID)_Weapon)
AND
IsTagged(_Weapon, "LLMIME_MIMICKED_WEAPON", 1)
THEN
DB_NOOP(1);
*/

QRY
LLWEAPONEX_QRY_ItemIsWeapon((ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "Shield", 0)
AND
StringContains(_Template, "WPN_", 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_ItemIsWeapon((ITEMGUID)_Item, (STRING)_Template)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
StringContains(_Template, "Shield", 0)
AND
StringContains(_Template, "WPN_", 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_ItemIsWeaponOrShield((ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "WPN_", 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_ItemIsWeaponOrShield((ITEMGUID)_Item, (STRING)_Template)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
StringContains(_Template, "WPN_", 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_CharacterIsUnarmed((CHARACTERGUID)_Player)
AND
CharacterGetEquippedItem(_Player, "Weapon", NULL_00000000-0000-0000-0000-000000000000)
AND
CharacterGetEquippedItem(_Player, "Shield", NULL_00000000-0000-0000-0000-000000000000)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_WeaponTypeEquipped((CHARACTERGUID)_Player, (STRING)_WeaponType)
AND
CharacterGetEquippedItem(_Player, "Weapon", _Item)
AND
IsTagged(_Item, _WeaponType, 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_WeaponTypeEquipped((CHARACTERGUID)_Player, (STRING)_WeaponType)
AND
CharacterGetEquippedItem(_Player, "Shield", _Item)
AND
IsTagged(_Item, _WeaponType, 1)
THEN
DB_NOOP(1);

//IsTagged(_Item, "LLWEAPONEX_WeaponMasteryComplete", 0)
QRY
LLWEAPONEX_QRY_WeaponTypeMasteryEnabled((CHARACTERGUID)_Player, (STRING)_WeaponType)
AND
CharacterGetEquippedItem(_Player, "Weapon", _Item)
AND
IsTagged(_Item, _WeaponType, 1)
AND
IsTagged(_Item, "LLWEAPONEX_WeaponMasteryDisabled", 0)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_WeaponTypeMasteryEnabled((CHARACTERGUID)_Player, (STRING)_WeaponType)
AND
CharacterGetEquippedItem(_Player, "Shield", _Item)
AND
IsTagged(_Item, _WeaponType, 1)
AND
IsTagged(_Item, "LLWEAPONEX_WeaponMasteryDisabled", 0)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_HasWeaponExpansionWeapon((CHARACTERGUID)_Character)
AND
CharacterGetEquippedItem(_Character, "Weapon", _Weapon)
AND
CharacterGetEquippedItem(_Character, "Shield", _Shield)
AND
GetTemplate(_Weapon, _WeaponTemplate)
AND
StringContains(_WeaponTemplate, "LLWEAPONEX", 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_HasWeaponExpansionWeapon((CHARACTERGUID)_Character)
AND
CharacterGetEquippedItem(_Character, "Shield", _Weapon)
AND
GetTemplate(_Weapon, _WeaponTemplate)
AND
StringContains(_WeaponTemplate, "LLWEAPONEX", 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_IsEquippedWeapon((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
CharacterGetEquippedItem(_Player, "Weapon", _Item)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_IsEquippedWeapon((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
CharacterGetEquippedItem(_Player, "Shield", _Item)
THEN
DB_NOOP(1);
//END_REGION

//REGION BEHAVIOR_HELPERS
QRY
LLWEAPONEX_QRY_SetItemAmount((ITEMGUID)_Item)
THEN
LLWEAPONEX_SetItemAmount(_Item);

PROC
LLWEAPONEX_SetItemAmount((ITEMGUID)_Item)
AND
NOT ItemGetAmount(_Item, _)
THEN
SetVarInteger(_Item, "LLWEAPONEX_ItemAmount", 1);

PROC
LLWEAPONEX_SetItemAmount((ITEMGUID)_Item)
AND
ItemGetAmount(_Item, _Amount)
THEN
SetVarInteger(_Item, "LLWEAPONEX_ItemAmount", _Amount);

PROC
LLWEAPONEX_SetItemAmount((ITEMGUID)_Item, (INTEGER)_Amount)
THEN
SetVarInteger(_Item, "LLWEAPONEX_ItemAmount", _Amount);

IF
StoryEvent((ITEMGUID)_Item, "LLWEAPONEX_SetItemAmount")
THEN
LLWEAPONEX_SetItemAmount(_Item);
//END_REGION

//REGION CHARACTER_CREATION
IF
RegionStarted(_Level)
AND
IsCharacterCreationLevel(_Level, 1)
THEN
DB_LLWEAPONEX_PostCCEnabled(1);

IF
GameStarted(_Level, _)
AND
DB_LLWEAPONEX_PostCCEnabled(1)
AND
IsGameLevel(_Level, 1)
THEN
TimerCancel("LLWEAPONEX_Timers_PostCCTimer_ResetDB");
TimerLaunch("LLWEAPONEX_Timers_PostCCTimer_ResetDB", 500);

IF
TimerFinished("LLWEAPONEX_Timers_PostCCTimer_ResetDB")
AND
DB_IsPlayer(_Player)
THEN
LLWEAPONEX_OnCharacterCreationFinished(_Player);

//Some PostCC scripts have rules for when an item is equipped, so re-equip WEAPONEX weapons after CC
PROC
LLWEAPONEX_OnCharacterCreationFinished((CHARACTERGUID)_Player)
AND
LLWEAPONEX_QRY_HasWeaponExpansionWeapon(_Player)
THEN
LeaderLib_Helper_RefreshWeapons(_Player, 250, 500);

IF
TimerFinished("LLWEAPONEX_Timers_PostCCTimer_ResetDB")
AND
DB_LLWEAPONEX_PostCCEnabled(1)
THEN
NOT DB_LLWEAPONEX_PostCCEnabled(1);
//END_REGION

//REGION SET_OVERRIDES
PROC
LLWEAPONEX_AnimationSetOverride_Set((CHARACTERGUID)_Player, (STRING)_OverrideSet)
THEN
LLWEAPONEX_AnimationSetOverride_ClearData(_Player);
CharacterSetAnimationSetOverride(_Player, _OverrideSet);
DB_LLWEAPONEX_Temp_AnimationSetOverride(_Player, _OverrideSet);

PROC
LLWEAPONEX_AnimationSetOverride_Clear((CHARACTERGUID)_Player)
THEN
LLWEAPONEX_AnimationSetOverride_ClearData(_Player);
CharacterSetAnimationSetOverride(_Player, "");

PROC
LLWEAPONEX_AnimationSetOverride_ClearData((CHARACTERGUID)_Player)
AND
DB_LLWEAPONEX_Temp_AnimationSetOverride(_Player, _OverrideSet)
THEN
NOT DB_LLWEAPONEX_Temp_AnimationSetOverride(_Player, _OverrideSet);

// Called when exiting the respec mirror
// Since the respec mirror uses its own set override, we need to reset this
PROC
Proc_HomesteadTeleportAfterMirror((CHARACTERGUID)_Player,(ITEMGUID)_Mirror,(TRIGGERGUID)_Trigger)
AND
DB_LLWEAPONEX_Temp_AnimationSetOverride(_Player, _OverrideSet)
THEN
CharacterSetAnimationSetOverride(_Player, _OverrideSet);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_WeaponExpansion"