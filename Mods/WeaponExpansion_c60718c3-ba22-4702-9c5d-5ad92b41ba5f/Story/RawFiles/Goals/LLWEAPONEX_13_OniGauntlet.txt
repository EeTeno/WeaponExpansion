Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_OniGauntlet_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLWEAPONEX_OniGauntlet_InitSettings()
THEN
//When this template reaches the minimum level, an upgrade will be available
//Template, MinLevel
DB_LLWEAPONEX_OniGauntlet_Upgrades("EQ_UNIQUE_LLWEAPONEX_OniGauntlet_Arms_A_1_27ac5dab-00a7-4013-9568-2d669b581939", 8);
DB_LLWEAPONEX_OniGauntlet_Upgrades("EQ_UNIQUE_LLWEAPONEX_OniGauntlet_Arms_A_2_32ca983f-3c37-4531-9e7a-597f6733cefe", 13);

DB_LLWEAPONEX_OniGauntlet_StartingWeapons("Shout_LLWEAPONEX_OniGauntlet_Summon_Raizan", "WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Raizan_1_b1320940-1afb-479a-b510-875afdb12d41", "Raizan");

DB_LLWEAPONEX_OniGauntlet_Weapons("WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Raizan_1_b1320940-1afb-479a-b510-875afdb12d41", "Raizan", 33);
DB_LLWEAPONEX_OniGauntlet_Weapons("WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Raizan_2_e03787b9-acac-4563-953e-a1149a6ebec1", "Raizan", 66);
DB_LLWEAPONEX_OniGauntlet_Weapons("WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Raizan_3_fa2345b0-68f5-443f-8665-d2a5991bbf29", "Raizan", 99);

DB_LLWEAPONEX_OniGauntlet_Orbs("LOOT_LLWEAPONEX_Orb_Thunder_A_dc34fbef-a647-4eea-a313-8ef1e859ce82", "Shout_LLWEAPONEX_OniGauntlet_Summon_Raizan", "Raizan", 15);
DB_LLWEAPONEX_OniGauntlet_Orbs("LOOT_LLWEAPONEX_Orb_Thunder_B_9fe7b80d-081d-4e5f-8125-10a452c2d105", "Shout_LLWEAPONEX_OniGauntlet_Summon_Raizan", "Raizan", 30);
DB_LLWEAPONEX_OniGauntlet_Orbs("LOOT_LLWEAPONEX_Orb_Thunder_C_5f9803c3-b8a3-4e23-8fdb-45aa75962e94", "Shout_LLWEAPONEX_OniGauntlet_Summon_Raizan", "Raizan", 60);

//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_OldVersion, 0,9,4,2)
THEN
LLWEAPONEX_OniGauntlet_InitSettings();
//END_REGION

//REGION ONI_GAUNTLET_EQUIPPING
IF
ItemTemplateUnEquipped("EQ_UNIQUE_LLWEAPONEX_OniGauntlet_Arms_A_1_27ac5dab-00a7-4013-9568-2d669b581939", _Player)
THEN
LLWEAPONEX_OniGauntlet_Internal_OnOniGauntletUnEquipped_Start(_Player);

IF
ItemTemplateUnEquipped("EQ_UNIQUE_LLWEAPONEX_OniGauntlet_Arms_A_2_32ca983f-3c37-4531-9e7a-597f6733cefe", _Player)
THEN
LLWEAPONEX_OniGauntlet_Internal_OnOniGauntletUnEquipped_Start(_Player);

IF
ItemTemplateUnEquipped("EQ_UNIQUE_LLWEAPONEX_OniGauntlet_Arms_A_3_79899ff9-6bc8-41cf-8a85-839167e2150a", _Player)
THEN
LLWEAPONEX_OniGauntlet_Internal_OnOniGauntletUnEquipped_Start(_Player);

IF
ItemTemplateEquipped("EQ_UNIQUE_LLWEAPONEX_OniGauntlet_Arms_A_1_27ac5dab-00a7-4013-9568-2d669b581939", _Player)
AND
CharacterGetEquippedItem(_Player, "Gloves", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
LLWEAPONEX_OniGauntlet_OnOniGauntletEquipped(_Player, _Item);

IF
ItemTemplateEquipped("EQ_UNIQUE_LLWEAPONEX_OniGauntlet_Arms_A_2_32ca983f-3c37-4531-9e7a-597f6733cefe", _Player)
AND
CharacterGetEquippedItem(_Player, "Gloves", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
LLWEAPONEX_OniGauntlet_OnOniGauntletEquipped(_Player, _Item);

IF
ItemTemplateEquipped("EQ_UNIQUE_LLWEAPONEX_OniGauntlet_Arms_A_3_79899ff9-6bc8-41cf-8a85-839167e2150a", _Player)
AND
CharacterGetEquippedItem(_Player, "Gloves", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
LLWEAPONEX_OniGauntlet_OnOniGauntletEquipped(_Player, _Item);

PROC
LLWEAPONEX_OniGauntlet_OnOniGauntletEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_Player, _OldItem)
THEN
NOT DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_Player, _OldItem);

PROC
LLWEAPONEX_OniGauntlet_OnOniGauntletEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item)
THEN
DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_Player, _Item);
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_OniGauntletEquipped", "WeaponExpansion");

PROC
LLWEAPONEX_OniGauntlet_OnOniGauntletEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
NOT DB_LLWEAPONEX_OniGauntlet_Temp_ActiveOrb(_Item, _, _, _)
AND
ItemGetRuneItemTemplate(_Item, 0, _Template)
AND
DB_LLWEAPONEX_OniGauntlet_Orbs(_Template, _Skill, _ID, _MinSouls)
THEN
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveOrb(_Item, _Skill, _Template, 0);

PROC
LLWEAPONEX_OniGauntlet_Internal_OnOniGauntletUnEquipped_Start((CHARACTERGUID)_Player)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_Player, _Item)
THEN
NOT DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_Player, _Item);
LLWEAPONEX_OniGauntlet_OnOniGauntletUnEquipped(_Player, _Item);

PROC
LLWEAPONEX_OniGauntlet_Internal_OnOniGauntletUnEquipped_Start((CHARACTERGUID)_Player)
AND
NOT DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_,_)
THEN
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_OniGauntletEquipped", "WeaponExpansion", 500);
//END_REGION

//REGION ONI_GAUNTLET_SOULS
QRY
LLWEAPONEX_OniGauntlet_QRY_HasMinSouls((ITEMGUID)_Gauntlet, (INTEGER)_MinSouls)
AND
ItemGetCharges(_Gauntlet, _Current)
AND
_Current >= _MinSouls
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_OniGauntlet_AddSouls((CHARACTERGUID)_Player, (ITEMGUID)_Gauntlet, (INTEGER)_Souls)
AND
ObjectGetFlag(_Gauntlet, "LLWEAPONEX_OniGaunlet_UpgradeAvailable", 0)
AND
ItemGetCharges(_Gauntlet, _Current)
AND
ItemGetMaxCharges(_Gauntlet, _Max)
AND
_Current < _Max
AND
IntegerSum(_Souls, _Current, _Next)
AND
IntegerMin(_Next, _Max, _ClampedNext)
THEN
ItemAddCharges(_Gauntlet, _Souls);
LLWEAPONEX_OniGauntlet_OnSoulsAdded(_Player, _Gauntlet, _ClampedNext, _Max);

PROC
LLWEAPONEX_OniGauntlet_OnSoulsAdded((CHARACTERGUID)_Player, (ITEMGUID)_Gauntlet, (INTEGER)_CurrentSouls, (INTEGER)_MaxSouls)
AND
_CurrentSouls >= _MaxSouls
AND
GetTemplate(_Gauntlet, _Template)
THEN
ObjectClearFlag(_Gauntlet, "LLWEAPONEX_OniGaunlet_UpgradeAvailable");
LLWEAPONEX_OniGauntlet_OnMaxSoulsReached(_Player, _Gauntlet, _Template);

PROC
LLWEAPONEX_OniGauntlet_OnMaxSoulsReached((CHARACTERGUID)_Player, (ITEMGUID)_Gauntlet, (STRING)_Template)
AND
DB_LLWEAPONEX_OniGauntlet_Upgrades(_Template, _MinLevel)
AND
CharacterGetLevel(_Player, _Level)
AND
_Level >= _MinLevel
THEN
ObjectSetFlag(_Gauntlet, "LLWEAPONEX_OniGaunlet_UpgradeAvailable", 0);
//END_REGION

//REGION ONI_GAUNTLET_ORB_INSERTED
/*
LOOT_LLWEAPONEX_Orb_Thunder_A_dc34fbef-a647-4eea-a313-8ef1e859ce82
LOOT_LLWEAPONEX_Orb_Thunder_B_9fe7b80d-081d-4e5f-8125-10a452c2d105
LOOT_LLWEAPONEX_Orb_Thunder_C_5f9803c3-b8a3-4e23-8fdb-45aa75962e94
*/

IF
RuneInserted(_Player, _Gauntlet, "LOOT_LLWEAPONEX_Orb_Thunder_A_dc34fbef-a647-4eea-a313-8ef1e859ce82", _Slot)
AND
IsTagged(_Gauntlet, "LLWEAPONEX_OniGauntlet", _IsGauntlet)
THEN
LLWEAPONEX_OniGauntlet_OnOrbInserted(_Player, _Gauntlet, "LOOT_LLWEAPONEX_Orb_Thunder_A_dc34fbef-a647-4eea-a313-8ef1e859ce82", _Slot, _IsGauntlet);

IF
RuneInserted(_Player, _Gauntlet, "LOOT_LLWEAPONEX_Orb_Thunder_B_9fe7b80d-081d-4e5f-8125-10a452c2d105", _Slot)
AND
IsTagged(_Gauntlet, "LLWEAPONEX_OniGauntlet", _IsGauntlet)
THEN
LLWEAPONEX_OniGauntlet_OnOrbInserted(_Player, _Gauntlet, "LOOT_LLWEAPONEX_Orb_Thunder_B_9fe7b80d-081d-4e5f-8125-10a452c2d105", _Slot, _IsGauntlet);

IF
RuneInserted(_Player, _Gauntlet, "LOOT_LLWEAPONEX_Orb_Thunder_C_5f9803c3-b8a3-4e23-8fdb-45aa75962e94", _Slot)
AND
IsTagged(_Gauntlet, "LLWEAPONEX_OniGauntlet", _IsGauntlet)
THEN
LLWEAPONEX_OniGauntlet_OnOrbInserted(_Player, _Gauntlet, "LOOT_LLWEAPONEX_Orb_Thunder_C_5f9803c3-b8a3-4e23-8fdb-45aa75962e94", _Slot, _IsGauntlet);

PROC
LLWEAPONEX_OniGauntlet_OnOrbInserted((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Template, (INTEGER)_SLot, 0)
THEN
DB_LLWEAPONEX_OniGauntlet_Temp_RemoveOrb(_Player, _Item, _Template, _Slot);
ProcObjectTimer(_Player, "LLWEAPONEX_Timers_OniGauntlet_RemoveOrb", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LLWEAPONEX_Timers_OniGauntlet_RemoveOrb")
AND
DB_LLWEAPONEX_OniGauntlet_Temp_RemoveOrb(_Player, _Item, _Template, _Slot)
AND
ItemRemoveRune(_Player, _Item, _Slot, _Orb)
THEN
ShowNotification(_Player, "LLWEAPONEX_Notifications_OniGauntletRequiredForOrb");

PROC
LLWEAPONEX_OniGauntlet_OnOrbInserted((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Template, (INTEGER)_SLot, 1)
AND
DB_LLWEAPONEX_OniGauntlet_Orbs(_Template, _Skill, _ID, _MinSouls)
THEN
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveOrb(_Item, _Skill, _Template, _Slot);

IF
RuneRemoved(_Player, _Gauntlet, _Orb, _Slot)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveOrb(_Gauntlet, _Skill, _Template, _Slot)
AND
GetTemplate(_Orb, _Template)
THEN
NOT DB_LLWEAPONEX_OniGauntlet_Temp_ActiveOrb(_Gauntlet, _Skill, _Template, _Slot);
LLWEAPONEX_OniGauntlet_OnOrbRemoved(_Player, _Gauntlet, _Orb, _Template, _Skill);

PROC
LLWEAPONEX_OniGauntlet_OnOrbRemoved((CHARACTERGUID)_Player, (ITEMGUID)_Gauntlet, (ITEMGUID)_Orb, (STRING)_Template, (STRING)_Skill)
THEN
DB_NOOP(1);
//END_REGION

//REGION ONI_GAUNTLET_ORB_WEAPONS_SKILL
PROC
LLWEAPONEX_OniGauntlet_Internal_AddActiveWeapon((CHARACTERGUID)_Player, (STRING)_Skill, (ITEMGUID)_Weapon, (STRING)_ID, (ITEMGUID)_Gauntlet)
THEN
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, _ID, _Gauntlet);

PROC
LLWEAPONEX_OniGauntlet_Internal_StoreActiveWeapon((CHARACTERGUID)_Player, (STRING)_ID)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, _ID, _Gauntlet)
THEN
NOT DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, _ID, _Gauntlet);
DB_LLWEAPONEX_OniGauntlet_Temp_StoredWeapons(_Gauntlet, _Skill, _Weapon, _ID);
ItemToInventory(_Weapon, _Gauntlet);

//Re-equip the last orb weapon if hands are empty
PROC
LLWEAPONEX_OniGauntlet_OnOniGauntletEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveOrb(_Item, _Skill, _Template, _Slot)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_StoredWeapons(_Gauntlet, _Skill, _Weapon, _ID)
AND
NOT DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _)
AND
LeaderLib_Helper_QRY_HasEmptyHands(_Player)
THEN
NOT DB_LLWEAPONEX_OniGauntlet_Temp_StoredWeapons(_Gauntlet, _Skill, _Weapon, _ID);
DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill);
LLWEAPONEX_OniGauntlet_Internal_AddActiveWeapon(_Player, _Skill, _Weapon, _ID, _Gauntlet);
ItemToInventory(_Weapon, _Player);
LeaderLib_Timers_StartCharacterItemTimer(_Player, _Weapon, 250, "LLWEAPONEX_Timers_EquipOniGauntletWeapon", "LLWEAPONEX_EquipOniGauntletWeapon");

PROC
LLWEAPONEX_OniGauntlet_OnOniGauntletEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill)
THEN
NOT DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill);

IF
SkillCast(_Player, "Shout_LLWEAPONEX_OniGauntlet_Summon_Raizan", _, _)
THEN
LLWEAPONEX_OniGauntlet_SummonWeapon(_Player, "Shout_LLWEAPONEX_OniGauntlet_Summon_Raizan");

PROC
LLWEAPONEX_OniGauntlet_SummonWeapon((CHARACTERGUID)_Player, (STRING)_Skill)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_Player, _Gauntlet)
THEN
ItemAddCharges(_Gauntlet, 1);

PROC
LLWEAPONEX_OniGauntlet_SummonWeapon((CHARACTERGUID)_Player, (STRING)_Skill)
AND
NOT DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_Player, _Gauntlet)
AND
NOT DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _, _, _Gauntlet)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_StoredWeapons(_Gauntlet, _Skill, _Weapon, _ID)
THEN
NOT DB_LLWEAPONEX_OniGauntlet_Temp_StoredWeapons(_Gauntlet, _Skill, _Weapon, _ID);
LLWEAPONEX_OniGauntlet_Internal_AddActiveWeapon(_Player, _Skill, _Weapon, _ID, _Gauntlet);
DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill);
ItemToInventory(_Weapon, _Player);
LeaderLib_Timers_StartCharacterItemTimer(_Player, _Weapon, 250, "LLWEAPONEX_Timers_EquipOniGauntletWeapon", "LLWEAPONEX_EquipOniGauntletWeapon");

PROC
LLWEAPONEX_OniGauntlet_SummonWeapon((CHARACTERGUID)_Player, (STRING)_Skill)
AND
NOT DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill)
AND
DB_LLWEAPONEX_OniGauntlet_StartingWeapons(_Skill, _Template, _ID)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_Player, _Gauntlet)
AND
NOT DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _, _, _Gauntlet)
AND
GetPosition(_Player, _x, _y, _z)
AND
CreateItemTemplateAtPosition(_Template, _x, _y, _z, _Weapon)
THEN
CharacterEquipItem(_Player, _Weapon);
LLWEAPONEX_OniGauntlet_Internal_AddActiveWeapon(_Player, _Skill, _Weapon, _ID, _Gauntlet);
DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill);

PROC
LLWEAPONEX_OniGauntlet_SummonWeapon((CHARACTERGUID)_Player, (STRING)_Skill)
AND
NOT DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, _ID, _Gauntlet)
AND
NOT LeaderLib_Helper_QRY_WeaponIsEquipped(_Player, _Weapon)
THEN
DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill);
ItemToInventory(_Weapon, _Player);
LeaderLib_Timers_StartCharacterItemTimer(_Player, _Weapon, 250, "LLWEAPONEX_Timers_EquipOniGauntletWeapon", "LLWEAPONEX_EquipOniGauntletWeapon");

IF
CharacterItemEvent(_Player, _Weapon, "LLWEAPONEX_EquipOniGauntletWeapon")
THEN
CharacterEquipItem(_Player, _Weapon);

PROC
LLWEAPONEX_OniGauntlet_SummonWeapon((CHARACTERGUID)_Player, (STRING)_Skill)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill)
THEN
NOT DB_LLWEAPONEX_OniGauntlet_Temp_SummonResolved(_Player, _Skill);
//END_REGION

//REGION ONI_GAUNTLET_ORB_WEAPONS_REMOVE
IF
ItemUnEquipped(_Weapon, _Player)
AND
NOT DB_LLWEAPONEX_OniGauntlet_Temp_JustCombined(_Player, _Weapon)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, _ID, _Gauntlet)
THEN
LLWEAPONEX_OniGauntlet_Internal_StoreActiveWeapon(_Player, _ID);

PROC
LLWEAPONEX_OniGauntlet_OnOniGauntletUnEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Gauntlet)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, _ID, _Gauntlet)
THEN
LLWEAPONEX_OniGauntlet_Internal_StoreActiveWeapon(_Player, _ID);

PROC
LLWEAPONEX_OniGauntlet_RemoveActiveWeaponBySkill((CHARACTERGUID)_Player, (STRING)_Skill)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, _ID, _Gauntlet)
THEN
LLWEAPONEX_OniGauntlet_Internal_StoreActiveWeapon(_Player, _ID);

PROC
LLWEAPONEX_OniGauntlet_Internal_StoreActiveWeapon((CHARACTERGUID)_Player, "Raizan")
AND
GetPosition(_Player, _x, _y, _z)
THEN
PlayEffectAtPosition("RS3_FX_Skills_Air_Lightning_Cast_Ground_01", _x, _y, _z);

PROC
LLWEAPONEX_OniGauntlet_OnOrbRemoved((CHARACTERGUID)_Player, (ITEMGUID)_Gauntlet, (ITEMGUID)_Orb, (STRING)_Template, (STRING)_Skill)
THEN
LLWEAPONEX_OniGauntlet_RemoveActiveWeaponBySkill(_Player, _Skill);
//END_REGION

//REGION ONI_GAUNTLET_COMBO_UPGRADE_SOUL_TAX
IF
ItemTemplateCombinedWithItemTemplate(_T1, _T2, _T3, _T4, _T5, _Player, _NewItem)
AND
//Leaderlog_QRY_Log("DEBUG", "[LLWEAPONEX_13_OniGauntlet:ItemTemplateCombinedWithItemTemplate] Combo: ", _T1, " | ", _T2, " | ", _T3, " | ", _T4, " | ", _T5)
//AND
LeaderLib_Helper_QRY_Combo_TemplateMatch("WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Raizan_", _T1, _T2, _T3, _T4, _T5)
//AND
//Leaderlog_QRY_Log("DEBUG", "[LLWEAPONEX_13_OniGauntlet:ItemTemplateCombinedWithItemTemplate] Template match with [WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Raizan_]")
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, "Raizan", _Gauntlet)
AND
GetTemplate(_NewItem, _NewTemplate)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX_13_OniGauntlet:ItemTemplateCombinedWithItemTemplate] Raizan was combined and changed to [",_NewTemplate,"].");
NOT DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, "Raizan", _Gauntlet);
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _NewItem, "Raizan", _Gauntlet);
DB_LLWEAPONEX_OniGauntlet_Temp_JustCombined(_Player, _NewItem);
LeaderLib_Timers_StartCharacterItemTimer(_Player, _NewItem, 250, "LLWEAPONEX_Timers_JustCombinedOniWeapon", "LLWEAPONEX_JustCombinedOniWeapon");
//END_REGION

//REGION ONI_GAUNTLET_WEAPON_COMBO
/*
IF
ObjectTransformed((ITEMGUID)_Weapon, _ToTemplate)
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, "Raizan")
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX_13_OniGauntlet:ObjectTransformed] Raizan was transformed.");
*/

IF
ItemTemplateCombinedWithItemTemplate(_T1, _T2, _T3, _T4, _T5, _Player, _NewItem)
AND
//Leaderlog_QRY_Log("DEBUG", "[LLWEAPONEX_13_OniGauntlet:ItemTemplateCombinedWithItemTemplate] Combo: ", _T1, " | ", _T2, " | ", _T3, " | ", _T4, " | ", _T5)
//AND
LeaderLib_Helper_QRY_Combo_TemplateMatch("WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Raizan_", _T1, _T2, _T3, _T4, _T5)
//AND
//Leaderlog_QRY_Log("DEBUG", "[LLWEAPONEX_13_OniGauntlet:ItemTemplateCombinedWithItemTemplate] Template match with [WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Raizan_]")
AND
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, "Raizan", _Gauntlet)
AND
GetTemplate(_NewItem, _NewTemplate)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX_13_OniGauntlet:ItemTemplateCombinedWithItemTemplate] Raizan was combined and changed to [",_NewTemplate,"].");
NOT DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _Weapon, "Raizan", _Gauntlet);
DB_LLWEAPONEX_OniGauntlet_Temp_ActiveWeapons(_Player, _Skill, _NewItem, "Raizan", _Gauntlet);
DB_LLWEAPONEX_OniGauntlet_Temp_JustCombined(_Player, _NewItem);
LeaderLib_Timers_StartCharacterItemTimer(_Player, _NewItem, 250, "LLWEAPONEX_Timers_JustCombinedOniWeapon", "LLWEAPONEX_JustCombinedOniWeapon");

IF
CharacterItemEvent(_Player, _Weapon, "LLWEAPONEX_JustCombinedOniWeapon")
AND
DB_LLWEAPONEX_OniGauntlet_Temp_JustCombined(_Player, _Weapon)
THEN
NOT DB_LLWEAPONEX_OniGauntlet_Temp_JustCombined(_Player, _Weapon);
//END_REGION

//REGION DEBUG
IF
TextEventSet("llweap_onisouls")
AND
DB_LLWEAPONEX_OniGauntlet_Temp_Equipped(_Player, _Item)
AND
ItemGetMaxCharges(_Item, _Max)
THEN
ItemAddCharges(_Item, _Max);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"
