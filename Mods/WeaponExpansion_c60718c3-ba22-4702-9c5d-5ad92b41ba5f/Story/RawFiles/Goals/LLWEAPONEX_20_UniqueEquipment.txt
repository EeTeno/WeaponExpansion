Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Uniques_InitSettings();
LLWEAPONEX_Uniques_RegisterGrenadeSkills();
KBSECTION

//REGION SETTINGS
PROC
LLWEAPONEX_Uniques_InitSettings()
THEN
DB_NOOP(1);
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 0, 9, 4, 10)
THEN
LLWEAPONEX_Uniques_InitSettings();
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 0, 9, 4, 10)
THEN
LLWEAPONEX_Uniques_RegisterGrenadeSkills();
//END_REGION

//REGION SETTINGS_SKILLS

PROC
LLWEAPONEX_Uniques_RegisterGrenadeSkills()
THEN
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_ArmorPiercing", "Projectile_LLWEAPONEX_Status_Demolition_ArmorPiercing");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_Nailbomb");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_Flashbang");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_Molotov");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_CursedMolotov");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_Love");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_MindMaggot");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_ChemicalWarfare");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_Terror");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_Ice");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_BlessedIce");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_Holy");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_Tremor");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_Taser");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_WaterBalloon");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_WaterBlessedBalloon");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_SmokeBomb");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_MustardGas");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_OilFlask");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_BlessedOilFlask");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_PoisonFlask");
DB_LLWEAPONEX_Demolition_SkillBonuses("Shared", "Projectile_Grenade_CursedPoisonFlask");
//END_REGION

//REGION BEHOLDER
IF
ItemTemplateEquipped("WPN_UNIQUE_LLWEAPONEX_Sword_2H_Parasite_A_1cc2baa1-cd58-40a3-8b53-89ef2e081616", _Player)
THEN
DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player);

IF
CharacterStatusApplied(_Player, "UNSHEATHED", _)
AND
DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player)
THEN
PROC_LoopEffect("LLWEAPONEX_FX_Overlay_Weapon_DarkEnergy_01", _Player, "LLWEAPONEX.FX.BeholderUnsheathed", "__ANY__", "Dummy_Root");
LeaderLog_Log("DEBUG", "[LLWEAPONEX_20_UniqueEquipment:Unsheathed] Playing Beholder unsheathed loop effect on character.");

IF
CharacterStatusRemoved(_Player, "UNSHEATHED", _)
AND
DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player)
THEN
PROC_StopLoopEffect(_Player, "LLWEAPONEX.FX.BeholderUnsheathed");
LeaderLog_Log("DEBUG", "[LLWEAPONEX_20_UniqueEquipment:Sheathed] Stopping Beholder unsheathed loop effect on character.");

IF
ItemTemplateUnEquipped("WPN_UNIQUE_LLWEAPONEX_Sword_2H_Parasite_A_1cc2baa1-cd58-40a3-8b53-89ef2e081616", _Player)
AND
DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player)
THEN
PROC_StopLoopEffect(_Player, "LLWEAPONEX.FX.BeholderUnsheathed");
NOT DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player);
//END_REGION

//REGION FROSTDYNE_SKILLS
/*
IF
CharacterStatusApplied(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_INFO", _)
AND
CharacterGetLevel(_Player, _Level)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);
LLWEAPONEX_Uniques_FrostDyne_AddSkills(_Player, _Level);

PROC
LLWEAPONEX_Uniques_FrostDyne_AddSkills((CHARACTERGUID)_Player, (INTEGER)_Level)
AND
_Level < 6
THEN
ApplyStatus(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_A", -1.0, 1, _Player);
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_A");

PROC
LLWEAPONEX_Uniques_FrostDyne_AddSkills((CHARACTERGUID)_Player, (INTEGER)_Level)
AND
_Level >= 6
AND
_Level < 14
THEN
ApplyStatus(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_B", -1.0, 1, _Player);
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_B");

PROC
LLWEAPONEX_Uniques_FrostDyne_AddSkills((CHARACTERGUID)_Player, (INTEGER)_Level)
AND
_Level >= 14
THEN
ApplyStatus(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_C", -1.0, 1, _Player);
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_C");

IF
CharacterStatusRemoved(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_INFO", _)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);

PROC
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills((CHARACTERGUID)_Player)
AND
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, _Status)
THEN
RemoveStatus(_Player, _Status);
NOT DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, _Status);

IF
CharacterLeveledUp(_Player)
AND
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, _Status)
AND
CharacterGetLevel(_Player, _Level)
THEN
LLWEAPONEX_Uniques_FrostDyne_OnPlayerLeveledUp(_Player, _Level);

PROC
LLWEAPONEX_Uniques_FrostDyne_OnPlayerLeveledUp((CHARACTERGUID)_Player, 4)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);
LLWEAPONEX_Uniques_FrostDyne_AddSkills(_Player, 4);

PROC
LLWEAPONEX_Uniques_FrostDyne_OnPlayerLeveledUp((CHARACTERGUID)_Player, 6)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);
LLWEAPONEX_Uniques_FrostDyne_AddSkills(_Player, 6);

PROC
LLWEAPONEX_Uniques_FrostDyne_OnPlayerLeveledUp((CHARACTERGUID)_Player, 14)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);
LLWEAPONEX_Uniques_FrostDyne_AddSkills(_Player, 14);
*/
//END_REGION

//REGION MURAMASA
IF
ItemTemplateEquipped("WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Muramasa_4be8ec78-17ed-4f61-b3d8-96c260d1c80a", _Character)
THEN
LLWEAPONEX_Uniques_OnMuramasaEquipped(_Character);
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_MuramasaEquipped", "WeaponExpansion");

PROC
LLWEAPONEX_Uniques_OnMuramasaEquipped((CHARACTERGUID)_Character)
AND
CharacterGetHitpointsPercentage(_Character, _HP)
AND
_HP <= 50
THEN
ApplyStatus(_Character, "LLWEAPONEX_MURAMASA_CURSE_WEAPONFX", -1.0, 0, _Character);

IF
ItemTemplateUnEquipped("WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Muramasa_4be8ec78-17ed-4f61-b3d8-96c260d1c80a", _Character)
THEN
LLWEAPONEX_Uniques_OnMuramasaUnequipped(_Character);
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_MuramasaEquipped", "WeaponExpansion", 500);

PROC
LLWEAPONEX_Uniques_OnMuramasaUnequipped((CHARACTERGUID)_Character)
AND
LeaderLib_Helper_QRY_HasStatus(_Character, "LLWEAPONEX_MURAMASA_CURSE_WEAPONFX")
THEN
RemoveStatus(_Character, "LLWEAPONEX_MURAMASA_CURSE_WEAPONFX");
//END_REGION

//REGION DEMON_GAUNTLET
IF
ItemTemplateEquipped("EQ_UNIQUE_LLWEAPONEX_DemonGauntlet_Arms_A_f5d0a9b3-b83c-4b78-8bc6-a097a26ddf53", _Character)
THEN
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_DemonGauntletEquipped", "WeaponExpansion");

IF
ItemTemplateUnEquipped("EQ_UNIQUE_LLWEAPONEX_DemonGauntlet_Arms_A_f5d0a9b3-b83c-4b78-8bc6-a097a26ddf53", _Character)
THEN
LLWEAPONEX_Uniques_OnDemonGauntletUnequipped(_Character);
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_DemonGauntletEquipped", "WeaponExpansion", 500);

PROC
LLWEAPONEX_Uniques_OnDemonGauntletUnequipped((CHARACTERGUID)_Character)
AND
LeaderLib_Helper_QRY_HasStatus(_Character, "LLWEAPONEX_DEMON_HAND_DEFENSE")
THEN
RemoveStatus(_Character, "LLWEAPONEX_DEMON_HAND_DEFENSE");

PROC
LLWEAPONEX_Uniques_OnDemonGauntletUnequipped((CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target)
THEN
NOT DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_DEMON_HAND_HIT", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);

PROC
LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _OtherTarget)
THEN
NOT DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _OtherTarget);

PROC
LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
THEN
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);

PROC
LLWEAPONEX_Uniques_DemonGauntlet_OnDemonKilledTarget((CHARACTERGUID)_Target)
AND
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target)
THEN
NOT DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);
PlayEffect(_Source, "LLWEAPONEX_FX_Status_DemonHandDefense_Impact_Back_01", "Dummy_BackFX_R");

IF
CharacterStatusRemoved(_Target, "LLWEAPONEX_DEMON_HAND_HIT", _)
AND
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target)
THEN
NOT DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);
//END_REGION

//REGION DEMONOLITION_BACKPACK
IF
ItemTemplateEquipped("LLWEAPONEX_80_TS_10_DemolitionBackpack-f5b6-4e71-ae84-a29ed4ca51d7", _Character)
THEN
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_DemolitionBackpackEquipped", "WeaponExpansion");

IF
ItemTemplateUnEquipped("LLWEAPONEX_80_TS_10_DemolitionBackpack-f5b6-4e71-ae84-a29ed4ca51d7", _Character)
THEN
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_DemolitionBackpackEquipped", "WeaponExpansion", 500);

//Enhanced Demolition
PROC
LLWEAPONEX_ThrowingMastery_OnThrowingSkillUsedOnTarget((CHARACTERGUID)_Player, (GUIDSTRING)_Target, (ITEMGUID)_Item, (STRING)_Skill)
AND
HasActiveStatus(_Player, "LLWEAPONEX_BACKPACK_DEMOLITION_EQUIPPED", 1)
AND
IsTagged(_Item, "GRENADES", 1)
THEN
DB_LLWEAPONEX_Uniques_Demolition_Temp_PostSkillBonus(_Player, _Target, _Item, _Skill);

PROC
LLWEAPONEX_Uniques_Demolition_GrantBonus((CHARACTERGUID)_Player, (GUIDSTRING)_Target, (ITEMGUID)_Item, (STRING)_Skill)
AND
LeaderLib_Roll_QRY(250, 999)
THEN
ApplyStatus(_Target, "LLWEAPONEX_BACKPACK_DEMOLITION_SABOTAGE", 0.0, 0, _Player);

PROC
LLWEAPONEX_ThrowingMastery_OnThrowingItemUsed((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Skill)
THEN
DB_NOOP(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"