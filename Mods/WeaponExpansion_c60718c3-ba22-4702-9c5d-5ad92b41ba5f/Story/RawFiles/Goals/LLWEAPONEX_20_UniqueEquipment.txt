Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Uniques_InitSettings();
KBSECTION

//REGION SETTINGS
PROC
LLWEAPONEX_Uniques_InitSettings()
THEN
DB_NOOP(1);
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 0, 9, 4, 10)
THEN
LLWEAPONEX_Uniques_InitSettings();
//END_REGION

//REGION BEHOLDER
IF
ItemTemplateEquipped("WPN_UNIQUE_LLWEAPONEX_Sword_2H_Parasite_A_1cc2baa1-cd58-40a3-8b53-89ef2e081616", _Player)
THEN
DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player);

IF
CharacterStatusApplied(_Player, "UNSHEATHED", _)
AND
DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player)
THEN
PROC_LoopEffect("LLWEAPONEX_FX_Overlay_Weapon_DarkEnergy_01", _Player, "LLWEAPONEX.FX.BeholderUnsheathed", "__ANY__", "Dummy_Root");
LeaderLog_Log("DEBUG", "[LLWEAPONEX_20_UniqueEquipment:Unsheathed] Playing Beholder unsheathed loop effect on character.");

IF
CharacterStatusRemoved(_Player, "UNSHEATHED", _)
AND
DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player)
THEN
PROC_StopLoopEffect(_Player, "LLWEAPONEX.FX.BeholderUnsheathed");
LeaderLog_Log("DEBUG", "[LLWEAPONEX_20_UniqueEquipment:Sheathed] Stopping Beholder unsheathed loop effect on character.");

IF
ItemTemplateUnEquipped("WPN_UNIQUE_LLWEAPONEX_Sword_2H_Parasite_A_1cc2baa1-cd58-40a3-8b53-89ef2e081616", _Player)
AND
DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player)
THEN
PROC_StopLoopEffect(_Player, "LLWEAPONEX.FX.BeholderUnsheathed");
NOT DB_LLWEAPONEX_Uniques_BeholderEquipped(_Player);
//END_REGION

//REGION FROSTDYNE_SKILLS
/*
IF
CharacterStatusApplied(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_INFO", _)
AND
CharacterGetLevel(_Player, _Level)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);
LLWEAPONEX_Uniques_FrostDyne_AddSkills(_Player, _Level);

PROC
LLWEAPONEX_Uniques_FrostDyne_AddSkills((CHARACTERGUID)_Player, (INTEGER)_Level)
AND
_Level < 6
THEN
ApplyStatus(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_A", -1.0, 1, _Player);
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_A");

PROC
LLWEAPONEX_Uniques_FrostDyne_AddSkills((CHARACTERGUID)_Player, (INTEGER)_Level)
AND
_Level >= 6
AND
_Level < 14
THEN
ApplyStatus(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_B", -1.0, 1, _Player);
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_B");

PROC
LLWEAPONEX_Uniques_FrostDyne_AddSkills((CHARACTERGUID)_Player, (INTEGER)_Level)
AND
_Level >= 14
THEN
ApplyStatus(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_C", -1.0, 1, _Player);
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_C");

IF
CharacterStatusRemoved(_Player, "LLWEAPONEX_UNIQUE_FROSTDYNE_SKILLS_INFO", _)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);

PROC
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills((CHARACTERGUID)_Player)
AND
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, _Status)
THEN
RemoveStatus(_Player, _Status);
NOT DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, _Status);

IF
CharacterLeveledUp(_Player)
AND
DB_LLWEAPONEX_Uniques_Temp_FrostDyneActive(_Player, _Status)
AND
CharacterGetLevel(_Player, _Level)
THEN
LLWEAPONEX_Uniques_FrostDyne_OnPlayerLeveledUp(_Player, _Level);

PROC
LLWEAPONEX_Uniques_FrostDyne_OnPlayerLeveledUp((CHARACTERGUID)_Player, 4)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);
LLWEAPONEX_Uniques_FrostDyne_AddSkills(_Player, 4);

PROC
LLWEAPONEX_Uniques_FrostDyne_OnPlayerLeveledUp((CHARACTERGUID)_Player, 6)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);
LLWEAPONEX_Uniques_FrostDyne_AddSkills(_Player, 6);

PROC
LLWEAPONEX_Uniques_FrostDyne_OnPlayerLeveledUp((CHARACTERGUID)_Player, 14)
THEN
LLWEAPONEX_Uniques_FrostDyne_RemoveSkills(_Player);
LLWEAPONEX_Uniques_FrostDyne_AddSkills(_Player, 14);
*/
//END_REGION

//REGION MURAMASA
IF
ItemTemplateEquipped("WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Muramasa_4be8ec78-17ed-4f61-b3d8-96c260d1c80a", _Character)
THEN
LLWEAPONEX_Uniques_OnMuramasaEquipped(_Character);
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_MuramasaEquipped", "WeaponExpansion");

PROC
LLWEAPONEX_Uniques_OnMuramasaEquipped((CHARACTERGUID)_Character)
AND
CharacterGetHitpointsPercentage(_Character, _HP)
AND
_HP <= 50
THEN
ApplyStatus(_Character, "LLWEAPONEX_MURAMASA_CURSE_WEAPONFX", -1.0, 0, _Character);

IF
ItemTemplateUnEquipped("WPN_UNIQUE_LLWEAPONEX_Katana_Dagger_Sword_2H_Muramasa_4be8ec78-17ed-4f61-b3d8-96c260d1c80a", _Character)
THEN
LLWEAPONEX_Uniques_OnMuramasaUnequipped(_Character);
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_MuramasaEquipped", "WeaponExpansion", 500);

PROC
LLWEAPONEX_Uniques_OnMuramasaUnequipped((CHARACTERGUID)_Character)
AND
LeaderLib_Helper_QRY_HasStatus(_Character, "LLWEAPONEX_MURAMASA_CURSE_WEAPONFX")
THEN
RemoveStatus(_Character, "LLWEAPONEX_MURAMASA_CURSE_WEAPONFX");
//END_REGION

//REGION DEMON_GAUNTLET
IF
ItemTemplateEquipped("EQ_UNIQUE_LLWEAPONEX_DemonGauntlet_Arms_A_f5d0a9b3-b83c-4b78-8bc6-a097a26ddf53", _Character)
THEN
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_DemonGauntletEquipped", "WeaponExpansion");

IF
ItemTemplateUnEquipped("EQ_UNIQUE_LLWEAPONEX_DemonGauntlet_Arms_A_f5d0a9b3-b83c-4b78-8bc6-a097a26ddf53", _Character)
THEN
LLWEAPONEX_Uniques_OnDemonGauntletUnequipped(_Character);
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_DemonGauntletEquipped", "WeaponExpansion", 500);

PROC
LLWEAPONEX_Uniques_OnDemonGauntletUnequipped((CHARACTERGUID)_Character)
AND
LeaderLib_Helper_QRY_HasStatus(_Character, "LLWEAPONEX_DEMON_HAND_DEFENSE")
THEN
RemoveStatus(_Character, "LLWEAPONEX_DEMON_HAND_DEFENSE");

PROC
LLWEAPONEX_Uniques_OnDemonGauntletUnequipped((CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target)
THEN
NOT DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_DEMON_HAND_HIT", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);

PROC
LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _OtherTarget)
THEN
NOT DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _OtherTarget);

PROC
LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
THEN
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);

PROC
LLWEAPONEX_Uniques_DemonGauntlet_OnDemonKilledTarget((CHARACTERGUID)_Target)
AND
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target)
THEN
NOT DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);
PlayEffect(_Source, "LLWEAPONEX_FX_Status_DemonHandDefense_Impact_Back_01", "Dummy_BackFX_R");

IF
CharacterStatusRemoved(_Target, "LLWEAPONEX_DEMON_HAND_HIT", _)
AND
DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target)
THEN
NOT DB_LLWEAPONEX_Uniques_DemonGauntlet_ListenForDeath(_Source, _Target);
//END_REGION

//REGION BLUNDERBUSS
IF
//ItemTemplateEquipped("", _Character)
CharacterStatusAttempt(_Character, "LLWEAPONEX_BLUNDERBUSS_EQUIPPED", _)
THEN
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_BlunderbussEquipped", "WeaponExpansion");

IF
//ItemTemplateUnEquipped("", _Character)
CharacterStatusRemoved(_Character, "LLWEAPONEX_BLUNDERBUSS_EQUIPPED", _)
AND
NOT LLWEAPONEX_QRY_WeaponTypeEquipped(_Character, "LLWEAPONEX_Blunderbuss")
THEN
//LeaderLib_Statuses_RemoveIfActive(_Character, "LLWEAPONEX_REMOTEMINE_ADD_RECHARGE");
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_BlunderbussEquipped", "WeaponExpansion", 1250);

PROC
LLWEAPONEX_Blunderbuss_OnAttackAtPosition((CHARACTERGUID)_Character, (REAL)_x, (REAL)_y, (REAL)_z)
AND
GetDistanceToPosition(_Character, _x, _y, _z, _Dist)
AND
RealProduct(_Dist, 85.0, _DelayR)
AND
Integer(_DelayR, _DelayInt)
AND
IntegerSum(_DelayInt, 255, _Delay)
AND
IntegerMax(_Delay, 1000, _DelayClamped)
THEN
SetVarFloat3(_Character, "LLWEAPONEX_Blunderbuss_AttackPosition", _x, _y, _z);
LeaderLib_Timers_StartObjectTimer(_Character, _DelayClamped, "LLWEAPONEX_Timers_Blunderbuss_ExplodeAtPosition", "LLWEAPONEX_Blunderbuss_ExplodeAtPosition");

IF
//CharacterUsedSkill(_Player, "Projectile_LLWEAPONEX_Blunderbuss_CannonLaunch", _, _)
CharacterUsedSkillAtPosition(_Player, _tx, _ty, _tz, "Projectile_LLWEAPONEX_Blunderbuss_CannonLaunch", _, _)
THEN
DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_WaitingOnSpawn(_Player, _tx, _ty, _tz);

IF
//CharacterUsedSkill(_Player, "Projectile_LLWEAPONEX_Blunderbuss_CannonLaunch", _, _)
CharacterUsedSkillOnTarget(_Player, _Target, "Projectile_LLWEAPONEX_Blunderbuss_CannonLaunch", _, _)
AND
NOT DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_WaitingOnSpawn(_Player, _, _, _)
AND
GetPosition(_Target, _tx, _ty, _tz)
THEN
DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_WaitingOnSpawn(_Player, _tx, _ty, _tz);

IF
StoryEvent(_Object, "LLWEAPONEX_Blunderbuss_CannonLaunch_Start")
AND
ObjectExists(_Object, 1)
AND
ObjectIsItem((ITEMGUID)_Object, 1)
AND
DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_WaitingOnSpawn(_Player, _tx, _ty, _tz)
THEN
NOT DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_WaitingOnSpawn(_Player, _tx, _ty, _tz);
LLWEAPONEX_Blunderbuss_OnCannonLaunchStarted(_Player, _Object, _tx, _ty, _tz);

PROC
LLWEAPONEX_Blunderbuss_OnCannonLaunchStarted((CHARACTERGUID)_Player, (ITEMGUID)_MovingObject, (REAL)_tx, (REAL)_ty, (REAL)_tz)
AND
GetPosition(_MovingObject, _x, _y, _z)
AND
CreateItemTemplateAtPosition("LLWEAPONEX_S_Blunderbuss_RollingCannonball_50e78a59-0ede-481a-a70f-21fedf834d6f", _x, _y, _z, _Cannonball)
THEN
DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_Active(_Player, _Cannonball);
ApplyStatus(_Cannonball, "LLWEAPONEX_CANNONBALL_DETECT_NEAR", -1.0, 0, _Player);
ItemMoveToPosition(_Cannonball, _tx, _ty, _tz, 20.0, 60.0, "LLWEAPONEX_Blunderbuss_CannonLaunch_BallDone", 0);
LeaderLib_Timers_StartObjectTimer(_Cannonball, 1500, "LLWEAPONEX_Timers_Blunderbuss_CannonLaunch_BallDone", "LLWEAPONEX_Blunderbuss_CannonLaunch_BallDone");

IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_CANNONBALL_NEAR", (ITEMGUID)_Cannonball)
AND
DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_Active(_Player, _Cannonball)
AND
LeaderLib_Combat_QRY_IsEnemy(_Target, _Player)
THEN
CharacterCharacterSetEvent(_Player, _Target, "LLWEAPONEX_Bluderbuss_CannonLaunch_OnHit");

IF
ItemStatusChange(_Target, "LLWEAPONEX_CANNONBALL_NEAR", (ITEMGUID)_Cannonball)
AND
DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_Active(_Player, _Cannonball)
AND
ItemIsDestructible(_Target, 1)
AND
ItemIsDestroyed(_Target, 0)
AND
_Target != _Cannonball
THEN
CharacterItemSetEvent(_Player, _Target, "LLWEAPONEX_Bluderbuss_CannonLaunch_OnHit");

IF
StoryEvent((ITEMGUID)_Cannonball, "LLWEAPONEX_Blunderbuss_CannonLaunch_BallDone")
AND
DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_Active(_Player, _Cannonball)
THEN
NOT DB_LLWEAPONEX_Blunderbuss_CannonLaunch_Temp_Active(_Player, _Cannonball);
ItemDestroy(_Cannonball);
LeaderLib_Timers_CancelObjectTimer(_Cannonball, "LLWEAPONEX_Timers_Blunderbuss_CannonLaunch_BallDone");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"