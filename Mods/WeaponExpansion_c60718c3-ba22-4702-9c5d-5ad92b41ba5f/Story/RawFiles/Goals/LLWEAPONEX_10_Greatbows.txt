Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Greatbows_InitSettings();

KBSECTION
//REGION SETTINGS
PROC
LLWEAPONEX_Greatbows_InitSettings()
THEN
/*
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_DWARF", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_DWARF", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_ELF", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_ELF", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_HUMAN", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Humans_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_HUMAN", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Humans_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_LIZARD", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizards_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_LIZARD", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizards_01", "Dummy_R_Hand", 900);

DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", "Undead_Dwarf", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Female", "LowerArm_R_Twist_Bone", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", "Undead_Dwarf", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Male", "LowerArm_R_Twist_Bone", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", "Undead_Elf", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Female", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", "Undead_Elf", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Male", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", "Undead_Human", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Female", "Dummy_R_Hand", 800);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", "Undead_Human", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Male", "Dummy_R_Hand", 800);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", "Undead_Lizard", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Female", "LowerArm_R_Twist_Bone", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", "Undead_Lizard", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Male", "LowerArm_R_Twist_Bone", 600);
*/

SysClear("DB_LLWEAPONEX_Greatbows_PrepareFX", 5);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Female", "LowerArm_R_Twist_Bone", 425);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Male", "LowerArm_R_Twist_Bone", 425);
//DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Female", "Dummy_R_Hand", 500);
//DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Male", "Dummy_R_Hand", 400);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Female", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Male", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Female", "Dummy_R_Hand", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Male", "Dummy_R_Hand", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Female", "LowerArm_R_Twist_Bone", 580);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Male", "LowerArm_R_Twist_Bone", 600);

//Omnibolt Greatbow
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Dwarf_Female", "LowerArm_R_Twist_Bone", 425);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Dwarf_Male", "LowerArm_R_Twist_Bone", 425);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Elf_Female", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Elf_Male", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Human_Female", "Dummy_R_Hand", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Human_Male", "Dummy_R_Hand", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Lizard_Female", "LowerArm_R_Twist_Bone", 580);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Lizard_Male", "LowerArm_R_Twist_Bone", 600);

/*
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 580);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 600);
*/

//Omnibolt
DB_LLWEAPONEX_Greatbows_TemplateToID("WPN_UNIQUE_LLWEAPONEX_Greatbow_Lightning_Bow_2H_A_7efec0e0-1c2e-4f0d-9ec5-e3a1f40c97b8", "Lightning");

DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(0.0, 1.2, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE1");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(1.2, 2.4, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE2");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(2.4, 3.6, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE3");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(3.6, 4.8, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE4");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(4.8, 6.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE5");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(6.0, 7.2, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE6");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(7.2, 9.4, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE7");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(9.4, 11.5, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE8");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(11.5, 999.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE9");

DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(0.25, 2.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE1");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(2.0, 3.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE2");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(3.0, 4.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE3");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(4.0, 5.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE4");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(5.0, 9.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE5");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(9.0, 12.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE6");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(12.0, 16.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE7");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(16.0, 19.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE8");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(19.0, 999.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE9");

DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(0, -999.0, 5.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(1, 5.0, 6.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(2, 6.0, 7.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(3, 7.0, 8.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(4, 8.0, 9.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(5, 9.0, 10.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(6, 10.0, 11.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(7, 11.0, 12.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(8, 12.0, 999.0);
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 0, 9, 3, 33)
THEN
LLWEAPONEX_Greatbows_InitSettings();
//END_REGION

//REGION EQUIPPED_DB
PROC
LLWEAPONEX_WeaponMastery_OnWeaponEquipped((CHARACTERGUID)_Character, (ITEMGUID)_Item, "LLWEAPONEX_Greatbow")
AND
GetTemplate(_Item, _Template)
THEN
DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Character, _Item, _Template);

PROC
LLWEAPONEX_WeaponMastery_OnWeaponUnEquipped((CHARACTERGUID)_Character, (ITEMGUID)_Item, (STRING)_WeaponType)
AND
DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Character, _Item, _Template)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Character, _Item, _Template);
//END_REGION

//REGION GREATBOW_START_ATTACK
IF
CharacterUsedSkill(_Character, "Projectile_LLMIME_MimicGreatBowAttack", _, _)
THEN
LLWEAPONEX_Greatbows_PlayPreparationEffect(_Character, "Default");

/*
PROC
LLWEAPONEX_Greatbows_StartPlayingPreparationEffect((CHARACTERGUID)_Character, (STRING)_Template)
AND
DB_LLWEAPONEX_Greatbows_TemplateToID(_Template, "Lightning")
THEN
//LLWEAPONEX_Greatbows_PlayPreparationEffect(_Character, _ID);
PROC_StopLoopBeamEffect(_Character, "LLWEAPONEX.FX.GreatbowAttackFX");
PROC_LoopBeamEffect("LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Beam_01", _Character, _Character, "Dummy_R_Hand", "Dummy_ProjectileFX", "LLWEAPONEX.FX.GreatbowAttackFX", "__ANY__");
*/

PROC
LLWEAPONEX_Greatbows_StartPlayingPreparationEffect((CHARACTERGUID)_Character, (STRING)_Template)
AND
DB_LLWEAPONEX_Greatbows_TemplateToID(_Template, _ID)
THEN
//LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_Greatbows:StartPlayingPreparationEffect] Playing prepare effect with ID [",_ID,"] from template [",_Template,"].");
LLWEAPONEX_Greatbows_PlayPreparationEffect(_Character, _ID);

PROC
LLWEAPONEX_Greatbows_StartPlayingPreparationEffect((CHARACTERGUID)_Character, (STRING)_Template)
AND
NOT DB_LLWEAPONEX_Greatbows_TemplateToID(_Template, _)
THEN
//LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_Greatbows:StartPlayingPreparationEffect] No ID found for template [",_Template,"]. Using default.");
LLWEAPONEX_Greatbows_PlayPreparationEffect(_Character, "Default");

PROC
LLWEAPONEX_Greatbows_PlayPreparationEffect((CHARACTERGUID)_Character, (STRING)_ID)
AND
NOT DB_LoopEffect(_Character, _, "LLWEAPONEX.FX.GreatbowAttackFX", _, _, _)
AND
IsTagged(_Character, "FEMALE", _IsFemale)
AND
CharacterGetRace(_Character, 1, _Race)
AND
DB_LeaderLib_RaceTags(_Race, _RaceTag, _BaseRaceTag)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_BaseRaceTag, _IsFemale, _ID, _EffectName, _BoneName, _Duration)
THEN
//Play the loop FX with behavior scripting so we can destroy the effect immediately
//LeaderLog_Log("TRACE", "[LLWEAPONEX:Greatbows:PlayPreparationEffect] Playing arrow FX on attacking character. [",_EffectName,":",_BoneName,"]");
PROC_StopLoopEffect(_Character, "LLWEAPONEX.FX.GreatbowAttackFX");
PROC_LoopEffect(_EffectName, _Character, "LLWEAPONEX.FX.GreatbowAttackFX", "__ANY__", _BoneName);
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_StopGreatbowPrepareFX", _Duration);

//NPCs
PROC
LLWEAPONEX_Greatbows_PlayPreparationEffect((CHARACTERGUID)_Character, (STRING)_ID)
AND
NOT DB_LoopEffect(_Character, _, "LLWEAPONEX.FX.GreatbowAttackFX", _, _, _)
AND
IsTagged(_Character, "FEMALE", _IsFemale)
AND
NOT CharacterGetRace(_Character, 1, _)
AND
DB_LeaderLib_RaceTags(_Race, _RaceTag, _BaseRaceTag)
AND
IsTagged(_Character, _RaceTag, 1)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_BaseRaceTag, _IsFemale, _ID, _EffectName, _BoneName, _Duration)
AND
NOT DB_LoopEffect(_Character, _, "LLWEAPONEX.FX.GreatbowAttackFX", _, _, _)
THEN
//Play the loop FX with behavior scripting so we can destroy the effect immediately
//LeaderLog_Log("TRACE", "[LLWEAPONEX:Greatbows:PlayPreparationEffect] Playing arrow FX on attacking character. [",_EffectName,":",_BoneName,"]");
PROC_StopLoopEffect(_Character, "LLWEAPONEX.FX.GreatbowAttackFX");
PROC_LoopEffect(_EffectName, _Character, "LLWEAPONEX.FX.GreatbowAttackFX", "__ANY__", _BoneName);
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_StopGreatbowPrepareFX", _Duration);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_StopGreatbowPrepareFX")
THEN
//LeaderLog_Log("TRACE", "[LLWEAPONEX:Greatbows:StopGreatbowPrepareFX] Destroying looped arrow effect.");
PROC_StopLoopEffect(_Character, "LLWEAPONEX.FX.GreatbowAttackFX");
//END_REGION

//REGION BONUS_EFFECTS
PROC
LLWEAPONEX_Greatbows_OnAttack((CHARACTERGUID)_Character, (STRING)_Template, (GUIDSTRING)_Target)
THEN
DB_NOOP(1);

/*
PROC
LLWEAPONEX_Greatbows_OnAttackAtPosition((CHARACTERGUID)_Character, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z)
THEN
DB_NOOP(1);
*/

/*
PROC
LLWEAPONEX_Greatbows_OnAttack((CHARACTERGUID)_Character, "WPN_UNIQUE_LLWEAPONEX_Greatbow_Lightning_Bow_2H_A_7efec0e0-1c2e-4f0d-9ec5-e3a1f40c97b8", (GUIDSTRING)_Target)
AND
LeaderLib_Roll_QRY(450, 999)
THEN
ApplyStatus(_Target, "LLWEAPONEX_GREATBOW_LIGHTNINGSTRIKE", 0.0, 0, _Character);
*/


QRY
LLWEAPONEX_Greatbows_QRY_GetAttackDelay((CHARACTERGUID)_Character, (STRING)_ID)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Character, _)
AND
IsTagged(_Character, "FEMALE", _IsFemale)
AND
CharacterGetRace(_Character, 1, _Race)
AND
DB_LeaderLib_RaceTags(_Race, _RaceTag, _BaseRaceTag)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_BaseRaceTag, _IsFemale, _ID, _EffectName, _BoneName, _AttackDelay)
THEN
DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Character, _AttackDelay);

QRY
LLWEAPONEX_Greatbows_QRY_GetAttackDelay((CHARACTERGUID)_Character, (STRING)_ID)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Character, _)
AND
IsTagged(_Character, "FEMALE", _IsFemale)
AND
NOT CharacterGetRace(_Character, 1, _)
AND
DB_LeaderLib_RaceTags(_Race, _RaceTag, _BaseRaceTag)
AND
IsTagged(_Character, _RaceTag, 1)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Character, _)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_BaseRaceTag, _IsFemale, _ID, _EffectName, _BoneName, _AttackDelay)
THEN
DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Character, _AttackDelay);
//END_REGION

//REGION OMNIBOLT
//Basic attack position summon lightning
PROC
LLWEAPONEX_Greatbows_OnAttackAtPosition((CHARACTERGUID)_Character, "WPN_UNIQUE_LLWEAPONEX_Greatbow_Lightning_Bow_2H_A_7efec0e0-1c2e-4f0d-9ec5-e3a1f40c97b8", (REAL)_x, (REAL)_y, (REAL)_z)
AND
LeaderLib_Roll_QRY(299)
AND
GetDistanceToPosition(_Character, _x, _y, _z, _Dist)
AND
RealProduct(_Dist, 85.0, _DelayR)
AND
Integer(_DelayR, _DelayInt)
AND
LLWEAPONEX_Greatbows_QRY_GetAttackDelay(_Character, "Lightning")
AND
DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Character, _AttackDelay)
AND
IntegerSum(_DelayInt, _AttackDelay, _Delay)
AND
IntegerMax(_Delay, 1300, _DelayClamped)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Character, _AttackDelay);
//LeaderLog_LogInt("DEBUG", "[LLWEAPONEX_10_Greatbows:OnAttackAtPosition] Shooting lightning bolt at position after [", _DelayClamped, "] ms.");
SetVarFloat3(_Character, "LLWEAPONEX_Greatbow_LightningStrikePosition", _x, _y, _z);
LeaderLib_Timers_StartObjectTimer(_Character, _DelayClamped, "LLWEAPONEX_Timers_Greatbow_Greatbow_ExplodeLightningStrikeAtPosition", "LLWEAPONEX_Greatbow_ExplodeLightningStrikeAtPosition");

//Fired when specific skills like Sky Shot are used.
PROC
LeaderLib_Timers_ObjectObjectTimerFinished((CHARACTERGUID)_Character, (GUIDSTRING)_Target, "LLWEAPONEX_Greatbows_ProcOmniBolt")
THEN
ApplyStatus(_Target, "LLWEAPONEX_GREATBOW_LIGHTNINGSTRIKE", 0.0, 0, _Character);
//END_REGION

//REGION CUSHION_FORCE
IF
CharacterStatusApplied(_Target, "LLWEAPONEX_GREATBOW_CUSHION_FORCE", (CHARACTERGUID)_Source)
AND
_Source != NULL_00000000-0000-0000-0000-000000000000
AND
GetPosition(_Target, _x, _y, _z)
AND
GetDistanceTo(_Target, _Source, _Dist)
AND
RealSubtract(12.0, _Dist, _ForceDist) // Closer == Launch farther
THEN
LLWEAPONEX_Greatbows_Internal_CushionForce_DetermineForceIndex(_Target, _Source, _ForceDist);
DB_LLWEAPONEX_Greatbows_Temp_CushionForce(_Target, _Source, _x, _y, _z);
CharacterCharacterSetEvent(_Target, _Source, "LLWEAPONEX_Greatbow_CushionForce_StartLaunch");

PROC
LLWEAPONEX_Greatbows_Internal_CushionForce_DetermineForceIndex((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_ForceDist)
AND
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(_Index, _Min, _Max)
AND
NOT DB_LLWEAPONEX_Greatbows_CushionForce_Temp_ForceResolved(_Target, _Source)
AND
_ForceDist >= _Min
AND
_ForceDist < _Max
THEN
DB_LLWEAPONEX_Greatbows_CushionForce_Temp_ForceResolved(_Target, _Source);
SetVarInteger(_Target, "LLWEAPONEX_CushionForce_ForceIndex", _Index);

PROC
LLWEAPONEX_Greatbows_Internal_CushionForce_DetermineForceIndex((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_ForceDist)
AND
DB_LLWEAPONEX_Greatbows_CushionForce_Temp_ForceResolved(_Target, _Source)
THEN
NOT DB_LLWEAPONEX_Greatbows_CushionForce_Temp_ForceResolved(_Target, _Source);

IF
CharacterStatusRemoved(_Target, "SHOCKWAVE", _)
AND
DB_LLWEAPONEX_Greatbows_Temp_CushionForce(_Target, _Source, _x, _y, _z)
AND
GetPosition(_Target, _tx, _ty, _tz)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_CushionForce(_Target, _Source, _x, _y, _z);
DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz);
ProcObjectTimerCancel(_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling");
ProcObjectTimer(_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling", 60);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling")
AND
DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz)
AND
GetPosition(_Target, _cx, _cy, _cz)
AND
RealSubtract(_ty, _cy, _HeightDiff)
AND
LeaderLib_Math_QRY_AbsoluteValue(_HeightDiff)
AND
DB_LeaderLib_Math_AbsoluteValue(_HeightDiff, _HeightDiffAbs)
THEN
NOT DB_LeaderLib_Math_AbsoluteValue(_HeightDiff, _HeightDiffAbs);
NOT DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz);
DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _cx, _cy, _cz);
LLWEAPONEX_Greatbows_Internal_CushionForce_CheckForFalling(_Target, _HeightDiffAbs);

PROC
LLWEAPONEX_Greatbows_Internal_CushionForce_CheckForFalling((CHARACTERGUID)_Target, (REAL)_HeightDiff)
AND
_HeightDiff > 0.01
THEN
ProcObjectTimerCancel(_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling");
ProcObjectTimer(_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling", 48);

PROC
LLWEAPONEX_Greatbows_Internal_CushionForce_CheckForFalling((CHARACTERGUID)_Target, (REAL)_HeightDiff)
AND
_HeightDiff <= 0.01
AND
DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz);
RemoveStatus(_Target, "LLWEAPONEX_GREATBOW_CUSHION_FORCE");
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Start(_Target, _Source, _x, _y, _z);

PROC
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Start((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_x, (REAL)_y, (REAL)_z)
AND
LeaderLib_Combat_QRY_IsEnemy(_Target, _Source) // Checks for friendly fire
AND
GetPosition(_Target, _tx, _ty, _tz)
AND
GetDistanceToPosition(_Target, _x, _y, _z, _Dist)
AND
RealSubtract(_ty, _y, _FallDistance)
AND
LeaderLib_Math_QRY_AbsoluteValue(_FallDistance)
AND
DB_LeaderLib_Math_AbsoluteValue(_FallDistance, _FallDistanceAbs)
THEN
NOT DB_LeaderLib_Math_AbsoluteValue(_FallDistance, _FallDistanceAbs);
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Apply(_Target, _Source, _Dist);
LLWEAPONEX_Greatbows_Internal_FallDamage_Apply(_Target, _Source, _FallDistanceAbs);

PROC
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Apply((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_TotalDist)
AND
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(_Min, _Max, _Status)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_DistanceDamageResolved(_Target, _Source)
AND
_TotalDist >= _Min
AND
_TotalDist <= _Max
THEN
DB_LLWEAPONEX_Greatbows_Temp_DistanceDamageResolved(_Target, _Source);
ApplyStatus(_Target, _Status, 0.0, 0, _Source);

PROC
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Apply((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_TotalDist)
AND
DB_LLWEAPONEX_Greatbows_Temp_DistanceDamageResolved(_Target, _Source)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_DistanceDamageResolved(_Target, _Source);

PROC
LLWEAPONEX_Greatbows_Internal_FallDamage_Apply((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_FallDistance)
AND
_FallDistance >= 0.25
AND
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(_Min, _Max, _Status)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_FallDamageResolved(_Target, _Source)
AND
_FallDistance >= _Min
AND
_FallDistance <= _Max
THEN
DB_LLWEAPONEX_Greatbows_Temp_FallDamageResolved(_Target, _Source);
ApplyStatus(_Target, _Status, 0.0, 0, _Source);

PROC
LLWEAPONEX_Greatbows_Internal_FallDamage_Apply((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_FallDistance)
AND
DB_LLWEAPONEX_Greatbows_Temp_FallDamageResolved(_Target, _Source)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_FallDamageResolved(_Target, _Source);
//END_REGION

//REGION DELAYED_BARRAGE
IF
CharacterUsedSkillAtPosition(_Char, _x, _y, _z, "Target_LLWEAPONEX_Greatbow_DelayedBarrage", _, _)
THEN
DB_LLWEAPONEX_Greatbows_Temp_DelayedBarrage(_Char, _x, _y, _z, 1);
ApplyStatus(_Char, "LLWEAPONEX_GREATBOW_DELAYEDBARRAGE_TURNCOUNTER", 6.0, 1, _Char);

IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_GREATBOW_DELAYEDBARRAGE_TURNCOUNTER", _)
AND
DB_LLWEAPONEX_Greatbows_Temp_DelayedBarrage(_Char, _x, _y, _z, _Remaining)
AND
IntegerSubtract(_Remaining, 1, _Next)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_DelayedBarrage(_Char, _x, _y, _z, _Remaining);
DB_LLWEAPONEX_Greatbows_Temp_DelayedBarrage(_Char, _x, _y, _z, _Next);
LLWEAPONEX_Greatbows_Internal_DelayedBarrageTurnCheck(_Char, _Next);

PROC
LLWEAPONEX_Greatbows_Internal_DelayedBarrageTurnCheck((CHARACTERGUID)_Char, (INTEGER)_RemainingTurns)
AND
_RemainingTurns < 0
AND
DB_LLWEAPONEX_Greatbows_Temp_DelayedBarrage(_Char, _x, _y, _z, _RemainingTurns)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_DelayedBarrage(_Char, _x, _y, _z, _RemainingTurns);
LeaderLib_Skills_UseSkillAtPosition(_Char, _x, _y, _z, "ProjectileStrike_Greatbow_DelayedBarrage_RainOfArrows", 1, 1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"