Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_WeaponMaster_InitSetting();

KBSECTION
PROC
LLWEAPONEX_WeaponMaster_InitSetting()
THEN
//S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5
//DB_Dialogs(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, "LLWEAPONEX_WeaponMaster_Trader");
LeaderLib_Trader_Register_Dialog("LLWEAPONEX.WeaponMaster.Trader", "LLWEAPONEX_WeaponMaster_Trader"); // Default

LeaderLib_Trader_Register_GlobalTrader("LLWEAPONEX.WeaponMaster.Trader", S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5);

LeaderLib_Trader_Register_StartingGold("LLWEAPONEX.WeaponMaster.Trader", "TUT_Tutorial_A", 250);
LeaderLib_Trader_Register_StartingGold("LLWEAPONEX.WeaponMaster.Trader", "FJ_FortJoy_Main", 3500);

LeaderLib_Trader_Register_Seat("LLWEAPONEX.WeaponMaster.Trader", "FJ_FortJoy_Main", FUR_Humans_Citz_Stool_C_006_f204c77a-6981-4362-9455-6f0f3d0ce99d);
LeaderLib_Trader_Register_Seat("LLWEAPONEX.WeaponMaster.Trader", "LV_HoE_Main", FUR_Humans_Citz_Stool_C_011_6bf9707b-cb27-40fb-943a-a0c9772e3ed9);
LeaderLib_Trader_Register_Seat("LLWEAPONEX.WeaponMaster.Trader", "RC_Main", FUR_Poor_Bar_Stool_B_015_a809eda1-9d42-4eb9-8233-3d0d6ffe2263);
LeaderLib_Trader_Register_Seat("LLWEAPONEX.WeaponMaster.Trader", "CoS_Main", FUR_Poor_Bar_Stool_B_004_LV_Topdeck_A_000_1ee2031f-d46b-447e-a813-252d87a2ae29);
LeaderLib_Trader_Register_Seat("LLWEAPONEX.WeaponMaster.Trader", "Arx_Main", FUR_Humns_Citz_Stool_D_005_ARX_Main_Rework_000_ef9141f1-531c-831f-2e8c-2e1174ff44a1);

//LeaderLib_Trader_Register_PositionObject("LLWEAPONEX.WeaponMaster.Trader", "FJ_FortJoy_Main", );

LeaderLib_Trader_Register_Position("LLWEAPONEX.WeaponMaster.Trader", "FJ_FortJoy_Main", 208.437, -6.144, 155.0);
LeaderLib_Trader_Register_Position("LLWEAPONEX.WeaponMaster.Trader", "LV_HoE_Main", 29.807, 11.044, 3.221);
LeaderLib_Trader_Register_Position("LLWEAPONEX.WeaponMaster.Trader", "RC_Main", 353.891, 0.378, 94.752);
LeaderLib_Trader_Register_Position("LLWEAPONEX.WeaponMaster.Trader", "CoS_Main", -4.062, 14.418, 706.99);
LeaderLib_Trader_Register_Position("LLWEAPONEX.WeaponMaster.Trader", "Arx_Main", 366.42, 16.621, 59.39);

CharacterSetImmortal(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, 1);

QRY
QRY_BlockAttackAD((CHARACTERGUID)S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5)
THEN
DB_NOOP(1);

IF
AttackedByObject(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, _Player, _, _, _)
AND
CharacterIsPlayer((CHARACTERGUID)_Player, 1)
AND
CharacterIsSummon(_Player, 0)
THEN
SetVarObject(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, "LLWEAPONEX_WeaponMaster_LastHostile", _Player);
Proc_StartDialog(1, "AD_LLWEAPONEX_WeaponMaster_AttackedByPlayer", S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5);

/*
IF
CharacterAttitudeTowardsPlayerChanged(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, _Player, _Attitude)
AND
_Attitude < 0
THEN
SetVarObject(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, "LLWEAPONEX_WeaponMaster_LastHostile", _Player);
*/

IF
CharacterAttitudeTowardsPlayerChanged(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, _Player, _Attitude)
AND
_Attitude <= -10
AND
ObjectGetFlag(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, "LLWEAPONEX_WeaponMaster_AutoSaveBeforeWipe", 0)
THEN
ObjectSetFlag(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, "LLWEAPONEX_WeaponMaster_AutoSaveBeforeWipe", 0);
AutoSave();

IF
CharacterAttitudeTowardsPlayerChanged(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, _Player, _Attitude)
AND
_Attitude >= 10
AND
ObjectGetFlag(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, "LLWEAPONEX_WeaponMaster_AutoSaveBeforeWipe", 1)
THEN
ObjectClearFlag(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, "LLWEAPONEX_WeaponMaster_AutoSaveBeforeWipe", 0);

IF
ObjectEnteredCombat(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, _CombatID)
THEN
LeaderLib_Timers_StartObjectTimer(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5, 50, "LLWEAPONEX_Timers_OmaeWaMouShindeiru_CheckForPlayers", "LLWEAPONEX_OmaeWaMouShindeiru_CheckForPlayers");
JumpToTurn(S_LLWEAPONEX_WeaponMaster_Trader_3cabc61d-6385-4ae3-b38f-c4f24a8d16b5);

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_CheckForPlayers")
AND
CombatGetIDForCharacter(_WeaponMaster, _CombatID)
AND
DB_CombatCharacters(_Char, _CombatID)
AND
CharacterIsPlayer(_Char, 1)
THEN
LeaderLib_Timers_StartObjectTimer(_WeaponMaster, 250, "LLWEAPONEX_Timers_OmaeWaMouShindeiru_Start", "LLWEAPONEX_OmaeWaMouShindeiru_Start");

/*
IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_CheckForPlayers")
AND
LeaderLib_Timers_QRY_ObjectTimerStarted(_WeaponMaster, "LLWEAPONEX_Timers_OmaeWaMouShindeiru_Start")
THEN
JumpToTurn(_WeaponMaster);
*/

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_Start")
THEN
Proc_StartDialog(1, "AD_LLWEAPONEX_WeaponMaster_OmaeWaMouShindeiru_Start", _WeaponMaster);
LeaderLib_Timers_StartObjectTimer(_WeaponMaster, 250, "LLWEAPONEX_Timers_OmaeWaMouShindeiru_Cast", "LLWEAPONEX_OmaeWaMouShindeiru_Cast");
CharacterSetFightMode(_WeaponMaster, 1, 0);

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_Cast")
THEN
CharacterUseSkill(_WeaponMaster, "Shout_LLWEAPONEX_WeaponMaster_OmaeWaMouShindeiru", _WeaponMaster, 1, 1, 1);

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_WEAPONMASTER_TARGET", (CHARACTERGUID)_WeaponMaster)
THEN
DB_LLWEAPONEX_WeaponMaster_Temp_KillTarget(_WeaponMaster, _Target);

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_WEAPONMASTER_TARGET", _WeaponMaster)
AND
NOT LeaderLib_Helper_QRY_VariableObjectIsSet(_WeaponMaster, "LLWEAPONEX_WeaponMaster_LastHostile")
THEN
SetVarObject(_WeaponMaster, "LLWEAPONEX_WeaponMaster_LastHostile", _Target);

IF
SkillCast(_WeaponMaster, "Shout_LLWEAPONEX_WeaponMaster_OmaeWaMouShindeiru", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_WeaponMaster, 250, "LLWEAPONEX_Timers_OmaeWaMouShindeiru_Next", "LLWEAPONEX_OmaeWaMouShindeiru_Next");

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_Next")
AND
DB_LLWEAPONEX_WeaponMaster_Temp_KillTarget(_WeaponMaster, _Target)
AND
NOT DB_LLWEAPONEX_WeaponMaster_Temp_NextTarget(_WeaponMaster, _)
AND
GetPosition(_WeaponMaster, _x, _y, _z)
THEN
Proc_StartDialog(1, "AD_LLWEAPONEX_Player_OmaeWaMouShindeiru_Hit", _Target);
DB_LLWEAPONEX_WeaponMaster_Temp_NextTarget(_WeaponMaster, _Target);
PlayEffect(_WeaponMaster, "LLWEAPONEX_FX_Skills_InstantTransmission_Dissolve_Body_01");
PlayEffectAtPosition("LLWEAPONEX_FX_Skills_InstantTransmission_Disappear_01", _x, _y, _z);
TeleportTo(_WeaponMaster, _Target, "LLWEAPONEX_OmaeWaMouShindeiru_Teleported", 0, 1, 0);
LeaderLib_Timers_StartCharacterCharacterTimer(_WeaponMaster, _Target, 25, "LLWEAPONEX_Timers_OmaeWaMouShindeiru_OneShot", "LLWEAPONEX_OmaeWaMouShindeiru_OneShot");

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_Teleported")
THEN
PlayEffect(_WeaponMaster, "LLWEAPONEX_FX_Skills_InstantTransmission_Appear_01");
CharacterFlushQueue(_WeaponMaster);
PlayAnimation(_WeaponMaster, "");
PlayAnimation(_WeaponMaster, "skill_attack_round_01_cast");

IF
CharacterCharacterEvent(_WeaponMaster, _Target, "LLWEAPONEX_OmaeWaMouShindeiru_OneShot")
THEN
NOT DB_LLWEAPONEX_WeaponMaster_Temp_KillTarget(_WeaponMaster, _Target);
NOT DB_LLWEAPONEX_WeaponMaster_Temp_NextTarget(_WeaponMaster, _Target);
ApplyStatus(_Target, "LLWEAPONEX_WEAPONMASTER_OMAEWAMOUSHINDEIRU", 0.0, 1, _WeaponMaster);
LeaderLib_Timers_StartObjectTimer(_WeaponMaster, 250, "LLWEAPONEX_Timers_OmaeWaMouShindeiru_Next", "LLWEAPONEX_OmaeWaMouShindeiru_Next");

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_Next")
AND
NOT DB_LLWEAPONEX_WeaponMaster_Temp_KillTarget(_WeaponMaster, _)
THEN
SetStoryEvent(_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_Cleanup");

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_Cleanup")
AND
DB_LLWEAPONEX_WeaponMaster_Temp_NextTarget(_WeaponMaster, _Target)
THEN
NOT DB_LLWEAPONEX_WeaponMaster_Temp_NextTarget(_WeaponMaster, _Target);

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_Cleanup")
AND
GetVarObject(_WeaponMaster, "LLWEAPONEX_WeaponMaster_LastHostile", _Player)
AND
GetPosition(_Player, _x, _y, _z)
AND
FindValidPosition(_x, _y, _z, 2.0, _WeaponMaster, _tx, _ty, _tz)
AND
RealSum(_tx, 1.0, _fx)
AND
RealSum(_tz, 1.0, _fz)
THEN
LeaderLog_TracePosition(_Player, "[LLWEAPONEX:Debug:WeaponMasterPosition]", "");
CharacterStopAllEffectsWithName(_WeaponMaster, "LLWEAPONEX_FX_Skills_InstantTransmission_Disappear_01");
//TeleportTo(_WeaponMaster, _Player, "LLWEAPONEX_OmaeWaMouShindeiru_FinalTeleport", 0, 1, 0);
TeleportToPosition(_WeaponMaster, _fx, _ty, _fz, "LLWEAPONEX_OmaeWaMouShindeiru_FinalTeleport", 0);

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_FinalTeleport")
THEN
PlayEffect(_WeaponMaster, "LLWEAPONEX_FX_Skills_InstantTransmission_Appear_01");
SetStoryEvent(_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_FinalDone");

/* IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_FinalTeleport")
AND
GetPosition(_WeaponMaster, _x, _y, _z)
AND
RealSum(_x, 0.5, _tx)
AND
RealSum(_z, 0.5, _tz)
THEN
LeaderLog_TracePosition(_WeaponMaster, "[LLWEAPONEX:Debug:WeaponMasterPosition]", "");
PlayEffect(_WeaponMaster, "LLWEAPONEX_FX_Skills_InstantTransmission_Appear_01");
//TeleportToPosition(_WeaponMaster, _tx, _y, _tz, "", 0);
CharacterMoveToPosition(_WeaponMaster, _tx, _y, _tz, 1, "LLWEAPONEX_OmaeWaMouShindeiru_FinalDone"); */

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_FinalDone")
THEN
//LeaderLog_TracePosition(_WeaponMaster, "[LLWEAPONEX:Debug:WeaponMasterPosition]", "");
Proc_StartDialog(1, "AD_LLWEAPONEX_WeaponMaster_OmaeWaMouShindeiru_End", _WeaponMaster);
CharacterFlushQueue(_WeaponMaster);
PlayAnimation(_WeaponMaster, "");
CharacterSetFightMode(_WeaponMaster, 0, 0);
LeaderLib_Timers_StartObjectTimer(_WeaponMaster, 2550, "LLWEAPONEX_Timers_OmaeWaMouShindeiru_DustOff", "LLWEAPONEX_OmaeWaMouShindeiru_DustOff");
//PlayAnimation(_WeaponMaster, "sheath", "LLWEAPONEX_OmaeWaMouShindeiru_DustOff_Start");

IF
StoryEvent((CHARACTERGUID)_WeaponMaster, "LLWEAPONEX_OmaeWaMouShindeiru_DustOff")
THEN
PlayAnimation(_WeaponMaster, "");
PlayAnimation(_WeaponMaster, "Dust_Off_01");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_WeaponExpansion"