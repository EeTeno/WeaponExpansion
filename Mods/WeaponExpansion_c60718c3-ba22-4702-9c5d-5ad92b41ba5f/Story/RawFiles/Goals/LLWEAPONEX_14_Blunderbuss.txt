Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION EQUIP_EVENTS
PROC
LLWEAPONEX_OnItemEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_Firearm_Blunderbuss_2H_A_507801c0-4c6b-4b39-9aed-13fcd0962dd9")
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLWEAPONEX_BlunderbussEquipped", "WeaponExpansion");

PROC
LLWEAPONEX_OnItemUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_Firearm_Blunderbuss_2H_A_507801c0-4c6b-4b39-9aed-13fcd0962dd9")
THEN
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_BlunderbussEquipped", "WeaponExpansion", 500);
//END_REGION

//REGION BASIC_ATTACK
IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_BULLET_HIT", (CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_Blunderbuss_Temp_BasicAttack(_Source, (GUIDSTRING)_Target)
THEN
NOT DB_LLWEAPONEX_Blunderbuss_Temp_BasicAttack(_Source, _Target);
LeaderLib_Timers_CancelObjectObjectTimer(_Source, _Target, "LLWEAPONEX_Timers_Blunderbuss_ClearBasicAttackEntry");
CharacterCharacterSetEvent(_Source, (CHARACTERGUID)_Target, "LLWEAPONEX_Blunderbuss_ExplodeProjectile");

IF
ItemStatusAttempt(_Target, "LLWEAPONEX_BULLET_HIT", (CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_Blunderbuss_Temp_BasicAttack(_Source, (GUIDSTRING)_Target)
THEN
NOT DB_LLWEAPONEX_Blunderbuss_Temp_BasicAttack(_Source, _Target);
LeaderLib_Timers_CancelObjectObjectTimer(_Source, _Target, "LLWEAPONEX_Timers_Blunderbuss_ClearBasicAttackEntry");
CharacterItemSetEvent(_Source, (ITEMGUID)_Target, "LLWEAPONEX_Blunderbuss_ExplodeProjectile");

PROC
LeaderLib_Timers_ObjectObjectTimerFinished((CHARACTERGUID)_Char, (GUIDSTRING)_Target, "LLWEAPONEX_Blunderbuss_ClearBasicAttackEntry")
AND
DB_LLWEAPONEX_Blunderbuss_Temp_BasicAttack(_Char, _Target)
THEN
NOT DB_LLWEAPONEX_Blunderbuss_Temp_BasicAttack(_Char, _Target);

PROC
LLWEAPONEX_Blunderbuss_OnAttackAtPosition((CHARACTERGUID)_Char, (REAL)_x, (REAL)_y, (REAL)_z)
AND
GetDistanceToPosition(_Char, _x, _y, _z, _Dist)
AND
RealProduct(_Dist, 80.0, _DelayR)
AND
Integer(_DelayR, _DelayInt)
AND
IntegerSum(_DelayInt, 245, _Delay)
AND
IntegerMax(_Delay, 950, _DelayClamped)
THEN
SetVarFloat3(_Char, "LLWEAPONEX_Blunderbuss_AttackPosition", _x, _y, _z);
LeaderLib_Timers_StartObjectTimer(_Char, _DelayClamped, "LLWEAPONEX_Timers_Blunderbuss_ExplodeAtPosition", "LLWEAPONEX_Blunderbuss_ExplodeAtPosition");
//END_REGION

//REGION SKILLS
IF
SkillCast(_Player, "Zone_LLWEAPONEX_Blunderbuss_Cannonball", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_Player, 300, "LLWEAPONEX_Timers_Blunderbuss_Cannonball_Explode", "LLWEAPONEX_Blunderbuss_Cannonball_Explode");
//END_REGION

//REGION DUD
IF
CharacterItemEvent(_Caster, _Dud, "LLWEAPONEX_DelayedExplosive_Initialized")
AND
LeaderLib_Combat_QRY_IsActiveTurn(_Caster)
THEN
DB_LLWEAPONEX_Blunderbuss_Temp_Duds_SkipTurn((GUIDSTRING)_Caster);

IF
CharacterItemEvent(_Caster, _Dud, "LLWEAPONEX_DelayedExplosive_Initialized")
AND
CharacterIsInCombat(_Caster, 1)
AND
CombatGetIDForCharacter(_Caster, _CombatID)
THEN
SetVarInteger(_Dud, "LLWEAPONEX_CombatID", _CombatID);
DB_LLWEAPONEX_Blunderbuss_Temp_Duds(_Caster, _Dud, _CombatID);

IF
StoryEvent((ITEMGUID)_Dud, "LLWEAPONEX_DelayedExplosive_Finished")
AND
DB_LLWEAPONEX_Blunderbuss_Temp_Duds(_Caster, _Dud, _CombatID)
THEN
NOT DB_LLWEAPONEX_Blunderbuss_Temp_Duds(_Caster, _Dud, _CombatID);
//END_REGION

//REGION GUN_ANIM_OVERRIDE
//PROC
//LLWEAPONEX_OnWeaponTypeEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, "LLWEAPONEX_Katana")
IF
ItemTemplateEquipped("WPN_UNIQUE_LLWEAPONEX_Gun_1H_VampireSlayer_A_Ranged_e1bb0780-be83-4425-850c-dce56e1706ef", _Player)
THEN
LLWEAPONEX_AnimationSetOverride_Set(_Player, "LLWEAPONEX_Override1");

//PROC
//LLWEAPONEX_WeaponMastery_OnWeaponUnEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, "LLWEAPONEX_Katana")
IF
ItemTemplateUnEquipped("WPN_UNIQUE_LLWEAPONEX_Gun_1H_VampireSlayer_A_Ranged_e1bb0780-be83-4425-850c-dce56e1706ef", _Player)
THEN
LLWEAPONEX_AnimationSetOverride_Clear(_Player);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"