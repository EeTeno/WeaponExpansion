Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//Blocking the first hit
IF
NRD_OnPrepareHit((CHARACTERGUID)_Char, (CHARACTERGUID)_Attacker, _Damage, _Handle)
AND
DB_LLWEAPONEX_DualShields_Temp_Covering(_Blocker, _Char)
AND
_Char != _Blocker
AND
_Damage > 0
AND
CharacterCanSee(_Blocker, _Attacker, 1)
AND
NRD_HitGetInt(_Handle, "HitType", _Val)
AND
_Val <=3 // Everything but Surface,DoT,Reflected
AND
LeaderLib_Hit_QRY_HitSucceeded((GUIDSTRING)_Char, _Handle, 1)
THEN
NRD_HitClearAllDamage(_Handle);
NRD_HitSetInt(_Handle,"SimulateHit",0);
NRD_HitSetInt(_Handle,"Dodged",0);
NRD_HitSetInt(_Handle,"Missed",0);
NRD_HitSetInt(_Handle,"Blocked",1);
NRD_HitSetInt(_Handle,"Hit",0);
NRD_HitSetInt(_Handle,"DontCreateBloodSurface",1);
DB_LLWEAPONEX_DualShields_Temp_BlockedHit((CHARACTERGUID)_Char, _Blocker, _Attacker);

IF
NRD_OnHit((CHARACTERGUID)_Char, (CHARACTERGUID)_Attacker, _Damage, _Handle)
AND
DB_LLWEAPONEX_DualShields_Temp_BlockedHit(_Char, _Blocker, _Attacker)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_BlockedHit(_Char, _Blocker, _Attacker);
NRD_StatusSetInt(_Char,_Handle,"AllowInterruptAction",0);
NRD_StatusSetInt(_Char,_Handle,"ForceInterrupt",0);
NRD_StatusSetInt(_Char,_Handle,"Interruption",0);
LLWEAPONEX_DualShields_ShieldCover(_Attacker, _Blocker, _Char);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"