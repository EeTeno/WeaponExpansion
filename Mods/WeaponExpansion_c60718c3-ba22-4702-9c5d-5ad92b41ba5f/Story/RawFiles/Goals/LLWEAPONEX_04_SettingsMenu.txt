Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

IF
ObjectFlagSet("LLWEAPONEX_Commands_StopGrimoireFX", (CHARACTERGUID)_Player, _)
THEN
ObjectClearFlag(_Player, "LLWEAPONEX_Commands_StopGrimoireFX", 0);
LLWEAPONEX_Grimoire_StopAllEffects_All(_Player);

IF
ObjectFlagSet("LLWEAPONEX_Commands_ResetBalrinAxe", (CHARACTERGUID)_Player, _)
AND
LeaderLib_Helper_QRY_ClearObjectFlag(_Player, "LLWEAPONEX_Commands_ResetBalrinAxe")
AND
DB_LLWEAPONEX_Throwing_Temp_ThrowingAxeCaster(_Player, _Target, _ThrowingAxe, _Success)
THEN
NOT DB_LLWEAPONEX_Throwing_Temp_ThrowingAxeCaster(_Player, _Target, _ThrowingAxe, _Success);
LeaderLib_Statuses_SafeRemoval(_Target, "LLWEAPONEX_WEAPON_THROW_UNIQUE_AXE1H_A");
LLWEAPONEX_Throwing_ReturnBalrinAxe(_Player, _ThrowingAxe);

//REGION MASTERY_MENU_TEXT
IF
DialogStarted("LLWEAPONEX_WeaponMasteryMenu", _Instance)
//DualDialogStart("LLWEAPONEX_WeaponMasteryMenu", _Instance)
//ObjectFlagSet("LLWEAPONEX_WeaponMasteryMenu_SetExperienceVariable", (CHARACTERGUID)_Player, _Instance)
//DualDialogRequested("LLWEAPONEX_WeaponMasteryMenu", _ParentInstance, _TargetInstance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
ObjectClearFlag(_Player, "LLWEAPONEX_WeaponMasteryMenu_SetExperienceVariable");
DB_LLWEAPONEX_WeaponMastery_Temp_CurrentExperienceString(_Player, _Instance, "");
LLWEAPONEX_SettingsMenu_Internal_SetExperienceString(_Player, _Instance);

PROC
LLWEAPONEX_SettingsMenu_Internal_SetExperienceString((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLWEAPONEX_WeaponMastery_MasteryVariables(_WeaponType, _CurrentXPVar, _LevelVar, _EnabledFlag)
AND
DB_LLWEAPONEX_WeaponMastery_MasteryNames(_WeaponType, _MasteryName)
AND
GetVarInteger(_Player, _CurrentXPVar, _CurrentExpInt)
//AND
//GetVarInteger(_Player, _MaxXPVar, _MaxExpInt)
AND
GetVarInteger(_Player, _LevelVar, _CurrentLevelInt)
AND
IntegerMin(_CurrentLevelInt, 4, _ClampedLevel) // Ranks 0-4 max currently
AND
IntegertoString(_CurrentExpInt, _CurrentExp)
//AND
//IntegertoString(_MaxExpInt, _MaxExp)
AND
DB_LLWEAPONEX_WeaponMastery_RankNames(_WeaponType, _ClampedLevel, _RankName)
AND
DB_LLWEAPONEX_WeaponMastery_Temp_CurrentExperienceString(_Player, _Instance, _String)
AND
LLWEAPONEX_SettingsMenu_Internal_QRY_CombineString(_String, _MasteryName, _RankName, _CurrentExpInt, _CurrentLevelInt, 4)
AND
DB_LeaderLog_Temp_CombinedString(_NewString)
THEN
NOT DB_LeaderLog_Temp_CombinedString(_NewString);
NOT DB_LLWEAPONEX_WeaponMastery_Temp_CurrentExperienceString(_Player, _Instance, _String);
DB_LLWEAPONEX_WeaponMastery_Temp_CurrentExperienceString(_Player, _Instance, _NewString);

QRY
LLWEAPONEX_SettingsMenu_Internal_QRY_CombineString((STRING)_Str, (STRING)_MasteryName, (STRING)_RankName, (INTEGER)_CurrentExp, (INTEGER)_Level, (INTEGER)_MaxLevel)
AND
_Level < _MaxLevel
AND
DB_LLWEAPONEX_WeaponMastery_MaxExperience(_Level, _MaxExperience)
AND
Real(_CurrentExp, _x1)
AND
Real(_MaxExperience, _x2)
AND
RealDivide(_x1, _x2, _r)
AND
RealProduct(_r, 100.0, _r2)
AND
Integer(_r2, _Percentage)
AND
IntegertoString(_Percentage, _PercentageStr)
THEN
LeaderLog_Log("COMBINE", _Str, _MasteryName, " - ",_RankName, " - <font color='#F228EC'>",_PercentageStr, "%</font><br>");

QRY
LLWEAPONEX_SettingsMenu_Internal_QRY_CombineString((STRING)_Str, (STRING)_MasteryName, (STRING)_RankName, (INTEGER)_CurrentExp, (INTEGER)_Level, (INTEGER)_MaxLevel)
AND
_Level >= _MaxLevel
THEN
LeaderLog_Log("COMBINE", _Str, _MasteryName, " - ",_RankName, "<br>");

PROC
LLWEAPONEX_SettingsMenu_Internal_SetExperienceString((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLWEAPONEX_WeaponMastery_Temp_CurrentExperienceString(_Player, _Instance, _String)
THEN
NOT DB_LLWEAPONEX_WeaponMastery_Temp_CurrentExperienceString(_Player, _Instance, _String);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:WeaponMastery:SetExperienceString] Set current experience dialog variable to [",_String,"].");
DialogSetVariableStringForInstance(_Instance, "LLWEAPONEX_WeaponMastery_Current_f4dda1ff-ab17-4141-be2f-763c6c22a7cc", _String);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_WeaponExpansion"
