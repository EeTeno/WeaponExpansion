Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Rods_InitSettings();
KBSECTION
PROC
LLWEAPONEX_Rods_InitSettings()
THEN
/*
DB_LLWEAPONEX_Rods_SkillStatuses("LLWEAPONEX_ROD_AIR", "LLWEAPONEX_ROD_SKILL_AIR", "LLWEAPONEX_ROD_SKILL_AIR_OFFHAND");
DB_LLWEAPONEX_Rods_SkillStatuses("LLWEAPONEX_ROD_CHAOS", "LLWEAPONEX_ROD_SKILL_CHAOS", "LLWEAPONEX_ROD_SKILL_CHAOS_OFFHAND");
DB_LLWEAPONEX_Rods_SkillStatuses("LLWEAPONEX_ROD_FIRE", "LLWEAPONEX_ROD_SKILL_FIRE", "LLWEAPONEX_ROD_SKILL_FIRE_OFFHAND");
DB_LLWEAPONEX_Rods_SkillStatuses("LLWEAPONEX_ROD_POISON", "LLWEAPONEX_ROD_SKILL_POISON", "LLWEAPONEX_ROD_SKILL_POISON_OFFHAND");
DB_LLWEAPONEX_Rods_SkillStatuses("LLWEAPONEX_ROD_WATER", "LLWEAPONEX_ROD_SKILL_WATER", "LLWEAPONEX_ROD_SKILL_WATER_OFFHAND");
*/
//Possibly preferred since RemoveHarmfulStatuses can clear the status version
DB_LLWEAPONEX_Rods_Skills("LLWEAPONEX_ROD_AIR", "Projectile_LLWEAPONEX_ShootRod_Air", "Projectile_LLWEAPONEX_ShootRod_Air_Offhand");
DB_LLWEAPONEX_Rods_Skills("LLWEAPONEX_ROD_CHAOS", "Projectile_LLWEAPONEX_ShootRod_Chaos", "Projectile_LLWEAPONEX_ShootRod_Chaos_Offhand");
DB_LLWEAPONEX_Rods_Skills("LLWEAPONEX_ROD_FIRE", "Projectile_LLWEAPONEX_ShootRod_Fire", "Projectile_LLWEAPONEX_ShootRod_Fire_Offhand");
DB_LLWEAPONEX_Rods_Skills("LLWEAPONEX_ROD_POISON", "Projectile_LLWEAPONEX_ShootRod_Poison", "Projectile_LLWEAPONEX_ShootRod_Poison_Offhand");
DB_LLWEAPONEX_Rods_Skills("LLWEAPONEX_ROD_WATER", "Projectile_LLWEAPONEX_ShootRod_Water", "Projectile_LLWEAPONEX_ShootRod_Water_Offhand");

IF
DB_LLWEAPONEX_Rods_Skills(_Status, _MainhandSkill, _OffhandSkill)
THEN
DB_LeaderLib_BlockedSkill(_MainhandSkill);
DB_LeaderLib_BlockedSkill(_OffhandSkill);

PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 0, 9, 3, 2)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods] Updating settings from version [",_PastVersion,"] to [",_NewVersion,"]");
SysClear("DB_LLWEAPONEX_Rods_SkillStatuses", 3);
SysClear("DB_LLWEAPONEX_Rods_Skills", 3);
LLWEAPONEX_Rods_InitSettings();

PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 0, 9, 3, 2)
AND
DB_IsPlayer(_Player)
AND
DB_LLWEAPONEX_Rods_Skills(_Status, _MainhandSkill, _OffhandSkill)
AND
HasActiveStatus(_Player, _Status, 0)
THEN
CharacterRemoveSkill(_Player, _MainhandSkill);
CharacterRemoveSkill(_Player, _OffhandSkill);

/*
IF
CanUseItem(_Character, _Item, _)
AND
GetTemplate(_Item, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:CanUseItem] Character attempting to use item (",_Template,").");

IF
ItemEquipped(_Item, _Character)
AND
CharacterGetEquippedItem(_Character, "Shield", _Item)
AND
GetTemplate(_Item, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:ItemEquipped] Character equipped shield slot item (",_Template,").");

IF
SkillAdded(_Character, _Skill, 0)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:SkillAdded] Skill (",_Skill,") added to character.");
*/

//REGION SKILL_REFRESH
QRY
LLWEAPONEX_Rods_QRY_IsRodSkill((STRING)_MainhandSkill)
AND
DB_LLWEAPONEX_Rods_Skills(_Type, _MainhandSkill, _OffhandSkill)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Rods_QRY_IsRodSkill((STRING)_OffhandSkill)
AND
DB_LLWEAPONEX_Rods_Skills(_Type, _MainhandSkill, _OffhandSkill)
THEN
DB_NOOP(1);

/*
PROC
LeaderLib_PresetMenu_BlockSkillCopying((CHARACTERGUID)_Player, (STRING)_Preset, (STRING)_Skill, (INTEGER)_ChangingRace)
AND
LLWEAPONEX_Rods_QRY_IsRodSkill(_Skill)
THEN
DB_LeaderLib_PresetMenu_Temp_BlockSkill(_Player, _Skill);
*/

//Post LeaderLib Preset
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
LLWEAPONEX_QRY_WeaponTypeEquipped(_Player, "LLWEAPONEX_Rod")
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods] Refreshing rod weapon(s) to determine related skill.");
LeaderLib_Helper_RefreshWeapons(_Player);

//PostCC
PROC
LeaderLib_Helper_CanRefreshItem((CHARACTERGUID)_Character, (ITEMGUID)_Item)
AND
IsTagged(_Item, "LLWEAPONEX_Rod", 1)
AND
NOT LLWEAPONEX_Rods_QRY_SkillVarsSet(_Item)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:CanRefreshItem] Listening for rod skill.");
DB_LLWEAPONEX_Rods_Temp_ListenForRodSkill(_Character, _Item);
ProcObjectTimerCancel(_Character, "LLWEAPONEX_Timers_RodListeningTimedOut");
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_RodListeningTimedOut", 500);

//Resurrection - Reapply Status
IF
CharacterStatusRemoved(_Character, "RESURRECT", _)
AND
DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _Skill)
THEN
LLWEAPONEX_Rods_AdjustRodSkill(_Character, _Rod);
//END_REGION

QRY
LLWEAPONEX_Rods_QRY_SkillVarsSet((ITEMGUID)_Rod)
AND
GetVarString(_Rod, "LLWEAPONEX_Rod_Skill", _MainhandSkill)
AND
GetVarString(_Rod, "LLWEAPONEX_Rod_Skill_Offhand", _OffhandSkill)
AND
_MainhandSkill != ""
AND
_OffhandSkill != ""
THEN
DB_NOOP(1);

//REGION ROD_SKILLS
IF
CanUseItem(_Character, _Item, _)
AND
IsTagged(_Item, "LLWEAPONEX_Rod", 1)
AND
IsTagged(_Item, "LLMIME_MIMICKED_WEAPON", 0)
AND
NOT LLWEAPONEX_Rods_QRY_SkillVarsSet(_Item)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:CanUseItem] Listening for rod skill.");
DB_LLWEAPONEX_Rods_Temp_ListenForRodSkill(_Character, _Item);
ProcObjectTimerCancel(_Character, "LLWEAPONEX_Timers_RodListeningTimedOut");
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_RodListeningTimedOut", 500);

IF
CharacterStatusAttempt(_Character, _TypeStatus, _)
AND
//DB_LLWEAPONEX_Rods_SkillStatuses(_TypeStatus, _MainhandSkillStatus, _OffhandSkill)
DB_LLWEAPONEX_Rods_Skills(_TypeStatus, _MainhandSkill, _OffhandSkill)
AND
NOT DB_LLWEAPONEX_Rods_Temp_ListenForRodSkill(_Character, _)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:CharacterStatusAttempt(",_TypeStatus,")] No rods waiting for a status.");

IF
CharacterStatusAttempt(_Character, _TypeStatus, _)
AND
//DB_LLWEAPONEX_Rods_SkillStatuses(_TypeStatus, _MainhandSkillStatus, _OffhandSkill)
DB_LLWEAPONEX_Rods_Skills(_TypeStatus, _MainhandSkill, _OffhandSkill)
AND
LeaderLog_QRY_Log("DEBUG", "[LLWEAPONEX:Rods:CharacterStatusAttempt] Status attempt (",_TypeStatus,").")
AND
DB_LLWEAPONEX_Rods_Temp_ListenForRodSkill(_Character, _Item)
AND
NOT LLWEAPONEX_Rods_QRY_SkillVarsSet(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:CharacterStatusAttempt(",_TypeStatus,")] Setting rod [",_Template,"] mainhand skill [",_MainhandSkill,"] and offhand skill (",_OffhandSkill,").");
NOT DB_LLWEAPONEX_Rods_Temp_ListenForRodSkill(_Character, _Item);
SetVarString(_Item, "LLWEAPONEX_Rod_Skill", _MainhandSkill);
SetVarString(_Item, "LLWEAPONEX_Rod_Skill_Offhand", _OffhandSkill);

//Switch back to the mainhand skill when the main hand is empty with an offhand rod
IF
ItemUnEquipped(_Item, _Character)
AND
DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _OffhandSkill)
AND
_Item != _Rod
AND
CharacterGetEquippedItem(_Character, "Shield", _Rod)
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "WPN_", 1)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:ItemUnEquipped(Weapon)] Reverting rod offhand skill to mainhand.");
NOT DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _OffhandSkill);
RemoveStatus(_Character, _OffhandSkill);
LeaderLib_Timers_StartCharacterItemTimer(_Character, _Rod, 50, "LLWEAPONEX_Timers_AdjustRodSkill", "LLWEAPONEX_Rods_AdjustSkills");

IF
CharacterItemEvent(_Character, _Rod, "LLWEAPONEX_Rods_AdjustSkills")
THEN
LLWEAPONEX_Rods_AdjustRodSkill(_Character, _Rod);

//Revert to offhand skill
IF
ItemEquipped(_Item, _Character)
AND
IsTagged(_Item, "LLMIME_MIMICKED_WEAPON", 0)
AND
DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _MainhandSkill)
AND
CharacterGetEquippedItem(_Character, "Weapon", _Item)
AND
CharacterGetEquippedItem(_Character, "Shield", _Rod)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:ItemEquipped(Weapon)] Reverting rod mainhand skill to offhand.");
NOT DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _MainhandSkill);
//RemoveStatus(_Character, _MainhandSkill);
CharacterRemoveSkill(_Character, _MainhandSkill);
LeaderLib_Timers_StartCharacterItemTimer(_Character, _Rod, 50, "LLWEAPONEX_Timers_AdjustRodSkill", "LLWEAPONEX_Rods_AdjustSkills");

IF
ItemEquipped(_Rod, _Character)
AND
DB_LLWEAPONEX_Rods_Temp_ListenForRodSkill(_Character, _Rod)
AND
NOT LLWEAPONEX_Rods_QRY_SkillVarsSet(_Rod) // Assume it's the same type as one equipped
AND
DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _OtherRod, _Status)
AND
GetVarString(_OtherRod, "LLWEAPONEX_Rod_Skill", _MainhandSkill)
AND
GetVarString(_OtherRod, "LLWEAPONEX_Rod_Skill_Offhand", _OffhandSkill)
THEN
NOT DB_LLWEAPONEX_Rods_Temp_ListenForRodSkill(_Character, _Rod);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:ItemEquipped] Rod status was never applied. Assuming it shares the same type as the rod equipped.");
SetVarString(_Rod, "LLWEAPONEX_Rod_Skill", _MainhandSkill);
SetVarString(_Rod, "LLWEAPONEX_Rod_Skill_Offhand", _OffhandSkill);

IF
ItemEquipped(_Rod, _Character)
AND
IsTagged(_Rod, "LLMIME_MIMICKED_WEAPON", 0)
AND
IsTagged(_Rod, "LLWEAPONEX_Rod", 1)
THEN
LeaderLib_Timers_StartCharacterItemTimer(_Character, _Rod, 50, "LLWEAPONEX_Timers_AdjustRodSkill", "LLWEAPONEX_Rods_AdjustSkills");

PROC
LLWEAPONEX_Rods_AdjustRodSkill((CHARACTERGUID)_Character, (ITEMGUID)_Rod)
AND
CharacterGetEquippedItem(_Character, "Shield", _Rod)
AND
CharacterGetEquippedItem(_Character, "Weapon", (ITEMGUID)_MainhandWeapon) // Only proceed if we're using a dual-wield animation set
AND
_MainhandWeapon != NULL_00000000-0000-0000-0000-000000000000
AND
GetVarString(_Rod, "LLWEAPONEX_Rod_Skill", _MainhandSkill)
AND
GetVarString(_Rod, "LLWEAPONEX_Rod_Skill_Offhand", _OffhandSkill)
THEN
LLWEAPONEX_Rods_RemoveMainhandSkill(_Character, _Rod, _MainhandSkill, _MainhandWeapon);
//ApplyStatus(_Character, _OffhandSkill, -1.0, 1, _Character);
CharacterAddSkill(_Character, _OffhandSkill, 0);
DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _OffhandSkill);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:AdjustRodSkill] Adding offhand rod skill status (",_OffhandSkill,").");

//Revert back to the mainhand skill
PROC
LLWEAPONEX_Rods_AdjustRodSkill((CHARACTERGUID)_Character, (ITEMGUID)_Rod)
AND
NOT DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _)
AND
GetVarString(_Rod, "LLWEAPONEX_Rod_Skill", _MainhandSkill)
AND
NOT LeaderLib_Helper_QRY_HasStatus(_Character, _MainhandSkill)
THEN
//ApplyStatus(_Character, _MainhandSkill, -1.0, 1, _Character);
CharacterAddSkill(_Character, _MainhandSkill, 0);
DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _MainhandSkill);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:AdjustRodSkill] Adding mainhand rod skill status (",_MainhandSkill,").");
//END_REGION

//REGION SKILL_REMOVAL
QRY
LLWEAPONEX_Rods_QRY_SkipSkillRemoval((CHARACTERGUID)_Character, (ITEMGUID)_Rod, (STRING)_MainhandSkill, (ITEMGUID)_MainhandWeapon)
AND
GetVarString(_MainhandWeapon, "LLWEAPONEX_Rod_Skill", _OtherMainhandSkillStatus)
AND
_OtherMainhandSkillStatus == _MainhandSkill
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_Rods_RemoveMainhandSkill((CHARACTERGUID)_Character, (ITEMGUID)_Rod, (STRING)_MainhandSkill, (ITEMGUID)_MainhandWeapon)
AND
NOT LLWEAPONEX_Rods_QRY_SkipSkillRemoval(_Character, _Rod, _MainhandSkill, _MainhandWeapon)
THEN
//RemoveStatus(_Character, _MainhandSkill);
CharacterRemoveSkill(_Character, _MainhandSkill);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:RemoveMainhandSkill] Removed mainhand rod skill (",_MainhandSkill,").");

IF
ItemUnEquipped(_Rod, _Character)
AND
DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _Skill)
THEN
NOT DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _Rod, _Skill);
//RemoveStatus(_Character, _Status);
CharacterRemoveSkill(_Character, _Skill);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Rods:ItemUnEquipped] Removed rod skill (",_Skill,").");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_RodListeningTimedOut")
AND
DB_LLWEAPONEX_Rods_Temp_ListenForRodSkill(_Character, _Rod)
THEN
NOT DB_LLWEAPONEX_Rods_Temp_ListenForRodSkill(_Character, _Rod);

//Failsafe
IF
CharacterStatusRemoved(_Character, _TypeStatus, _)
AND
//DB_LLWEAPONEX_Rods_SkillStatuses(_TypeStatus, _MainhandSkillStatus, _OffhandSkill)
DB_LLWEAPONEX_Rods_Skills(_TypeStatus, _MainhandSkill, _OffhandSkill)
AND
NOT DB_LLWEAPONEX_Rods_Temp_RemoveStatusWhenUnequipped(_Character, _, _)
AND
NOT LeaderLib_Helper_QRY_HasStatus(_Character, _TypeStatus)
THEN
//RemoveStatus(_Character, _MainhandSkillStatus);
//RemoveStatus(_Character, _OffhandSkill);
CharacterRemoveSkill(_Character, _MainhandSkill);
CharacterRemoveSkill(_Character, _OffhandSkill);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"