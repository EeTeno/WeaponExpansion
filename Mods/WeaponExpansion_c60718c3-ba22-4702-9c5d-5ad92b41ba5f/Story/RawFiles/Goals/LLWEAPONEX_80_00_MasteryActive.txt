Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION

//REGION SKILL_GAINS
/*
Disabled for now, since weapon-based skills should be applying LLWEAPONEX_WEAPONMASTERY_EXPERIENCE on hit
IF
CharacterUsedSkill(_Player, _Skill, _, _)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player, "LLWEAPONEX_DisableWeaponMasteryExperience", 0)
AND
ObjectGetFlag(_Player, "LLWEAPONEX_MasteryActive", 1)
AND
DB_LLWEAPONEX_WeaponMastery_MasteryVariables(_WeaponType, _CurrentXPVar, _MaxXPVar, _LevelVar, _EnabledFlag)
AND
ObjectGetFlag(_Player, _EnabledFlag, 1)
THEN
DB_LLWEAPONEX_WeaponMastery_Temp_ListenForSkillDamage(_Player, _WeaponType);

IF
//AttackedByObject(_Target, _, (CHARACTERGUID)_Player, _, _)
CharacterStatusApplied(_Target, "HIT", _Player)
AND
_Player != NULL_00000000-0000-0000-0000-000000000000
AND
DB_LLWEAPONEX_WeaponMastery_Temp_ListenForSkillDamage((CHARACTERGUID)_Player, _WeaponType)
THEN
NOT DB_LLWEAPONEX_WeaponMastery_Temp_ListenForSkillDamage(_Player, _WeaponType);
LLWEAPONEX_WeaponMastery_AddExperienceForWeaponType(_Player, _WeaponType, 1);
*/
//END_REGION

//REGION PARTY_WIDE_EXPERIENCE
IF
CharacterDying(_Enemy)
AND
DB_CombatCharacters(_Enemy, _CombatID)
AND
DB_IsPlayer(_Player)
AND
NOT DB_LLWEAPONEX_WeaponMastery_Temp_IteratingParty(_,_Enemy)
AND
CombatGetIDForCharacter(_Player, _CombatID)
AND
CharacterIsEnemy(_Enemy, _Player, 1)
THEN
LLWEAPONEX_WeaponMastery_AddPartyExperience(_Player, _Enemy);
//END_REGION

//REGION SKILL_REQUIREMENTS_MET
IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Player, _)
AND
DB_LLWEAPONEX_WeaponMastery_SkillRequirements(_WeaponType, _SkillID, _Flag, "Flag")
AND
DB_LLWEAPONEX_WeaponMastery_Learned(_Player, _WeaponType, _SkillID)
THEN
LLWEAPONEX_WeaponMastery_OnSkillLearned(_Player, _WeaponType, _SkillID);

IF
ObjectWasTagged((CHARACTERGUID)_Player, _Tag)
AND
DB_LLWEAPONEX_WeaponMastery_SkillRequirements(_WeaponType, _SkillID, _Tag, "Tag")
AND
DB_LLWEAPONEX_WeaponMastery_Learned(_Player, _WeaponType, _SkillID)
THEN
LLWEAPONEX_WeaponMastery_OnSkillLearned(_Player, _WeaponType, _SkillID);
//END_REGION

//REGION SKILL_REQUIREMENTS_LOST
IF
ObjectFlagCleared(_Flag, (CHARACTERGUID)_Player, _)
AND
DB_LLWEAPONEX_WeaponMastery_SkillRequirements(_WeaponType, _SkillID, _Flag, "Flag")
AND
DB_LLWEAPONEX_WeaponMastery_ActiveSkills(_Player, _WeaponType, _SkillID, _Skill)
THEN
LLWEAPONEX_WeaponMastery_RemoveSkill(_Player, _WeaponType, _SkillID);

IF
ObjectLostTag((CHARACTERGUID)_Player, _Tag)
AND
DB_LLWEAPONEX_WeaponMastery_SkillRequirements(_WeaponType, _SkillID, _Tag, "Tag")
AND
DB_LLWEAPONEX_WeaponMastery_ActiveSkills(_Player, _WeaponType, _SkillID, _Skill)
THEN
LLWEAPONEX_WeaponMastery_RemoveSkill(_Player, _WeaponType, _SkillID);
//END_REGION

/*
IF
ObjectTransformed((CHARACTERGUID)_Player, _)
AND
ObjectGetFlag(_Player, "LLWEAPONEX_MasteryActive", 1)
THEN
SetTag(_Player, "LLWEAPONEX_MasteryActive");
*/

//REGION BONUS_STATUS
IF
CharacterResurrected(_Char)
THEN
LLWEAPONEX_WeaponMastery_ApplyBonusStatus(_Char);
//END_REGION

EXITSECTION
LLWEAPONEX_WeaponMastery_TS_RemoveListenEntries();

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"