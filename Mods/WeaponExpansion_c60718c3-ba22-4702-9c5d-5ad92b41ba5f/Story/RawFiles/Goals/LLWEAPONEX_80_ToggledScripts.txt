Version 1
SubGoalCombiner SGC_AND
INITSECTION

LLWEAPONEX_ToggleSettings_InitSettings();
LLWEAPONEX_ToggleSettings_RegisterScripts();

KBSECTION

//REGION SETTINGS
PROC
LLWEAPONEX_ToggleSettings_InitSettings()
THEN
SysClear("DB_LLWEAPONEX_ToggleSettings_MasteryFlags", 3);

PROC
LLWEAPONEX_ToggleSettings_InitSettings()
AND
DB_LLWEAPONEX_WeaponMastery_MasteryVariables(_WeaponType, _CurrentXPVar, _MaxXPVar, _LevelVar, _EnabledFlag) // Enabled masteries
THEN
LLWEAPONEX_ToggleSettings_AddTag(_WeaponType);

PROC
LLWEAPONEX_ToggleSettings_InitSettings()
AND
DB_LLWEAPONEX_Grimoire_Tags(_Template, _Tag)
THEN
LLWEAPONEX_ToggleSettings_AddTag(_Tag);

PROC
LLWEAPONEX_ToggleSettings_AddTag((STRING)_Tag)
AND
StringConcatenate(_Tag, "_ScriptEnabled", _Flag)
AND
StringConcatenate("LLWEAPONEX_Timers_DisableScript_", _Tag, _TimerName)
THEN
DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _Flag, _TimerName);

PROC
LLWEAPONEX_ToggleSettings_RegisterScripts()
THEN
//LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_00_MasteryActive", "LLWEAPONEX_MasteryActive");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_01_WM_Greatbow", "LLWEAPONEX_Greatbow");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_01_WM_HandCrossbow", "LLWEAPONEX_HandCrossbow");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_01_WM_Katana", "LLWEAPONEX_Katana");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_01_WM_Rapier", "LLWEAPONEX_Rapier");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_01_WM_Runeblade", "LLWEAPONEX_Runeblade");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_02_Grimoire__Main", "LLWEAPONEX_Grimoire");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_02_Grimoire_Death", "LLWEAPONEX_Grimoire_Death");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_02_Grimoire_Mimicry", "LLWEAPONEX_Grimoire_Mimicry");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_02_Grimoire_Fire", "LLWEAPONEX_Grimoire_Fire");
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_03_WeaponFX", "LLWEAPONEX_CustomUnsheathedFX");
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_OldVersion, 0,9,4,3)
AND
DB_LLWEAPONEX_ToggleSettings_Tags("LLWEAPONEX_MasteryActive", "LLWEAPONEX_80_00_MasteryActive", _TimerName)
THEN
NOT DB_LLWEAPONEX_ToggledScripts_Script("LLWEAPONEX_80_00_MasteryActive", "LLWEAPONEX_MasteryActive");
NOT DB_LLWEAPONEX_ToggleSettings_Tags("LLWEAPONEX_MasteryActive", "LLWEAPONEX_80_00_MasteryActive", _TimerName);
LLWEAPONEX_ToggleSettings_RegisterScripts();

PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_OldVersion, 0,9,4,3)
AND
//DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _Item, _WeaponType, _EnabledFlag)
SysCount("DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries", 4, _Count)
AND
_Count > 0
THEN
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_MasterySystemActive", "WeaponExpansion");

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_OldVersion, 0,9,4,2)
THEN
LeaderLib_ToggleScripts_Register_Script("LLWEAPONEX_80_09_OniGauntlet", "LLWEAPONEX_OniGauntletEquipped");

PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_OldVersion, 0, 9, 3, 8)
THEN
LLWEAPONEX_ToggleSettings_Register_GoalForTag("LLWEAPONEX_80_02_Grimoire__Main", "LLWEAPONEX_Grimoire");

PROC
LeaderUpdater_ModUpdated("WeaponExpansion", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_OldVersion, 0, 9, 2, 1)
THEN
SysActivateGoal("LLWEAPONEX_80_ToggledScripts");
LLWEAPONEX_ToggleSettings_RegisterScripts();
//END_REGION

//REGION REGISTERING
PROC
LLWEAPONEX_ToggleSettings_Register_Flag((STRING)_GoalName, (STRING)_Flag)
THEN
DB_LLWEAPONEX_ToggledScripts_Script(_GoalName, _Flag);

PROC
LLWEAPONEX_ToggleSettings_Register_GoalForTag((STRING)_GoalName, (STRING)_Tag)
AND
NOT DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _, _)
THEN
LLWEAPONEX_ToggleSettings_AddTag(_Tag);

PROC
LLWEAPONEX_ToggleSettings_Register_GoalForTag((STRING)_GoalName, (STRING)_Tag)
AND
DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _Flag, _TimerName)
THEN
DB_LLWEAPONEX_ToggledScripts_Script(_GoalName, _Flag);

QRY
LLWEAPONEX_ToggleSettings_QRY_RegisterOnce_Tag((STRING)_GoalName, (STRING)_Tag)
AND
NOT DB_LLWEAPONEX_ToggledScripts_Script(_GoalName, _)
AND
DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _Flag, _TimerName)
THEN
DB_LLWEAPONEX_ToggledScripts_Script(_GoalName, _Flag);
//END_REGION

//REGION TOGGLING
PROC
LLWEAPONEX_ToggleSettings_ActivateScript((STRING)_Flag)
AND
DB_LLWEAPONEX_ToggledScripts_Script(_GoalName, _Flag)
AND
NOT SysIsActive(_GoalName)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:ToggleSettings:ActivateScript] Activating script [",_GoalName,"] for flag [",_Flag,"].");
SysActivateGoal(_GoalName);

PROC
LLWEAPONEX_ToggleSettings_DeactivateScript((STRING)_Flag)
AND
DB_LLWEAPONEX_ToggledScripts_Script(_GoalName, _Flag)
AND
SysIsActive(_GoalName)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:ToggleSettings:DeactivateScript] Deactivating script [",_GoalName,"] for flag [",_Flag,"].");
SysCompleteGoal(_GoalName);
//END_REGION

//REGION TOGGLE_EVENTS_MASTERY
/*
PROC
LLWEAPONEX_WeaponMastery_OnMasteryActivated((CHARACTERGUID)_Player, (STRING)_WeaponType)
THEN
LLWEAPONEX_ToggleSettings_OnTagAdded(_Player, _WeaponType);

PROC
LLWEAPONEX_WeaponMastery_OnMasteryDeactivated((CHARACTERGUID)_Player, (STRING)_WeaponType)
AND
NOT LLWEAPONEX_WeaponMastery_QRY_HasActiveMastery(_Player, _WeaponType)
THEN
LLWEAPONEX_ToggleSettings_OnTagRemoved(_Player, _WeaponType);
*/
//END_REGION

//REGION TOGGLE_EVENTS_TAGS
PROC
LLWEAPONEX_OnItemEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Template)
AND
DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _Flag, _TimerName)
AND
IsTagged(_Item, _Tag, 1)
THEN
LLWEAPONEX_ToggleSettings_OnTagAdded(_Player, _Item, _Tag, _Template);

/*
PROC
LLWEAPONEX_ToggleSettings_OnTagAdded((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Tag, (STRING)_Template)
AND
NOT DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _, _)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:ToggleSettings:OnTagAdded] Script for tag [",_Tag,"] not registered!");
*/

PROC
LLWEAPONEX_ToggleSettings_OnTagAdded((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Tag, (STRING)_Template)
AND
DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _Flag, _TimerName)
THEN
TimerCancel(_TimerName);
DB_LLWEAPONEX_ToggleSettings_Temp_ActiveTags(_Player, _Item, _Tag);

PROC
LLWEAPONEX_ToggleSettings_OnTagAdded((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Tag, (STRING)_Template)
AND
DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _Flag, _TimerName)
AND
GlobalGetFlag(_Flag, 0)
THEN
GlobalSetFlag(_Flag);
LLWEAPONEX_ToggleSettings_ActivateScript(_Flag);

PROC
LLWEAPONEX_OnItemUnEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Template)
AND
DB_LLWEAPONEX_ToggleSettings_Temp_ActiveTags(_Player, _Item, _Tag)
THEN
NOT DB_LLWEAPONEX_ToggleSettings_Temp_ActiveTags(_Player, _Item, _Tag);
LLWEAPONEX_ToggleSettings_OnTagRemoved(_Player, _Tag, _Item, _Template);

PROC
LLWEAPONEX_ToggleSettings_OnTagRemoved((CHARACTERGUID)_Player, (STRING)_Tag, (ITEMGUID)_Item, (STRING)_Template)
AND
DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _Flag, _TimerName)
AND
NOT DB_LLWEAPONEX_ToggleSettings_Temp_ActiveTags(_Player, _, _Tag)
AND
GlobalGetFlag(_Flag, 1)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, 1500);

IF
TimerFinished(_TimerName)
AND
DB_LLWEAPONEX_ToggleSettings_Tags(_Tag, _Flag, _TimerName)
AND
NOT DB_LLWEAPONEX_ToggleSettings_Temp_ActiveTags(_, _, _Tag)
THEN
GlobalClearFlag(_Flag);
LLWEAPONEX_ToggleSettings_DeactivateScript(_Flag);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"