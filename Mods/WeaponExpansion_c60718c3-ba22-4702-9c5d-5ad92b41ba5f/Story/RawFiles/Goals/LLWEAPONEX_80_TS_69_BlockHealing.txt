Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
IF
CharacterStatusAttempt(_Char, _Status, _)
AND
HasActiveStatus(_Char, "LLWEAPONEX_BLOCK_HEALING", 1)
AND
NOT LLWEAPONEX_BlockHealing_QRY_OriginallyInvulnerable(_Char)
AND
LLWEAPONEX_BlockHealing_QRY_BlockStatus(_Char, _Status)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX_DEBUG_BLOCK_HEALING] Blocking healing status damage for [", _Status, "].");
LLWEAPONEX_BlockHealing_PlayEffect(_Char);
ProcSetInvulnerable(_Char, 1);
ProcObjectTimerCancel(_Char, "LLWEAPONEX_Timers_BlockHealing_ResetInvulnerable");
ProcObjectTimer(_Char, "LLWEAPONEX_Timers_BlockHealing_ResetInvulnerable", 250);
DB_LLWEAPONEX_BlockingHealingStatus(_Char, _Status);

IF
CharacterStatusRemoved(_Char, _Status, _)
AND
DB_LLWEAPONEX_BlockingHealingStatus(_Char, _Status)
THEN
NOT DB_LLWEAPONEX_BlockingHealingStatus(_Char, _Status);
ProcObjectTimerCancel(_Char, "LLWEAPONEX_Timers_BlockHealing_ResetInvulnerable");
ProcObjectTimerFinished(_Char, "LLWEAPONEX_Timers_BlockHealing_ResetInvulnerable");

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_MENDING", (CHARACTERGUID)_Source)
AND
HasActiveStatus(_Char, "LLWEAPONEX_BLOCK_HEALING", 1)
AND
CharacterGetHitpointsPercentage(_Char, _Percentage)
AND
_Percentage < 100.0
AND
CharacterGetAbility(_Source, "WaterSpecialist", _Resto)
AND
IntegerProduct(_Resto, 25, _RestoMult)
AND
IntegerMin(_RestoMult, 250, _CappedMult) // 250 max
AND
IntegerMax(_CappedMult, 1, _FinalMult) // 1 minimum
AND
Real(_FinalMult, _RMult)
AND
RealDivide(_RMult, 10.0, _AddPercentage)
AND
RealSum(_Percentage, _AddPercentage, _NextPercentage) // 25.0 percentage with 10 Hydrosophist, 1% at 0
AND
LeaderLib_Helper_QRY_CapPercentage(_Char, _NextPercentage)
AND
DB_LeaderLib_Helper_Temp_CappedPercentage(_Char, _FinalPercentage)
THEN
NOT DB_LeaderLib_Helper_Temp_CappedPercentage(_Char, _FinalPercentage);
CharacterSetHitpointsPercentage(_Char, _FinalPercentage);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"