Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//Auto-Insert Bolts
IF
ItemTemplateAddedToCharacter(_ItemTemplateGuid, _Bolts, _Character)
AND
IsTagged(_Character, "LLWEAPONEX_HandCrossbow_Equipped", 1)
AND
IsTagged(_Character, "LLWEAPONEX_HandCrossbow_HasAmmo", 0)
AND
String(_ItemTemplateGuid, _RuneTemplate)
AND
DB_LeaderLib_Helper_Runes_Templates("WeaponExpansion.HandCrossbowBolt", _RuneTemplate)
AND
LLWEAPONEX_HandCrossbow_QRY_GetHandcrossbow(_Character, 0)
AND
DB_LLWEAPONEX_Handcrossbow_Temp_Equipped(_Character, _HandCrossbow)
THEN
NOT DB_LLWEAPONEX_Handcrossbow_Temp_Equipped(_Character, _HandCrossbow);
ItemInsertRune(_Character, _HandCrossbow, _RuneTemplate, 0);
ItemRemove(_Bolts);
LeaderLib_Tags_PreserveTag(_Character, "LLWEAPONEX_HandCrossbow_HasAmmo");
ApplyStatus(_Character, "LLWEAPONEX_REFRESH_UI", 0.0);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_14_HandCrossbow:OnEquipped] Auto-inserted crossbow bolts into empty Handcrossbow.");
CharacterStatusText(_Character, "LLWEAPONEX_Handcrossbow_BoltAutoInserted");

IF
RuneInserted(_Character, _HandCrossbow, _RuneTemplate, _Slot)
AND
DB_LeaderLib_Helper_Runes_Templates("WeaponExpansion.HandCrossbowBolt", _RuneTemplate)
AND
IsTagged(_HandCrossbow, "LLWEAPONEX_HandCrossbow", 1)
THEN
LeaderLib_Tags_PreserveTag(_Character, "LLWEAPONEX_HandCrossbow_HasAmmo");
ApplyStatus(_Character, "LLWEAPONEX_REFRESH_UI", 0.0);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_14_HandCrossbow:OnEquipped] Crossbow now has ammo. ");

IF
RuneRemoved(_Character, _HandCrossbow, _Bolt, _Slot)
AND
IsTagged(_HandCrossbow, "LLWEAPONEX_HandCrossbow", 1)
AND
GetTemplate(_Bolt, _RuneTemplate)
AND
DB_LeaderLib_Helper_Runes_Templates("WeaponExpansion.HandCrossbowBolt", _RuneTemplate)
THEN
ObjectSetFlag(_HandCrossbow, "LLWEAPONEX_HandCrossbow_DisableAutoInsert", 0);
LeaderLib_Tags_ClearPreservedTag(_Character, "LLWEAPONEX_HandCrossbow_HasAmmo");
LeaderLib_Tags_ClearPreservedTag(_Character, "LLWEAPONEX_HandCrossbow_CanReload");
LeaderLog_Log("DEBUG", "[LLWEAPONEX_14_HandCrossbow:OnEquipped] Crossbow does not have ammo. ");

//REGION TAG_FOR_RELOAD
IF
SkillCast(_Character, "Projectile_LLWEAPONEX_HandCrossbow_Shoot", _, _)
THEN
LLWEAPONEX_HandCrossbow_TagForReload(_Character);

IF
SkillCast(_Character, "Projectile_LLWEAPONEX_HandCrossbow_Shoot_Air", _, _)
THEN
LLWEAPONEX_HandCrossbow_TagForReload(_Character);

IF
SkillCast(_Character, "Projectile_LLWEAPONEX_HandCrossbow_Shoot_Earth", _, _)
THEN
LLWEAPONEX_HandCrossbow_TagForReload(_Character);

IF
SkillCast(_Character, "Projectile_LLWEAPONEX_HandCrossbow_Shoot_Fire", _, _)
THEN
LLWEAPONEX_HandCrossbow_TagForReload(_Character);

IF
SkillCast(_Character, "Projectile_LLWEAPONEX_HandCrossbow_Shoot_Poison", _, _)
THEN
LLWEAPONEX_HandCrossbow_TagForReload(_Character);

IF
SkillCast(_Character, "Projectile_LLWEAPONEX_HandCrossbow_Shoot_Water", _, _)
THEN
LLWEAPONEX_HandCrossbow_TagForReload(_Character);

IF
SkillCast(_Character, "Projectile_LLWEAPONEX_HandCrossbow_Shoot_Shadow", _, _)
THEN
LLWEAPONEX_HandCrossbow_TagForReload(_Character);

IF
SkillCast(_Character, "Projectile_LLWEAPONEX_HandCrossbow_Shoot_Corrosive", _, _)
THEN
LLWEAPONEX_HandCrossbow_TagForReload(_Character);

IF
SkillCast(_Character, "Projectile_LLWEAPONEX_HandCrossbow_Shoot_WeaponProxy", _, _)
THEN
LLWEAPONEX_HandCrossbow_TagForReload(_Character);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"