Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

PROC
LLWEAPONEX_OnItemTemplateUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "EQ_UNIQUE_LLWEAPONEX_Backpack_Demolition_A_8995798f-f5b6-4e71-ae84-a29ed4ca51d7")
THEN
NOT DB_LLWEAPONEX_Demolition_PlayerData_BackpackEquipped(_Char, _Item);
LeaderLib_Statuses_RemoveIfActive(_Char, "LLWEAPONEX_REMOTEMINE_ADD_RECHARGE");
LeaderLib_ToggleScripts_DisableScriptForObject(_Char, "LLWEAPONEX_DemolitionBackpackEquipped", "WeaponExpansion", 1);

PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "EQ_UNIQUE_LLWEAPONEX_Backpack_Demolition_A_8995798f-f5b6-4e71-ae84-a29ed4ca51d7")
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLWEAPONEX_DemolitionBackpackEquipped", "WeaponExpansion", 1);
DB_LLWEAPONEX_Demolition_PlayerData_BackpackEquipped(_Char, _Item);

QRY
LLWEAPONEX_Demolition_QRY_BackpackIsEquipped((CHARACTERGUID)_Char)
AND
CharacterGetEquippedItem(_Char, "Belt", (ITEMGUID)_DemoBackpack)
AND
ObjectExists(_DemoBackpack, 1)
AND
IsTagged(_DemoBackpack, "LLWEAPONEX_DemolitionBackpack", 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Demolition_QRY_DemoBackpackNeedsRecharging((CHARACTERGUID)_Char)
AND
//CharacterFindTaggedItem(_Char, "LLWEAPONEX_DemolitionBackpack", _DemoBackpack)
CharacterGetEquippedItem(_Char, "Belt", (ITEMGUID)_DemoBackpack)
AND
ObjectExists(_DemoBackpack, 1)
AND
IsTagged(_DemoBackpack, "LLWEAPONEX_DemolitionBackpack", 1)
AND
NOT LeaderLib_Helper_QRY_ChargesAreMaxed(_DemoBackpack)
THEN
DB_NOOP(1);

//REGION CHARGES
IF
CharacterUsedSkill(_Char, "Target_LLWEAPONEX_RemoteMine_Detonate", _, _)
THEN
DB_LLWEAPONEX_RemoteMines_Temp_JustDetonated(_Char);
ProcObjectTimerCancel(_Char, "LLWEAPONEX_Timers_RemoteMines_ResetJustDetonated");
ProcObjectTimer(_Char, "LLWEAPONEX_Timers_RemoteMines_ResetJustDetonated", 1500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LLWEAPONEX_Timers_RemoteMines_ResetJustDetonated")
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_JustDetonated(_Char);

PROC
LLWEAPONEX_Demolition_AddCharge((CHARACTERGUID)_Char)
AND
//CharacterFindTaggedItem(_Char, "LLWEAPONEX_DemolitionBackpack", _DemoBackpack)
CharacterGetEquippedItem(_Char, "Belt", (ITEMGUID)_DemoBackpack)
AND
ObjectExists(_DemoBackpack, 1)
AND
IsTagged(_DemoBackpack, "LLWEAPONEX_DemolitionBackpack", 1)
AND
NOT LeaderLib_Helper_QRY_ChargesAreMaxed(_DemoBackpack)
THEN
ItemAddCharges(_DemoBackpack, 1);

IF
SkillCast(_Char, "Shout_LLWEAPONEX_RemoteMine_Add", _, _)
AND
GlobalGetFlag("LLWEAPONEX_DemolitionBackpackAutoRechargeEnabled", 1)
AND
LLWEAPONEX_Demolition_QRY_BackpackIsEquipped(_Char)
THEN
LLWEAPONEX_Demolition_StartRecharge(_Char);

PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "EQ_UNIQUE_LLWEAPONEX_Backpack_Demolition_A_8995798f-f5b6-4e71-ae84-a29ed4ca51d7")
THEN
LLWEAPONEX_Demolition_StartRecharge(_Char);

PROC
LLWEAPONEX_Demolition_StartRecharge((CHARACTERGUID)_Char)
AND
GlobalGetFlag("LLWEAPONEX_DemolitionBackpackAutoRechargeEnabled", 1) // Charges are restored by enemies dying in combat, by default
AND
IsHardcoreMode(_HardMode)
THEN
LLWEAPONEX_Demolition_Internal_StartRecharge(_Char, _HardMode);

PROC
LLWEAPONEX_Demolition_Internal_StartRecharge((CHARACTERGUID)_Char, (INTEGER)_HardMode)
AND
HasActiveStatus(_Char, "LLWEAPONEX_REMOTEMINE_ADD_RECHARGE", 1)
THEN
//For skipping another recharge call when forcing the duration
ObjectSetFlag(_Char, "LLWEAPONEX_DemolitionBackpack_SkipNextRecharge", 0);

PROC
LLWEAPONEX_Demolition_Internal_StartRecharge((CHARACTERGUID)_Char, 0)
AND
LLWEAPONEX_Demolition_QRY_DemoBackpackNeedsRecharging(_Char)
THEN
ApplyStatus(_Char, "LLWEAPONEX_REMOTEMINE_ADD_RECHARGE", 24.0, 1, _Char);

//Tactician and above gets a larger cooldown
PROC
LLWEAPONEX_Demolition_Internal_StartRecharge((CHARACTERGUID)_Char, 1)
AND
CharacterGetEquippedItem(_Char, "Belt", (ITEMGUID)_DemoBackpack)
AND
ObjectExists(_DemoBackpack, 1)
AND
IsTagged(_DemoBackpack, "LLWEAPONEX_DemolitionBackpack", 1)
AND
ItemGetCharges(_DemoBackpack, _Charges)
AND
ItemGetMaxCharges(_DemoBackpack, _MaxCharges)
AND
_Charges < _MaxCharges
AND
IntegerSubtract(_MaxCharges, _Charges, _Mult)
AND
IntegerProduct(24, _Mult, _IntDuration)
AND
Real(_IntDuration, _Duration)
THEN
ApplyStatus(_Char, "LLWEAPONEX_REMOTEMINE_ADD_RECHARGE", _Duration, 1, _Char);

IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_REMOTEMINE_ADD_RECHARGE", _)
AND
ObjectGetFlag(_Char, "LLWEAPONEX_DemolitionBackpack_SkipNextRecharge", 0)
AND
HasActiveStatus(_Char, "LLWEAPONEX_BACKPACK_DEMOLITION_EQUIPPED", 1)
THEN
LLWEAPONEX_Demolition_AddCharge(_Char);
LLWEAPONEX_Demolition_StartRecharge(_Char);

IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_REMOTEMINE_ADD_RECHARGE", _)
THEN
ObjectClearFlag(_Char, "LLWEAPONEX_DemolitionBackpack_SkipNextRecharge", 0);
//END_REGION

//REGION RECHARGE_GLOBAL_FLAGS
IF
GlobalFlagSet("LLWEAPONEX_DemolitionBackpackAutoRechargeEnabled")
AND
DB_LLWEAPONEX_Demolition_PlayerData_BackpackEquipped(_Char, _Item)
THEN
LLWEAPONEX_Demolition_StartRecharge(_Char);

IF
GlobalFlagCleared("LLWEAPONEX_DemolitionBackpackAutoRechargeEnabled")
AND
DB_LLWEAPONEX_Demolition_PlayerData_BackpackEquipped(_Char, _Item)
THEN
LeaderLib_Statuses_RemoveIfActive(_Char, "LLWEAPONEX_REMOTEMINE_ADD_RECHARGE");
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"