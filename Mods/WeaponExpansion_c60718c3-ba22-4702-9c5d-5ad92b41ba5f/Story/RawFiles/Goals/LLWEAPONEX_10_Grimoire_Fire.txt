Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION GRIMOIRE_BONUS
PROC
LLWEAPONEX_Grimoire_OnSkillCast((CHARACTERGUID)_Caster, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_Skill, "projectile", (STRING)_SkillElement, "Fire")
AND
LeaderLib_Random_QRY(2,3)
AND
DB_LeaderLib_Random(_Ran)
AND
Real(_Ran, _Radius)
THEN
NOT DB_LeaderLib_Random(_Ran);
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition(_Caster, _x, _y, _z, "SurfaceFireCloud", _Radius, 2);

PROC
LLWEAPONEX_Grimoire_OnSkillCast((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, "projectile", (STRING)_SkillElement, "Fire")
AND
GetPosition(_Target, _x, _y, _z)
AND
LeaderLib_Random_QRY(2,3)
AND
DB_LeaderLib_Random(_Ran)
AND
Real(_Ran, _Radius)
AND
HasActiveStatus(_Caster, "UNSHEATHED", _Unsheathed)
AND
ObjectGetFlag(_Caster, "LLWEAPONEX_GrimoireOpened", _ForcedOpen)
AND
IntegerMax(_Unsheathed, _ForcedOpen, _IsUnsheathed)
THEN
NOT DB_LeaderLib_Random(_Ran);
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition(_Caster, _x, _y, _z, "SurfaceFireCloud", _Radius, 2);

PROC
LLWEAPONEX_Grimoire_OnSkillCast((CHARACTERGUID)_Caster, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_Skill, "target", (STRING)_SkillElement, "Fire")
THEN
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition(_Caster, _x, _y, _z, "SurfaceFireCloud", 2.0, 2);

PROC
LLWEAPONEX_Grimoire_OnSkillCast((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, "target", (STRING)_SkillElement, "Fire")
AND
GetPosition(_Target, _x, _y, _z)
THEN
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition(_Caster, _x, _y, _z, "SurfaceFireCloud", 2.0, 2);

PROC
LLWEAPONEX_Grimoire_OnSkillCast((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement, "Fire")
AND
HasActiveStatus(_Caster, "UNSHEATHED", _Unsheathed)
AND
ObjectGetFlag(_Caster, "LLWEAPONEX_GrimoireOpened", _ForcedOpen)
AND
IntegerMax(_Unsheathed, _ForcedOpen, _IsUnsheathed)
THEN
LLWEAPONEX_Grimoire_ShootBeamFX(_Caster, _Target, "LLWEAPONEX_FX_Grimoire_Beam_Fire_01", "Dummy_BodyFX", _IsUnsheathed);

PROC
LLWEAPONEX_Grimoire_OnSkillCast((CHARACTERGUID)_Caster, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement, "Fire")
AND
CreateItemTemplateAtPosition("LOOT_LeaderLib_BackPack_Invisible_98fa7688-0810-4113-ba94-9a8c8463f830", _x, _y, _z, _Backpack)
AND
HasActiveStatus(_Caster, "UNSHEATHED", _Unsheathed)
AND
ObjectGetFlag(_Caster, "LLWEAPONEX_GrimoireOpened", _ForcedOpen)
AND
IntegerMax(_Unsheathed, _ForcedOpen, _IsUnsheathed)
THEN
LLWEAPONEX_Grimoire_ShootBeamFX(_Caster, _Backpack, "LLWEAPONEX_FX_Grimoire_Beam_Fire_01", "Dummy_Root", _IsUnsheathed);
LeaderLib_Timers_StartObjectTimer(_Backpack, 1000, "LLWEAPONEX_Timers_RemoveGrimoireBeamTarget", "LeaderLib_Commands_RemoveItem");

/*
PROC
LLWEAPONEX_Grimoire_OnSkillCast((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement, "Fire")
AND
GetPosition(_Target, _tx, _ty, _tz)
THEN
LLWEAPONEX_Grimoire_Fire_DrawFireSurface(_Caster, _tx, _ty, _tz, "SurfaceFire");

PROC
LLWEAPONEX_Grimoire_OnSkillCast((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement, "Fire")
AND
GetPosition(_Target, _tx, _ty, _tz)
THEN
LLWEAPONEX_Grimoire_Fire_DrawFireSurface(_Caster, _tx, _ty, _tz, "SurfaceFire");

PROC
LLWEAPONEX_Grimoire_OnSkillCast((CHARACTERGUID)_Caster, (REAL)_tx, (REAL)_ty, (REAL)_tz, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement, "Fire")
THEN
LLWEAPONEX_Grimoire_Fire_DrawFireSurface(_Caster, _tx, _ty, _tz, "SurfaceFire");
*/

PROC
LLWEAPONEX_Grimoire_Fire_DrawFireSurface((CHARACTERGUID)_Caster, (REAL)_tx, (REAL)_ty, (REAL)_tz, (STRING)_Surface)
AND
NOT DB_LLWEAPONEX_Grimoire_Temp_FireSurfaceCreator(_Caster, _, _, _)
AND
GetPosition(_Caster, _x, _y, _z)
AND
TemporaryCharacterCreateAtPosition(_x, _y, _z, "LeaderLib_SkillDummy_94668062-11ea-4ecf-807c-4cc225cbb236", 0, _Dummy)
AND
DrawSurfaceOnPath(_Caster, _Dummy, _Surface, 1.0, 12.0, _SurfaceActionHandle)
THEN
DB_LLWEAPONEX_Grimoire_Temp_FireSurfaceCreator(_Caster, _Dummy, _Surface);
SetVarFloat(_Dummy, "LeaderLib_SkillDummy_WalkSpeed", 15.0);
SetVarFloat(_Dummy, "LeaderLib_SkillDummy_RunSpeed", 15.0);
SetStoryEvent(_Dummy, "LeaderLib_Events_SkillDummyArrived");
CharacterSetImmortal(_Dummy, 1);
CharacterMakeStoryNpc(_Dummy, 1);
SetVisible(_Dummy, 0);
CharacterSetDetached(_Dummy, 1);
CharacterDisableAllCrimes(_Dummy);
CharacterMoveToPosition(_Dummy, _tx, _ty, _tz, 0, "LLWEAPONEX_Grimoire_FireSurfaceDummyDone");
LeaderLib_Timers_StartObjectObjectTimer(_Caster, _Dummy, 500, "LLWEAPONEX_Timers_Grimoire_Fire_StartPathDrawing", "LLWEAPONEX_Grimoire_Fire_StartPathDrawing");

PROC
LeaderLib_Timers_ObjectObjectTimerFinished((CHARACTERGUID)_Caster, (CHARACTERGUID)_Dummy, "LLWEAPONEX_Grimoire_Fire_StartPathDrawing")
AND
DB_LLWEAPONEX_Grimoire_Temp_FireSurfaceCreator(_Caster, _Dummy, _Surface)
AND
DrawSurfaceOnPath(_Caster, _Dummy, _Surface, 1.0, 12.0, _SurfaceActionHandle)
THEN
NOT DB_LLWEAPONEX_Grimoire_Temp_FireSurfaceCreator(_Caster, _Dummy, _Surface);
DB_LLWEAPONEX_Grimoire_Temp_FireSurfaceCreator(_Caster, _Dummy, _SurfaceActionHandle, _Surface);

IF
StoryEvent((CHARACTERGUID)_Dummy, "LLWEAPONEX_Grimoire_FireSurfaceDummyDone")
AND
DB_LLWEAPONEX_Grimoire_Temp_FireSurfaceCreator(_Caster, _Dummy, _SurfaceActionHandle, _Surface)
THEN
NOT DB_LLWEAPONEX_Grimoire_Temp_FireSurfaceCreator(_Caster, _Dummy, _SurfaceActionHandle, _Surface);
StopDrawSurfaceOnPath(_SurfaceActionHandle);
RemoveTemporaryCharacter(_Dummy);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"