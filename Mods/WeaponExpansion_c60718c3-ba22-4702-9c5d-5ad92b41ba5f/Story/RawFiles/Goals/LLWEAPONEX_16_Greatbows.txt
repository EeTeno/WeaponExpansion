Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Greatbows_InitSettings();

KBSECTION
PROC
LLWEAPONEX_Greatbows_InitSettings()
THEN
/*
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_DWARF", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_DWARF", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_ELF", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_ELF", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_HUMAN", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Humans_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_HUMAN", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Humans_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_LIZARD", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizards_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_LIZARD", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizards_01", "Dummy_R_Hand", 900);
*/
SysClear("DB_LLWEAPONEX_Greatbows_PrepareFX", 6);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", "Undead_Dwarf", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Female", "LowerArm_R_Twist_Bone", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", "Undead_Dwarf", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Male", "LowerArm_R_Twist_Bone", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", "Undead_Elf", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Female", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", "Undead_Elf", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Male", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", "Undead_Human", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Female", "Dummy_R_Hand", 800);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", "Undead_Human", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Male", "Dummy_R_Hand", 800);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", "Undead_Lizard", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Female", "LowerArm_R_Twist_Bone", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", "Undead_Lizard", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Male", "LowerArm_R_Twist_Bone", 600);

SysClear("DB_LLWEAPONEX_Greatbows_QuiverFX", 4);
DB_LLWEAPONEX_Greatbows_QuiverFX("DWARF", "Undead_Dwarf", "LLWEAPONEX_FX_Quiver_A_Dwarf", "Thigh_R_Twist_Bone");
DB_LLWEAPONEX_Greatbows_QuiverFX("ELF", "Undead_Elf", "LLWEAPONEX_FX_Quiver_A", "Thigh_R_Twist_Bone");
DB_LLWEAPONEX_Greatbows_QuiverFX("HUMAN", "Undead_Human", "LLWEAPONEX_FX_Quiver_A", "Thigh_R_Twist_Bone");
DB_LLWEAPONEX_Greatbows_QuiverFX("LIZARD", "Undead_Lizard", "LLWEAPONEX_FX_Quiver_A", "HorseLink_R_Bone");

//REGION GREATBOW_START_ATTACK
IF
CharacterStartAttackObject(_, _, _Character)
AND
CharacterGetEquippedWeapon(_Character, _Greatbow)
AND
IsTagged(_Greatbow, "LLWEAPONEX_Greatbow", 1)
THEN
LLWEAPONEX_Greatbows_PlayPreparationEffect(_Character);

IF
CharacterStartAttackPosition(_, _, _, _, _Character)
AND
CharacterGetEquippedWeapon(_Character, _Greatbow)
AND
IsTagged(_Greatbow, "LLWEAPONEX_Greatbow", 1)
THEN
LLWEAPONEX_Greatbows_PlayPreparationEffect(_Character);

IF
CharacterUsedSkill(_Character, "Projectile_LLMIME_MimicGreatBowAttack", _, _)
THEN
LLWEAPONEX_Greatbows_PlayPreparationEffect(_Character);

QRY
LLWEAPONEX_QRY_RaceMatch((CHARACTERGUID)_Character, (STRING)_Race, (STRING)_UndeadRace)
AND
DB_IsPlayer(_Character)
AND
CharacterGetRace(_Character, 1, _Race)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_RaceMatch((CHARACTERGUID)_Character, (STRING)_Race, (STRING)_UndeadRace)
AND
DB_IsPlayer(_Character)
AND
CharacterGetRace(_Character, 1, _UndeadRace)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_QRY_RaceMatch((CHARACTERGUID)_Character, (STRING)_Race)
AND
NOT DB_IsPlayer(_Character)
AND
IsTagged(_Character, _Race, 1)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_Greatbows_PlayPreparationEffect((CHARACTERGUID)_Character)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _)
AND
IsTagged(_Character, "FEMALE", _Female)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_RaceTag, _UndeadRace, _Female, _EffectName, _BoneName, _Duration)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _)
AND
LLWEAPONEX_QRY_RaceMatch(_Character, _RaceTag, _UndeadRace)
THEN
//Play the loop FX with behavior scripting so we can destroy the effect immediately
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Greatbows:PlayPreparationEffect] Playing arrow FX on attacking character. [",_EffectName,":",_BoneName,"]");
PROC_LoopEffect(_EffectName, _Character, "LLWEAPONEX.FX.GreatbowAttackFX", "__ANY__", _BoneName);
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_StopGreatbowPrepareFX", _Duration);
/*
SetVarString(_Character, "LLWEAPONEX_Greatbow_RaceTag", _RaceTag);
SetVarString(_Character, "LLWEAPONEX_Greatbow_AttackEffectName", _EffectName);
SetVarFixedString(_Character, "LLWEAPONEX_Greatbow_AttackEffectBone", _BoneName);
SetStoryEvent(_Character, "LLWEAPONEX_Greatbow_PlayAttackFX");
*/

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_StopGreatbowPrepareFX")
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Greatbows:StopGreatbowPrepareFX] Destroying looped arrow effect.");
PROC_StopLoopEffect(_Character, "LLWEAPONEX.FX.GreatbowAttackFX");
//SetStoryEvent(_Character, "LLWEAPONEX_Greatbow_StopAttackFX");

IF
StoryEvent((CHARACTERGUID)_Character, "LLWEAPONEX_Greatbow_StopAttackFX")
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:Greatbows:StopGreatbowPrepareFX] Destroying looped arrow effect.");
//SetStoryEvent(_Character, "LLWEAPONEX_Greatbow_StopAttackFX");

IF
TextEventSet("llweap_greatbowfxanim")
AND
CharacterGetHostCharacter(_Character)
THEN
DB_LLWEAPONEX_Debug_LoopingAnim(_Character);
PlayAnimation(_Character, "attack1", "LLWEAPONEX_Debug_KeepPlayingAtttackAnim");

IF
StoryEvent((CHARACTERGUID)_Character, "LLWEAPONEX_Debug_KeepPlayingAtttackAnim")
AND
DB_LLWEAPONEX_Debug_LoopingAnim(_Character)
THEN
PlayAnimation(_Character, "attack1", "LLWEAPONEX_Debug_KeepPlayingAtttackAnim");

IF
TextEventSet("llweap_greatbowfxanimtest")
AND
CharacterGetHostCharacter(_Character)
THEN
DB_LLWEAPONEX_Debug_LoopingAnim(_Character);
PlayAnimation(_Character, "attack1", "LLWEAPONEX_Debug_KeepPlayingAtttackAnim");

IF
TextEventSet("llweap_greatbowfxanimtest")
AND
CharacterGetHostCharacter(_Character)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _)
AND
IsTagged(_Character, "FEMALE", _Female)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_RaceTag, _UndeadRace, _Female, _EffectName, _BoneName, _Duration)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _)
AND
LLWEAPONEX_QRY_RaceMatch(_Character, _RaceTag, _UndeadRace)
AND
PlayLoopEffect(_Character, _EffectName, _BoneName, _FxHandle)
THEN
DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _FxHandle);

IF
TextEventSet("llweap_greatbowfxteststop")
AND
CharacterGetHostCharacter(_Character)
THEN
NOT DB_LLWEAPONEX_Debug_LoopingAnim(_Character);

IF
TextEventSet("llweap_greatbowfxtest")
AND
CharacterGetHostCharacter(_Character)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _)
AND
IsTagged(_Character, "FEMALE", _Female)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_RaceTag, _UndeadRace, _Female, _EffectName, _BoneName, _Duration)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _)
AND
LLWEAPONEX_QRY_RaceMatch(_Character, _RaceTag, _UndeadRace)
AND
PlayLoopEffect(_Character, _EffectName, _BoneName, _FxHandle)
THEN
DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _FxHandle);

IF
TextEventSet("llweap_greatbowfxteststop")
AND
DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _FxHandle)
THEN
StopLoopEffect(_FxHandle);
NOT DB_LLWEAPONEX_Greatbows_Temp_ActivePrepareFX(_Character, _FxHandle);
//END_REGION

//REGION QUIVER_FX
IF
ItemEquipped(_Quiver, _Player)
AND
IsTagged(_Quiver, "LLWEAPONEX_Quiver", 1)
THEN
LLWEAPONEX_Greatbows_PlayQuiverEffect(_Player);

PROC
LLWEAPONEX_Greatbows_PlayQuiverEffect((CHARACTERGUID)_Player)
THEN
PROC_StopLoopEffect(_Player, "LLWEAPONEX.FX.Quiver");

PROC
LLWEAPONEX_Greatbows_PlayQuiverEffect((CHARACTERGUID)_Player)
AND
DB_LLWEAPONEX_Greatbows_QuiverFX(_Race, _UndeadRace, _EffectName, _BoneName)
AND
NOT DB_LoopEffect(_Player, _, "LLWEAPONEX.FX.Quiver", _, _, _)
AND
LLWEAPONEX_QRY_RaceMatch(_Player, _Race, _UndeadRace)
THEN
PROC_LoopEffect(_EffectName, _Player, "LLWEAPONEX.FX.Quiver", "__ANY__", _BoneName);

IF
CharacterPolymorphedInto(_Player, _Race)
AND
DB_LoopEffect(_Source, _Handle, "LLWEAPONEX.FX.Quiver", _Region, _Effect, _BoneName)
THEN
LLWEAPONEX_Greatbows_PlayQuiverEffect(_Player);

IF
ItemUnEquipped(_Quiver, _Player)
AND
IsTagged(_Quiver, "LLWEAPONEX_Quiver", 1)
THEN
PROC_StopLoopEffect(_Player, "LLWEAPONEX.FX.Quiver");
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_WeaponExpansion"