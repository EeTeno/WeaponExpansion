Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION START_EVENTS
IF
ItemEquipped(_Item, _Char)
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
AND
GetTemplate(_Item, _Template)
THEN
LLWEAPONEX_OnItemEquipped(_Char, _Item, _Template);

QRY
LLWEAPONEX_QRY_ItemIsWeaponOrShield_TagIfWeapon((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
StringContains(_Template, "WPN_", 1)
AND
StringContains(_Template, "Shield", _IsShield)
THEN
LLWEAPONEX_Internal_TagIfNotShield(_Char, _IsShield);

PROC
LLWEAPONEX_Internal_TagIfNotShield((CHARACTERGUID)_Char, 0)
THEN
LeaderLib_Tags_PreserveTag(_Char, "LLWEAPONEX_AnyWeaponEquipped");

PROC
LLWEAPONEX_Internal_TagIfNotShield((CHARACTERGUID)_Char, 0)
AND
CharacterIsPlayer(_Char, 1)
THEN
ApplyStatus(_Char, "LLWEAPONEX_REFRESH_UI", 0.0);

PROC
LLWEAPONEX_OnItemEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
LLWEAPONEX_QRY_ItemIsWeaponOrShield_TagIfWeapon(_Char, _Item, _Template)
THEN
LLWEAPONEX_OnWeaponEquipped(_Char, _Item, _Template);

//Try and tag non-WeaponEx items
PROC
LLWEAPONEX_OnWeaponEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
IsTagged(_Item, "LLWEAPONEX_TaggedWeaponType", 0)
AND
NOT LLWEAPONEX_TagHelper_QRY_TagFromTemplate(_Char, _Item, _Template) // Vanilla template to tag
AND
LLWEAPONEX_TagHelper_QRY_CheckWeaponType(_Char, _Item) // Try and get the type via behavior
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX_00_EquipEvents:OnWeaponEquipped] Tagged weapon [",_Template,"] with a new weapon type via [LLWEAPONEX_TagHelper_QRY_CheckWeaponType].");

IF
ItemUnEquipped(_Item, _Char)
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
AND
GetTemplate(_Item, _Template)
THEN
LLWEAPONEX_OnItemUnEquipped(_Char, _Item, _Template);

PROC
LLWEAPONEX_OnItemUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
LLWEAPONEX_QRY_ItemIsWeapon(_Item, _Template)
AND
NOT LeaderLib_Helper_QRY_WeaponIsEquipped(_Char)
THEN
LeaderLib_Tags_ClearPreservedTag(_Char, "LLWEAPONEX_AnyWeaponEquipped");
//END_REGION

//REGION TYPE_EVENTS
//Mastery weapon equipped
PROC
LLWEAPONEX_OnWeaponEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
CharacterIsPlayer(_Char, _IsPlayer)
AND
DB_LLWEAPONEX_WeaponMastery_MasteryVariables(_WeaponType, _CurrentXPVar, _LevelVar, _EnabledFlag)
AND
IsTagged(_Item, _WeaponType, 1)
THEN
LLWEAPONEX_OnWeaponTypeEquipped(_Char, _Item, _WeaponType, _IsPlayer);

PROC
LLWEAPONEX_OnWeaponTypeEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_WeaponType, (INTEGER)_IsPlayer)
THEN
ObjectSetFlag(_Item, "LLWEAPONEX_HasWeaponType", 0);

//Delay removal in case the same weapon type is being equipped
IF
ItemUnEquipped(_Item, _Char)
AND
ObjectGetFlag(_Item, "LLWEAPONEX_HasWeaponType", 1)
AND
CharacterIsPlayer(_Char, _IsPlayer)
AND
DB_LLWEAPONEX_WeaponMastery_MasteryVariables(_WeaponType, _CurrentXPVar, _LevelVar, _EnabledFlag)
AND
IsTagged(_Item, _WeaponType, 1)
THEN
LLWEAPONEX_OnWeaponTypeUnEquipped(_Char, _Item, _WeaponType, _IsPlayer);

PROC
LLWEAPONEX_OnWeaponTypeUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_WeaponType, (INTEGER)_IsPlayer)
THEN
DB_NOOP(1);
//END_REGION

//REGION PRESET_APPLIED
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Char, _Slot,  (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LLWEAPONEX_OnItemEquipped(_Char, _Item, _Template);
//END_REGION

//REGION ANIMATION_OVERRIDE
PROC
LLWEAPONEX_OnWeaponTypeEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "LLWEAPONEX_Katana", (INTEGER)_IsPlayer)
AND
IsTagged(_Item, "LLWEAPONEX_TwoHanded", 1)
THEN
LLWEAPONEX_AnimationSetOverride_Set(_Char, "LLWEAPONEX_Override1");

PROC
LLWEAPONEX_OnWeaponTypeUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "LLWEAPONEX_Katana", (INTEGER)_IsPlayer)
AND
IsTagged(_Item, "LLWEAPONEX_TwoHanded", 1)
THEN
LLWEAPONEX_AnimationSetOverride_Clear(_Char);

PROC
LLWEAPONEX_OnWeaponTypeEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "LLWEAPONEX_Rapier", (INTEGER)_IsPlayer)
AND
IsTagged(_Item, "LLWEAPONEX_TwoHanded", 0)
THEN
LLWEAPONEX_AnimationSetOverride_Set(_Char, "LLWEAPONEX_Override1");

PROC
LLWEAPONEX_OnWeaponTypeUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "LLWEAPONEX_Rapier", (INTEGER)_IsPlayer)
AND
IsTagged(_Item, "LLWEAPONEX_TwoHanded", 0)
THEN
LLWEAPONEX_AnimationSetOverride_Clear(_Char);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"