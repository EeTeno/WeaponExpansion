Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Menu_InitSettings();
KBSECTION

PROC
LLWEAPONEX_Menu_InitSettings()
THEN
DB_LLWEAPONEX_Menu_PresetFlags("LLWEAPONEX_ApplyPreset_DragonSlayer", "LLWEAPONEX_DragonSlayer");
DB_LLWEAPONEX_Menu_PresetFlags("LLWEAPONEX_ApplyPreset_MonkQuarterstaff", "LLWEAPONEX_MonkQuarterstaff");
DB_LLWEAPONEX_Menu_PresetFlags("LLWEAPONEX_ApplyPreset_RunicKnight", "LLWEAPONEX_RunicKnight");
DB_LLWEAPONEX_Menu_PresetFlags("LLWEAPONEX_ApplyPreset_ChaosWeaver", "LLWEAPONEX_ChaosWeaver");

//REGION PRESETS
IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Character, _Instance)
AND
DB_LLWEAPONEX_Menu_PresetFlags(_Flag, _Preset)
AND
GetPosition(_Character, _x, _y, _z)
AND
CreateItemTemplateAtPosition("LOOT_LeaderLib_BackPack_Invisible_98fa7688-0810-4113-ba94-9a8c8463f830", _x, _y, _z, _Backpack)
THEN
MoveAllItemsTo(_Character, _Backpack, 1, 1, 0);
DB_LLWEAPONEX_Menu_Temp_PresetBackpack(_Character, _Backpack, _Preset);
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_Settings_ApplyPreset", 50);
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_Settings_OnPresetApplied", 500);

IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Character, _Instance)
AND
DB_LLWEAPONEX_Menu_PresetFlags(_Flag, _Preset)
THEN
ObjectClearFlag(_Character, _Flag);

//Bartering Tweaks
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_Settings_ApplyPreset")
AND
CharacterHasTalent(_Character, "AnimalEmpathy", 1)
AND
ObjectGetFlag(_Character, "LLBARTER_LearnedPetPal", 1)
THEN
DB_LLWEAPONEX_Menu_Temp_CharacterHasPetPal(_Character);

//Free Pet Pal
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_Settings_ApplyPreset")
AND
CharacterHasTalent(_Character, "AnimalEmpathy", 1)
AND
IsTagged(_Character, "TAG_LadyC_PP_ReceivedPetPal", 1)
THEN
DB_LLWEAPONEX_Menu_Temp_CharacterHasPetPal(_Character);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_Settings_ApplyPreset")
AND
DB_LLWEAPONEX_Menu_Temp_PresetBackpack(_Character, _Backpack, _Preset)
THEN
CharacterApplyPreset(_Character, _Preset);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_Settings_OnPresetApplied")
AND
DB_LLWEAPONEX_Menu_Temp_PresetBackpack(_Character, _Backpack, _Preset)
THEN
NOT DB_LLWEAPONEX_Menu_Temp_PresetBackpack(_Character, _Backpack, _Preset);
MoveAllItemsTo(_Backpack, _Character, 1, 1, 0);
ProcObjectTimer(_Backpack, "LLWEAPONEX_Timers_Settings_DeleteBackpack", 100);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_Settings_OnPresetApplied")
AND
DB_LLWEAPONEX_Menu_Temp_CharacterHasPetPal(_Character)
THEN
NOT DB_LLWEAPONEX_Menu_Temp_CharacterHasPetPal(_Character);
CharacterAddTalent(_Character, "AnimalEmpathy");

PROC
ProcObjectTimerFinished((ITEMGUID)_Backpack, "LLWEAPONEX_Timers_Settings_DeleteBackpack")
THEN
ItemRemove(_Backpack);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_WeaponExpansion"