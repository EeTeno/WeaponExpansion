Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_RemoteMines_InitSettings();
KBSECTION

PROC
LLWEAPONEX_RemoteMines_InitSettings()
THEN
DB_LLWEAPONEX_RemoteMines_Templates("WPN_LLWEAPONEX_RemoteMine_A_Breach_55cf7240-33d4-41a6-8d64-528d6fdc9d1d");
DB_LLWEAPONEX_RemoteMines_Templates("WPN_LLWEAPONEX_RemoteMine_A_Displacement_0dc36b1c-278d-4c88-9b24-8d4d5dd685fc");
DB_LLWEAPONEX_RemoteMines_Templates("WPN_LLWEAPONEX_RemoteMine_A_Explosive_9723e927-785b-4fe9-91e7-a8ad1af30716");
DB_LLWEAPONEX_RemoteMines_Templates("WPN_LLWEAPONEX_RemoteMine_A_PoisonGas_c4c1b48a-678a-45b4-b8e3-13fe918b5fba");
DB_LLWEAPONEX_RemoteMines_Templates("WPN_LLWEAPONEX_RemoteMine_A_Shrapnel_2886439a-60c0-4557-8062-9c55fb103d47");
DB_LLWEAPONEX_RemoteMines_Templates("WPN_LLWEAPONEX_RemoteMine_A_Tar_c1707573-e520-4513-96dd-8bd459a9f8c1");

IF
CanPickupItem(_Player, _RemoteMine, _)
AND
IsTagged(_RemoteMine, "LLWEAPONEX_RemoteMine", 1)
THEN
CharacterItemSetEvent(_Player, _RemoteMine, "LLWEAPONEX_RemoteMine_OnPrePickedUp");

//REGION ADD_MINE
IF
CharacterStatusApplied(_Character, "LLWEAPONEX_ADD_REMOTEMINE", _)
THEN
CharacterGiveReward(_Character, "S_LLWEAPONEX_RemoteMines_AddRandom", 1);

PROC
LLWEAPONEX_RemoteMines_SetOwner((CHARACTERGUID)_Character, (ITEMGUID)_RemoteMine)
AND
NOT LeaderLib_Helper_QRY_ObjectVariableSet(_RemoteMine, "LLWEAPONEX_Mine_Owner")
THEN
SetVarObject(_RemoteMine, "LLWEAPONEX_Mine_Owner", _Character);

/*
IF
ItemTemplateAddedToCharacter(WPN_LLWEAPONEX_Trap_Mine_Remote_A_9723e927-785b-4fe9-91e7-a8ad1af30716, _Item,  _Character)
AND
LeaderLib_Helper_QRY_HasStatus(_Character, "LLWEAPONEX_ADD_REMOTEMINE")
THEN
SetVarObject(_Item, "LLWEAPONEX_Mine_Owner", _Character);
*/
//END_REGION

//REGION HOLDING_TAG
IF
ItemAddedToCharacter(_RemoteMine, _Character)
AND
ObjectExists(_RemoteMine, 1) //prevent stacked items from asserting all over the place
AND
IsTagged(_RemoteMine, "LLWEAPONEX_RemoteMine", 1)
THEN
LeaderLib_Tags_PreserveTag(_Character, "LLWEAPONEX_HoldingRemoteMine");

IF
ItemRemovedFromCharacter(_RemoteMine, _Character)
AND
IsTagged(_RemoteMine, "LLWEAPONEX_RemoteMine", 1)
AND
ItemTagIsInCharacterInventory(_Character, "LLWEAPONEX_RemoteMine", _Count)
AND
_Count <= 0
THEN
LeaderLib_Tags_ClearPreservedTag(_Character, "LLWEAPONEX_HoldingRemoteMine");

IF
ItemAddedToContainer(_RemoteMine, _Container)
AND
ObjectExists(_RemoteMine, 1) //prevent stacked items from asserting all over the place
AND
IsTagged(_RemoteMine, "LLWEAPONEX_RemoteMine", 1)
THEN
SetTag(_Container, "LLWEAPONEX_HoldingRemoteMine");

IF
ItemRemovedFromContainer(_RemoteMine, _Container)
AND
IsTagged(_RemoteMine, "LLWEAPONEX_RemoteMine", 1)
THEN
ClearTag(_Container, "LLWEAPONEX_HoldingRemoteMine");
InventoryLaunchTagIterator(_Container, "LLWEAPONEX_RemoteMine", "", "LLWEAPONEX_Iterator_RemoteMineFound", "");

IF
StoryEvent((ITEMGUID)_RemoteMine, "LLWEAPONEX_Iterator_RemoteMineFound")
AND
ObjectExists(_RemoteMine, 1)
AND
GetInventoryOwner(_RemoteMine, (ITEMGUID)_Container)
THEN
SetTag(_Container, "LLWEAPONEX_HoldingRemoteMine");
//END_REGION

//REGION DELAYED_DETONATION
IF
CharacterStatusApplied(_Item, "LLWEAPONEX_REMOTEMINE_DETONATE_DELAYED", _Source)
THEN
LeaderLib_Timers_StartObjectObjectTimer(_Item, _Source, 1000, "LLWEAPONEX_Timers_RemoteMine_DelayedDetonation", "LLWEAPONEX_RemoteMine_ApplyDetonation");

IF
ItemStatusChange(_Item, "LLWEAPONEX_REMOTEMINE_DETONATE_DELAYED", _Source)
THEN
LeaderLib_Timers_StartObjectObjectTimer(_Item, _Source, 1000, "LLWEAPONEX_Timers_RemoteMine_DelayedDetonation", "LLWEAPONEX_RemoteMine_ApplyDetonation");

PROC
LeaderLib_Timers_ObjectObjectTimerFinished(_Item, _Source, "LLWEAPONEX_RemoteMine_ApplyDetonation")
THEN
ApplyStatus(_Item, "LLWEAPONEX_REMOTEMINE_DETONATE", 0.0, 0, _Source);
//END_REGION

//REGION INVENTORY_DETONATION
QRY
LLWEAPONX_RemoteMines_QRY_ClampDetonation((GUIDSTRING)_Target, (CHARACTERGUID)_Source, (INTEGER)_Count)
AND
NOT DB_LLWEAPONEX_RemoteMines_Temp_DetonationCount(_Target, _Source, _)
AND
DB_GlobalFlag("WeaponExpansion_Settings_RemoteChargeDetonationCountDisabled")
THEN
DB_LLWEAPONEX_RemoteMines_Temp_DetonationCount(_Target, _Source, _Count);

QRY
LLWEAPONX_RemoteMines_QRY_ClampDetonation((GUIDSTRING)_Target, (CHARACTERGUID)_Source, (INTEGER)_Count)
AND
NOT DB_LLWEAPONEX_RemoteMines_Temp_DetonationCount(_Target, _Source, _)
AND
NOT DB_GlobalFlag("WeaponExpansion_Settings_RemoteChargeDetonationCountDisabled")
AND
_Count > 0
AND
IntegerMin(_Count, 5, _ClampedCount)
THEN
DB_LLWEAPONEX_RemoteMines_Temp_DetonationCount(_Target, _Source, _ClampedCount);

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_REMOTEMINE_DETONATE", (CHARACTERGUID)_Source)
AND
ItemTagIsInCharacterInventory(_Target, "LLWEAPONEX_RemoteMine", _Count)
AND
LLWEAPONX_RemoteMines_QRY_ClampDetonation(_Target, _Source, _Count)
THEN
LLWEAPONEX_RemoteMines_DetonateMines(_Source, _Target);

IF
CharacterCharacterEvent(_Source, _Target, "LLWEAPONEX_RemoteMines_StartNextDetonation")
THEN
LLWEAPONEX_RemoteMines_DetonateMines(_Source, _Target);

QRY
LLWEAPONX_RemoteMines_QRY_ReduceDetonationCount((GUIDSTRING)_Target, (CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_RemoteMines_Temp_DetonationCount(_Target, _Source, _Count)
AND
_Count > 0
AND
IntegerSubtract(_Count, 1, _NextCount)
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_DetonationCount(_Target, _Source, _Count);
DB_LLWEAPONEX_RemoteMines_Temp_DetonationCount(_Target, _Source, _NextCount);

PROC
LLWEAPONEX_RemoteMines_DetonateMines((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
LLWEAPONX_RemoteMines_QRY_ReduceDetonationCount(_Target, _Source)
AND
CharacterFindTaggedItem(_Target, "LLWEAPONEX_RemoteMine", _RemoteMine)
AND
IsTagged(_RemoteMine, "LLWEAPONEX_RemoteMine_WorldOnly", 0)
AND
ItemGetAmount(_RemoteMine, _Amount)
THEN
LeaderLog_LogInt("DEBUG", "[LLWEAPONEX_12_Demolition_RemoteMines:DetonateMines] Detonating mine with total amount [",_Amount,"].");
LLWEAPONEX_RemoteMines_SetOwner(_Source, _RemoteMine); // Failsafe
LLWEAPONEX_SetItemAmount(_RemoteMine, _Amount);
CharacterItemSetEvent(_Target, _RemoteMine, "LLWEAPONEX_RemoteMine_StartDetonation_Character");
LeaderLib_Timers_StartCharacterCharacterTimer(_Source, _Target, 250, "LLWEAPONEX_Timers_RemoteMines_NextDetonation", "LLWEAPONEX_RemoteMines_StartNextDetonation");

PROC
LLWEAPONEX_RemoteMines_DetonateMines((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
DB_LLWEAPONEX_RemoteMines_Temp_DetonationCount(_Target, _Source, _Count)
AND
_Count <= 0
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_DetonationCount(_Target, _Source, _Count);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_12_Demolition_RemoteMines:DetonateMines] Detonating done.");
//END_REGION

IF
ItemStatusChange(_RemoteMine, "LLWEAPONEX_REMOTEMINE_DETONATE", (CHARACTERGUID)_Source)
AND
IsTagged(_RemoteMine, "LLWEAPONEX_RemoteMine", 1)
THEN
LLWEAPONEX_SetItemAmount(_RemoteMine);
LLWEAPONEX_RemoteMines_SetOwner(_Source, _RemoteMine);
CharacterItemSetEvent(_Source, _RemoteMine, "LLWEAPONEX_RemoteMine_DetonationStatus");

//REGION CONTAINER_DETONATION
QRY
LLWEAPONX_RemoteMines_QRY_RemoteMineIsInContainer((ITEMGUID)_Target, (CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_RemoteMines_Templates(_Template)
AND
ItemTemplateIsInContainer(_Target, _Template, _Count)
AND
_Count > 0
THEN
LLWEAPONEX_RemoteMines_Internal_AddToContainerCount(_Target, _Source, _Count);
LeaderLog_LogIntWithString("DEBUG", "[LLWEAPONEX_12_Demolition_RemoteMines:QRY:RemoteMineIsInContainer] Mine template [",_Template,"] has count [",_Count,"] in a container.");

PROC
LLWEAPONEX_RemoteMines_Internal_AddToContainerCount((ITEMGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_Count)
AND
DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _LastCount)
AND
IntegerSum(_LastCount, _Count, _NextCount)
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _LastCount);
DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _NextCount);

PROC
LLWEAPONEX_RemoteMines_Internal_AddToContainerCount((ITEMGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_Count)
AND
NOT DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _)
THEN
DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _Count);

PROC
LLWEAPONEX_RemoteMines_Internal_ClampContainerCount((ITEMGUID)_Target, (CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _Count)
AND
IntegerMin(_Count, 5, _ClampedCount)
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _Count);
DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _ClampedCount);
LeaderLog_LogInt("DEBUG", "[LLWEAPONEX_12_Demolition_RemoteMines:Internal:ClampContainerCount] Blowing up [",_ClampedCount,"] mines in container.");

IF
ItemStatusChange(_Target, "LLWEAPONEX_REMOTEMINE_DETONATE", (CHARACTERGUID)_Source)
AND
NOT DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _, _)
AND
ObjectExists(_Target, 1)
AND
ItemIsContainer(_Target, 1)
AND
ItemIsDestroyed(_Target, 0)
AND
LLWEAPONX_RemoteMines_QRY_RemoteMineIsInContainer(_Target, _Source)
THEN
LLWEAPONEX_RemoteMines_Internal_ClampContainerCount(_Target, _Source);
InventoryLaunchTagIterator(_Target, "LLWEAPONEX_RemoteMine", "", "LLWEAPONEX_Iterator_RemoteMines_Detonate", "");
ProcObjectTimer(_Target, "LLWEAPONEX_Timers_RemoteMines_ClearDetonatingDB", 500);

PROC
ProcObjectTimerFinished((ITEMGUID)_Target, "LLWEAPONEX_Timers_RemoteMines_ClearDetonatingDB")
AND
DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _Count)
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _Count);

IF
StoryEvent((ITEMGUID)_RemoteMine, "LLWEAPONEX_Iterator_RemoteMines_Detonate")
AND
ObjectExists(_RemoteMine, 1)
AND
ItemIsDestroyed(_RemoteMine, 0)
AND
IsTagged(_RemoteMine, "LLWEAPONEX_RemoteMine_WorldOnly", 0)
AND
GetInventoryOwner(_RemoteMine, (ITEMGUID)_Target)
AND
DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _Count)
AND
_Count > 0
AND
IntegerSubtract(_Count, 1, _NextCount)
AND
ItemGetAmount(_RemoteMine, _Amount)
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _Count);
DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _NextCount);
LeaderLog_LogInt("DEBUG", "[LLWEAPONEX_12_Demolition_RemoteMines:DetonationDone] Detonating mines in inventory with total amount [",_Amount,"].");
LLWEAPONEX_RemoteMines_SetOwner(_Source, _RemoteMine); // Failsafe
SetVarObject(_RemoteMine, "LLWEAPONEX_Mine_Inventory", _Target);
LLWEAPONEX_SetItemAmount(_RemoteMine, _Amount);
CharacterItemSetEvent(_Source, _RemoteMine, "LLWEAPONEX_RemoteMine_StartDetonation_Container");

IF
CharacterItemEvent(_Source, _RemoteMine, "LLWEAPONEX_RemoteMine_DetonationDone")
AND
GetInventoryOwner(_RemoteMine, (ITEMGUID)_Target)
AND
DB_LLWEAPONEX_RemoteMines_Temp_ContainerDetonationCount(_Target, _Source, _Count)
AND
_Count > 0
THEN
ProcObjectTimerCancel(_Target, "LLWEAPONEX_Timers_RemoteMines_ClearDetonatingDB");
ProcObjectTimer(_Target, "LLWEAPONEX_Timers_RemoteMines_ClearDetonatingDB", 500);
LeaderLib_Timers_StartObjectTimer(_RemoteMine, 50, "LLWEAPONEX_Timers_RemoteMines_StartNextDetonation", "LLWEAPONEX_Iterator_RemoteMines_Detonate");
//END_REGION

//REGION BEHAVIOR_SCRIPT_DETONATION_EVENTS
IF
CharacterItemEvent(_Character, _Mine, "LLWEAPONEX_RemoteMine_DetonateInWorld_BySkillID")
AND
GetVarString(_Mine, "LLWEAPONEX_Mine_SkillID", _Skill)
AND
GetPosition(_Mine, _x, _y, _z)
AND
CharacterGetLevel(_Character, _Level)
THEN
CreateExplosionAtPosition(_x, _y, _z, _Skill, _Level);
SetStoryEvent(_Mine, "LLWEAPONEX_RemoteMine_DetonationDone");

IF
CharacterItemEvent(_Character, _Mine, "LLWEAPONEX_RemoteMine_DetonateInWorld_BySkillID")
AND
GetVarString(_Mine, "LLWEAPONEX_Mine_ExplosionStatusID", _Status)
THEN
ApplyStatus(_Mine, _Status, 0.0, 1, _Character);
SetStoryEvent(_Mine, "LLWEAPONEX_RemoteMine_DetonationDone");

IF
CharacterItemEvent(_Target, _Mine, "LLWEAPONEX_RemoteMine_DetonateInInventory_BySkillID")
AND
GetVarString(_Mine, "LLWEAPONEX_Mine_SkillID", _Skill)
AND
GetVarObject(_Mine, "LLWEAPONEX_Mine_Owner", (CHARACTERGUID)_Owner)
AND
GetPosition(_Target, _x, _y, _z)
AND
CharacterGetLevel(_Owner, _Level)
THEN
CreateExplosionAtPosition(_x, _y, _z, _Skill, _Level);
SetStoryEvent(_Mine, "LLWEAPONEX_RemoteMine_DetonationDone");

IF
CharacterItemEvent(_Target, _Mine, "LLWEAPONEX_RemoteMine_DetonateInInventory_ByStatusID")
AND
GetVarString(_Mine, "LLWEAPONEX_Mine_ExplosionStatusID", _Status)
AND
GetVarObject(_Mine, "LLWEAPONEX_Mine_Owner", (CHARACTERGUID)_Owner)
THEN
ApplyStatus(_Target, _Status, 0.0, 1, _Owner);
SetStoryEvent(_Mine, "LLWEAPONEX_RemoteMine_DetonationDone");
//END_REGION

//REGION THROWING
/*
IF
StoryEvent((ITEMGUID)_Object, "LLWEAPONEX_MovingObjectRemoteMine_Init")
AND
ObjectExists(_Object, 1)
AND
ItemGetOwner(_Object, _Owner)
AND
String(_Owner, _OwnerStr)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX_12_RemoteMines:LLWEAPONEX_MovingObjectRemoteMine_Init] Owner [",_OwnerStr,"]");
*/

PROC
ProcProcessUseOfItem((CHARACTERGUID)_Player, (ITEMGUID)_Item, (INTEGER)_RequestID)
AND
NOT DB_CustomUseItemResponse(_Player, _Item, 0)
AND
IsTagged(_Item, "LLWEAPONEX_RemoteMine", 1)
THEN
DB_LLWEAPONEX_RemoteMines_Temp_ThrowingStart(_Player, _Item);

IF
CharacterStoppedUsingItem(_Player, _RemoteMine)
AND
DB_LLWEAPONEX_RemoteMines_Temp_ThrowingStart(_Player, _RemoteMine)
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_ThrowingStart(_Player, _RemoteMine);

PROC
LLWEAPONEX_RemoteMines_Internal_SetThrowingActive((CHARACTERGUID)_Player, (ITEMGUID)_RemoteMine, (ITEMGUID)_MovingObject)
THEN
DB_LLWEAPONEX_RemoteMines_Temp_ThrowingActive(_Player, _RemoteMine, _MovingObject);

IF
StoryEvent(_Object, "LLWEAPONEX_MovingObjectRemoteMine_Init")
AND
ObjectExists(_Object, 1)
AND
ObjectIsItem((ITEMGUID)_Object, 1)
AND
ItemGetOwner(_Object, _Player)
AND
DB_LLWEAPONEX_RemoteMines_Temp_ThrowingStart(_Player, _RemoteMine)
AND
NOT DB_LLWEAPONEX_RemoteMines_Temp_ThrowingActive((CHARACTERGUID)_, _, _Object)
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_ThrowingStart(_Player, _RemoteMine);
LLWEAPONEX_RemoteMines_Internal_SetThrowingActive(_Player, _RemoteMine, _Object);

IF
CharacterItemEvent(_Player, _Object, "LLWEAPONEX_MovingObjectRemoteMine_Landed")
AND
DB_LLWEAPONEX_RemoteMines_Temp_ThrowingActive(_Player, _RemoteMine, _Object)
THEN
NOT DB_LLWEAPONEX_RemoteMines_Temp_ThrowingActive(_Player, _RemoteMine, _Object);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_12_RemoteMines:LLWEAPONEX_MovingObjectRemoteMine_Landed] Remote Mine projectile landed.");
LeaderLib_Helpers_CopyItemTransform(_RemoteMine, _Object, 0, 1, _Player);
SetOnStage(_Object, 0);
ItemDestroy(_Object);
SetStoryEvent(_RemoteMine, "LLWEAPONEX_RemoteMine_Thrown");

IF
StoryEvent((ITEMGUID)_RemoteMine, "LLWEAPONEX_MovingObjectRemoteMine_SnapToGround")
AND
GetPosition(_RemoteMine, _x, _y, _z)
THEN
ItemMoveToPosition(_RemoteMine, _x, _y, _z, 20.0, 0.0);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"