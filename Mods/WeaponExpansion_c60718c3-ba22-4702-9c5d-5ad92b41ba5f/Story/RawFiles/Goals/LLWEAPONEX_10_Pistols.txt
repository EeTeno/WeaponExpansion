Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Pistols_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLWEAPONEX_Pistols_InitSettings()
THEN
DB_LLWEAPONEX_Pistols_HandProjectiles("DWARF", 0, "Projectile_LLWEAPONEX_Pistol_A_Shoot_LeftHand", 600, "LLWEAPONEX_FX_PISTOL_EXPLOSION_LEFT");
DB_LLWEAPONEX_Pistols_HandProjectiles("DWARF", 1, "Projectile_LLWEAPONEX_Pistol_A_Shoot_RightHand", 600, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");
DB_LLWEAPONEX_Pistols_HandProjectiles("ELF", 0, "Projectile_LLWEAPONEX_Pistol_A_Shoot_RightHand", 250, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");
DB_LLWEAPONEX_Pistols_HandProjectiles("ELF", 1, "Projectile_LLWEAPONEX_Pistol_A_Shoot_RightHand", 250, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");
DB_LLWEAPONEX_Pistols_HandProjectiles("HUMAN", 0, "Projectile_LLWEAPONEX_Pistol_A_Shoot_LeftHand", 400, "LLWEAPONEX_FX_PISTOL_EXPLOSION_LEFT");
DB_LLWEAPONEX_Pistols_HandProjectiles("HUMAN", 1, "Projectile_LLWEAPONEX_Pistol_A_Shoot_LeftHand", 400, "LLWEAPONEX_FX_PISTOL_EXPLOSION_LEFT");
DB_LLWEAPONEX_Pistols_HandProjectiles("LIZARD", 0, "Projectile_LLWEAPONEX_Pistol_A_Shoot_RightHand", 800, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");
DB_LLWEAPONEX_Pistols_HandProjectiles("LIZARD", 1, "Projectile_LLWEAPONEX_Pistol_A_Shoot_RightHand", 800, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");

DB_LLWEAPONEX_Pistols_ShootingStatus("EQ_LLWEAPONEX_Belt_Pistol_A_94838d55-d5e6-4115-b736-b8b26f321003", "LLWEAPONEX_FX_PISTOL_A_SHOOTING");
DB_LLWEAPONEX_Pistols_ShootingStatus("EQ_UNIQUE_LLWEAPONEX_Belt_Pistol_A_Isshin_A_6e667d23-c8a6-46d6-b6ec-2aa6aa95241d", "LLWEAPONEX_FX_UNIQUE_PISTOL_A_ISSHIN_SHOOTING");

DB_LLWEAPONEX_Pistols_BulletTemplates("Default", "Template", "LLWEAPONEX_PISTOL_DAMAGE_DEFAULT_WEAPON", "LLWEAPONEX_PISTOL_DAMAGE_DEFAULT_SCALED");
DB_LLWEAPONEX_Pistols_BulletTemplates("Default", "Template", "LLWEAPONEX_PISTOL_DAMAGE_DEFAULT_WEAPON", "LLWEAPONEX_PISTOL_DAMAGE_DEFAULT_SCALED");

PROC
LLWEAPONEX_Pisolts_RegisterBulletTemplate((STRING)_BulletType, (STRING)_Template, (STRING)_WeaponDamageStatus, (STRING)_ScaledDamageStatus)
THEN
DB_LLWEAPONEX_Pistols_BulletTemplates(_BulletType, _Template, _WeaponDamageStatus, _ScaledDamageStatus);
//END_REGION

//REGION PISTOL_SHOOT_FX
//Dwarves and elves don't have a good shoot -> stow animation like humans/lizards do
QRY
LLWEAPONEX_Pistols_QRY_NeedsToSheathe((CHARACTERGUID)_Char)
AND
LeaderLib_Helper_QRY_IsRace(_Char, "DWARF")
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Pistols_QRY_NeedsToSheathe((CHARACTERGUID)_Char)
AND
LeaderLib_Helper_QRY_IsRace(_Char, "ELF")
THEN
DB_NOOP(1);

IF
CharacterUsedSkill(_Char, "Target_LLWEAPONEX_Pistol_A_Shoot", _, _)
THEN
LeaderLib_Skills_CharacterUsedSkill(_Char, "Target_LLWEAPONEX_Pistol_A_Shoot");

IF
CharacterUsedSkill(_Char, "Target_LLWEAPONEX_Pistol_A_Shoot_Enemy", _, _)
THEN
LeaderLib_Skills_CharacterUsedSkill(_Char, "Target_LLWEAPONEX_Pistol_A_Shoot");

PROC
LeaderLib_Skills_CharacterUsedSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_A_Shoot")
AND
LLWEAPONEX_Pistols_QRY_NeedsToSheathe(_Char)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 350, "Timers_LLWEAPONEX_Pistols_PlayPistolFX", "LLWEAPONEX_Pistols_PlayPistolFX");

PROC
LeaderLib_Skills_CharacterUsedSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_A_Shoot")
AND
NOT LLWEAPONEX_Pistols_QRY_NeedsToSheathe(_Char)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 50, "Timers_LLWEAPONEX_Pistols_PlayPistolFX", "LLWEAPONEX_Pistols_PlayPistolFX");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_PlayPistolFX")
AND
CharacterGetEquippedItem(_Char, "Belt", _Pistol)
AND
GetTemplate(_Pistol, _Template)
AND
DB_LLWEAPONEX_Pistols_ShootingStatus(_Template, _Status)
THEN
ApplyStatus(_Char, _Status, 12.0, 1, _Char);

IF
SkillCast(_Char, "Target_LLWEAPONEX_Pistol_A_Shoot", _, _)
THEN
LeaderLib_Skills_CharacterCastSkill(_Char, "Target_LLWEAPONEX_Pistol_A_Shoot");

IF
SkillCast(_Char, "Target_LLWEAPONEX_Pistol_A_Shoot_Enemy", _, _)
THEN
LeaderLib_Skills_CharacterCastSkill(_Char, "Target_LLWEAPONEX_Pistol_A_Shoot");

PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_A_Shoot")
AND
//NOT LLWEAPONEX_Pistols_QRY_NeedsToSheathe(_Char)
//AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
DB_LLWEAPONEX_Pistols_HandProjectiles(_Race, _IsFemale, _ProjectileSkill, _HideHandPistolDelay, _ExplosionStatusFX)
AND
LeaderLib_Helper_QRY_IsRace(_Char, _Race)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, _HideHandPistolDelay, "Timers_LLWEAPONEX_Pistols_RemovePistolFX", "LLWEAPONEX_Pistols_RemovePistolFX");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_RemovePistolFX")
AND
DB_LLWEAPONEX_Pistols_ShootingStatus(_Template, _Status)
THEN
RemoveStatus(_Char, _Status);

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_RemovePistolFX")
AND
LLWEAPONEX_Pistols_QRY_NeedsToSheathe(_Char)
THEN
LeaderLib_Helper_ClearActionQueue(_Char);

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_StopAnimation")
THEN
LeaderLib_Helper_ClearActionQueue(_Char);
PlayAnimation(_Char, "skill_cast_ll_pistol_01_sheathe", "");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_StopAnimation")
AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
DB_LLWEAPONEX_Pistols_HandProjectiles(_Race, _IsFemale, _ProjectileSkill, _HideHandPistolDelay, _ExplosionStatusFX)
AND
LeaderLib_Helper_QRY_IsRace(_Char, _Race)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, _HideHandPistolDelay, "Timers_LLWEAPONEX_Pistols_RemovePistolFX", "LLWEAPONEX_Pistols_RemovePistolFX");

PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_A_Shoot")
AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
DB_LLWEAPONEX_Pistols_HandProjectiles(_Race, _IsFemale, _ProjectileSkill, _HideHandPistolDelay, _ExplosionStatusFX)
AND
LeaderLib_Helper_QRY_IsRace(_Char, _Race)
THEN
ApplyStatus(_Char, _ExplosionStatusFX, 0.2, 1, _Char);
LeaderLib_Timers_StartObjectTimer(_Char, 50, "Timers_LLWEAPONEX_Pistols_RemoveExplosionStatusFX", "LLWEAPONEX_Pistols_RemoveExplosionStatusFX");
SetVarFixedString(_Char, "LLWEAPONEX_Pistols_ShootProjectileSkill", _ProjectileSkill);

IF
StoryEvent(_Char, "LLWEAPONEX_Pistols_RemoveExplosionStatusFX")
AND
DB_LLWEAPONEX_Pistols_HandProjectiles(_Race, _IsFemale, _ProjectileSkill, _HideHandPistolDelay, _ExplosionStatusFX)
AND
LeaderLib_Helper_QRY_HasStatus(_Char, _ExplosionStatusFX)
THEN
RemoveStatus(_Char, _ExplosionStatusFX);
//END_REGION

//REGION PISTOL_SHOOT_PROJECTILE
IF
CharacterUsedSkillAtPosition(_Char, _x, _y, _z, "Target_LLWEAPONEX_Pistol_A_Shoot", _, _)
THEN
SetVarFloat3(_Char, "LLWEAPONEX_Pistols_ShootProjectileTarget", _x, _y, _z);

IF
CharacterUsedSkillOnTarget(_Char, _Target, "Target_LLWEAPONEX_Pistol_A_Shoot", _, _)
AND
GetPosition(_Target, _x, _y, _z)
THEN
LLWEAPONEX_Pistols_Internal_FlagManualTarget(_Char, _Target);
SetVarFloat3(_Char, "LLWEAPONEX_Pistols_ShootProjectileTarget", _x, _y, _z);

IF
CharacterUsedSkillAtPosition(_Char, _x, _y, _z, "Target_LLWEAPONEX_Pistol_A_Shoot_Enemy", _, _)
THEN
SetVarFloat3(_Char, "LLWEAPONEX_Pistols_ShootProjectileTarget", _x, _y, _z);

IF
CharacterUsedSkillOnTarget(_Char, _Target, "Target_LLWEAPONEX_Pistol_A_Shoot_Enemy", _, _)
AND
GetPosition(_Target, _x, _y, _z)
THEN
LLWEAPONEX_Pistols_Internal_FlagManualTarget(_Char, _Target);
SetVarFloat3(_Char, "LLWEAPONEX_Pistols_ShootProjectileTarget", _x, _y, _z);

PROC
LLWEAPONEX_Pistols_Internal_FlagManualTarget((CHARACTERGUID)_Char, (GUIDSTRING)_Target)
AND
ObjectIsCharacter((CHARACTERGUID)_Target, 1)
THEN
SetVarObject(_Char, "LLWEAPONEX_Pistols_ShootProjectileTargetObject", _Target);

PROC
LLWEAPONEX_Pistols_Internal_FlagManualTarget((CHARACTERGUID)_Char, (GUIDSTRING)_Target)
AND
ObjectIsItem((ITEMGUID)_Target, 1)
AND
ItemIsDestructible(_Target, 1)
THEN
SetVarObject(_Char, "LLWEAPONEX_Pistols_ShootProjectileTargetObject", _Target);

PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_A_Shoot")
THEN
LLWEAPONEX_Pistols_Internal_ShootBullet_Start(_Char, S_LeaderLib_Dummy_TargetHelper_A_36069245-0e2d-44b1-9044-6797bd29bb15);

// Shooting at a position
PROC
LLWEAPONEX_Pistols_Internal_ShootBullet_Start((CHARACTERGUID)_Char, (CHARACTERGUID)_TargetHelperDummy)
AND
NOT LeaderLib_Variables_QRY_ObjectVariableSet(_Char, "LLWEAPONEX_Pistols_ShootProjectileTargetObject")
THEN
CharacterCharacterSetEvent(_Char, _TargetHelperDummy, "LLWEAPONEX_Pistols_ShootProjectile");
DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char, _TargetHelperDummy);
LeaderLib_Timers_StartCharacterCharacterTimer(_Char, _TargetHelperDummy, 1250, "Timers_LLWEAPONEX_Pistols_ResetTargetHelperDummy", "LLWEAPONEX_Pistols_ResetTargetHelperDummy");

PROC
LLWEAPONEX_Pistols_Internal_ShootBullet_Start((CHARACTERGUID)_Char, (CHARACTERGUID)_TargetHelperDummy)
AND
NOT DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char, _TargetHelperDummy)
AND
LeaderLib_Variables_QRY_ObjectVariableSet(_Char, "LLWEAPONEX_Pistols_ShootProjectileTargetObject")
AND
GetVarObject(_Char, "LLWEAPONEX_Pistols_ShootProjectileTargetObject", _Target)
AND
ObjectIsItem(_Target, _IsItem)
THEN
DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char, _TargetHelperDummy);
LeaderLib_Timers_StartCharacterCharacterTimer(_Char, _TargetHelperDummy, 1250, "Timers_LLWEAPONEX_Pistols_ResetTargetHelperDummy", "LLWEAPONEX_Pistols_ResetTargetHelperDummy");
LLWEAPONEX_Pistols_Internal_ShootBullet_AtTarget(_Char, _TargetHelperDummy, _Target, _IsItem);

PROC
LLWEAPONEX_Pistols_Internal_ShootBullet_AtTarget((CHARACTERGUID)_Char, (CHARACTERGUID)_TargetHelperDummy, (GUIDSTRING)_Target, 1)
THEN
CharacterCharacterSetEvent(_Char, _TargetHelperDummy, "LLWEAPONEX_Pistols_ShootProjectile_AtItem");

// For some reason, KNOCKED_DOWN types makes the target un-hittable by projectiles shot by scripts
QRY
LLWEAPONEX_Pistols_QRY_HitDirectly((GUIDSTRING)_Target)
AND
HasActiveStatus(_Target, "UNCONSCIOUS", _a)
AND
HasActiveStatus(_Target, "KNOCKED_DOWN", _b)
AND
HasActiveStatus(_Target, "InstantKnockdown", _c)
AND
HasActiveStatus(_Target, "SHOCKWAVE", _d)
AND
LeaderLib_Math_QRY_IsEqualToAny(1, _a, _b, _c, _d)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_Pistols_Internal_ShootBullet_AtTarget((CHARACTERGUID)_Char, (CHARACTERGUID)_TargetHelperDummy, (GUIDSTRING)_Target, 0)
AND
NOT LLWEAPONEX_Pistols_QRY_HitDirectly(_Target)
THEN
CharacterCharacterSetEvent(_Char, _TargetHelperDummy, "LLWEAPONEX_Pistols_ShootProjectile_AtCharacter");

PROC
LLWEAPONEX_Pistols_Internal_ShootBullet_AtTarget((CHARACTERGUID)_Char, (CHARACTERGUID)_TargetHelperDummy, (GUIDSTRING)_Target, 0)
AND
LLWEAPONEX_Pistols_QRY_HitDirectly(_Target)
AND
GetVarFixedString(_Char, "LLWEAPONEX_Pistols_ShootProjectileSkill", _Skill)
THEN
LeaderLib_Explode_ExplodeProjectile(_Char, _Skill, _Target, 0);

/*
PROC
LLWEAPONEX_Pistols_Internal_FlagManualTarget((CHARACTERGUID)_Char, (GUIDSTRING)_Target)
THEN
SetVarObject(_Char, "LLWEAPONEX_Pistols_ShootProjectileObject", _Target);

PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_A_Shoot")
AND
ObjectGetFlag(_Char, "LLWEAPONEX_Pistols_ManualTarget", 0)
THEN
CharacterCharacterSetEvent(_Char, S_LeaderLib_Dummy_TargetHelper_A_36069245-0e2d-44b1-9044-6797bd29bb15, "LLWEAPONEX_Pistols_ShootProjectile");
DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char, S_LeaderLib_Dummy_TargetHelper_A_36069245-0e2d-44b1-9044-6797bd29bb15);
LeaderLib_Timers_StartCharacterCharacterTimer(_Char, S_LeaderLib_Dummy_TargetHelper_A_36069245-0e2d-44b1-9044-6797bd29bb15, 1250, "Timers_LLWEAPONEX_Pistols_ResetTargetHelperDummy", "LLWEAPONEX_Pistols_ResetTargetHelperDummy");

PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_A_Shoot")
AND
ObjectGetFlag(_Char, "LLWEAPONEX_Pistols_ManualTarget", 1)
AND
GetVarObject(_Char, "LLWEAPONEX_Pistols_ShootProjectileObject", _Target)
AND
GetVarFixedString(_Char, "LLWEAPONEX_Pistols_ShootProjectileSkill", _Skill)
AND
CharacterGetLevel(_Char, _Level)
AND
GetPosition(_Char, _x, _y, _z)
AND
RealSum(_y, 2.0, _ty)
THEN
NRD_ProjectilePrepareLaunch();
NRD_ProjectileSetString("SkillId", _Skill);
NRD_ProjectileSetInt("CasterLevel", _Level);
//NRD_ProjectileSetGuidString("SourcePosition", _Char);
NRD_ProjectileSetVector3("SourcePosition", _x, _ty, _z);
NRD_ProjectileSetGuidString("Caster", _Char);
NRD_ProjectileSetGuidString("Source", _Char);
NRD_ProjectileSetGuidString("HitObject", _Target);
//NRD_ProjectileSetGuidString("HitObjectPosition", _Target);
NRD_ProjectileSetGuidString("TargetPosition", _Target);
NRD_ProjectileLaunch();
ClearVarObject(_Char, "LLWEAPONEX_Pistols_ShootProjectileObject");
ObjectClearFlag(_Char, "LLWEAPONEX_Pistols_ManualTarget", 0);
*/

/*
//Use a global dummy if it's available
PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_A_Shoot")
AND
NOT DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_, S_LeaderLib_Dummy_TargetHelper_A_36069245-0e2d-44b1-9044-6797bd29bb15)
THEN
DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char, (CHARACTERGUID)S_LeaderLib_Dummy_TargetHelper_A_36069245-0e2d-44b1-9044-6797bd29bb15);
LeaderLib_Timers_StartCharacterCharacterTimer(_Char, S_LeaderLib_Dummy_TargetHelper_A_36069245-0e2d-44b1-9044-6797bd29bb15, 1250, "Timers_LLWEAPONEX_Pistols_ResetTargetHelperDummy", "LLWEAPONEX_Pistols_ResetTargetHelperDummy");
CharacterCharacterSetEvent(_Char, S_LeaderLib_Dummy_TargetHelper_A_36069245-0e2d-44b1-9044-6797bd29bb15, "LLWEAPONEX_Pistols_ShootProjectile");
//SetStoryEvent(_Char, "LLWEAPONEX_Pistols_ShootProjectile");

//Use a temp dummy if the global one is in use
PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_A_Shoot")
AND
NOT DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char,_)
AND
TemporaryCharacterCreateAtPosition(0.0, 0.0, 0.0, "LeaderLib_SkillDummy_94668062-11ea-4ecf-807c-4cc225cbb236", 0, _Dummy)
THEN
LeaderLib_Skills_Internal_PrepareTargetDummy(_Dummy, 1, 0, 1);
SetFaction(_Dummy, "LLWEAPONEX_Dummy_TargetHelper");
DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char, _Dummy);
LeaderLib_Timers_StartCharacterCharacterTimer(_Char, _Dummy, 1250, "Timers_LLWEAPONEX_Pistols_ResetTargetHelperDummy", "LLWEAPONEX_Pistols_ResetTargetHelperDummy");
CharacterCharacterSetEvent(_Char, _Dummy, "LLWEAPONEX_Pistols_ShootProjectile");
*/

IF
CharacterCharacterEvent(_Char, _Dummy, "LLWEAPONEX_Pistols_ResetTargetHelperDummy")
AND
DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char, _Dummy)
THEN
NOT DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char, _Dummy);
ClearVarObject(_Char, "LLWEAPONEX_Pistols_ShootProjectileTargetObject");

/*
IF
CharacterCharacterEvent(_Char, _Dummy, "LLWEAPONEX_Pistols_ResetTargetHelperDummy")
AND
ObjectIsGlobal(_Dummy, 0)
THEN
RemoveTemporaryCharacter(_Dummy);
*/
//END_REGION

//REGION PISTOL_DAMAGE
IF
StoryEvent((ITEMGUID)_Bullet, "LLWEAPONEX_Bullet_SetOwner")
AND
ItemGetOwner(_Bullet, _Owner)
THEN
SetVarObject(_Bullet, "LLWEAPONEX_Bullet_Owner", _Owner);
SetStoryEvent(_Bullet, "LLWEAPONEX_Bullet_CheckTargets");

IF
CharacterCharacterEvent(_Char, _Target, "LLWEAPONEX_Bullet_OnHit")
AND
CharacterIsPlayer(_Char, _a)
AND
CharacterGameMaster(_Char, _b)
AND
IntegerMax(_a, _b, _IsPlayer) // If any of the above is greater or equal to 1, this is a player
THEN
//LLWEAPONEX_Pistols_Internal_OnHit_Start((GUIDSTRING)_Target, _Char, _IsPlayer);
LLWEAPONEX_Pistols_Internal_OnHit_Default((GUIDSTRING)_Target, _Char, _IsPlayer);

IF
CharacterItemEvent(_Char, _Target, "LLWEAPONEX_Bullet_OnHit")
AND
CharacterIsPlayer(_Char, _a)
AND
CharacterGameMaster(_Char, _b)
AND
IntegerMax(_a, _b, _IsPlayer)
THEN
//LLWEAPONEX_Pistols_Internal_OnHit_Start((GUIDSTRING)_Target, _Char, _IsPlayer);
LLWEAPONEX_Pistols_Internal_OnHit_Default((GUIDSTRING)_Target, _Char, _IsPlayer);

PROC
LLWEAPONEX_Pistols_Internal_OnHit_Default((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, 1)
AND
ObjectGetFlag(_Cause, "LLWEAPONEX_DisableWeaponMasteryExperience", 0)
THEN
LLWEAPONEX_WeaponMastery_AddExperienceForWeaponType(_Cause, "LLWEAPONEX_Pistol", 1, _Target);

PROC
LLWEAPONEX_Pistols_Internal_OnHit_Start((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, (INTEGER)_IsPlayer)
AND
NOT LeaderLib_Variables_QRY_StringVariableSet(_Cause, "LLWEAPONEX_Pistol_BulletType", 0)
THEN
LLWEAPONEX_Pistols_Internal_OnHit_Default(_Target, _Cause, _IsPlayer);

PROC
LLWEAPONEX_Pistols_Internal_OnHit_Start((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, (INTEGER)_IsPlayer)
AND
LeaderLib_Variables_QRY_StringVariableSet(_Cause, "LLWEAPONEX_Pistol_BulletType", 0)
AND
GetVarString(_Cause, "LLWEAPONEX_Pistol_BulletType", _BulletType)
THEN
LLWEAPONEX_Pistols_Internal_OnHit_WithBulletType(_Target, _Cause, _IsPlayer, _BulletType);

PROC
LLWEAPONEX_Pistols_Internal_OnHit_WithBulletType((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, 1, (STRING)_BulletType)
AND
DB_LLWEAPONEX_Pistols_BulletTemplates(_BulletType, _Template, _WeaponStatus, _ScaledStatus)
AND
NOT DB_LLWEAPONEX_Pistols_Temp_HitResolved(_Cause, _Target, _)
AND
LeaderLib_Helper_QRY_WeaponIsEquipped(_Cause)
THEN
DB_LLWEAPONEX_Pistols_Temp_HitResolved(_Cause, _Target, _WeaponStatus);
ApplyStatus(_Target, _WeaponStatus, 0.0, 1, _Cause);

PROC
LLWEAPONEX_Pistols_Internal_OnHit_WithBulletType((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, 1, (STRING)_BulletType)
AND
DB_LLWEAPONEX_Pistols_BulletTemplates(_BulletType, _Template, _WeaponStatus, _ScaledStatus)
AND
NOT DB_LLWEAPONEX_Pistols_Temp_HitResolved(_Cause, _Target, _)
AND
NOT LeaderLib_Helper_QRY_WeaponIsEquipped(_Cause)
THEN
DB_LLWEAPONEX_Pistols_Temp_HitResolved(_Cause, _Target, _ScaledStatus);
ApplyStatus(_Target, _ScaledStatus, 0.0, 1, _Cause);

PROC
LLWEAPONEX_Pistols_Internal_OnHit_WithBulletType((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, 0, (STRING)_BulletType)
AND
DB_LLWEAPONEX_Pistols_BulletTemplates(_BulletType, _Template, _WeaponStatus, _ScaledStatus)
AND
NOT DB_LLWEAPONEX_Pistols_Temp_HitResolved(_Cause, _Target, _)
THEN
DB_LLWEAPONEX_Pistols_Temp_HitResolved(_Cause, _Target, _ScaledStatus);
ApplyStatus(_Target, _ScaledStatus, 0.0, 1, _Cause);

PROC
LLWEAPONEX_Pistols_Internal_OnHit_WithBulletType((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, (INTEGER)_IsPlayer, (STRING)_BulletType)
AND
DB_LLWEAPONEX_Pistols_Temp_HitResolved(_Cause, _Target, _Status)
THEN
NOT DB_LLWEAPONEX_Pistols_Temp_HitResolved(_Cause, _Target, _Status);

PROC
LLWEAPONEX_Pistols_Internal_OnHit_Default((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, 0)
THEN
//ApplyStatus(_Target, "LLWEAPONEX_PISTOL_DAMAGE_DEFAULT_SCALED", 0.0, 1, _Cause);
//LeaderLib_Ext_ExplodeProjectile(_Cause, _Target, "Projectile_LLWEAPONEX_Pistol_Damage_Default_Scaled");
LeaderLib_Explode_ExplodeProjectile(_Target,"Projectile_LLWEAPONEX_Pistol_Damage_Default_Scaled", _Cause, 0);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_Pistols:OnHit_Default] Exploding (Projectile_LLWEAPONEX_Pistol_Damage_Default_Scaled) (Not a Player) ");

PROC
LLWEAPONEX_Pistols_Internal_OnHit_Default((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, 1)
AND
NOT LeaderLib_Helper_QRY_WeaponIsEquipped(_Cause)
THEN
//ApplyStatus(_Target, "LLWEAPONEX_PISTOL_DAMAGE_DEFAULT_SCALED", 0.0, 1, _Cause);
//LeaderLib_Ext_ExplodeProjectile(_Cause, _Target, "Projectile_LLWEAPONEX_Pistol_Damage_Default_Scaled");
LeaderLib_Explode_ExplodeProjectile(_Target, "Projectile_LLWEAPONEX_Pistol_Damage_Default_Scaled", _Cause, 0);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_Pistols:OnHit_Default] Exploding (Projectile_LLWEAPONEX_Pistol_Damage_Default_Scaled) (No Weapon)");

PROC
LLWEAPONEX_Pistols_Internal_OnHit_Default((GUIDSTRING)_Target, (CHARACTERGUID)_Cause, 1)
AND
LeaderLib_Helper_QRY_WeaponIsEquipped(_Cause)
THEN
//ApplyStatus(_Target, "LLWEAPONEX_PISTOL_DAMAGE_DEFAULT_WEAPON", 0.0, 1, _Cause);
//LeaderLib_Ext_ExplodeProjectile(_Cause, _Target, "Projectile_LLWEAPONEX_Pistol_Damage_Default_Weapon");
LeaderLib_Explode_ExplodeProjectile(_Target, "Projectile_LLWEAPONEX_Pistol_Damage_Default_Weapon", _Cause, 0);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_Pistols:OnHit_Default] Exploding (Projectile_LLWEAPONEX_Pistol_Damage_Default_Weapon) (Weapon)");
//END_REGION

//REGION HIT_EVENTS
IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_PISTOL_SHOOT_HIT", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Pistols_Internal_OnBulletHit(_Target, _Source, 0);

IF
ItemStatusAttempt(_Target, "LLWEAPONEX_PISTOL_SHOOT_HIT", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Pistols_Internal_OnBulletHit(_Target, _Source, 1);

QRY
LLWEAPONEX_Pistols_QRY_GetHitSource((CHARACTERGUID)_HitSource)
AND
DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_Char, _HitSource)
THEN
LeaderLib_Timers_RestartCharacterCharacterTimer(_Char, "Timers_LLWEAPONEX_Pistols_ResetTargetHelperDummy", 50);
DB_LLWEAPONEX_Pistols_Temp_DamageSource(_HitSource, _Char);

QRY
LLWEAPONEX_Pistols_QRY_GetHitSource((CHARACTERGUID)_HitSource)
AND
NOT DB_LLWEAPONEX_Pistols_Temp_DamageSource(_HitSource, _)
AND
DB_LLWEAPONEX_Pistols_Temp_ConditionDummy(_HitSource, _Dummy)
THEN
DB_LLWEAPONEX_Pistols_Temp_DamageSource(_HitSource, _HitSource);
LeaderLib_Timers_RestartCharacterCharacterTimer(_HitSource, "Timers_LLWEAPONEX_Pistols_ResetTargetHelperDummy", 50);

QRY
LLWEAPONEX_Pistols_QRY_GetHitSource((CHARACTERGUID)_HitSource)
AND
NOT DB_LLWEAPONEX_Pistols_Temp_DamageSource(_HitSource, _)
THEN
DB_LLWEAPONEX_Pistols_Temp_DamageSource(_HitSource, _HitSource);

PROC
LLWEAPONEX_Pistols_Internal_OnBulletHit((GUIDSTRING)_Target, (CHARACTERGUID)_HitSource, 0)
AND
LLWEAPONEX_Pistols_QRY_GetHitSource(_HitSource)
AND
DB_LLWEAPONEX_Pistols_Temp_DamageSource(_HitSource, _Char)
THEN
NOT DB_LLWEAPONEX_Pistols_Temp_DamageSource(_HitSource, _Char);
CharacterCharacterSetEvent(_Char, (CHARACTERGUID)_Target, "LLWEAPONEX_Bullet_OnHit");

PROC
LLWEAPONEX_Pistols_Internal_OnBulletHit((GUIDSTRING)_Target, (CHARACTERGUID)_HitSource, 1)
AND
LLWEAPONEX_Pistols_QRY_GetHitSource(_HitSource)
AND
DB_LLWEAPONEX_Pistols_Temp_DamageSource(_HitSource, _Char)
THEN
NOT DB_LLWEAPONEX_Pistols_Temp_DamageSource(_HitSource, _Char);
CharacterItemSetEvent(_Char, (ITEMGUID)_Target, "LLWEAPONEX_Bullet_OnHit");
//END_REGION

//REGION PISTOL_AMMO
PROC
LLWEAPONEX_OnWeaponTypeUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "LLWEAPONEX_Pistol", 1)
THEN
LeaderLib_Tags_ClearPreservedTag(_Char, "LLWEAPONEX_Pistol_HasAmmo");

PROC
LLWEAPONEX_OnWeaponTypeEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "LLWEAPONEX_Pistol", 1)
AND
ObjectGetFlag(_Item, "LLWEAPONEX_Pistol_HasAmmo", _HasAmmo)
THEN
LLWEAPONEX_Pistols_Internal_CheckLastBulletType(_Char);
LLWEAPONEX_Pistols_OnPistolEquipped(_Char, _Item, _HasAmmo);

PROC
LLWEAPONEX_Pistols_Internal_CheckLastBulletType((CHARACTERGUID)_Char)
AND
GetVarString(_Char, "LLWEAPONEX_Pistol_LastBulletType", _BulletType)
AND
NOT DB_LLWEAPONEX_Pistols_BulletTemplates(_BulletType, _, _, _)
THEN
SetVarString(_Char, "LLWEAPONEX_Pistol_LastBulletType", "");

PROC
LLWEAPONEX_Pistols_Internal_CheckLastBulletType((CHARACTERGUID)_Char)
AND
NOT GetVarString(_Char, "LLWEAPONEX_Pistol_LastBulletType", _)
THEN
SetVarString(_Char, "LLWEAPONEX_Pistol_LastBulletType", "");

PROC
LLWEAPONEX_Pistols_OnPistolEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, 1)
THEN
LeaderLib_Tags_PreserveTag(_Char, "LLWEAPONEX_Pistol_HasAmmo");

PROC
LLWEAPONEX_Pistols_OnPistolEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, 0)
AND
GetVarString(_Char, "LLWEAPONEX_Pistol_LastBulletType", _LastBulletType)
THEN
LLWEAPONEX_Pistols_Internal_LoadPistolAmmo(_Char, _Item, _LastBulletType);

PROC
LLWEAPONEX_Pistols_Internal_LoadPistolAmmo((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_LastBulletType)
AND
DB_LLWEAPONEX_Pistols_BulletTemplates(_LastBulletType, _Template, _WeaponStatus, _ScaledStatus)
AND
NOT DB_LLWEAPONEX_Pistols_Temp_AutoLoadedBullets(_Char, _Item, _)
THEN
LLWEAPONEX_Pistols_Internal_LoadPistolAmmoTemplate(_Char, _Item, _Template, _LastBulletType);

PROC
LLWEAPONEX_Pistols_Internal_LoadPistolAmmo((CHARACTERGUID)_Char, (ITEMGUID)_Item, "")
AND
NOT DB_LLWEAPONEX_Pistols_Temp_AutoLoadedBullets(_Char, _Item, _)
AND
DB_LLWEAPONEX_Pistols_BulletTemplates(_BulletType, _Template, _WeaponStatus, _ScaledStatus)
AND
NOT DB_LLWEAPONEX_Pistols_Temp_AutoLoadedBullets(_Char, _Item, _)
THEN
LLWEAPONEX_Pistols_Internal_LoadPistolAmmoTemplate(_Char, _Item, _Template, _BulletType);

PROC
LLWEAPONEX_Pistols_Internal_LoadPistolAmmoTemplate((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template, (STRING)_BulletType)
AND
GetItemForItemTemplateInInventory(_Char, _Template, _Bullets)
AND
ObjectExists(_Bullets, 1)
THEN
DB_LLWEAPONEX_Pistols_Temp_AutoLoadedBullets(_Char, _Item, _Bullets);
LeaderLib_Tags_PreserveTag(_Char, "LLWEAPONEX_Pistol_HasAmmo");
SetVarString(_Char, "LLWEAPONEX_Pistol_BulletType", _BulletType);
SetVarString(_Char, "LLWEAPONEX_Pistol_LastBulletType", _BulletType);

IF
DB_LLWEAPONEX_Pistols_Temp_AutoLoadedBullets(_Char, _Item, _Bullets)
AND
IsTagged(_Bullets, "LLWEAPONEX_Infinite", 0)
THEN
LeaderLib_Helper_ModifyItemAmount(_Bullets, -1);

PROC
LLWEAPONEX_Pistols_Internal_LoadPistolAmmoTemplate((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template, (STRING)_BulletType)
AND
DB_LLWEAPONEX_Pistols_Temp_AutoLoadedBullets(_Char, _Item, _Bullets)
THEN
NOT DB_LLWEAPONEX_Pistols_Temp_AutoLoadedBullets(_Char, _Item, _Bullets);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"