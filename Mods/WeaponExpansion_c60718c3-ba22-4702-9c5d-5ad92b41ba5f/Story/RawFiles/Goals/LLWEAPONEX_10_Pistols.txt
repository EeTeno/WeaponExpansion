Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Pistols_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLWEAPONEX_Pistols_InitSettings()
THEN
SysClear("DB_LLWEAPONEX_Pistols_HandProjectiles", 5);
DB_LLWEAPONEX_Pistols_HandProjectiles("DWARF", 0, "Projectile_LLWEAPONEX_Pistol_Shoot_LeftHand", 600, "LLWEAPONEX_FX_PISTOL_EXPLOSION_LEFT");
DB_LLWEAPONEX_Pistols_HandProjectiles("DWARF", 1, "Projectile_LLWEAPONEX_Pistol_Shoot_RightHand", 600, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");
DB_LLWEAPONEX_Pistols_HandProjectiles("ELF", 0, "Projectile_LLWEAPONEX_Pistol_Shoot_RightHand", 250, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");
DB_LLWEAPONEX_Pistols_HandProjectiles("ELF", 1, "Projectile_LLWEAPONEX_Pistol_Shoot_RightHand", 250, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");
DB_LLWEAPONEX_Pistols_HandProjectiles("HUMAN", 0, "Projectile_LLWEAPONEX_Pistol_Shoot_LeftHand", 400, "LLWEAPONEX_FX_PISTOL_EXPLOSION_LEFT");
DB_LLWEAPONEX_Pistols_HandProjectiles("HUMAN", 1, "Projectile_LLWEAPONEX_Pistol_Shoot_LeftHand", 400, "LLWEAPONEX_FX_PISTOL_EXPLOSION_LEFT");
DB_LLWEAPONEX_Pistols_HandProjectiles("LIZARD", 0, "Projectile_LLWEAPONEX_Pistol_Shoot_RightHand", 800, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");
DB_LLWEAPONEX_Pistols_HandProjectiles("LIZARD", 1, "Projectile_LLWEAPONEX_Pistol_Shoot_RightHand", 800, "LLWEAPONEX_FX_PISTOL_EXPLOSION_RIGHT");

SysClear("DB_LLWEAPONEX_Pistols_ShootingStatus", 2);
DB_LLWEAPONEX_Pistols_ShootingStatus("EQ_LLWEAPONEX_Belt_Pistol_A_94838d55-d5e6-4115-b736-b8b26f321003", "LLWEAPONEX_FX_PISTOL_A_SHOOTING");
DB_LLWEAPONEX_Pistols_ShootingStatus("EQ_UNIQUE_LLWEAPONEX_Belt_Pistol_Captain_A_6e667d23-c8a6-46d6-b6ec-2aa6aa95241d", "LLWEAPONEX_FX_UNIQUE_PISTOL_A_CAPTAIN_SHOOTING");

SysClear("DB_LLWEAPONEX_Pistols_ShootingSkills", 1);
DB_LLWEAPONEX_Pistols_ShootingSkills("Target_LLWEAPONEX_Pistol_Shoot");
DB_LLWEAPONEX_Pistols_ShootingSkills("Target_LLWEAPONEX_Pistol_Shoot_Enemy");

PROC
LeaderLib_Mods_OnVersionChanged(c60718c3-ba22-4702-9c5d-5ad92b41ba5f, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
_LastVersion < 152436742
THEN
LLWEAPONEX_Pistols_InitSettings();
//END_REGION

//REGION PISTOL_SHOOT_FX
//Dwarves and elves don't have a good shoot -> stow animation like humans/lizards do
QRY
LLWEAPONEX_Pistols_QRY_NeedsToSheathe((CHARACTERGUID)_Char)
AND
LeaderLib_Helper_QRY_IsRace(_Char, "DWARF")
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Pistols_QRY_NeedsToSheathe((CHARACTERGUID)_Char)
AND
LeaderLib_Helper_QRY_IsRace(_Char, "ELF")
THEN
DB_NOOP(1);

IF
CharacterUsedSkill(_Char, "Target_LLWEAPONEX_Pistol_Shoot", _, _)
THEN
LeaderLib_Skills_CharacterUsedSkill(_Char, "Target_LLWEAPONEX_Pistol_Shoot");

IF
CharacterUsedSkill(_Char, "Target_LLWEAPONEX_Pistol_Shoot_Enemy", _, _)
THEN
LeaderLib_Skills_CharacterUsedSkill(_Char, "Target_LLWEAPONEX_Pistol_Shoot");

PROC
LeaderLib_Skills_CharacterUsedSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_Shoot")
AND
LLWEAPONEX_Pistols_QRY_NeedsToSheathe(_Char)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 350, "Timers_LLWEAPONEX_Pistols_PlayPistolFX", "LLWEAPONEX_Pistols_PlayPistolFX");

PROC
LeaderLib_Skills_CharacterUsedSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_Shoot")
AND
NOT LLWEAPONEX_Pistols_QRY_NeedsToSheathe(_Char)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 50, "Timers_LLWEAPONEX_Pistols_PlayPistolFX", "LLWEAPONEX_Pistols_PlayPistolFX");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_PlayPistolFX")
AND
CharacterGetEquippedItem(_Char, "Belt", _Pistol)
AND
GetTemplate(_Pistol, _Template)
AND
DB_LLWEAPONEX_Pistols_ShootingStatus(_Template, _Status)
THEN
ApplyStatus(_Char, _Status, 12.0, 1, _Char);

IF
SkillCast(_Char, "Target_LLWEAPONEX_Pistol_Shoot", _, _)
THEN
LeaderLib_Skills_CharacterCastSkill(_Char, "Target_LLWEAPONEX_Pistol_Shoot");

IF
SkillCast(_Char, "Target_LLWEAPONEX_Pistol_Shoot_Enemy", _, _)
THEN
LeaderLib_Skills_CharacterCastSkill(_Char, "Target_LLWEAPONEX_Pistol_Shoot");

PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_Shoot")
AND
//NOT LLWEAPONEX_Pistols_QRY_NeedsToSheathe(_Char)
//AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
DB_LLWEAPONEX_Pistols_HandProjectiles(_Race, _IsFemale, _ProjectileSkill, _HideHandPistolDelay, _ExplosionStatusFX)
AND
LeaderLib_Helper_QRY_IsRace(_Char, _Race)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, _HideHandPistolDelay, "Timers_LLWEAPONEX_Pistols_RemovePistolFX", "LLWEAPONEX_Pistols_RemovePistolFX");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_RemovePistolFX")
AND
DB_LLWEAPONEX_Pistols_ShootingStatus(_Template, _Status)
THEN
RemoveStatus(_Char, _Status);

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_RemovePistolFX")
AND
LLWEAPONEX_Pistols_QRY_NeedsToSheathe(_Char)
THEN
LeaderLib_Helper_ClearActionQueue(_Char);

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_StopAnimation")
THEN
LeaderLib_Helper_ClearActionQueue(_Char);
PlayAnimation(_Char, "skill_cast_ll_pistol_01_sheathe", "");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Pistols_StopAnimation")
AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
DB_LLWEAPONEX_Pistols_HandProjectiles(_Race, _IsFemale, _ProjectileSkill, _HideHandPistolDelay, _ExplosionStatusFX)
AND
LeaderLib_Helper_QRY_IsRace(_Char, _Race)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, _HideHandPistolDelay, "Timers_LLWEAPONEX_Pistols_RemovePistolFX", "LLWEAPONEX_Pistols_RemovePistolFX");

PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_Shoot")
AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
DB_LLWEAPONEX_Pistols_HandProjectiles(_Race, _IsFemale, _ProjectileSkill, _HideHandPistolDelay, _ExplosionStatusFX)
AND
LeaderLib_Helper_QRY_IsRace(_Char, _Race)
THEN
ApplyStatus(_Char, _ExplosionStatusFX, 0.2, 1, _Char);
LeaderLib_Timers_StartObjectTimer(_Char, 50, "Timers_LLWEAPONEX_Pistols_RemoveExplosionStatusFX", "LLWEAPONEX_Pistols_RemoveExplosionStatusFX");

IF
StoryEvent(_Char, "LLWEAPONEX_Pistols_RemoveExplosionStatusFX")
AND
DB_LLWEAPONEX_Pistols_HandProjectiles(_Race, _IsFemale, _ProjectileSkill, _HideHandPistolDelay, _ExplosionStatusFX)
AND
LeaderLib_Helper_QRY_HasStatus(_Char, _ExplosionStatusFX)
THEN
RemoveStatus(_Char, _ExplosionStatusFX);
//END_REGION

//REGION PISTOL_SHOOT_PROJECTILE
PROC
LLWEAPONEX_Pistols_Internal_ClearTargetData((CHARACTERGUID)_Char)
AND
DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _x, _y, _z)
THEN
NOT DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _x, _y, _z);

PROC
LLWEAPONEX_Pistols_Internal_ClearTargetData((CHARACTERGUID)_Char)
AND
DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _Target)
THEN
NOT DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _Target);

IF
CharacterUsedSkillAtPosition(_Char, _x, _y, _z, "Target_LLWEAPONEX_Pistol_Shoot", _, _)
THEN
DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _x, _y, _z);

IF
CharacterUsedSkillOnTarget(_Char, _Target, "Target_LLWEAPONEX_Pistol_Shoot", _, _)
THEN
DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _Target);

IF
CharacterUsedSkillAtPosition(_Char, _x, _y, _z, "Target_LLWEAPONEX_Pistol_Shoot_Enemy", _, _)
THEN
DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _x, _y, _z);

IF
CharacterUsedSkillOnTarget(_Char, _Target, "Target_LLWEAPONEX_Pistol_Shoot_Enemy", _, _)
THEN
DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _Target);

PROC
LeaderLib_Skills_CharacterCastSkill((CHARACTERGUID)_Char, "Target_LLWEAPONEX_Pistol_Shoot")
THEN
LLWEAPONEX_Pistols_Shoot(_Char);

PROC
LLWEAPONEX_Pistols_Shoot((CHARACTERGUID)_Char)
AND
DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _Target)
THEN
NOT DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _Target);
LLWEAPONEX_Pistols_Internal_ClearTargetData(_Char);
LLWEAPONEX_ShootPistolAtObject(_Char, _Target);

PROC
LLWEAPONEX_Pistols_Shoot((CHARACTERGUID)_Char)
AND
DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _x, _y, _z)
THEN
NOT DB_LLWEAPONEX_Pistols_Temp_ShootTarget(_Char, _x, _y, _z);
LLWEAPONEX_Pistols_Internal_ClearTargetData(_Char);
LLWEAPONEX_ShootPistolAtPosition(_Char, _x, _y, _z);

// For some reason, KNOCKED_DOWN types makes the target un-hittable by projectiles shot by scripts
QRY
LLWEAPONEX_Pistols_QRY_HitDirectly((GUIDSTRING)_Target)
AND
HasActiveStatus(_Target, "UNCONSCIOUS", _a)
AND
HasActiveStatus(_Target, "KNOCKED_DOWN", _b)
AND
HasActiveStatus(_Target, "InstantKnockdown", _c)
AND
HasActiveStatus(_Target, "SHOCKWAVE", _d)
AND
LeaderLib_Math_QRY_IsEqualToAny(1, _a, _b, _c, _d)
THEN
DB_NOOP(1);
//END_REGION

//REGION PISTOL_MASTERY_XP
IF
CharacterStatusApplied(_Target, "LLWEAPONEX_PISTOL_SHOOT_HIT", (CHARACTERGUID)_Source)
AND
CharacterIsPlayer(_Source, 1)
AND
ObjectGetFlag(_Source, "LLWEAPONEX_DisableWeaponMasteryExperience", 0)
THEN
LLWEAPONEX_WeaponMastery_AddExperienceForWeaponType(_Source, "LLWEAPONEX_Pistol", 1, _Target);

IF
ItemStatusChange(_Target, "LLWEAPONEX_PISTOL_SHOOT_HIT", (CHARACTERGUID)_Source)
AND
CharacterIsPlayer(_Source, 1)
AND
ObjectGetFlag(_Source, "LLWEAPONEX_DisableWeaponMasteryExperience", 0)
THEN
LLWEAPONEX_WeaponMastery_AddExperienceForWeaponType(_Source, "LLWEAPONEX_Pistol", 1, _Target);
//END_REGION

PROC
LLWEAPONEX_OnWeaponTypeEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "LLWEAPONEX_Pistol", 1)
THEN
DB_NOOP(1);

//REGION RELOADING
IF
SkillCast(_Char, "Target_LLWEAPONEX_Pistol_Shoot", _, _)
THEN
NRD_SkillSetCooldown(_Char, "Shout_LLWEAPONEX_Pistol_Reload", 0.0);
ApplyStatus(_Char, "LLWEAPONEX_PISTOL_EMPTY", 6.0, 1, _Char);

IF
SkillCast(_Char, "Target_LLWEAPONEX_Pistol_Shoot_Enemy", _, _)
THEN
NRD_SkillSetCooldown(_Char, "Shout_LLWEAPONEX_Pistol_Reload", 0.0);
ApplyStatus(_Char, "LLWEAPONEX_PISTOL_EMPTY", 6.0, 1, _Char);

IF
CharacterUsedSkill(_Char, "Shout_LLWEAPONEX_Pistol_Reload", _, _)
THEN
RemoveStatus(_Char, "LLWEAPONEX_PISTOL_EMPTY");

IF
CharacterUsedSkill(_Char, "Shout_LLWEAPONEX_Pistol_Reload", _, _)
AND
DB_LLWEAPONEX_Pistols_ShootingSkills(_Skill)
AND
CharacterHasSkill(_Char, _Skill, 1)
THEN
NRD_SkillSetCooldown(_Char, _Skill, 0.0);

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_PISTOL_EMPTY", _)
AND
DB_LLWEAPONEX_Pistols_ShootingSkills(_Skill)
AND
NRD_SkillGetCooldown(_Char, _Skill, _CD)
AND
_CD > 0.0
THEN
SetTag(_Char, "LLWEAPONEX_Pistol_CanReload");

IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_PISTOL_EMPTY", _)
AND
DB_LLWEAPONEX_Pistols_ShootingSkills(_Skill)
AND
NRD_SkillGetCooldown(_Char, _Skill, _CD)
AND
_CD <= 0.0
THEN
ClearTag(_Char, "LLWEAPONEX_Pistol_CanReload");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"