Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Katanas_InitSettings();
KBSECTION
PROC
LLWEAPONEX_Katanas_InitSettings()
THEN
DB_LLWEAPONEX_Katanas_ComboStatus(1, "LLWEAPONEX_KATANA_COMBO1");
DB_LLWEAPONEX_Katanas_ComboStatus(2, "LLWEAPONEX_KATANA_COMBO2");
DB_LLWEAPONEX_Katanas_ComboStatus(3, "LLWEAPONEX_KATANA_COMBO3");
DB_LLWEAPONEX_Katanas_ComboStatus(4, "LLWEAPONEX_KATANA_COMBO4");
DB_LLWEAPONEX_Katanas_ComboStatus(5, "LLWEAPONEX_KATANA_COMBO5");
DB_LLWEAPONEX_Katanas_ComboStatus(6, "LLWEAPONEX_KATANA_COMBO6");
DB_LLWEAPONEX_Katanas_ComboStatus(7, "LLWEAPONEX_KATANA_COMBO7");
DB_LLWEAPONEX_Katanas_ComboStatus(8, "LLWEAPONEX_KATANA_COMBO8");
DB_LLWEAPONEX_Katanas_ComboStatus(9, "LLWEAPONEX_KATANA_COMBO9");

DB_LLWEAPONEX_Katanas_ComboFinisher("LLWEAPONEX_KATANA_FINISHER_APPLY", "Default");
DB_LLWEAPONEX_Katanas_ComboFinisher("LLWEAPONEX_KATANA_FINISHER_IAIDO_APPLY", "Iaido");

IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_KATANA_HIT", (CHARACTERGUID)_Source)
THEN
CharacterCharacterSetEvent(_Target, _Source, "LLWEAPONEX_OnKatanaHit");

//REGION COMBO_TURN_ENDING
IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO1", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO1");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO2", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO2");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO3", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO3");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO4", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO4");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO5", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO5");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO6", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO6");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO7", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO7");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO8", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO8");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO9", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO9");

PROC
LLWEAPONEX_WeaponMastery_OnMasteryDeactivated((CHARACTERGUID)_Source, "LLWEAPONEX_Katana")
AND
DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, _Status)
THEN
NOT DB_LLWEAPONEX_Katanas_ListenForTurnEnding(_Source, _Target, _Status);
//END_REGION

//REGION FINISHER_COMBO
PROC
LLWEAPONEX_Katanas_SetFinisherHits((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_Hits, (STRING)_Status, (STRING)_ComboFinisher)
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _OtherSource, _OtherHits, _LastComboStatus, _LastComboFinisher)
THEN
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _OtherSource, _OtherHits, _LastComboStatus, _LastComboFinisher);

PROC
LLWEAPONEX_Katanas_SetFinisherHits((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_Hits, (STRING)_Status, (STRING)_ComboFinisher)
THEN
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_StartFinisher((CHARACTERGUID)_Target, (CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher)
THEN
LeaderLib_Timers_StartCharacterCharacterTimer(_Target, _Source, 50, "LLWEAPONEX_Timers_Katanas_StartFinisher", "LLWEAPONEX_Katanas_FinisherHit");

IF
CharacterCharacterEvent(_Target, _Source, "LLWEAPONEX_Katanas_FinisherHit")
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher)
AND
CharacterIsDead(_Target, _IsDead)
THEN
LLWEAPONEX_Katanas_Internal_OnFinisherHit(_Target, _Source, _Hits, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_Internal_OnFinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_Hits, (STRING)_Status, (STRING)_ComboFinisher)
AND
IntegerSubtract(_Hits, 1, _NextHits)
AND
CharacterIsDead(_Target, _IsDead)
THEN
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher);
LLWEAPONEX_Katanas_FinisherHit(_Target, _Source, _Hits, _Status, _ComboFinisher, _IsDead);
//CharacterCharacterSetEvent(_Target, _Source, "LLWEAPONEX_Katanas_OnFinisherHit");
LLWEAPONEX_Katanas_Internal_StartNextHit(_Target, _Source, _NextHits, _IsDead, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_Internal_StartNextHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_NextHits, 0, (STRING)_Status, (STRING)_ComboFinisher)
AND
_NextHits > 0
AND
String(_Target, _TargetStr)
AND
String(_Source, _SourceStr)
AND
IntegertoString(_NextHits, _NextHitsStr)
THEN
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _NextHits, _Status, _ComboFinisher);
LeaderLib_Timers_StartCharacterCharacterTimer(_Target, _Source, 275, "LLWEAPONEX_Timers_Katanas_NextFinisher", "LLWEAPONEX_Katanas_FinisherHit");
//LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_Katanas:Internal:StartNextHit] Starting timer again for target [",_TargetStr,"] and source [",_SourceStr,"]. Hits [",_NextHitsStr,"].");

PROC
LLWEAPONEX_Katanas_Internal_StartNextHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_NextHits, (INTEGER)_IsDead, (STRING)_Status, (STRING)_ComboFinisher)
AND
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _, _Status, _ComboFinisher)
THEN
LLWEAPONEX_Katanas_FinisherDone(_Target, _Source, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_FinisherDone((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_Status, (STRING)_ComboFinisher)
THEN
ObjectClearFlag(_Target, "LLWEAPONEX_Katanas_DisableCombo", 0);
RemoveStatus(_Target, _Status);
//END_REGION

//REGION FINISHER_HITS
PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, (STRING)_ComboFinisher, (INTEGER)_IsDead)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, "Default", 0)
THEN
ApplyStatus(_Target, "LLWEAPONEX_KATANA_FINISHER_DAMAGE", 0.0, 0, _Source);

PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, "Iaido", 0)
AND
NOT LeaderLib_Math_QRY_IsEqualToAny(_HitNumber, 3, 6, 9)
THEN
ApplyStatus(_Target, "LLWEAPONEX_KATANA_FINISHER_IAIDO_DAMAGE", 0.0, 0, _Source);

PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, "Iaido", 0)
AND
LeaderLib_Math_QRY_IsEqualToAny(_HitNumber, 3, 6, 9)
THEN
ApplyStatus(_Target, "LLWEAPONEX_RUPTURE", 0.0, 0, _Source);
//END_REGION

//REGION ANIMATION_OVERRIDE
PROC
LLWEAPONEX_WeaponMastery_OnWeaponEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, "LLWEAPONEX_Katana")
AND
IsTagged(_Item, "LLWEAPONEX_TwoHanded", 1)
THEN
CharacterSetAnimationSetOverride(_Player, "LLWEAPONEX_Override1");

PROC
LLWEAPONEX_WeaponMastery_OnWeaponUnEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, "LLWEAPONEX_Katana")
AND
IsTagged(_Item, "LLWEAPONEX_TwoHanded", 1)
THEN
CharacterSetAnimationSetOverride(_Player, "");

PROC
LLWEAPONEX_WeaponMastery_OnWeaponEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, "LLWEAPONEX_Rapier")
AND
IsTagged(_Item, "LLWEAPONEX_TwoHanded", 0)
THEN
CharacterSetAnimationSetOverride(_Player, "LLWEAPONEX_Override1");

PROC
LLWEAPONEX_WeaponMastery_OnWeaponUnEquipped((CHARACTERGUID)_Player, (ITEMGUID)_Item, "LLWEAPONEX_Rapier")
AND
IsTagged(_Item, "LLWEAPONEX_TwoHanded", 0)
THEN
CharacterSetAnimationSetOverride(_Player, "");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"
