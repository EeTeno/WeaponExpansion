Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Katanas_InitSettings();
KBSECTION
PROC
LLWEAPONEX_Katanas_InitSettings()
THEN
DB_LLWEAPONEX_Katanas_ComboStatus(1, "LLWEAPONEX_KATANA_COMBO1");
DB_LLWEAPONEX_Katanas_ComboStatus(2, "LLWEAPONEX_KATANA_COMBO2");
DB_LLWEAPONEX_Katanas_ComboStatus(3, "LLWEAPONEX_KATANA_COMBO3");
DB_LLWEAPONEX_Katanas_ComboStatus(4, "LLWEAPONEX_KATANA_COMBO4");
DB_LLWEAPONEX_Katanas_ComboStatus(5, "LLWEAPONEX_KATANA_COMBO5");
DB_LLWEAPONEX_Katanas_ComboStatus(6, "LLWEAPONEX_KATANA_COMBO6");
DB_LLWEAPONEX_Katanas_ComboStatus(7, "LLWEAPONEX_KATANA_COMBO7");
DB_LLWEAPONEX_Katanas_ComboStatus(8, "LLWEAPONEX_KATANA_COMBO8");
DB_LLWEAPONEX_Katanas_ComboStatus(9, "LLWEAPONEX_KATANA_COMBO9");

DB_LLWEAPONEX_Katanas_ComboFinisher("LLWEAPONEX_KATANA_FINISHER_APPLY", "Default");
DB_LLWEAPONEX_Katanas_ComboFinisher("LLWEAPONEX_KATANA_FINISHER_IAIDO_APPLY", "Iaido");
DB_LLWEAPONEX_Katanas_ComboFinisher("LLWEAPONEX_KATANA_FINISHER_VANQUISHER_APPLY", "Vanquisher");

IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_KATANA_HIT", (CHARACTERGUID)_Source)
THEN
CharacterCharacterSetEvent(_Target, _Source, "LLWEAPONEX_OnKatanaHit");

IF
ItemStatusChange(_Target, "LLWEAPONEX_KATANA_HIT", (CHARACTERGUID)_Source)
THEN
CharacterItemSetEvent(_Source, _Target, "LLWEAPONEX_OnKatanaHit");

//REGION COMBO_TURN_ENDING
/*
DB_LLWEAPONEX_Statuses_ListenForTurnEnding is cleared when the stored status is removed.
Since combos share the same stackID and have appropriate priorities, higher combo statuses will clear the previous entry.
*/

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO1", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO1");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO2", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO2");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO3", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO3");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO4", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO4");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO5", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO5");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO6", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO6");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO7", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO7");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO8", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO8");

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_KATANA_COMBO9", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding(_Source, _Target, "LLWEAPONEX_KATANA_COMBO9");

PROC
LLWEAPONEX_WeaponMastery_OnMasteryDeactivated((CHARACTERGUID)_Source, "LLWEAPONEX_Katana")
AND
DB_LLWEAPONEX_Statuses_ListenForTurnEnding(_Source, _Target, _Status)
AND
DB_LLWEAPONEX_Katanas_ComboStatus(_Hits, _Status)
THEN
NOT DB_LLWEAPONEX_Statuses_ListenForTurnEnding(_Source, _Target, _Status);
LLWEAPONEX_Katanas_ComboRemoved(_Source, (CHARACTERGUID)_Target);
//END_REGION

//REGION FINISHER_COMBO
PROC
LLWEAPONEX_Katanas_SetFinisherHits((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_Hits, (STRING)_Status, (STRING)_ComboFinisher)
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _OtherSource, _OtherHits, _LastComboStatus, _LastComboFinisher)
THEN
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _OtherSource, _OtherHits, _LastComboStatus, _LastComboFinisher);

PROC
LLWEAPONEX_Katanas_SetFinisherHits((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_Hits, (STRING)_Status, (STRING)_ComboFinisher)
THEN
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_StartFinisher((CHARACTERGUID)_Target, (CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher)
THEN
DB_LLWEAPONEX_Katanas_Temp_FinisherActive(_Source, _Target, _Status, _ComboFinisher);
LeaderLib_Timers_StartCharacterCharacterTimer(_Target, _Source, 50, "LLWEAPONEX_Timers_Katanas_StartFinisher", "LLWEAPONEX_Katanas_FinisherHit");

IF
CharacterCharacterEvent(_Target, _Source, "LLWEAPONEX_Katanas_FinisherHit")
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher)
AND
CharacterIsDead(_Target, _IsDead)
THEN
LLWEAPONEX_Katanas_Internal_OnFinisherHit(_Target, _Source, _Hits, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_Internal_OnFinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_Hits, (STRING)_Status, (STRING)_ComboFinisher)
AND
IntegerSubtract(_Hits, 1, _NextHits)
AND
CharacterIsDead(_Target, _IsDead)
THEN
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher);
LLWEAPONEX_Katanas_FinisherHit(_Target, _Source, _Hits, _Status, _ComboFinisher, _IsDead);
//CharacterCharacterSetEvent(_Target, _Source, "LLWEAPONEX_Katanas_OnFinisherHit");
LLWEAPONEX_Katanas_Internal_StartNextHit(_Target, _Source, _NextHits, _IsDead, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_Internal_StartNextHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_NextHits, 0, (STRING)_Status, (STRING)_ComboFinisher)
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherActive(_Source, _Target, _Status, _ComboFinisher)
AND
_NextHits > 0
AND
String(_Target, _TargetStr)
AND
String(_Source, _SourceStr)
AND
IntegertoString(_NextHits, _NextHitsStr)
THEN
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _NextHits, _Status, _ComboFinisher);
LeaderLib_Timers_StartCharacterCharacterTimer(_Target, _Source, 275, "LLWEAPONEX_Timers_Katanas_NextFinisher", "LLWEAPONEX_Katanas_FinisherHit");
//LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_Katanas:Internal:StartNextHit] Starting timer again for target [",_TargetStr,"] and source [",_SourceStr,"]. Hits [",_NextHitsStr,"].");

QRY
LLWEAPONEX_Katanas_Internal_QRY_FinisherDone((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_Status, (STRING)_ComboFinisher)
AND
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _, _Status, _ComboFinisher)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Katanas_Internal_QRY_FinisherDone((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_Status, (STRING)_ComboFinisher)
AND
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherActive(_Source, _Target, _Status, _ComboFinisher)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_Katanas_Internal_StartNextHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_NextHits, (INTEGER)_IsDead, (STRING)_Status, (STRING)_ComboFinisher)
AND
LLWEAPONEX_Katanas_Internal_QRY_FinisherDone(_Target, _Source, _Status, _ComboFinisher)
THEN
LLWEAPONEX_Katanas_FinisherDone(_Target, _Source, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_FinisherDone((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_Status, (STRING)_ComboFinisher)
AND
CharacterIsDead(_Target, 0)
THEN
RemoveStatus(_Target, _Status);

PROC
LLWEAPONEX_Katanas_FinisherDone((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_Status, (STRING)_ComboFinisher)
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher)
THEN
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_FinisherDone((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_Status, (STRING)_ComboFinisher)
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherActive(_Source, _Target, _Status, _ComboFinisher)
THEN
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherActive(_Source, _Target, _Status, _ComboFinisher);

PROC
LLWEAPONEX_Katanas_FinisherDone((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_Status, (STRING)_ComboFinisher)
THEN
ObjectClearFlag(_Target, "LLWEAPONEX_Katanas_DisableCombo", 0);
//END_REGION

//REGION FINISHER_HITS
PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, (STRING)_ComboFinisher, (INTEGER)_IsDead)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, "Default", 0)
THEN
ApplyStatus(_Target, "LLWEAPONEX_KATANA_FINISHER_DAMAGE", 0.0, 0, _Source);

PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, "Iaido", 0)
AND
NOT LeaderLib_Math_QRY_IsEqualToAny(_HitNumber, 3, 6, 9)
THEN
ApplyStatus(_Target, "LLWEAPONEX_KATANA_FINISHER_IAIDO_DAMAGE", 0.0, 0, _Source);

PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, "Iaido", 0)
AND
LeaderLib_Math_QRY_IsEqualToAny(_HitNumber, 3, 6, 9)
THEN
ApplyStatus(_Target, "LLWEAPONEX_RUPTURE", 0.0, 0, _Source);

PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, "Vanquisher", 0)
THEN
ApplyStatus(_Target, "LLWEAPONEX_KATANA_FINISHER_VANQUISHER_DAMAGE", 0.0, 0, _Source);

PROC
LLWEAPONEX_Katanas_FinisherHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_HitNumber, (STRING)_Status, "Vanquisher", 1)
AND
DB_LLWEAPONEX_Katanas_Temp_FinisherActive(_Source, _Target, _Status, _ComboFinisher)
THEN
NOT DB_LLWEAPONEX_Katanas_Temp_FinisherActive(_Source, _Target, _Status, _ComboFinisher);
LLWEAPONEX_Katanas_Internal_OnVanquisherFinisher(_Source, _Target, _HitNumber);

PROC
LLWEAPONEX_Katanas_Internal_OnVanquisherFinisher((CHARACTERGUID)_Source, (CHARACTERGUID)_Target, (INTEGER)_HitNumber)
AND
CombatGetIDForCharacter(_Source, _CombatID)
AND
DB_CombatCharacters(_Char, _CombatID)
AND
_Char != _Source
AND
_Char != _Target
AND
CharacterIsEnemy(_Char, _Source, 1)
AND
GetDistanceTo(_Target, _Char, _Dist)
AND
_Dist <= 6.0
AND
DB_LLWEAPONEX_Katanas_ComboStatus(_HitNumber, _Status)
THEN
ApplyStatus(_Char, _Status, 6.0, 0, _Source);
//END_REGION

//REGION VANQUISH
PROC
LLWEAPONEX_Statuses_TurnEnded_OnStatusRemoved((CHARACTERGUID)_Source, (GUIDSTRING)_Target, (STRING)_Status)
AND
DB_LLWEAPONEX_Katanas_ComboStatus(_Hits, _Status)
AND
ObjectIsCharacter((CHARACTERGUID)_Target, 1)
THEN
LLWEAPONEX_Katanas_ComboRemoved(_Source, _Target);

PROC
LLWEAPONEX_Katanas_ComboRemoved((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
NOT DB_LLWEAPONEX_Statuses_ListenForTurnEnding(_Source, _, _)
THEN
ClearTag(_Source, "LLWEAPONEX_Blademaster_Target_Available");

IF
SkillCast(_Source, "Shout_LLWEAPONEX_Katana_VanquishersPath", _, _)
THEN
LLWEAPONEX_Katanas_Internal_VanquishNext(_Source);

PROC
LLWEAPONEX_Katanas_Internal_VanquishNext((CHARACTERGUID)_Source)
AND
DB_LLWEAPONEX_Statuses_ListenForTurnEnding(_Source, _Target, _Status)
AND
NOT DB_LLWEAPONEX_Katanas_Temp_VanquisherTeleporting(_Source, _)
THEN
DB_LLWEAPONEX_Katanas_Temp_VanquisherTeleporting(_Source, _Target);
TeleportTo(_Source, _Target, "LLWEAPONEX_Katanas_Vanquisher_Teleported", 0, 1, 0);

IF
StoryEvent((CHARACTERGUID)_Source, "LLWEAPONEX_Katanas_Vanquisher_Teleported")
AND
DB_LLWEAPONEX_Katanas_Temp_VanquisherTeleporting(_Source, _Target)
THEN
NOT DB_LLWEAPONEX_Katanas_Temp_VanquisherTeleporting(_Source, _Target);
LeaderLib_Helper_ClearActionQueue(_Source);
CharacterUseSkill(_Source, "Target_LLWEAPONEX_Katana_VanquishersPath_Hit", _Target, 1, 1, 1);
ProcObjectTimer(_Source, "LLWEAPONEX_Timers_Katanas_ContinueVanquisher", 2000);

IF
SkillCast(_Source, "Target_LLWEAPONEX_Katana_VanquishersPath_Hit", _, _)
THEN
ProcObjectTimerCancel(_Source, "LLWEAPONEX_Timers_Katanas_ContinueVanquisher");
ProcObjectTimer(_Source, "LLWEAPONEX_Timers_Katanas_ContinueVanquisher", 500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Source, "LLWEAPONEX_Timers_Katanas_ContinueVanquisher")
THEN
LLWEAPONEX_Katanas_Internal_VanquishNext(_Source);
//END_REGION

//REGION COMBO_FINISHER_STATUS_EVENTS
IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_KATANA_FINISHER_APPLY", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katana_Internal_OnComboFinisherApplied(_Source, _Target, "LLWEAPONEX_KATANA_FINISHER_APPLY");

IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_KATANA_FINISHER_IAIDO_APPLY", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katana_Internal_OnComboFinisherApplied(_Source, _Target, "LLWEAPONEX_KATANA_FINISHER_IAIDO_APPLY");

IF
CharacterStatusAttempt(_Target, "LLWEAPONEX_KATANA_FINISHER_VANQUISHER_APPLY", (CHARACTERGUID)_Source)
THEN
LLWEAPONEX_Katana_Internal_OnComboFinisherApplied(_Source, _Target, "LLWEAPONEX_KATANA_FINISHER_VANQUISHER_APPLY");
//END_REGION

//REGION COMBO_FINISHER
PROC
LLWEAPONEX_Katana_Internal_OnComboFinisherApplied((CHARACTERGUID)_Source, (CHARACTERGUID)_Target, (STRING)_ComboFinisherStatus)
AND
DB_LLWEAPONEX_Katanas_ComboFinisher(_ComboFinisherStatus, _ComboFinisher)
AND
DB_LLWEAPONEX_Katanas_ComboStatus(_Hits, _Status)
AND
ObjectGetFlag(_Target, "LLWEAPONEX_Katanas_DisableCombo", 0)
AND
HasActiveStatus(_Target, _Status, 1)
THEN
ObjectSetFlag(_Target, "LLWEAPONEX_Katanas_DisableCombo", 0);
LLWEAPONEX_Katanas_SetFinisherHits(_Target, _Source, _Hits, _Status, _ComboFinisher);
LLWEAPONEX_Katanas_StartFinisher(_Target, _Source);

PROC
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding((CHARACTERGUID)_Source, (GUIDSTRING)_Target, (STRING)_Status)
THEN
LLWEAPONEX_Statuses_ListenForTurnEnding(_Source, _Target, _Status);

PROC
LLWEAPONEX_Katanas_Internal_ListenForTurnEnding((CHARACTERGUID)_Source, (GUIDSTRING)_Target, (STRING)_Status)
AND
IsTagged(_Source, "LLWEAPONEX_Blademaster_Target_Available", 0)
THEN
SetTag(_Source, "LLWEAPONEX_Blademaster_Target_Available");

//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"
