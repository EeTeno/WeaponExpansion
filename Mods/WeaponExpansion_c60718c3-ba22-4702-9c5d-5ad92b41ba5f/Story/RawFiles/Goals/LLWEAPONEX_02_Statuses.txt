Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Statuses_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLWEAPONEX_Statuses_InitSettings()
THEN
DB_LLWEAPONEX_Statuses_TormentDebuff(0, 100, "LLWEAPONEX_CORPSE_EXPLOSION_EXPLODE", 0.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(1, 100, "LLWEAPONEX_MAGIC_CRIPPLED_CHECK", 6.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(101, 166, "CURSED", 6.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(167, 201, "ENTANGLED", 6.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(202, 239, "DISEASED", 6.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(240, 320, "SLOWED", 6.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(321, 341, "WEAK", 6.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(342, 468, "LLWEAPONEX_RUPTURE", 0.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(469, 697, "LLWEAPONEX_SOUL_BURN_PROC", 0.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(698, 899, "LLWEAPONEX_MAGIC_BLEEDING_CHECK", 6.0);
DB_LLWEAPONEX_Statuses_TormentDebuff(900, 999, "LLWEAPONEX_MAGIC_KNOCKDOWN_CHECK", 6.0);
//END_REGION

//REGION TARRED_BURNING_BYPASS
IF
CharacterStatusAttempt(_Character, "BURNING", _Source)
AND
HasActiveStatus(_Character, "LLWEAPONEX_TARRED", 1)
AND
HasActiveStatus(_Character, "BURNING", 0)
AND
NOT DB_LLWEAPONEX_Statuses_Temp_BurningBypassedArmor(_Character)
AND
GetStatusTurns(_Character, "BURNING", _Turns)
AND
Real(_Turns, _TurnsR)
AND
RealProduct(_TurnsR, 6.0, _Duration)
THEN
ApplyStatus(_Character, "BURNING", _Duration, 1, _Source);
DB_LLWEAPONEX_Statuses_Temp_BurningBypassedArmor(_Character);

IF
CharacterStatusAttempt(_Character, "BURNING", _Source)
AND
HasActiveStatus(_Character, "LLWEAPONEX_TARRED", 1)
AND
HasActiveStatus(_Character, "BURNING", 0)
AND
NOT DB_LLWEAPONEX_Statuses_Temp_BurningBypassedArmor(_Character)
AND
NOT GetStatusTurns(_Character, "BURNING", _)
THEN
ApplyStatus(_Character, "BURNING", 12.0, 1, _Source);
DB_LLWEAPONEX_Statuses_Temp_BurningBypassedArmor(_Character);

IF
CharacterStatusApplied(_Character, "BURNING", _Source)
AND
DB_LLWEAPONEX_Statuses_Temp_BurningBypassedArmor(_Character)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_BurningBypassedArmor(_Character);
//END_REGION

//REGION CHAOS_SURFACE_STATUSES
IF
CharacterStatusAttempt(_Character, "LLWEAPONEX_RANDOM_SURFACE_SMALL", _)
AND
GetStatusTurns(_Character, "LLWEAPONEX_RANDOM_SURFACE_SMALL", _Turns)
AND
_Turns > 0
AND
GetPosition(_Character, _x, _y, _z)
AND
Real(_Turns, _TurnsR)
AND
RealProduct(_TurnsR, 6.0, _Lifetime)
AND
LeaderLib_Array_QRY_Random("LLWEAPONEX_ChaosSurfaces")
AND
DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface)
THEN
NOT DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface);
CreateSurfaceAtPosition(_x, _y, _z, _Surface, 1.0, _Lifetime);
RemoveStatus(_Character, "LLWEAPONEX_RANDOM_SURFACE_SMALL");

IF
CharacterStatusAttempt(_Character, "LLWEAPONEX_RANDOM_SURFACE_SMALL", _)
AND
NOT GetStatusTurns(_Character, "LLWEAPONEX_RANDOM_SURFACE_SMALL", _)
AND
GetPosition(_Character, _x, _y, _z)
AND
LeaderLib_Array_QRY_Random("LLWEAPONEX_ChaosSurfaces")
AND
DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface)
THEN
NOT DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface);
CreateSurfaceAtPosition(_x, _y, _z, _Surface, 1.0, 12.0);
RemoveStatus(_Character, "LLWEAPONEX_RANDOM_SURFACE_SMALL");

IF
CharacterStatusRemoved(_Character, "HIT", _)
AND
HasActiveStatus(_Character, "LLWEAPONEX_CHAOS_POWER", 1)
AND
LeaderLib_Array_QRY_Random("LLWEAPONEX_ChaosSurfaces")
AND
DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface)
THEN
NOT DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface);
CreateSurface(_Character, _Surface, 2.0, 12.0);
//END_REGION

//REGION DEATH_SENTENCE
IF
CharacterStatusApplied(_Enemy, "LLWEAPONEX_DEATH_SENTENCE", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source);

IF
CharacterStatusRemoved(_Enemy, "LLWEAPONEX_DEATH_SENTENCE", _)
AND
CharacterIsDead(_Enemy, 0)
AND
DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source);

IF
CharacterDying(_Enemy)
AND
DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source)
AND
GetPosition(_Enemy, _x, _y, _z)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_SourceJar_Death_Impact_01", _x, _y, _z);
PlayEffectAtPosition("RS3_FX_GP_Impacts_Ghost_01", _x, _y, _z);

IF
CharacterDied(_Enemy)
AND
DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source)
THEN
LLWEAPONEX_Statuses_CreateRevenant(_Enemy, _Source, "LLWEAPONEX_Revenant_Base_bb15f97e-b6bf-4648-9190-71b42a7744c4", " (Revenant)", "LLWEAPONEX_REVENANT", 12.0);
NOT DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source);

IF
CharacterStatusRemoved(_Character, "LLWEAPONEX_DEATH_SENTENCE", _)
AND
CharacterIsDead(_Character, 0)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Character, _Revenant, _Source, "LLWEAPONEX_REVENANT")
THEN
LLWEAPONEX_Statuses_KillRevenant(_Revenant);
//END_REGION

//REGION REVENANTS_CREATE
PROC
LLWEAPONEX_Statuses_CreateRevenant((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_Template, (STRING)_NameSuffix, (STRING)_Status, (REAL)_Duration)
AND
GetPosition(_Target, _x, _y, _z)
AND
TemporaryCharacterCreateAtPosition(_x, _y, _z, _Template, 0, _Revenant)
AND
CharacterGetLevel(_Target, _Level)
AND
CharacterGetDisplayName(_Target, _, _Name)
AND
StringConcatenate(_Name, _NameSuffix, _RevenantName)
AND
FindValidPosition(_x, _y, _z, 6.0, _Revenant, _tx, _ty, _tz)
THEN
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Target, _Revenant, _Source, _Status);
SetOnStage(_Revenant, 0);
CharacterSetDetached(_Revenant, 1);

//CharacterTransformAppearanceTo(_Revenant, _Target, 1, 1);
//CharacterTransformFromCharacter(_Revenant, _Target, 0, 1, 0, 1, 1, 0, 0);
/*
CharacterTransformFromCharacter((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (INTEGER)_ReplaceScripts, (INTEGER)_ReplaceScale, (INTEGER)_ReplaceStats, (INTEGER)_ReplaceEquipment, (INTEGER)_ReplaceSkills, (INTEGER)_UseCustomLooks, (INTEGER)_ReleasePlayerData)
*/
//CharacterTransformFromCharacter(_Revenant, _Target, 0, 1, 0, 0, _ReplaceSkills, 0, 0);
CharacterTransformAppearanceTo(_Revenant, _Target, 0, 1);

//CharacterCloneSkillsTo(_Target, _Revenant, 0);
CharacterLevelUpTo(_Revenant, _Level);

SetCanJoinCombat(_Revenant, 0);
SetCanFight(_Revenant, 0);
SetFaction(_Revenant, "Good NPC");
SetRelationIndivFactionToPlayers(_Revenant, 100);

ApplyStatus(_Revenant, _Status, _Duration, 1, _Source);

CharacterSetHitpointsPercentage(_Revenant, 100.0);
TeleportToPosition(_Revenant, _tx, _ty, _tz, "LLWEAPONEX_Events_Revenant_SetAttached", 0, 1);

DB_LLWEAPONEX_Statuses_Temp_WaitForFinished(_Revenant, _Target, _Source, _Status, _RevenantName);

IF
StoryEvent((CHARACTERGUID)_Revenant, "LLWEAPONEX_Events_Revenant_SetAttached")
AND
DB_LLWEAPONEX_Statuses_Temp_WaitForFinished(_Revenant, _Target, _Source, _Status, _RevenantName)
THEN
CharacterSetCustomName(_Revenant, _RevenantName);
LeaderLib_Helper_CopyEquipment(_Revenant, _Target);
SetOnStage(_Revenant, 1);
CharacterSetDetached(_Revenant, 0);
PlayEffect(_Revenant, "RS3_FX_Char_Ghosts_Teleport_in_01", "");
LeaderLib_Timers_StartObjectTimer(_Revenant, 500, "LLWEAPONEX_Timers_RevenantReadyTimer", "LLWEAPONEX_Events_RevenantReady");

IF
StoryEvent((CHARACTERGUID)_Revenant, "LLWEAPONEX_Events_RevenantReady")
AND
DB_LLWEAPONEX_Statuses_Temp_WaitForFinished(_Revenant, _Target, _Source, _Status, _RevenantName)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_WaitForFinished(_Revenant, _Target, _Source, _Status, _RevenantName);
SetCanJoinCombat(_Revenant, 1);
SetCanFight(_Revenant, 1);
LLWEAPONEX_Statuses_OnRevenantCreated(_Revenant, _Target, _Source, _Status);

PROC
LLWEAPONEX_Statuses_OnRevenantCreated((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, "LLWEAPONEX_REVENANT")
AND
CharacterIsInCombat(_Revenant, 0)
AND
GetClosestAlivePlayer(_Revenant, _Player, _Dist)
AND
CharacterIsInCombat(_Player, 0)
THEN
ProcCharacterFollowCharacter(_Revenant, _Player);

/*
IF
StoryEvent((CHARACTERGUID)_Revenant, "LLWEAPONEX_Events_RevenantReady")
AND
CharacterGetHostCharacter(_Host)
THEN
//CharacterMakePlayer(_Revenant, _Host);
TeleportTo(_Revenant, _Host, "", 0, 1, 1);
*/

IF
CharacterStatusApplied(_Character, "RESURRECT", _)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Character, _Revenant, _Source, _Status)
THEN
LLWEAPONEX_Statuses_KillRevenant(_Revenant);

IF
CharacterStatusRemoved(_Revenant, _Status, _)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Enemy, _Revenant, _Source, _Status)
THEN
LLWEAPONEX_Statuses_KillRevenant(_Revenant);

PROC
LLWEAPONEX_Statuses_KillRevenant((CHARACTERGUID)_Revenant)
AND
GetPosition(_Revenant, _x, _y, _z)
THEN
SetOnStage(_Revenant, 0);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_GhostDissipate_01", _x, _y, _z);
CharacterDieImmediate(_Revenant, 0, "LifeTime", _Revenant);

IF
CharacterDied(_Revenant)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Target, _Revenant, _Source, _Status)
THEN
LLWEAPONEX_Statuses_OnRevenantDied(_Revenant, _Target, _Source, _Status);

PROC
LLWEAPONEX_Statuses_OnRevenantDied((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, "LLWEAPONEX_REVENANT")
AND
GetPosition(_Revenant, _x, _y, _z)
THEN
TransformSurfaceAtPosition(_x, _y, _z, "Vaporize", "Ground", 1.0, 12.0, _Source);
LLWEAPONEX_Statuses_RemoveRevenant(_Revenant);

PROC
LLWEAPONEX_Statuses_RemoveRevenant((CHARACTERGUID)_Revenant)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Enemy, _Revenant, _Source, _Status)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_Revenants(_Enemy, _Revenant, _Source, _Status);
RemoveTemporaryCharacter(_Revenant);
//END_REGION

//REGION BEAMS
IF
CharacterStatusApplied(_Character, "LLWEAPONEX_RUNEBLADE_CONTAMINATION_CHECK", (CHARACTERGUID)_AuraCharacter)
THEN
PlayBeamEffect(_AuraCharacter, _Character, "RS3_FX_GP_Beams_AcidBeam_01", "Dummy_BodyFX", "Dummy_BodyFX");

/*
IF
CharacterStatusApplied(_Character, "LLWEAPONEX_RUNEBLADE_CONTAMINATION_CHECK", (CHARACTERGUID)_AuraCharacter)
THEN
DB_LLWEAPONEX_Statuses_Temp_AuraBeamEffects(_Character, _AuraCharacter, "POISONED", "RS3_FX_GP_Beams_AcidBeam_01");

IF
CharacterStatusApplied(_Character, _Status, _)
AND
DB_LLWEAPONEX_Statuses_Temp_AuraBeamEffects(_Character, _AuraCharacter, _Status, _BeamEffect)
THEN
PlayBeamEffect(_AuraCharacter, _Target, _BeamEffect, "Dummy_BodyFX", "Dummy_BodyFX");
NOT DB_LLWEAPONEX_Statuses_Temp_AuraBeamEffects(_Character, _AuraCharacter, _Status, _BeamEffect);
*/
//END_REGION

//REGION RUNEBLADE_SHOCKED
IF
CharacterStatusApplied(_Character, "LLWEAPONEX_SHOCKED_RESISTANCE_RUNEBLADE_CHECK", _)
THEN
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_ShockedResistanceCheck", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_ShockedResistanceCheck")
AND
NOT LeaderLib_Helper_QRY_HasStatus(_Character, "LLWEAPONEX_SHOCKED_RESISTANCE_RUNEBLADE")
AND
LeaderLib_Helper_QRY_HasStatus(_Character, "STUNNED")
AND
GetStatusTurns(_Character, "LLWEAPONEX_SHOCKED_RESISTANCE_RUNEBLADE_CHECK", _Turns)
AND
Real(_Turns, _TurnsR)
AND
RealProduct(_TurnsR, 6.0, _Duration)
THEN
ApplyStatus(_Character, "LLWEAPONEX_SHOCKED_RESISTANCE_RUNEBLADE", _Duration, 0, _Character);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_ShockedResistanceCheck")
AND
LeaderLib_Helper_QRY_HasStatus(_Character, "LLWEAPONEX_SHOCKED_RESISTANCE_RUNEBLADE_CHECK")
THEN
RemoveStatus(_Character, "LLWEAPONEX_SHOCKED_RESISTANCE_RUNEBLADE_CHECK");
//END_REGION

//REGION TORMENT
IF
CharacterStatusApplied(_Target, "LLWEAPONEX_TORMENT", (CHARACTERGUID)_Caster)
AND
LeaderLib_Random_QRY(999)
AND
DB_LeaderLib_Random(_Roll)
AND
_Roll > 0
AND
DB_LLWEAPONEX_Statuses_TormentDebuff(_MinRoll, _MaxRoll, _Status, _Duration)
AND
_Roll >= _MinRoll
AND
_Roll <= _MaxRoll
THEN
ApplyStatus(_Target, _Status, _Duration, 0, _Caster);

/*
IF
CharacterStatusApplied(_Target, "LLWEAPONEX_TORMENT", (CHARACTERGUID)_Caster)
THEN
CharacterStatusText(_Target, "LLWEAPONEX_Status_Torment_Applied");
*/

//END_REGION

//REGION REVENANTS_TORMENT
PROC
LLWEAPONEX_Statuses_OnRevenantCreated((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, "LLWEAPONEX_TORMENTED_GHOST")
THEN
CharacterAddToPlayerCharacter(_Revenant, _Target);

PROC
LLWEAPONEX_Statuses_OnRevenantDied((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, "LLWEAPONEX_TORMENTED_GHOST")
THEN
LLWEAPONEX_Statuses_RemoveRevenant(_Revenant);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"
