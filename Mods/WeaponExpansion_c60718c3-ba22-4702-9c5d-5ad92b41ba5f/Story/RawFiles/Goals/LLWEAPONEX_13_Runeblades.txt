Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Runeblades_InitSettings();
KBSECTION
PROC
LLWEAPONEX_Runeblades_InitSettings()
THEN
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_AVALANCHE");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_BLOOD_AIR");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_BLOOD_EARTH");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_BLOOD_FIRE");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_BLOOD_POISON");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_BLOOD_WATER");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_CONDUCTION");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_CONTAMINATION");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_DUST");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_EXPLOSIVE");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_GAS");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_HEATWAVE");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_LAVA");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_SEARING");
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses("LLWEAPONEX_ACTIVATE_RUNEBLADE_TAR");

DB_LLWEAPONEX_Runeblades_TagToSkill("LLWEAPONEX_Runeblade_Air", "Shout_LLWEAPONEX_ActivateRuneblade_Air_DualWield");
DB_LLWEAPONEX_Runeblades_TagToSkill("LLWEAPONEX_Runeblade_Earth", "Shout_LLWEAPONEX_ActivateRuneblade_Earth_DualWield");
DB_LLWEAPONEX_Runeblades_TagToSkill("LLWEAPONEX_Runeblade_Fire", "Shout_LLWEAPONEX_ActivateRuneblade_Fire_DualWield");
DB_LLWEAPONEX_Runeblades_TagToSkill("LLWEAPONEX_Runeblade_Poison", "Shout_LLWEAPONEX_ActivateRuneblade_Poison_DualWield");
DB_LLWEAPONEX_Runeblades_TagToSkill("LLWEAPONEX_Runeblade_Water", "Shout_LLWEAPONEX_ActivateRuneblade_Water_DualWield");
DB_LLWEAPONEX_Runeblades_TagToSkill("LLWEAPONEX_Runeblade_Chaos", "Shout_LLWEAPONEX_ActivateRuneblade_Chaos_DualWield");

DB_LLWEAPONEX_Runeblades_StatusSound("LLWEAPONEX_RUNEBLADE_HEATBURST_SPREAD", "Skill_Fire_Cast_AoE_Air_Fire", 800);
DB_LLWEAPONEX_Runeblades_FlagSound("LLWEAPONEX_Runeblade_PlayHeatBurstCheckSound", "Skill_Fire_Cast_Target_Totem_Fire", 800);

PROC
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses((STRING)_BaseStatus)
THEN
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses((STRING)_BaseStatus, 2);

PROC
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses((STRING)_BaseStatus, (INTEGER)_RegisterSecond)
AND
StringConcatenate(_BaseStatus, "_DAMAGE1", _BonusDamageStatus)
THEN
DB_LLWEAPONEX_Runeblades_BonusDamageStatuses(_BaseStatus, _BonusDamageStatus, "");

//REGION CHAOS_RUNE_SURFACE_ABSORB
QRY
LLWEAPONEX_Runeblades_QRY_IsInSurface((CHARACTERGUID)_Character, (STRING)_Surface)
AND
GetSurfaceGroundAt(_Character, _Surface)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Runeblades_QRY_IsInSurface((CHARACTERGUID)_Character, (STRING)_Surface)
AND
GetSurfaceCloudAt(_Character, _Surface)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Runeblades_QRY_IgnoreSurfaceRemoval((CHARACTERGUID)_Character, (STRING)_Surface)
AND
StringContains(_Surface, "Blessed", 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Runeblades_QRY_IgnoreSurfaceRemoval((STRING)_Surface)
AND
StringContains(_Surface, "Cursed", 1)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_Runeblades_QRY_IgnoreSurfaceRemoval((STRING)_Surface)
AND
StringContains(_Surface, "Purified", 1)
THEN
DB_NOOP(1);

/* Handled in LLWEAPONEX_RuneBlades.gameScript
IF
CharacterStatusAttempt(_Character, "LLWEAPONEX_ACTIVATE_RUNEBLADE_CHAOS", _)
AND
DB_LeaderLib_Surfaces(_Index, _Surface)
AND
ObjectGetFlag(_Character, "LLWEAPONEX_ChaosRune_PreserveSurface", 0)
AND
LLWEAPONEX_Runeblades_QRY_IsInSurface(_Character, _Surface)
AND
LLWEAPONEX_Runeblades_QRY_IgnoreSurfaceRemoval(_Surface)
THEN
ObjectSetFlag(_Character, "LLWEAPONEX_ChaosRune_PreserveSurface");

IF
CharacterStatusRemoved(_Character, "LLWEAPONEX_ACTIVATE_RUNEBLADE_CHAOS", _)
AND
DB_LLWEAPONEX_Runeblades_AbsorbedSurface(_Character)
AND
GetPosition(_Character, _x, _y, _z)
THEN
NOT DB_LLWEAPONEX_Runeblades_AbsorbedSurface(_Character);
//RemoveStatus(_Character, "LLWEAPONEX_ACTIVATE_RUNEBLADE_CHAOS");
PlayEffect(_Character, "RS3_FX_Skills_Void_Cast_Weapon_Hand_01", "Dummy_CastFX");
CreateSurfaceAtPosition(_x, _y, _z, "SurfaceNone", 1.0, -1.0); // Absorb the surface
*/

IF
CharacterStatusRemoved(_Character, "LLWEAPONEX_ACTIVATE_RUNEBLADE_CHAOS", _)
AND
ObjectGetFlag(_Character, "LLWEAPONEX_ChaosRune_PreserveSurface", 1)
THEN
ObjectClearFlag(_Character, "LLWEAPONEX_ChaosRune_PreserveSurface");
//CharacterStatusText(_Character, "LLWEAPONEX_NoElementFoundForChaosRune");
//END_REGION

//REGION BONUS_DAMAGE_STATUS
PROC
LLWEAPONEX_Runeblades_RegisterBonusDamageStatuses((STRING)_BaseStatus, 2)
AND
DB_LLWEAPONEX_Runeblades_BonusDamageStatuses(_BaseStatus, _BonusDamageStatus1, _EmptyStatus)
AND
StringConcatenate(_BaseStatus, "_DAMAGE2", _BonusDamageStatus2)
THEN
NOT DB_LLWEAPONEX_Runeblades_BonusDamageStatuses(_BaseStatus, _BonusDamageStatus1, _EmptyStatus);
DB_LLWEAPONEX_Runeblades_BonusDamageStatuses(_BaseStatus, _BonusDamageStatus1, _BonusDamageStatus2);

IF
CharacterStatusApplied(_Character, _Status, _)
AND
DB_LLWEAPONEX_Runeblades_BonusDamageStatuses(_Status, _BonusDamage1, _BonusDamage2)
AND
GetStatusTurns(_Character, _Status, _TurnsInt)
AND
Real(_TurnsInt, _TurnsReal)
AND
RealProduct(_TurnsReal, 6.0, _Duration)
THEN
LLWEAPONEX_Runeblades_ApplyBonusDamageStatus(_Character, _BonusDamage1, _Duration);
LLWEAPONEX_Runeblades_ApplyBonusDamageStatus(_Character, _BonusDamage2, _Duration);

PROC
LLWEAPONEX_Runeblades_ApplyBonusDamageStatus((CHARACTERGUID)_Character, (STRING)_Status, (REAL)_Duration)
AND
_Status != ""
THEN
ApplyStatus(_Character, _Status, _Duration, 0, _Character);

IF
CharacterStatusRemoved(_Character, _Status, _)
AND
DB_LLWEAPONEX_Runeblades_BonusDamageStatuses(_Status, _BonusDamage1, _BonusDamage2)
THEN
LLWEAPONEX_Runeblades_RemoveBonusDamageStatus(_Character, _BonusDamage1);
LLWEAPONEX_Runeblades_RemoveBonusDamageStatus(_Character, _BonusDamage2);

PROC
LLWEAPONEX_Runeblades_RemoveBonusDamageStatus((CHARACTERGUID)_Character, (STRING)_Status)
AND
_Status != ""
THEN
RemoveStatus(_Character, _Status);
//END_REGION

//REGION RANDOM_ROLL
IF
CharacterCharacterEvent(_Source, _Target, "LLWEAPONEX_Commands_Runeblade_StartRandom")
AND
LeaderLib_Random_QRY(100)
AND
DB_LeaderLib_Random(_Ran)
THEN
NOT DB_LeaderLib_Random(_Ran);
SetVarInteger(_Source, "LLWEAPONEX_Runeblade_Roll", _Ran);
CharacterCharacterSetEvent(_Source, _Target, "LLWEAPONEX_Events_Runeblade_RandomSet");
//END_REGION

//REGION DOUBLE_HITS
IF
CharacterStatusApplied(_Character, "LLWEAPONEX_PREVENT_DOUBLE_HITS", _)
THEN
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_PreventDoubleHits", 500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_PreventDoubleHits")
THEN
RemoveStatus(_Character, "LLWEAPONEX_PREVENT_DOUBLE_HITS");
//END_REGION

//REGION SOUL_BURN_TICK
IF
StoryEvent((CHARACTERGUID)_Character, "LLWEAPONEX_Commands_StartSoulBurnTick")
AND
HasActiveStatus(_Character, "LLWEAPONEX_SOUL_BURN1", 0) // Skip since it's at the bottom of the stack
THEN
ObjectClearFlag(_Character, "LLWEAPONEX_SkipSoulBurnTick");
ApplyStatus(_Character, "LLWEAPONEX_SOUL_BURN_TICK", 6.0, 1, _Character);

IF
StoryEvent((CHARACTERGUID)_Character, "LLWEAPONEX_Commands_StopSoulBurnTick")
THEN
ObjectSetFlag(_Character, "LLWEAPONEX_SkipSoulBurnTick");
RemoveStatus(_Character, "LLWEAPONEX_SOUL_BURN_TICK");

IF
CharacterStatusRemoved(_Character, "LLWEAPONEX_SOUL_BURN_TICK", _)
AND
ObjectGetFlag(_Character, "LLWEAPONEX_SkipSoulBurnTick", 1)
THEN
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_ResetSkipSoulBurnTickTimerFlag", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_ResetSkipSoulBurnTickTimerFlag")
THEN
ObjectClearFlag(_Character, "LLWEAPONEX_SkipSoulBurnTick");
//END_REGION

IF
CharacterStatusApplied(_Character, "LLWEAPONEX_RUNEBLADE_THUNDERBOLT_EXPLODE", (CHARACTERGUID)_Cause)
THEN
LeaderLib_Skills_UseSkillOnTarget(_Cause, _Character, "ProjectileStrike_LLWEAPONEX_Runeblade_Dummy_Thunderbolt", 1, 1);
/*
AND
CharacterGetLevel(_Cause, _Level)
THEN
CreateProjectileStrikeAt(_Character, "ProjectileStrike_Stormbolt_Lightning", _Level);
*/

//REGION DOUBLE_RUNEBLADE_SKILLS
IF
ItemUnEquipped(_Runeblade, _Character)
AND
IsTagged(_Runeblade, "LLWEAPONEX_Runeblade", 1)
AND
DB_LLWEAPONEX_Runeblades_Temp_AddedDualWieldingSkill(_Character, _Skill)
THEN
NOT DB_LLWEAPONEX_Runeblades_Temp_AddedDualWieldingSkill(_Character, _Skill);
CharacterRemoveSkill(_Character, _Skill);

IF
ItemEquipped(_Runeblade, _Character)
AND
IsTagged(_Runeblade, "LLWEAPONEX_Runeblade", 1)
AND
IsTagged(_Runeblade, "LLMIME_MIMICKED_WEAPON", 0)
AND
CharacterGetEquippedItem(_Character, "Weapon", (ITEMGUID)_Runeblade)
AND
CharacterGetEquippedItem(_Character, "Shield", (ITEMGUID)_OtherRuneblade)
AND
IsTagged(_OtherRuneblade, "LLWEAPONEX_Runeblade", 1)
AND
DB_LLWEAPONEX_Runeblades_TagToSkill(_Tag, _Skill)
AND
CharacterHasSkill(_Character, _Skill, 0)
AND
IsTagged(_Runeblade, _Tag, 1)
AND
IsTagged(_OtherRuneblade, _Tag, 1)
THEN
CharacterAddSkill(_Character, _Skill);
DB_LLWEAPONEX_Runeblades_Temp_AddedDualWieldingSkill(_Character, _Skill);

IF
ItemEquipped(_Runeblade, _Character)
AND
IsTagged(_Runeblade, "LLWEAPONEX_Runeblade", 1)
AND
IsTagged(_Runeblade, "LLMIME_MIMICKED_WEAPON", 0)
AND
CharacterGetEquippedItem(_Character, "Shield", (ITEMGUID)_Runeblade)
AND
CharacterGetEquippedItem(_Character, "Weapon", (ITEMGUID)_OtherRuneblade)
AND
IsTagged(_OtherRuneblade, "LLWEAPONEX_Runeblade", 1)
AND
DB_LLWEAPONEX_Runeblades_TagToSkill(_Tag, _Skill)
AND
CharacterHasSkill(_Character, _Skill, 0)
AND
IsTagged(_Runeblade, _Tag, 1)
AND
IsTagged(_OtherRuneblade, _Tag, 1)
THEN
CharacterAddSkill(_Character, _Skill);
DB_LLWEAPONEX_Runeblades_Temp_AddedDualWieldingSkill(_Character, _Skill);
//END_REGION

//REGION SOUNDS
IF
CharacterStatusApplied(_Character, _Status, _)
AND
DB_LLWEAPONEX_Runeblades_StatusSound(_Status, _Sound, _Delay)
AND
NOT DB_LLWEAPONEX_Runeblades_Temp_PreventSound(_Status, _)
AND
StringConcatenate("LLWEAPONEX_Timers_ResetStatusSound_", _Status, _TimerName)
THEN
DB_LLWEAPONEX_Runeblades_Temp_PreventSound(_Status, _TimerName);
PlaySound(_Character, _Sound);
TimerLaunch(_TimerName, _Delay);

IF
ObjectFlagSet(_Flag, _Object, _)
AND
DB_LLWEAPONEX_Runeblades_FlagSound(_Flag, _Sound, _Delay)
AND
NOT DB_LLWEAPONEX_Runeblades_Temp_PreventSound(_Flag, _)
AND
StringConcatenate("LLWEAPONEX_Timers_ResetStatusSound_", _Flag, _TimerName)
THEN
DB_LLWEAPONEX_Runeblades_Temp_PreventSound(_Flag, _TimerName);
PlaySound(_Object, _Sound);
TimerLaunch(_TimerName, _Delay);

IF
ObjectFlagSet(_Flag, _Object, _)
AND
DB_LLWEAPONEX_Runeblades_FlagSound(_Flag, _Sound, _Delay)
THEN
ObjectClearFlag(_Object, _Flag);

IF
TimerFinished(_TimerName)
AND
DB_LLWEAPONEX_Runeblades_Temp_PreventSound(_String, _TimerName)
THEN
NOT DB_LLWEAPONEX_Runeblades_Temp_PreventSound(_String, _TimerName);
//END_REGION

//REGION RUNE_BLOCKING
IF
RuneInserted(_Character, _Item, _RuneTemplate, _Slot)
AND
StringContains(_RuneTemplate, "LOOT_Rune_LLWEAPONEX_Runeblade_", 1)
AND
IsTagged(_Item, "LLWEAPONEX_Runeblade", 0)
//AND
//GetTemplate(_Item, _ItemTemplate)
//AND
//StringContains(_ItemTemplate, "WPN_", 1)
THEN
DB_LLWEAPONEX_Runeblades_Temp_RemoveRune(_Character, _Item, _Slot, _RuneTemplate);
LeaderLib_StartCharacterItemTimer(_Character, _Item, 50, "LLWEAPONEX_Timers_RemoveRune_", "LLWEAPONEX_Events_RemoveBlockedRune");

IF
CharacterItemEvent(_Character, _Item, "LLWEAPONEX_Events_RemoveBlockedRune")
AND
DB_LLWEAPONEX_Runeblades_Temp_RemoveRune(_Character, _Item, _Slot, _RuneTemplate)
AND
LeaderLog_QRY_SetNextOneshotStatus(_Character, "#FF0000", "22")
AND
LeaderLog_QRY_Log("STATUS", "*This rune can only be inserted into a <font color='#40E0D0'>Runeblade</font>.*")
AND
ItemRemoveRune(_Character, _Item, _Slot, _Rune)
THEN
NOT DB_LLWEAPONEX_Runeblades_Temp_RemoveRune(_Character, _Item, _Slot, _RuneTemplate);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_WeaponExpansion"
