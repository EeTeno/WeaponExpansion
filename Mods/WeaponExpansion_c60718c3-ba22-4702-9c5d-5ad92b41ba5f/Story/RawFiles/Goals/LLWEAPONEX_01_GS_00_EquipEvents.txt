Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//REGION MAIN_EVENTS
IF
ItemUnEquipped(_Item, _Char)
AND
DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item)
AND
LLWEAPONEX_Ext_QRY_IsPlayer(_Char, _IsPlayer)
AND
GetTemplate(_Item, _Template)
THEN
NOT DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item);
LLWEAPONEX_OnItemTemplateUnEquipped(_Char, _Item, _Template);
LLWEAPONEX_Equipment_ClearItem(_Char, _Item, _IsPlayer);
LLWEAPONEX_WeaponMastery_RemovedTrackedMasteries(_Char, _Item);

IF
ItemEquipped(_Item, _Char)
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
THEN
LLWEAPONEX_Ext_OnItemEquipped(_Char, _Item);
//END_REGION

//REGION ITEM_TAGS
PROC
LLWEAPONEX_Equipment_TrackItem((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Tag, (INTEGER)_IsPlayer)
AND
NOT DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item)
AND
GetTemplate(_Item, _Template)
THEN
DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item);
LLWEAPONEX_OnItemTemplateEquipped(_Char, _Item, _Template);

PROC
LLWEAPONEX_Equipment_TrackItem((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Tag, (INTEGER)_IsPlayer)
THEN
DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _Item);
LLWEAPONEX_Equipment_OnTaggedItemEquipped(_Char, _Item, _Tag, _IsPlayer);
SetTag(_Char, _Tag);

PROC
LLWEAPONEX_Equipment_ClearItem((CHARACTERGUID)_Char, (ITEMGUID)_Item, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item)
THEN
NOT DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item);

PROC
LLWEAPONEX_Equipment_ClearItem((CHARACTERGUID)_Char, (ITEMGUID)_Item, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _Item)
THEN
NOT DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _Item);
LLWEAPONEX_Equipment_OnTaggedItemUnEquipped(_Char, _Item, _Tag, _IsPlayer);
LLWEAPONEX_Equipment_Internal_CheckTags(_Char, _Tag);

PROC
LLWEAPONEX_Equipment_Internal_CheckTags((CHARACTERGUID)_Char, (STRING)_Tag)
AND
NOT DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _)
THEN
ClearTag(_Char, _Tag);
//END_REGION

//REGION TAG_EQUIPPED_EVENTS
PROC
LLWEAPONEX_Equipment_OnTaggedItemEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Tag, (INTEGER)_IsPlayer)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_Equipment_OnTaggedItemUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Tag, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_Temp_AnimationSetOverride(_Char, "LLWEAPONEX_Override1", _Tag)
THEN
LLWEAPONEX_AnimationSetOverride_Clear(_Char);
//END_REGION

//REGION TEMPLATE_EVENTS
PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_OnItemTemplateUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
THEN
DB_NOOP(1);
//END_REGION

//REGION PRESET_APPLIED
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Char, _Slot,  (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LLWEAPONEX_OnItemTemplateEquipped(_Char, _Item, _Template);
//END_REGION

//REGION CLEANUP
IF
GameStarted(_Region, _)
AND
IsGameLevel(_Region, 1)
AND
DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item)
AND
ObjectExists(_Char, _a)
AND
ObjectExists(_Item, _b)
AND
LeaderLib_Math_QRY_IsEqualToAny(0, _a, _b)
AND
GetTemplate(_Item, _Template)
THEN
NOT DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item);
LLWEAPONEX_Equipment_Internal_CleanupEntry(_Char, _Item, _a, _b);

PROC
LLWEAPONEX_Equipment_Internal_CleanupEntry((CHARACTERGUID)_Char, (ITEMGUID)_Item, 1, 1)
AND
GetTemplate(_Item, _Template)
THEN
LLWEAPONEX_OnItemTemplateUnEquipped(_Char, _Item, _Template);

PROC
LLWEAPONEX_Equipment_Internal_CleanupEntry((CHARACTERGUID)_Char, (ITEMGUID)_Item, 1, (INTEGER)_ItemExists)
AND
LLWEAPONEX_Ext_QRY_IsPlayer(_Char, _IsPlayer)
THEN
LLWEAPONEX_Equipment_ClearItem(_Char, _Item, _IsPlayer);

PROC
LLWEAPONEX_Equipment_Internal_CleanupEntry((CHARACTERGUID)_Char, (ITEMGUID)_Item, 0, (INTEGER)_ItemExists)
THEN
LLWEAPONEX_Equipment_ClearItem(_Char, _Item, 1);

PROC
LLWEAPONEX_Equipment_Internal_ClearEntries((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item)
THEN
NOT DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item);

PROC
LLWEAPONEX_Equipment_Internal_ClearEntries((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _Item)
THEN
NOT DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _Item);

PROC
LLWEAPONEX_Equipment_Internal_ClearEntries((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_Temp_AnimationSetOverride(_Char, _Override, _Tag)
THEN
NOT DB_LLWEAPONEX_Temp_AnimationSetOverride(_Char, _Override, _Tag);
//END_REGION
EXITSECTION
ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_03_GameScripts"