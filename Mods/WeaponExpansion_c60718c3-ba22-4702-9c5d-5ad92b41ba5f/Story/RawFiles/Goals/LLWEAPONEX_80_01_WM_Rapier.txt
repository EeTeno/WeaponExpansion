Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION

//REGION EMPTY_HAND_EVENTS
IF
ItemEquipped(_Item, _Player)
AND
DB_LLWEAPONEX_WeaponMastery_ActiveSkills(_Player, "LLWEAPONEX_Rapier", "DuelistStance", _)
AND
HasActiveStatus(_Player, "LLWEAPONEX_RAPIER_MASTERY_STANCE_DUELIST", 1)
AND
LLWEAPONEX_QRY_IsEquippedWeapon(_Player, _Item)
AND
LLWEAPONEX_Rapiers_QRY_HasFullHands(_Player)
THEN
LLWEAPONEX_WeaponMastery_Rapiers_OnEmptyHandLost(_Player);
//LLWEAPONEX_WeaponMastery_RemoveSkill(_Player, "LLWEAPONEX_Rapier", "DuelistStance");

IF
ItemUnEquipped(_Item, _Player)
AND
DB_LLWEAPONEX_WeaponMastery_Learned(_Player, "LLWEAPONEX_Rapier", "DuelistStance")
AND
HasActiveStatus(_Player, "LLWEAPONEX_RAPIER_MASTERY_STANCE_DUELIST", 0)
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "WPN", 1)
AND
LLWEAPONEX_QRY_WeaponTypeEquipped(_Player, "LLWEAPONEX_Rapier")
AND
NOT LLWEAPONEX_Rapiers_QRY_HasFullHands(_Player)
THEN
LLWEAPONEX_WeaponMastery_Rapiers_OnEmptyHandGained(_Player);
//END_REGION

//REGION DUELIST_BONUS_AP
IF
CharacterCriticalHitBy(_Target, _, _Character)
AND
HasActiveStatus(_Character, "LLWEAPONEX_RAPIER_MASTERY_STANCE_DUELIST", 1)
AND
CharacterIsInCombat(_Character, 1)
AND
NOT LeaderLib_Helper_QRY_HasStatus(_Character, "LLWEAPONEX_RAPIER_MASTERY_DUELIST_BONUS")
THEN
CharacterAddActionPoints(_Character, 1);
ApplyStatus(_Character, "LLWEAPONEX_RAPIER_MASTERY_DUELIST_BONUS", 12.0, 0, _Character);
CharacterStatusText(_Character, "LLWEAPONEX_Skills_DuelistStance_Bonus");
//END_REGION

//REGION SUCKER_PUNCH_COMBOS
IF
SkillCast(_Character, "Target_SingleHandedAttack", _, _)
AND
IsTagged(_Character, "LLWEAPONEX_Rapier_SuckerCombo1Enabled", 1)
THEN
ApplyStatus(_Character, "LLWEAPONEX_WS_RAPIER_SUCKERCOMBO1", -1.0, 0, _Character);

IF
CharacterStatusRemoved(_Character, "KNOCKED_DOWN", _)
AND
HasAppliedStatus(_Character, "KNOCKED_DOWN", 0)
AND
HasActiveStatus(_Character, "LLWEAPONEX_RAPIER_MASTERY_DELAYED_DAZED", 1)
THEN
//RemoveStatus(_Character, "LLWEAPONEX_RAPIER_MASTERY_DELAYED_DAZED");
SetStoryEvent(_Character, "LLWEAPONEX_Events_DelayDazedReady");
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"