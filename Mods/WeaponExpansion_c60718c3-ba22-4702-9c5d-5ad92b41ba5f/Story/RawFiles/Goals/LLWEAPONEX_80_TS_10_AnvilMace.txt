Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
IF
CharacterCriticalHitBy(_Target, _, _Char)
AND
ObjectGetFlag(_Char, "LLWEAPONEX_AnvilMaceEquipped", 1)
AND
ObjectGetFlag(_Char, "LLWEAPONEX_IsBasicAttacking", 1)
THEN
PlaySound(_Target, "LeaderLib_Impacts_Anvil_01");
ApplyStatus(_Target, "LLWEAPONEX_DIZZY", 6.0, 0, _Char);

IF
CharacterStartAttackPosition(_x, _y, _z, _, _Char)
AND
ObjectGetFlag(_Char, "LLWEAPONEX_AnvilMaceEquipped", 1)
THEN
PlayEffectAtPosition("LLWEAPONEX_FX_AnvilMace_Impact_01", _x, _y, _z);
SetVarFloat3(_Char, "LLWEAPONEX_AnvilMace_AttackPosition", _x, _y, _z);
SetStoryEvent(_Char, "LLWEAPONEX_AnvilMace_CreateTimerName");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_AnvilMace_TimerNameCreated")
AND
GetVarString(_Char, "LLWEAPONEX_AnvilMace_TimerName", _TimerName)
AND
GetVarFloat3(_Char, "LLWEAPONEX_AnvilMace_AttackPosition", _x, _y, _z)
THEN
DB_LLWEAPONEX_AnvilMace_ImpactPosition(_Char, _TimerName, _x, _y, _z);
TimerLaunch(_TimerName, 1000);

IF
TimerFinished(_TimerName)
AND
DB_LLWEAPONEX_AnvilMace_ImpactPosition(_Char, _TimerName, _x, _y, _z)
THEN
NOT DB_LLWEAPONEX_AnvilMace_ImpactPosition(_Char, _TimerName, _x, _y, _z);
ObjectClearFlag(_Char, "LLWEAPONEX_IsBasicAttacking", 0);
SetVarFloat3(_Char, "LLWEAPONEX_AnvilMace_AttackPosition", _x, _y, _z);
SetStoryEvent(_Char, "LLWEAPONEX_AnvilMace_ExplodeAtPosition");

EXITSECTION
SysClear("DB_LLWEAPONEX_AnvilMace_ImpactPosition", 4);
ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"